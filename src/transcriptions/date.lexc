! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! ================================== !
! The Livvi Karelian (Olonets) dates !
! ================================== !

! pakkaskuu 1
! tuhukuu 2
! kevätkuu 3
! sulakuu 4
! oraskuu 5
! kezäkuu 6
! heinykuu 7
! elokuu 8
! syvyskuu 9
! ligakuu 10
! kylmykuu 11
! talvikuu 12

! 12.8.
! kahtestostu päivy elokuudu
! 12. elokuudu

Multichar_Symbols

@U.MONTH.1@ 
@U.MONTH.2@ 
@U.MONTH.3@ 
@U.MONTH.4@ 
@U.MONTH.5@ 
@U.MONTH.6@ 
@U.MONTH.7@ 
@U.MONTH.8@ 
@U.MONTH.9@ 
@U.MONTH.10@ 
@U.MONTH.11@ 
@U.MONTH.12@ 

+Use/NG ! For substandard month names used in Oahpa
! They will be accepted but not generated.



LEXICON Root

! For each month: First the normative, then the non-normatives, then initial capital

pakkaskuun% @U.MONTH.1@:@U.MONTH.1@ 31DAYS ;

Pakkaskuun+Use/NG% @U.MONTH.1@:@U.MONTH.1@ 31DAYS ;


tuhukuun% @U.MONTH.2@:@U.MONTH.2@ 29DAYS ;

Tuhukuun+Use/NG% @U.MONTH.2@:@U.MONTH.2@ 29DAYS ;


kevätkuun% @U.MONTH.3@:@U.MONTH.3@ 31DAYS ;

Kevätkuun+Use/NG% @U.MONTH.3@:@U.MONTH.3@ 31DAYS ;


sulakuun% @U.MONTH.4@:@U.MONTH.4@ 30DAYS ;

Sulakuun+Use/NG% @U.MONTH.4@:@U.MONTH.4@ 30DAYS ;


oraskuun% @U.MONTH.5@:@U.MONTH.5@ 31DAYS ;

Oraskuun+Use/NG% @U.MONTH.5@:@U.MONTH.5@ 31DAYS ;


kezäkuun% @U.MONTH.6@:@U.MONTH.6@ 30DAYS ;

Kezäkuun+Use/NG% @U.MONTH.6@:@U.MONTH.6@ 30DAYS ;


heinykuun% @U.MONTH.7@:@U.MONTH.7@ 31DAYS ;

Heinykuun+Use/NG% @U.MONTH.7@:@U.MONTH.7@ 31DAYS ;


elokuun% @U.MONTH.8@:@U.MONTH.8@ 31DAYS ;

Elokuun+Use/NG% @U.MONTH.8@:@U.MONTH.8@ 31DAYS ;


syvyskuun% @U.MONTH.9@:@U.MONTH.9@ 30DAYS ;

Syvyskuun+Use/NG% @U.MONTH.9@:@U.MONTH.9@ 30DAYS ;


ligakuun% @U.MONTH.10@:@U.MONTH.10@ 31DAYS ;

Ligakuun+Use/NG% @U.MONTH.10@:@U.MONTH.10@ 31DAYS ;



kylmykuun% @U.MONTH.11@:@U.MONTH.11@ 30DAYS ; ! bytt NG?

Kylmykuun+Use/NG% @U.MONTH.11@:@U.MONTH.11@ 30DAYS ; ! bytt NG?


talvikuun% @U.MONTH.12@:@U.MONTH.12@ 31DAYS ;

Talvikuun+Use/NG% @U.MONTH.12@:@U.MONTH.12@ 31DAYS ;


LEXICON 29DAYS
enzimäine% :1 B ;
toine% :2 B ;
kolmas% :3 B ;
nelljäs% :4 B ;
viijes% :5 B ;
kuvves% :6 B ;
seiččemäs% :7 B ;
kaheksas% :8 B ;
yheksäs% :9 B ;
kymmenes% :1%0 B ;
yhtestostu% :11 B ;
kahtestostu% :12 B ;
kolmastostu% :13 B ;
nelljästostu% :14 B ;
viijestostu% :15 B ;
kuvvestostu% :16 B ;
seiččemästostu% :17 B ;
kaheksastostu% :18 B ;
yheksästostu% :19 B ;
kahteskymmenes% :20 B ;
kahteskymmenesenzimäine% :21 B ;
kahteskymmenestoine% :22 B ;
kahteskymmeneskolmas% :23 B ;
kahteskymmenesnelljäs% :24 B ;
kahteskymmenesviijes% :25 B ;
kahteskymmeneskuvves% :26 B ;
kahteskymmenesseiččemäs% :27 B ;
kahteskymmeneskaheksas% :28 B ;
kahteskymmenesyheksäs% :29 B ;

LEXICON 30DAYS
kolmaskymmenes% :3%0 B ;
29DAYS ;

LEXICON 31DAYS
kolmaskymmenesenzimäine% :31 B ;
30DAYS ;

LEXICON B
päivy:%. MONTH ;
päivy:%. MONTH_SECOND ;

LEXICON MONTH
@U.MONTH.1@:1@U.MONTH.1@ X ;
@U.MONTH.2@:2@U.MONTH.2@ X ;
@U.MONTH.3@:3@U.MONTH.3@ X ;
@U.MONTH.4@:4@U.MONTH.4@ X ;
@U.MONTH.5@:5@U.MONTH.5@ X ;
@U.MONTH.6@:6@U.MONTH.6@ X ;
@U.MONTH.7@:7@U.MONTH.7@ X ;
@U.MONTH.8@:8@U.MONTH.8@ X ;
@U.MONTH.9@:9@U.MONTH.9@ X ;
@U.MONTH.10@:1%0@U.MONTH.10@ X ;
@U.MONTH.11@:11@U.MONTH.11@ X ;
@U.MONTH.12@:12@U.MONTH.12@ X ;

LEXICON MONTH_SECOND
pakkaskuudu@U.MONTH.1@:1@U.MONTH.1@ X ;
tuhukuudu@U.MONTH.2@:2@U.MONTH.2@ X ;
kevätkuudu@U.MONTH.3@:3@U.MONTH.3@ X ;
sulakuudu@U.MONTH.4@:4@U.MONTH.4@ X ;
oraskuudu@U.MONTH.5@:5@U.MONTH.5@ X ;
kezäkuudu@U.MONTH.6@:6@U.MONTH.6@ X ;
heinykuudu@U.MONTH.7@:7@U.MONTH.7@ X ;
elokuudu@U.MONTH.8@:8@U.MONTH.8@ X ;
syvyskuudu@U.MONTH.9@:9@U.MONTH.9@ X ;
ligakuudu@U.MONTH.10@:1%0@U.MONTH.10@ X ;
kylmykuudu@U.MONTH.11@:11@U.MONTH.11@ X ;
talvikuudu@U.MONTH.12@:12@U.MONTH.12@ X ;

LEXICON X
:%. # ;
! # ; ! sloppy punctuation
