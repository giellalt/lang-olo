! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

Multichar_Symbols

  +Use/NG    ! Do not generate, for isme-ped.fst and apertium
  +String    ! Tag to denote non-numeric strings
  +NumNum    ! Tag to denote real numbers in one form or another

LEXICON Root
< %+String [a|b|c|d|e|f|g|h|i|j|k|l|m|n|o|p|q|r|s|t|u|v|w|q|y|z|æ|ø|å|ä|ö|á|č|đ|ŋ|A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z|Æ|Ø|Å|Ä|Ö|Á|Č|Đ|Ŋ|Š|Ŧ|Ž]* > COMMA ; ! This first line is to allow all letter strings.
 CARD-NUMBERSECTION ;
 ORD-NUMBERSECTION ;
!+NumNum NUMBERSECTION ;
!+NumNum COMMASECTION ;

LEXICON CARD-NUMBERSECTION
HUNDREDSM ;		! 200M
sada% :1 HUNDREDM ;	! 100M
TENSM ;			! 20-99M
TEENSM ;		! 10-19M
ONESM ;			! 1-9M
HUNDREDST ;		! 200000-999999
sada% :1 HUNDREDT ;	! 100000-100999
TENST ;			! 20000-99999,10000-10999
TEENST ;		! 11000-19999
ONEST ;			! 2000-9999
tuhat:1 THOUSAND ;	! 1000-1999
UNDERTHOUSAND ;		! 100-999
TENS ;			! 20-99
TEENS ;			! 10-19
ONES ;			! 1-9

LEXICON HUNDREDSM
kaksi:2  CUODIM ;
kolme:3 CUODIM ;
nelli:4 CUODIM ; ! Google: 459
viizi:5 CUODIM ;
kuuzi:6 CUODIM ;
seiččie:7 CUODIM ;
kaheksa:8 CUODIM ;
yheksä:9 CUODIM ;

LEXICON CUODIM
sadua% : HUNDREDM ;

LEXICON HUNDREDM
      TENSM ;
      TEENSM ;
:%0   ONESM ;
:%0%0 MILJON ;

LEXICON TEENSM
yksitostu:11     LOHKAIM ;
kaksitostu:12   LOHKAIM ;
kolmetostu:13    LOHKAIM ;
nellitostu:14 LOHKAIM ;
viizistostu:15   LOHKAIM ;
kuuzitostu:16   LOHKAIM ;
seiččietostu:17 LOHKAIM ;
kaheksatostu:18 LOHKAIM ;
yheksätostu:19   LOHKAIM ;

LEXICON LOHKAIM
MILJON ;

LEXICON TENSM
kymmene:1%0 MILJON ;
kaksi:2   LUHKIEM ;
kolme:3    LUHKIEM ;
nelli:4 LUHKIEM ;
viizi:5   LUHKIEM ;
kuuzi:6   LUHKIEM ;
seiččie:7 LUHKIEM ;
kaheksa:8 LUHKIEM ;
yheksä:9   LUHKIEM ;

LEXICON LUHKIEM
kymmen:%0 MILJON ;
kymmen:   ONESM ;

LEXICON ONESM
yksi% miljon% :1 OVERTHOUSANDS ;
kaksi:2         MILJON ;
kolme:3          MILJON ;
nelli:4       MILJON ;
viizi:5         MILJON ;
kuuzi:6         MILJON ;
seiččie:7       MILJON ;
kaheksa:8       MILJON ;
yheksä:9         MILJON ; 

LEXICON MILJON
% miljonua% : OVERTHOUSANDS ;

! =================
! Under the million
! =================

LEXICON OVERTHOUSANDS
HUNDREDST ;		! x00.000.000
sada:1 HUNDREDT ;	! 100.000.000
:%0 TENST ;		!  20.000.000
:%0 TEENST ;		!  10.000.000
:%0%0 ONEST ;		!   2.000.000
tuhat:%0%01 THOUSAND ;	!   1.000.000
:%0%0%0 UNDERTHOUSAND ;	
:%0%0%0%0 TENS ;
:%0%0%0%0 TEENS ;
:%0%0%0%0%0 ONES ;
:%0%0%0%0%0%0 COMMA ;	!  x000000

LEXICON HUNDREDST
kaksi:2   CUODIT ;	! 200000-299999
kolme:3    CUODIT ;	! 300000-399999
nelli:4 CUODIT ;	! 400000-499999
viizi:5   CUODIT ;	! 500000-599999
kuuzi:6   CUODIT ;	! 600000-699999
seiččie:7 CUODIT ;	! 700000-799999
kaheksa:8 CUODIT ;	! 800000-899999
yheksä:9   CUODIT ;	! 900000-999999

LEXICON CUODIT
sadua% : HUNDREDT ;	! 100000-199999

LEXICON HUNDREDT	! X = 1-9, Y = 0-9
TENST ;			! X2XYYY, X10YYY
TEENST ;		! X1XYYY
yksi:%01 THOUSANDS ;
:%0 ONEST ;		! XX0YYY
:%0%0 THOUSANDS ;	! X00YYY

LEXICON TEENST
yksitostu:11     LOHKAIT ;
kaksitostu:12   LOHKAIT ;
kolmetostu:13    LOHKAIT ;
nellitostu:14 LOHKAIT ;
viizitostu:15   LOHKAIT ;
kuuzitostu:16   LOHKAIT ;
seiččietostu:17 LOHKAIT ;
kaheksatostu:18 LOHKAIT ;
yheksätostu:19   LOHKAIT ;

LEXICON LOHKAIT
THOUSANDS ;

LEXICON TENST
kymmene:1%0 THOUSANDS ;
kaksikymmen:2   LUHKIET ;
kolmekymmen:3    LUHKIET ;
nellikymmen:4 LUHKIET ;
viizikymmen:5   LUHKIET ;
kuuzikymmen:6   LUHKIET ;
seiččiekymmen:7 LUHKIET ;
kaheksakymmen:8 LUHKIET ;
yheksäkymmen:9   LUHKIET ;

LEXICON LUHKIET
:%0 THOUSANDS ;
yksi:1 THOUSANDS ;
ONEST ;

LEXICON ONEST
kaksi:2 THOUSANDS ;
kolme:3 THOUSANDS ;
nelli:4 THOUSANDS ;
viizi:5 THOUSANDS ;
kuuzi:6 THOUSANDS ;
seiččie:7 THOUSANDS ;
kaheksa:8 THOUSANDS ;
yheksä:9 THOUSANDS ;

LEXICON THOUSANDS      ! x > 1
tuhattu: THOUSAND ;

LEXICON THOUSAND
% : UNDERTHOUSAND ;	   ! 1100-1999
% :%0 TENS ;	   ! 1020-1099
% :%0 TEENS ;	   ! 1010-1019
% :%0%0 ONES ;	   ! 1001-1009
:%0%0%0 COMMA ;	   ! 1000

!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
HUNDREDS ;
sada:1 HUNDRED ;
suas:1  CUODAAT ; ! Is this correct?

LEXICON HUNDREDS
kaksi:2   CUODI ;
kolme:3    CUODI ;
nelli:4 CUODI ;
viizi:5   CUODI ;
kuuzi:6   CUODI ;
seiččie:7 CUODI ;
kaheksa:8 CUODI ;
yheksä:9   CUODI ;

LEXICON CUODI
sadua:   HUNDRED ;
tjuatede:  CUODAAT ;

LEXICON HUNDRED
% :      TENS ;
% :      TEENS ;
% :%0   ONES ;
:%0%0 COMMA ;

LEXICON CUODAAT
:%0%0%.    COMMA ;

LEXICON TEENS
yksitostu:11 COMMA ;
kaksitostu:12 COMMA ;
kolmetostu:13 COMMA ;
nellitostu:14 COMMA ;
viizitostu:15 COMMA ;
kuuzitostu:16 COMMA ;
seiččietostu:17 COMMA ;
kaheksatostu:18 COMMA ;
yheksätostu:19 COMMA ;

LEXICON TENS
kymmene:1%0 COMMA ;
kymmenes:1%0%. COMMA ;
kaksi:2 LUHKIE ;
kolme:3 LUHKIE ;
nelli:4 LUHKIE ;
viizi:5 LUHKIE ;
kuuzi:6 LUHKIE ;
seiččie:7 LUHKIE ;
kaheksa:8 LUHKIE ;
yheksä:9 LUHKIE ;

LEXICON LUHKIE
kymmen:%0 COMMA ;
kymmenes:%0%. COMMA ;
kymmen: ONES ;

LEXICON ONES
CARDINAL ;
!ORDINAL ;

LEXICON CARDINAL
yksi:1 COMMA ;
kaksi:2 COMMA ;
kolme:3 COMMA ;
nelli:4 COMMA ;
viizi:5 COMMA ;
kuuzi:6 COMMA ;
seiččie:7 COMMA ;
kaheksa:8 COMMA ;
yheksä:9 COMMA ;

!!
LEXICON ORD-NUMBERSECTION
ORD-HUNDREDSM ;		! 200M
suas% :1 ORD-HUNDREDM ;	! 100M
ORD-TENSM ;			! 20-99M
ORD-TEENSM ;		! 10-19M
ORD-ONESM ;			! 1-9M
ORD-HUNDREDST ;		! 200000-999999
suas% :1 ORD-HUNDREDT ;	! 100000-100999
ORD-TENST ;			! 20000-99999,10000-10999
ORD-TEENST ;		! 11000-19999
ORD-ONEST ;			! 2000-9999
tuhannes:1 ORD-THOUSAND ;	! 1000-1999
ORD-UNDERTHOUSAND ;		! 100-999
ORD-TENS ;			! 20-99
ORD-TEENS ;			! 10-19
ORD-ONES ;			! 1-9

LEXICON ORD-HUNDREDSM
kahtes:2  ORD-CUODIM ;
kolmas:3 ORD-CUODIM ;
nelljäs:4 ORD-CUODIM ; ! Google: 459
viijes:5 ORD-CUODIM ;
kuvves:6 ORD-CUODIM ;
seiččemäs:7 ORD-CUODIM ;
kaheksas:8 ORD-CUODIM ;
yheksäs:9 ORD-CUODIM ;

LEXICON ORD-CUODIM
suas% : ORD-HUNDREDM ;

LEXICON ORD-HUNDREDM
      ORD-TENSM ;
      ORD-TEENSM ;
:%0   ORD-ONESM ;
:%0%0 ORD-MILJON ;

LEXICON ORD-TEENSM
yhtestostu:11     ORD-LOHKAIM ;
kahtestostu:12   ORD-LOHKAIM ;
kolmastostu:13    ORD-LOHKAIM ;
nelljästostu:14 ORD-LOHKAIM ;
viijestostu:15   ORD-LOHKAIM ;
kuvvestostu:16   ORD-LOHKAIM ;
seiččemästostu:17 ORD-LOHKAIM ;
kaheksastostu:18 ORD-LOHKAIM ;
yheksästostu:19   ORD-LOHKAIM ;

LEXICON ORD-LOHKAIM
ORD-MILJON ;

LEXICON ORD-TENSM
kymmenes:1%0 ORD-MILJON ;
kahtes:2   ORD-LUHKIEM ;
kolmas:3    ORD-LUHKIEM ;
nelljäs:4 ORD-LUHKIEM ;
viijes:5   ORD-LUHKIEM ;
kuvves:6   ORD-LUHKIEM ;
seiččemäs:7 ORD-LUHKIEM ;
kaheksas:8 ORD-LUHKIEM ;
yheksäs:9   ORD-LUHKIEM ;

LEXICON ORD-LUHKIEM
kymmenes:%0 ORD-MILJON ;
kymmenes:   ORD-ONESM ;

LEXICON ORD-ONESM
yhtes% miljonas% :1 ORD-OVERTHOUSANDS ;
kahtes:2         ORD-MILJON ;
kolmas:3          ORD-MILJON ;
nelljäs:4       ORD-MILJON ;
viijes:5         ORD-MILJON ;
kuvves:6         ORD-MILJON ;
seiččemäs:7       ORD-MILJON ;
kaheksas:8       ORD-MILJON ;
yheksäs:9         ORD-MILJON ; 

LEXICON ORD-MILJON
% miljonas% : ORD-OVERTHOUSANDS ;

! =================
! Under the million
! =================

LEXICON ORD-OVERTHOUSANDS
ORD-HUNDREDST ;		! x00.000.000
suas:1 ORD-HUNDREDT ;	! 100.000.000
:%0 ORD-TENST ;		!  20.000.000
:%0 ORD-TEENST ;		!  10.000.000
:%0%0 ORD-ONEST ;		!   2.000.000
tuhannes:%0%01 ORD-THOUSAND ;	!   1.000.000
:%0%0%0 ORD-UNDERTHOUSAND ;	
:%0%0%0%0 ORD-TENS ;
:%0%0%0%0 ORD-TEENS ;
:%0%0%0%0%0 ORD-ONES ;
:%0%0%0%0%0%0%. COMMA ;	!  x000000

LEXICON ORD-HUNDREDST
kahtes:2   ORD-CUODIT ;	! 200000-299999
kolmas:3    ORD-CUODIT ;	! 300000-399999
nelljäs:4 ORD-CUODIT ;	! 400000-499999
viijes:5   ORD-CUODIT ;	! 500000-599999
kuvves:6   ORD-CUODIT ;	! 600000-699999
seiččemäs:7 ORD-CUODIT ;	! 700000-799999
kaheksas:8 ORD-CUODIT ;	! 800000-899999
yheksäs:9   ORD-CUODIT ;	! 900000-999999

LEXICON ORD-CUODIT
suas% : ORD-HUNDREDT ;	! 100000-199999

LEXICON ORD-HUNDREDT	! X = 1-9, Y = 0-9
ORD-TENST ;			! X2XYYY, X10YYY
ORD-TEENST ;		! X1XYYY
yhtes:%01 ORD-THOUSANDS ;
:%0 ORD-ONEST ;		! XX0YYY
:%0%0 ORD-THOUSANDS ;	! X00YYY

LEXICON ORD-TEENST
yhtestostu:11     ORD-LOHKAIT ;
kahtestostu:12   ORD-LOHKAIT ;
kolmastostu:13    ORD-LOHKAIT ;
nelljästostu:14 ORD-LOHKAIT ;
viijestostu:15   ORD-LOHKAIT ;
kuvvestostu:16   ORD-LOHKAIT ;
seiččemästostu:17 ORD-LOHKAIT ;
kaheksastostu:18 ORD-LOHKAIT ;
yheksästostu:19   ORD-LOHKAIT ;

LEXICON ORD-LOHKAIT
ORD-THOUSANDS ;

LEXICON ORD-TENST
kymmenes:1%0 ORD-THOUSANDS ;
kahteskymmenes:2   ORD-LUHKIET ;
kolmaskymmenes:3    ORD-LUHKIET ;
nelljäskymmenes:4 ORD-LUHKIET ;
viijeskymmenes:5   ORD-LUHKIET ;
kuvveskymmenes:6  ORD-LUHKIET ;
seiččemäskymmenes:7 ORD-LUHKIET ;
kaheksaskymmenes:8 ORD-LUHKIET ;
yheksäskymmenes:9   ORD-LUHKIET ;

LEXICON ORD-LUHKIET
:%0 ORD-THOUSANDS ;
yhtes:1 ORD-THOUSANDS ;
ORD-ONEST ;

LEXICON ORD-ONEST
kahtes:2 ORD-THOUSANDS ;
kolmas:3 ORD-THOUSANDS ;
nelljäs:4 ORD-THOUSANDS ;
viijes:5 ORD-THOUSANDS ;
kuvves:6 ORD-THOUSANDS ;
seiččemäs:7 ORD-THOUSANDS ;
kaheksas:8 ORD-THOUSANDS ;
yheksäs:9 ORD-THOUSANDS ;

LEXICON ORD-THOUSANDS      ! x > 1
tuhannes: ORD-THOUSAND ;

LEXICON ORD-THOUSAND
% : ORD-UNDERTHOUSAND ;	   ! 1100-1999
% :%0 ORD-TENS ;	   ! 1020-1099
% :%0 ORD-TEENS ;	   ! 1010-1019
% :%0%0 ORD-ONES ;	   ! 1001-1009
:%0%0%0%. COMMA ;	   ! 1000

!===========================
!Here starts the 999 numbers
!===========================

LEXICON ORD-UNDERTHOUSAND
ORD-HUNDREDS ;
suas:1  ORD-CUODAAT ; ! Is this correct?

LEXICON ORD-HUNDREDS
kahtes:2   ORD-CUODI ;
kolmas:3    ORD-CUODI ;
nelljäs:4 ORD-CUODI ;
viijes:5   ORD-CUODI ;
kuvves:6   ORD-CUODI ;
seiččemäs:7 ORD-CUODI ;
kaheksas:8 ORD-CUODI ;
yheksäs:9   ORD-CUODI ;

LEXICON ORD-CUODI
suas:  ORD-CUODAAT ;

LEXICON ORD-HUNDRED
% :      ORD-TENS ;
% :      ORD-TEENS ;
% :%0   ORD-ONES ;
:%0%0%. COMMA ;

LEXICON ORD-CUODAAT
:  ORD-HUNDRED ;
:%0%0%.    COMMA ;

LEXICON ORD-TEENS
yhtestostu:11%. COMMA ;
kahtestostu:12%. COMMA ;
kolmastostu:13%. COMMA ;
nelljästostu:14%. COMMA ;
viijestostu:15%. COMMA ;
kuvvestostu:16%. COMMA ;
seiččemästostu:17%. COMMA ;
kaheksastostu:18%. COMMA ;
yheksästostu:19%. COMMA ;

LEXICON ORD-TENS
kymmenes:1%0%. COMMA ;
kahtes:2 ORD-LUHKIE ;
kolmas:3 ORD-LUHKIE ;
nelljäs:4 ORD-LUHKIE ;
viijes:5 ORD-LUHKIE ;
kuvves:6 ORD-LUHKIE ;
seiččemäs:7 ORD-LUHKIE ;
kaheksas:8 ORD-LUHKIE ;
yheksäs:9 ORD-LUHKIE ;

LEXICON ORD-LUHKIE
kymmenes:%0%. COMMA ;
kymmenes: ORD-ONES ;
!!
LEXICON ORD-ONES
enzimäine:1%.     COMMA ;
toine:2%.   COMMA ;
kolmas:3%.   COMMA ;
nelläs:4%. COMMA ;
viijes:5%.   COMMA ;
kuvves:6%.   COMMA ;
seiččemäs:7%.    COMMA ;
kaheksas:8%.   COMMA ;
yheksäs:9%.    COMMA ;

LEXICON COMMA
 ENDLEX ;
 COMMASECTION ;

LEXICON COMMASECTION  ! these are not spellchecked!!!
                  ENDLEX ;
! % komma% :,      Root ;
! % tjuohkkis% :%. Root ;
! % kolon% :%:     Root ;
! % sárggis% :%-   Root ; 
 % la% :%=         Root ; 
 % gráda% :%°      Root ; 
 % paragráfa% :§   Root ; 
! % násti% :%*     Root ; 
 % ja% :&          Root ; 

LEXICON ENDLEX
 # ;
