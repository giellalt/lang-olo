!! # Livvi adjective inflection
! --------------------


!! ## Temporary lexica

!LEXICON Q_X !!≈ * @CODE@ 
!+TYÄ: # ;

LEXICON A_ !!≈ * @CODE@ 
+TYÄ: # ;

!! ## Somewhat open issues

LEXICON A_TULLUT !!≈ * @CODE@ IS THIS LIVVI? tullut:tullu
+Pos:%{back%} NMN_TULLUT ; 

LEXICON A_EX !!≈ * @CODE@ needs to be changed to @exclude=fst
+TYÄ: # ;

LEXICON A_BAHUV !!≈ @CODE@ 
+Bahuv: # ;

LEXICON A_UNDECL  !!≈ @CODE@ undeclinable fin: taipumaton
: # ;

LEXICON A_IS-N-PL-GEN !!≈ @CODE@ genitive plural attributes
+N+Pl+Gen: # ;

LEXICON A_IS-N-SG-GEN !!≈ @CODE@ genitive plural attributes
+N+Sg+Gen: # ;

LEXICON A-DEM_NÄMÄ !!≈ @CODE@ nämä:nämä
!+Dem+Pl+Abl:l K ;
!+Dem+Pl+Abl:lpäi K ;
+Dem+Pl+Ade:%^RmVow%>il K ;
+Dem+Pl+All:%^RmVow%>ile K ;
+Dem+Pl+Ess:%^RmVow%>inny K ;
+Dem+Pl+Gen:%^RmVow%>ien K ;
+Dem+Pl+Ill:%^RmVow%>ih K ;
+Dem+Pl+Ine:%^RmVow%>is K ;
+Dem+Pl+Nom: K ;
+Dem+Pl+Par:%^RmVow%>ii K ;
!+Dem+Pl+Tra:kse K ;



!! ### ONE-SYLLABLE VOWEL-FINAL STEMS ENDING IN LONG VOWEL SEGMENT
LEXICON A_KUU !!≈ @CODE@ kuu:kuu
+Pos:%{back%} NMN_KUU/PII ;

LEXICON A_MUA !!≈ @CODE@ mua:maa
:%{back%} NMN_MUA ;

LEXICON A_PIÄ !!≈ @CODE@ piä:piä
+Pos:%{front%} NMN_PIÄ ;


!! ## Ordinary inflection

!! ### TWO-SYLLABLE VOWEL-FINAL STEMS
LEXICON A_HYVÄ !!≈ @CODE@ hyvä:hyvä
!! the comparatives and superlatives are supletive
+Pos:%{front%} NMN_OZA/SÄYNÄ ;

LEXICON A_OZA !!≈ @CODE@ oza:oza
+Pos:%{back%} NMN_OZA/SÄYNÄ ;
+Comp:%{back%}%^E1%>mb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{back%}%^RmVow%>i NMN_ENIN        ;

LEXICON A_SÄYNÄ !!≈ @CODE@ säynä:säynä
+Pos:%{front%} NMN_OZA/SÄYNÄ ;
+Comp:%{front%}%^E1%>mb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{front%}%^RmVow%>i NMN_ENIN        ;

LEXICON A_KALA !!≈ @CODE@ kala:kala
+Pos:%{back%} NMN_KALA ;
+Comp:%{back%}%^E1%>mb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{back%}%^RmVow%>i NMN_ENIN        ;

LEXICON A_KOIVU !!≈ @CODE@ koivu:koivu
+Pos:%{back%}        NMN_KOIVU/HERY ;
+Comp:%{back%}%>mb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{back%}vi NMN_ENIN        ;

LEXICON A_HERY !!≈ @CODE@ hery:hery
+Pos:%{front%}        NMN_KOIVU/HERY ;
+Comp:%{front%}%>mb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{front%}vi NMN_ENIN        ;

LEXICON A_LUGU !!≈ @CODE@ lugu:lugu
+Pos:%{back%}        NMN_LUGU/IDY ;
+Comp:%{back%}%^WGStem%>mb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{back%}vi NMN_ENIN        ;

LEXICON A_IDY !!≈ @CODE@ idy:idy
+Pos:%{front%}        NMN_LUGU/IDY ;
+Comp:%{front%}%^WGStem%>mb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{front%}vi NMN_ENIN        ;

LEXICON A_RUNO
+Pos:%{back%}	NMN_RUNO/HÖRÖ ;
+Comp:%{back%}%>mb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{back%}vi NMN_ENIN        ;

LEXICON A_RUADO
+Pos:%{back%}	NMN_RUADO/KYNDÖ ;
+Comp:%{back%}%>mb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{back%}vi NMN_ENIN        ;

!LEXICON A_PAPPI !!≈ @CODE@ pappi:pappi
!:%{back%} NMN_PAPPI/HÄKKI ;

LEXICON A_HÄKKI !!≈ @CODE@ häkki:häkki
+Pos:%{front%} NMN_PAPPI/HÄKKI ;

LEXICON A_ARTELI !!≈ @CODE@ 
+Pos:%{back%}     NMN_ARTELI/LEIRI ;
!LEXICON A_LEIRI !!≈ @CODE@ 
!:%{front%}     NMN_ARTELI/LEIRI ;

LEXICON A_ALUS !!≈ @CODE@ alus:aluks
+Pos:%{back%} NMN_ALUS/ILVES   ;
+Comp:%{back%}e%>mb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{back%}i NMN_ENIN        ;

!LEXICON A_SUALIŠ !!≈ @CODE@ suališ:suali
!:%{back%}	 NMN_SUALIŠ ;
!+Superl:%{back%}vi NMN_ENIN        ;

LEXICON A_KUURNIS !!≈ @CODE@ kuurnis:kuurne
+Pos:%{back%}      NMN_KUURNIS ;

LEXICON A_PUHTAHUS !!≈ @CODE@ puhtahus:puhtahu
+Pos:%{back%}	NMN_PUHTAHUS/VÄHYS ;
!LEXICON A_VÄHYS !!≈ @CODE@ vähys:vähy
!:%{front%}	NMN_PUHTAHUS/VÄHYS ;

LEXICON A_OLUT !!≈ @CODE@ olut:olu
+Pos:%{back%}	 NMN_OLUT ;

!! ### TWO-SYLLABLE VOWEL-FINAL STEM WITH UNIQUE +Nom+Sg VOWEL
LEXICON A_PÄIVY !!≈ @CODE@ päivy:päivä
+Pos:%{front%} NMN_KOIRU/PÄIVY ;
+Comp:%{front%}%^E1%>mb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{front%}%^RmVow%>i NMN_ENIN        ;

LEXICON A_MUARJU !!≈ @CODE@ muarju:muarja
+Pos:%{back%}	NMN_MUARJU/PIÄSTÄNDY ;
+Comp:%{back%}%^E1%>mb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{back%}%^RmVow%>i NMN_ENIN        ;

!LEXICON A_PIÄSTÄNDY !!≈ @CODE@ piäständy:piäständä
!:%{front%}	NMN_MUARJU/PIÄSTÄNDY ;
!+Comp:%{front%}%^E1%>mb NMN_PAREMBI/ENÄMBI        ;
!+Superl:%{front%}%^RmVow%>i NMN_ENIN        ;

LEXICON A_AKKU !!≈ @CODE@ akku:akka
+Pos:%{back%}	NMN_AKKU ;
+Comp:%{back%}%^E1%>mb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{back%}%^RmVow%>i NMN_ENIN        ;

LEXICON A_KOIRU
+Pos:%{back%} NMN_KOIRU/PÄIVY ;
+Comp:%{back%}%^E1%>mb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{back%}%^RmVow%>i NMN_ENIN        ;


LEXICON A_VALGEI !!≈ @CODE@ valgei:valge
+Pos:%{back%} NMN_VALGEI/LIBEI        ;
+Comp:%{back%}%>emb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{back%}vi NMN_ENIN        ;

LEXICON A_RAHMANNOI !!≈ @CODE@ rahmannoi:rahmannoi
+Pos:%{back%} NMN_RAHMANNOI        ;


LEXICON A_HARDIE
+Pos:%{back%} NMN_HARDIE	;

LEXICON A_OSTAI
+Pos:%{back%}%>%{ijPRC%} NMN_OSTAI ;

LEXICON A_PAGIZII !!≈ @CODE@ pagizii:pagizi
+Pos:%{back%}%>%{ijPRC%} NMN_PAGIZII ;

LEXICON A_PEZII
+Pos:%{front%}%>%{ijPRC%} NMN_PEZII ;

LEXICON A_KONDII
+Pos:%{back%} NMN_KONDII/STIPENDII ;
!LEXICON A_STIPENDII
!:%{front%} NMN_KONDII/STIPENDII ;

LEXICON A_KESTÄY !!≈ @CODE@ kestäy:kestä
+Pos:%{front%} NMN_KESTÄY ;

!! ### TWO-SYLLABLE VOWEL-FINAL STEMS WITH CONSONANT-FINAL PARTITIVE STEM


!! ### THREE-SYLLABLE VOWEL-FINAL STEMS
LEXICON A_PAREMBI !!≈ @CODE@ parembi:paremb
+Pos:%{back%} NMN_PAREMBI/ENÄMBI	  ;
+Der/MWN:%{back%} N_PAREMBI ;
!+N:%{aä%} Adj-NB-i ; !2012-09-27 unknown -loi vs -i

LEXICON A_JIÄTÖI !!≈ @CODE@ jiätöi:jiät
:%{front%} A_HUOLETOI/JIÄTÖI ;
LEXICON A_HUOLETOI !!≈ @CODE@ huoletoi:huolet
:%{back%} A_HUOLETOI/JIÄTÖI ;
LEXICON A_HUOLETOI/JIÄTÖI !!≈ @CODE@ huoletoi:huolet
+Der/Us+N:t%{oö%}m%{uy%} NMN_PUHTAHUS/VÄHYS ;
+Pos:	NMN_HUOLETOI/JIÄTÖI   ;
+Der/MWN: N_HUOLETOI ; 
+Der/MWN+N:t%{oö%}m%{aä%} Adj-NB-i ; !2012-09-27 unknown -loi vs -i
+Comp:t%{oö%}memb NMN_PAREMBI/ENÄMBI        ;
+Superl:t%{oö%}mi NMN_ENIN        ;

LEXICON A_KARJALAINE !!≈ @CODE@ karjalaine:karjala
+Der/Us:%{back%}z%{uy%} N_PUHTAHUS ;
+Pos:%{back%}	NMN_KARJALAINE/LIYGILÄINE	 ;
+Comp:%{back%}zemb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{back%}zi NMN_ENIN        ;

LEXICON A_LIYGILÄINE !!≈ @CODE@ liygiläine:liygilä
+Der/Us:%{front%}z%{uy%} N_PUHTAHUS ;
+Pos:%{front%}	NMN_KARJALAINE/LIYGILÄINE ;
+Comp:%{front%}zemb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{front%}zi NMN_ENIN        ;

LEXICON A_NAINE !!≈ @CODE@ naine:nai
:%{back%} A_NAINE/KIELINE_01 ;
LEXICON A_KIELINE !!≈ @CODE@ kieline:kieli
:%{front%} A_NAINE/KIELINE_01 ;
LEXICON A_NAINE/KIELINE_01 !!≈ @CODE@ kieline:kieli naine:nai
+Der/Us+N:z%{uy%} NMN_PUHTAHUS/VÄHYS ;
+Pos: NMN_NAINE/KIELINE ;
+Comp:zemb NMN_PAREMBI/ENÄMBI        ;
+Superl:zi NMN_ENIN        ;

LEXICON A_TOINE !!≈ @CODE@ toine:to
+Pos:%{back%} NMN_TOINE ;
+Comp:%{back%}izemb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{back%}izi NMN_ENIN        ;
LEXICON A_TOINE-PL !!≈ @CODE@ toine:to
+Pos:%{back%} NMN_TOINE-PL ;

!! ### THREE-SYLLABLE STEMS WITH TWO-SYLLABLE NOMINATIVE SINGULAR
!LEXICON A_KARJAL !!≈ @CODE@ karjal:karjal
!:%{back%}     NMN_KARJAL ;
!+Comp:%{back%}emb NMN_PAREMBI/ENÄMBI        ;
!+Superl:%{back%}i NMN_ENIN        ;

LEXICON A_MADAL !!≈ @CODE@ madal:madal
+Der/Us:%{back%}%{uy%} N_PUHTAHUS ;
+Pos:%{back%}	NMN_MADAL ;
+Comp:%{back%}emb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{back%}i NMN_ENIN        ;

LEXICON A_PIIRAI !!≈ @CODE@ piirai:piira 
+Pos:%{back%}	NMN_PIIRAI/VERÄI   ;
!LEXICON A_VERÄI !!≈ @CODE@ veräi:verä 
!:%{front%}	NMN_PIIRAI/VERÄI   ;

LEXICON A_RAIŠ !!≈ @CODE@ raiš:ra
+Pos:%{back%}     NMN_RAIŠ   ;

!LEXICON A_TOVESTEH !!≈ @CODE@ pereh:pereh
!:%{back%}	NMN_TOVESTEH/PEREH	;

LEXICON A_PEREH !!≈ @CODE@ pereh:pereh
+Pos:%{front%}	NMN_TOVESTEH/PEREH	;

LEXICON A_TULLUH !!≈ @CODE@ tulluh:tullu
!!≈ * Yaml: **A-tulluh**
+Pos:%{back%} NMN_TULLUH/PESSYH ;

LEXICON A_PESSYH !!≈ @CODE@ pessyh:pessy
+Pos:%{front%} NMN_TULLUH/PESSYH ;

LEXICON A_ARMAS !!≈ @CODE@ armas:arma
+Pos:%{back%} NMN_ARMAS/EVAES        ;
+Comp:%{back%}h%{aä%}mb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{back%}hi NMN_ENIN        ;

LEXICON A_VARVAS !!≈ @CODE@ varvas:varva
+Pos:%{back%} NMN_VARVAS        ;
+Comp:%{back%}h%{aä%}mb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{back%}hi NMN_ENIN        ;

LEXICON A_TUORES !!≈ @CODE@ tuores:tuore
!!≈ * Yaml: **A-tuores**
+Pos:%{back%} NMN_TUORES ;
+Comp:%{back%}hemb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{back%}hi NMN_ENIN        ;

!LEXICON A_SUURIM !!≈ @CODE@ suurim:suurim
!:%{back%}	NMN_SUURIM ;

LEXICON A_SUARI !!≈ @CODE@ suari:suar
+Pos:%{back%} A_SUARI/KIELI_01 ;

LEXICON A_KIELI !!≈ @CODE@ kieli:kiel
:%{front%} A_SUARI/KIELI_01 ;
LEXICON A_SUARI/KIELI_01 !!≈ @CODE@ suari:suar
+Pos:	NMN_SUARI/KIELI	;
+Comp:emb NMN_PAREMBI/ENÄMBI        ;
+Superl:i NMN_ENIN        ;

LEXICON A_VUOZI !!≈ @CODE@ vuozi:vuod
+Pos:%{back%}	NMN_VUOZI/VEZI ;
+Comp:%{back%}%^WGStem%>emb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{back%}%^WGStem%>i NMN_ENIN        ;

LEXICON A_VEZI !!≈ @CODE@ vezi:ved
+Pos:%{front%}     NMN_VUOZI/VEZI ;

LEXICON A_NIMI !!≈ @CODE@ nimi:nim
+Pos:%{front%}        NMN_HANGI/NIMI  ;
+Comp:%{front%}emb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{front%}i NMN_ENIN        ;

LEXICON A_JÄLGI  !!≈ @CODE@ jälgi:jälg
!! front vowel
!! gradation Yes
+Pos:%{front%}        NMN_JÄLGI  ;
+Comp:%{front%}emb NMN_PAREMBI/ENÄMBI        ;
+Superl:%{front%}i NMN_ENIN        ;



!! ### TWO-SYLLABLE WORD WITH CONSONANT-FINAL STEM
!LEXICON A_VANUIN !!≈ @CODE@ vanuin:vanui
!:%{back%} NMN_VANUIN	 ;

LEXICON A_VAŽEN !!≈ @CODE@ važen:važe
+Pos:%{back%} NMN_VAŽEN   ;


LEXICON A_LÄMMIN !!≈ @CODE@ lämmin:lämbi
+Pos:%{front%}	NMN_LÄMMIN   ;

LEXICON A_TAIGIN !!≈ @CODE@ taigin:taigin
+Pos:%{back%}	NMN_TAIGIN   ;

LEXICON A_SALBOIN !!≈ @CODE@ salboin:salboi
+Pos:%{back%} NMN_SALBOIN ;

LEXICON A_ENIN !!≈ @CODE@ enin:eni
+Pos:%{front%} NMN_ENIN-SG ;
+Pos:%{front%} NMN_ENIN-PL ;



LEXICON Adj-NB-i
! These cases are symmetrically marked for number
+Pos+Sg: Adj-NB-01 ;
+Pos+Pl:%>i Adj-NB-01 ;
! The next two share the same stem vowel
+Pos+Sg: SG-ACC/APR/COM/GENSUF ;
+Pos+Pl: PLNOMSUF ;

!LEXICON Adj-NB-loi
!! These cases are symmetrically marked for number
!+Sg: Adj-NB-01 ;
!+Pl:%>loi Adj-NB-01 ;
!! The next two share the same stem vowel
!+Sg: SG-ACC/APR/COM/GENSUF ;
!+Pl: PLNOMSUF ;

LEXICON Adj-NB-01
+Ess:%^WGStem%>nn%{uy%} K ;
+Tra:%^WGStem%>kse K ;
+Ine:%^WGStem%>s K ;
+Ela:%^WGStem%>s K ;
+Ill:%>h K ;
+Ade:%^WGStem%>l K ;
+Abl:%^WGStem%>l K ;
+All:%^WGStem%>le K ;
+Abe:%^WGStem%>tt%{aä%}h K ;
+Err/Orth+Abe:%^WGStem%>tt%{aä%} K ;
+Err/Orth+Abe:%^WGStem%>t%{aä%} K ;
+Prl:%>či K ;
+Apr:%^WGStem%>ll%{uy%}%{oö%} K ;
+Ter:%^WGStem%>ss%{aä%}h K ;



! vim: set ft=xfst-lexc:

