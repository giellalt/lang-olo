
!! !!!The Livvi (Olonets) Karelian morphophonological/twolc rules file 



Alphabet
a b c č d e f g h i j k l m n o p r s š ş t u v w y z ž ƶ ü ä ö
A B C Č D E F G H I J K L M N O P R S Š Ş T U V W Y Z Ž Ƶ Ü Ä Ö
y
Y
! MODIFIER LETTER APOSTROPHE 02BC
ʼ

! Vowel harmony with "(t)a/ä"
!%{aä%}:a
%{aä%}:ä	!!= @CODE@  This is for the partitive and other vowel harmony not including AA infinitive
!%{aä%}:0


 %{aäInf%}:ä !!= @CODE@  This is for the infinitive marker after i and u stems

 %{ui%}:i !!= @CODE@  in imperative before %{aä%}:

! Vowel harmony with "loi/löi" also kalatOÖ1in
!%{oö%}:o
%{oö%}:ö

! Vowel harmony with "ttu/tty"
!%{uy%}:u
%{uy%}:y

! in verb type V_AVATA
!A2:a
A2:ä
!A2:o
!A2:ö

! this appears in the illative
V1:a
V1:e
V1:i
V1:o
V1:u
V1:y
V1:ä
V1:ö

! These appear with the inessive and adessive
V2:a
V2:e
V2:i
V2:o
V2:u
V2:y
V2:ä
V2:ö

! These reduplicate the preceding vowel if it in turn is preceded by a consonant
V3:a
V3:e
V3:i
V3:o
V3:u
V3:y
V3:ä
V3:ö
V3:0

%^DEVOICE:0   !!= @CODE@ 
%^VOICE:0   !!= @CODE@  pestä : pezen

! syllable boundary for V_AVATA
%^SyllBound:0

! Used with ks:s, ps:s
%^KS2S:0

! no change
%^NONE:0 ! This will break vowel change, e.g. sa%^NONEamelaine

! this k is not effected by gradation
K1:k
%^NoGrad:0 ! This will be placed after a stem to break Gradation

! this weakens the stem _ompel_ to _ommel_
%^WGStem:0

! The ti => zi
%^T2Z:0

! the a => o
%^A2O:0

! the a => u, ä => y
%^A2U:0

%^ILoss:0  !!= @CODE@  the i => 0 __reboi reboloi__

! Vowel raising
%^RVws:0

%- !Hyphen in  constructions 
%>
#   ! Word boundary for both lexicalised and dynamic compounds


%^LVws:0
%^LCns:0
%^WCns:0
%^AtoO:0
%^ÄtoÖ:0
%^OddSyll:0
%^StretchSyll2:0
%^SyllBr:0
%^E1:0


%^WEAK:0 !!= * @CODE@ 

;


Sets

VwsBack = a o u A O U ;
VwsFront = ä ö y ü Ä Ö Y Ü ;
VwsNeutral = e i E I ; 
VwsNonHigh = a o ä ö e A O Ä Ö E ;
Vws = a o u ä ö y ü e i 
      A O U Ä Ö Y Ü E I ;

Cns = b c č d f g h j k l m n p r s ş š t v z ƶ ž 
      B C Č D F G H J K L M N P R S Ş Š T V Z Ƶ Ž ;

Letters = Vws Cns ;


Definitions

! Front Trigger
FT =  [ #: Cns* [ VwsFront | VwsNeutral | i: ] | :VwsFront ] [ %> | VwsFront: | Cns: | %{aä%}:ä | %{oö%}:ö | %{uy%}:y ]*  ;


! Back Trigger 
BT =    #: Cns* ( [ VwsNeutral | i: ] [ %> | %^WEAK:0 | %^E1:0 | %^SyllBound:0 | %^KS2S:0 | VwsBack: | :VwsBack | Cns: | :Cns ]* )  [ VwsBack: | :VwsBack ] [ %> | %^WEAK:0  | %^VOICE:0 | %^KS2S:0 | (%^WEAK:0) %^E1:0 | %^SyllBound:0 | VwsNeutral | i: (%^ILoss:) | VwsBack: | :VwsBack | Cns: | :Cns ]* ;

!Back Trigger with consonant
BTCons =   #: Cns* ( [ VwsNeutral | i: ] [ %> | %^WEAK:0 | %^E1:0 | %^SyllBound:0 | %^KS2S:0 | VwsBack: | :VwsBack | Cns: | :Cns ]* )  [ VwsBack: | :VwsBack ]  [ %> | %^WEAK:0  | %^KS2S:0 | %^E1:0 | %^SyllBound:0 | VwsNeutral | i: | VwsBack: | :VwsBack | Cns: | :Cns ]* [ Cns:  ] ;

! Gradation strong to weak
!GS2W = [ p:v | m p:m | t:0 | l t:l | r t:r | n t:n | s t:s | k:0 | l k:0 | r k:0 | n k:g | s k:0 ] ;

! Short vowel
SV = [ [ #: | Cns+ ] :Vws ] ;

 ! If a vowel is preceeded by this, is can be long
!  in both the inessive and the adessive
! Inessive lengthening of vowel
IL = [ #: Cns* Vws : Cns:+ | Cns* Vws+ Cns:+  Vws:+ Cns:+  ] ;

!! Right context for gradation
GRC =  [ [ ( %>: ) %{aä%}: | %{oö%}: | %{uy%}: | Vws: | :Vws ] ( %>:  i )  |  %>:  i ] %^WEAK:0  ;

! oma:ommaa, 
! Subsequent Long Vowel
! preceeded by "Cns _"
! 2012-10-11 SLV =  [ :Vws [ ( %>: %^LCns:0 %^LVws:0 | %>: %^LCns:0 | %>: i %>: | %>: ) [ V1:Vws | %{aä%}: ] |  0:Vws %>: ( %^LCns:0 | %^WEAK: ) %^LVws: | %>: %^LCns:0  ] ] ;

!! !!!Rules
Rules

"%{aäInf%}:o"
!! __@RULENAME@__
%{aäInf%}:o <=> o: %> _ ( %^WEAK:0 ) [ # | %>  [ n | t | %{uy%}: | m m %{oö%}: | t t %{oö%}: ]];
         [ o: | u] %> _ # ;

! Vowel harmony suffixes Back
"%{aäInf%}:a"
!! __@RULENAME@__
 %{aäInf%}:a <=> [ BTCons %> |  a:  %> | BT %> [ d: | j | t ]] _ [ ( %^WEAK:0 ) h | i | %> n %{uy%}: | %> g i | # ] ;
   [ BTCons %> | BT ( %^WEAK:0 ) %> [ t (t) | s s ]] _ [  h | %> g | # ] ;


"%{aä%}:o"
!! __@RULENAME@__
%{aä%}:o <=> o: %> _ ( %^WEAK:0 ) [ # | %>  [ n | t | %{uy%}: | m m %{oö%}: | t t %{oö%}: ]];
         [ o: ] %> _ # ;

"%{aä%}:u"
!! __@RULENAME@__
%{aä%}:u <=> u: %> _ ( %^WEAK:0 ) [ # | %>  [ g i | n | t | %{uy%}: | m m %{oö%}: | t t %{oö%}: ]];
         [ u ] %> _ # ;

! Vowel harmony suffixes Back
"%{aä%}:a"
!! __@RULENAME@__
 %{aä%}:a <=> [ BTCons %> |  a:  %> | BT %> [ d: | j | t ]] _ [ ( %^WEAK:0 ) h | %^WEAK:0 [ %> | # ] | s | i | (n) n | %> n %{uy%}: | %> g i | # ] ;
   [ BTCons %> | [ BT ] ( %^WEAK:0 (%^E1:) ) %> [ (k) k (%{ui%}:u) | (t) t | s s ]] _ [  h | s | ( n ) n | t t | m m | v | %> g | # ] ;


"%{aä%}:e"
!! __@RULENAME@__
%{aä%}:e <=> i %> _ # ;

"%{aä%}:i"
!! __@RULENAME@__
%{aä%}:i <=> _ %> %{aä%}:ä ;

"%{aä%}:y"
!! __@RULENAME@__
%{aä%}:y <=> FT _ %^A2U:0 [ # | %> g i ] ;

"%{aä%}:u"
!! __@RULENAME@__
%{aä%}:u <=> BT _ %^A2U:0 [ # | %> g i ] ;

"%{aä%}:0"
!! __@RULENAME@__
%{aä%}:0 <=> _ %> i  ;



"%{ui%}:u"
!! __@RULENAME@__
%{ui%}:u <=> BT %> (k) k _  %{aä%}: ; !! * @CODE@ ! imperative forms


"A2:a"
!! __@RULENAME@__
A2:a <=> [ a: ] %> _ ( %^WEAK:0 ) [ # | %>  [ n | t | %{uy%}: | m m %{oö%}: | t t %{oö%}: ]];

"A2:u"
!! __@RULENAME@__
A2:u <=> [ u: ] %> _ ( %^WEAK:0 ) [ # | %>  ];

"A2:y"
!! __@RULENAME@__
A2:y <=> [ y: ] %> _ ( %^WEAK:0 ) [ # | %>  ];




"a:u before subsequent a"
!! __@RULENAME@__  Diphthong a+a => ua
a:u <=> Cns:+ _ [ (d:0) a | %> [ %{aä%}:a | %{aäInf%}:a | A2:a ] ]; 
        _ %^A2U:0 ;


"ä:i before subsequent A2"
!! __@RULENAME@__  Diphthong ä+ä => iä
ä:i <=> Cns _ [ ä | %> [ %{aä%}:ä | A2:ä ] ]; 

 
"o:u before subsequent o"
!! __@RULENAME@__ Diphthong o+o => uo erota:eruou
o:u <=> Cns _ %> [ %{aä%}:o ] ; 

! Word final u, y
"ä:y word final"
!! __@RULENAME@__
ä:y <=> _ %^A2U:0 ;


"a:0 in suaja:sai"
!! __@RULENAME@__
a:0 <=>
 a _ %> i ; !!= * @CODE@ Vowel shortening before subsequent "i"
[ # | Cns:* ] [[ u | o ] (i)| u o ] [ Cns:+ |:Cns+] _ %> i (%^WEAK:) [ # | (e) %> ] ;


! iToj between vowels
"i:j"
!! __@RULENAME@__
 i:j <=> :Vws ( %>:  %^LCns:0 ) %>: _ %>: :Vws ;
!2013-04-29!         Vws _ Cns:0 Vws: ;
!2013-04-29!veitte%>%^WCnsttUY1%>%^LCns%>i%>AÄ1
!2013-04-29!€ em0o0i%>%^LCnsAÄ1
!2013-04-29!€ emmooj00a

"a:o in the plural and preterite"
!! __@RULENAME@__
a:o <=> # Cns* [ a | i | e ] Cns:+ _ %>: i: ( %^WEAK:0 )  ;


"a:e"
!! __@RULENAME@__  pidiä
 a:e <=> _ %^WEAK:0 %^E1:0 ;


"ä:e piettih and in comparatives"
!! __@RULENAME@__  pidiä
ä:e <=> i d: _ %^WEAK:0 %^E1:0 ;

"ä:0 pidiä:pidi"
!! __@RULENAME@__
ä:0 <=> _ %> i ;

"i:0 reboi:reboloi"
!! __@RULENAME@__
i:0 <=> o _ %^ILoss: %> l %{oö%}: i ;



"%{oö%}:o"
!! __@RULENAME@__ Vowel harmony suffixes Back
 %{oö%}:o <=>  BT (  %> ) _ ;


"%{uy%}:u"
!! __@RULENAME@__ Vowel harmony suffixes Back
 %{uy%}:u <=>  [ BT | :VwsBack Cns:* ] ( %> ) _ ;

!! !!Consonant change

! sulgu:sullan
"g:l"
!! __@RULENAME@__  
g:l <=> l _ [ a: ] ( %> i ) %^WEAK:0 ;

! vuozi:vuvven
"t:v"
!! __@RULENAME@__  
t:v <=> u o: _ e %^WEAK:0 ;

"o:v Lengthening with Ut:vv weakening"
!! __@RULENAME@__  
o:v <=> u _ t: e %^WEAK:0 ;
![ #: ] Cns* Vws [ u: | y: ] [ t: | k: ]  _ [ GRC | %^TS:0 i %^WEAK:0 %>: ] ;
!2013-04-29!         [ #: ][ Cns* Vws | Cns* Vws+ Cns:+ Vws+ :0* Cns+ :Vws ] v _  ( :Vws | %{aä%}: ) [ 0:Vws %>: %^LCns: | %>: %^LCns: :0* :Vws ] ; ! arvattava:arvattavvaa
!2013-04-29!        #: Cns* Vws v _ Vws [ Vws 0: Cns ( Cns:0 ) :Vws | Vws %^SyllBr:0 e ] %^StretchSyll2:0 ; ! kevät:kevväen, tavvaara
!2013-04-29!

! vuozi:vuot
"t:z"
!! __@RULENAME@__  
t:z <=> _ %^T2Z:0 %> i ;

! vuozi:vuot+d%{uy%}, armas:armas+d%{uy%}
"d:t in partitive"
!! __@RULENAME@__  
d:t <=> [ h | t | s ] ( %^KS2S:0 ) %> _ ;

"s:z"
!! __@RULENAME@__
s:z <=> _ %^VOICE:0 ; !!= * @CODE@ pestä: pezen

"k:g"
!! __@RULENAME@__ pestä: peskäh
k:g <=> [ n | l |r ] %> _ (%{ui%}:) %{aä%}: ; 


!2013-04-29!
!2013-04-29!
!2013-04-29! viis: viijes
!2013-04-29!"it:j"
!2013-04-29! t:j <=> [ i | o ] i _ [ e | o ] %^WEAK:0 %>: ;
!2013-04-29!
!2013-04-29!"st:ss weakening"
!2013-04-29!t:s <=> Vws+ s _ ( %^TS:0 ) GRC ;
!2013-04-29! last-minute verbs
!2013-04-29! "ti:si in verbs"
!2013-04-29!   [ n | l | r | Vws Vws ] _ %^TS:0 ; !  pittää: piti 
!2013-04-29!

"rd:rr weakening"
!! __@RULENAME@__  
d:r <=>
     Vws: r _ Vws: (%> i) %^WEAK:0 [ (%^E1:) %> | # ] ;
   Vws r %> _ GRC ; !!= @CODE@ purra+Ind+Prs+ScPl3: purrah

"ld:ll weakening"
!! __@RULENAME@__  
d:l <=>
  Vws: l _ Vws: (%> i) %^WEAK:0 [ (%^E1:) %> | # ] ;
  Vws l %> _ GRC ; !!= @CODE@ tulla+Ind+Prs+ScPl3: tullah

"nd:nn weakening"
!! __@RULENAME@__  
d:n <=>
     Vws: n _ Vws: (%> i) %^WEAK:0 [ (%^E1:) %> | # ] ;
  Vws ( %^WEAK:0 %>: ) n %> _ GRC ; !!= @CODE@ mennä+Ind+Prs+ScPl3: mennäh

"rn:rr in prtprc"
!! __@RULENAME@__  
n:r <=> r %> _ (%{uy%}:) (%^WEAK:)  ;

"ln:ll in prtprc"
!! __@RULENAME@__  
n:l <=> l %> _ (%{uy%}:) (%^WEAK:)  ;

"sn:ss in prtprc"
!! __@RULENAME@__  
n:s <=> s %> _ (%{uy%}:) (%^WEAK:)  ;

!2013-04-29! p
!2013-04-29!
!2013-04-29!"mp:mm weakening"
!2013-04-29!p:m <=> [ :Vws ( %^WEAK:0 %>: ) | [ a: | ä: ] %^WEAK:0 %>: %^E1:0 | %{aä%}: | Vws+ :Cns+ Vws  %^SyllBr:0 e %^StretchSyll2:0 ] m _ [ GRC | %{aä%}: %^WEAK:0 %>: ( V3: ) Cns | %^TS:0 i %^WEAK:0 %>: ] ;
!2013-04-29!        m _ Vws l %^WGStem:0 ; ! ompel- to ommel-
!2013-04-29! [ ( %>: ) :Vws ( 0: ) %^WEAK:0 %>: | Vws:0 %>: i: %^WEAK:0 %>: | Vws:0 %>:  %^WEAK:0 %>: i: %>: ] ;
!2013-04-29!%> %^WCns %^E1: mp
!2013-04-29!
!2013-04-29!
!2013-04-29!"nk:ng weakening"
!2013-04-29!k:g <=> [ #: ] Cns* Vws n _ [ GRC | %^TS:0 i %^WEAK:0 %>: ] ;

 
"kToZero"
!! __@RULENAME@__  aika: ajan
k:0 <=> _ s %^KS2S:0 ;

!2013-04-29! [  #:  Cns* Vws (( r | i | VwsNonHigh ) k | t | h | l | r | s ) | Vws :Cns+ :Vws+ k ] _ [ GRC | %^TS:0 i %^WEAK:0 %>: ] ;
!2013-04-29![ [ #: ] Cns* Vws (( r | i ) k | t | h | l | r | s ) | Vws :Cns+ :Vws+ k ] _ [ Vws: | :Vws | ( Vws ) %>: i ] ( %>: i: | %^StretchSyll2:0 ) %^WEAK:0 %>: ;
!2013-04-29!        [ #: ] Cns* VwsBack i: _ [ GRC | %^TS:0 i %^WEAK:0 %>: ] ;

!2013-04-29! liika:liijan, reikä:reijän
!2013-04-29!"kToj"
!2013-04-29! k:j <=> [ #: ] Cns* [ VwsFront | VwsNeutral ] i: _ [ GRC | %^TS:0 i %^WEAK:0 %>: ] ;
!2013-04-29!
!2013-04-29! mato : maot, ! mato : maool
!2013-04-29! tToZero in weak
!2013-04-29!"Double stop:weakens tToZero"
!2013-04-29!t:0 <=> [ #: Cns* Vws | #: Cns* Vws [ Vws - [ i | u | y ] ] | Vws h [ y | u ] ] _  GRC ;
!2013-04-29!        [ #: Cns*  Vws+ [ h | (r) t ] | Vws ( %^WEAK:0 %>: ) t | Cns* Vws+ Cns+ Vws+ ( n ) t | Vws t ] _  [ GRC | %^TS:0 i %^WEAK:0 %>: ] ;
!2013-04-29!        t _ Vws l %^WGStem:0 ; ! ottel- to otel-

! luba : luvan
"b:v"
!! __@RULENAME@__  
b:v <=> [ #: ] Cns* Vws ( r | l | Vws ) _ [ GRC | %^TS:0 i %^WEAK:0 %>: ] ;

!2013-04-29!"double pp to p"
!2013-04-29!p:0 <=> [ #: Cns* Vws ( m ) p | #: Cns* Vws Vws p | Cns* Vws+ Cns+ Vws+ p ] _ [ GRC | %^TS:0 i %^WEAK:0 %>: ] ;
"p:0 in lapsi"
!! __@RULENAME@__  
p:0 <=> Vws _ s  %^KS2S:0  ;
 Vws: _ p Vws: (%> i) %^WEAK:0  ;

!2013-04-29! Vowel raising
!2013-04-29!"o:u ö:y in Inf"
!2013-04-29! Vx:Vy <=> _ Vx: %>: v v %{aä%}: %^RVws:0 ;
!2013-04-29!           Vx: _ %>: v v %{aä%}: %^RVws:0 ;
!2013-04-29!          where Vx in (o ö)
!2013-04-29!                Vy in (u y) matched ;

!! !!Consonant loss
"d:0"
!! __@RULENAME@__   pidiä:piän
d:0 <=> Vws: _ Vws: %^WEAK:0  ;


!"Disallow V3 after two consecutive realized vowels"
! V3: /<= :Vws :0* :Vws :0* _ ;
