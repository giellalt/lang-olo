!! !!!The Livvi (Olonets) Karelian morphophonological/twolc rules file 



Alphabet
a b c č d e f g h i j k l m n o p r s š ş t u v w y z ž ƶ ü ä ö
A B C Č D E F G H I J K L M N O P R S Š Ş T U V W Y Z Ž Ƶ Ü Ä Ö
y
Y
! MODIFIER LETTER APOSTROPHE 02BC
ʼ

! Vowel harmony with "(t)a/ä"
!
%{aäPar%}:ä	!!= @CODE@  This is for the partitive and other vowel harmony not including diphthong phenomena
!%{aä%}:0


 %{aäDiph%}:ä !!= @CODE@  This is for the infinitive marker after i and u stems

 %{ui%}:i !!= @CODE@  in imperative before %{aä%}:

! Vowel harmony with "loi/löi" also kalatOÖ1in
!%{oö%}:o
%{oö%}:ö

%{oöØ%}:0 !!= @CODE@  Used in present participle

! Vowel harmony with "ttu/tty"
!%{uy%}:u
%{uy%}:y

 %{ijPRC%}:j !!= @CODE@ participle


! in verb type V_AVATA
!A2:a
A2:ä
!A2:o
!A2:ö

! this appears in the illative
V1:a
V1:e
V1:i
V1:o
V1:u
V1:y
V1:ä
V1:ö

! These appear with the inessive and adessive
V2:a
V2:e
V2:i
V2:o
V2:u
V2:y
V2:ä
V2:ö

! These reduplicate the preceding vowel if it in turn is preceded by a consonant
V3:a
V3:e
V3:i
V3:o
V3:u
V3:y
V3:ä
V3:ö
V3:0

%^DEVOICE:0   !!= @CODE@ 
%^VOICE:0   !!= @CODE@  pestä : pezen

! syllable boundary for V_AVATA
%^SyllBound:0

! Used with ks:s, ps:s
%^KS2S:0

! no change
%^NONE:0 ! This will break vowel change, e.g. sa%^NONEamelaine

! this k is not effected by gradation
K1:k
%^NoGrad:0 ! This will be placed after a stem to break Gradation

! this weakens the stem _ompel_ to _ommel_
%^WGStem:0


%^D2Z:0	 !!= @CODE@ ! The ti => zi

! the a => o
%^A2O:0

! the a => u, ä => y
%^A2U:0

%^ILoss:0  !!= @CODE@  the i => 0 __reboi reboloi__

! Vowel raising
%^RVow:0   !!= _@CODE@_ 
%^VowRM:0   !!= _@CODE@_ vowel removal, e.g. with superlative

%- !Hyphen in  constructions 
%>
#   ! Word boundary for both lexicalised and dynamic compounds


%^LVow:0
%^LCns:0
%^WCns:0
%^AtoO:0
%^ÄtoÖ:0
%^OddSyll:0
%^StretchSyll2:0
%^SyllBr:0
%^E1:0



;


Sets

VowBack = a o u A O U ;
VowFront = ä ö y ü Ä Ö Y Ü ;
VowNeutral = e i E I ; 
VowNonHigh = a o ä ö e A O Ä Ö E ;
VowNotHiLab = a o ä ö e i 
              A O Ä Ö E I ;

Vow = a o u ä ö y ü e i 
      A O U Ä Ö Y Ü E I ;

Cns = b c č d f g h j k l m n p r s ş š t v z ƶ ž 
      B C Č D F G H J K L M N P R S Ş Š T V Z Ƶ Ž ;

Letters = Vow Cns ;

Dummy = %^ILoss %^KS2S %^WGStem %^E1 %^SyllBound  %^VOICE %> %^NONE ;


Definitions

! Front Trigger
FT =  [ #: Cns* [ VowFront | VowNeutral | i: ] | :VowFront ] [ %> | VowFront: | ʼ | Cns: | %{aä%}:ä | %{oö%}:ö | %{uy%}:y ]* \[ # | VowBack: | :VowBack ]* ;
! [ #: Cns* [ VowFront | VowNeutral | i: ] | :VowFront ] [ %> | VowFront: | Cns: | %{aä%}:ä | %{oö%}:ö | %{uy%}:y ]*  ;


! Back Trigger 
BT =     #: Cns* ( [ VowNeutral | i: ] [ Dummy: | %> | VowNeutral | VowBack: | :VowBack | Cns: | :Cns | ʼ ]* )  [ VowBack: | :VowBack ] \[ # | VowFront: | :VowFront ]* ;
!    #: Cns* ( [ VowNeutral | i: ] [ Dummy: | %> | VowBack: | :VowBack | Cns: | :Cns ]* )  [ VowBack: | :VowBack ] [ Dummy: | %> | VowNeutral | i: | %{aä%}:0 | VowBack: | :VowBack | Cns: | :Cns ]* ;

!Front Trigger with consonant
FTCons = FT [ Cns: (ʼ:) ] ;

!Back Trigger with consonant
BTCons = BT [ Cns: (ʼ:) ] ;

! Cns or Cns with palatal
ConsPalOrNot = [Cns: (ʼ:)]+ ;

! Gradation strong to weak
!GS2W = [ p:v | m p:m | t:0 | l t:l | r t:r | n t:n | s t:s | k:0 | l k:0 | r k:0 | n k:g | s k:0 ] ;

! Short vowel
SV = [ [ #: | Cns+ ] :Vow ] ;

 ! If a vowel is preceeded by this, is can be long
!  in both the inessive and the adessive
! Inessive lengthening of vowel
IL = [ #: Cns* Vow : Cns:+ | Cns* Vow+ Cns:+  Vow:+ Cns:+  ] ;

!! Right context for gradation
GRC =  [ [ ( %> ) [ %{aäDiph%}: | %{aäPar%}: ] | %{oö%}: | %{uy%}: | Vow: | :Vow ] ( %>  i )  |  %>  i ] %^WGStem:0  ;

! oma:ommaa, 
! Subsequent Long Vowel
! preceeded by "Cns _"
! 2012-10-11 SLV =  [ :Vow [ ( %> %^LCns:0 %^LVow:0 | %> %^LCns:0 | %> i %> | %> ) [ V1:Vow | %{aä%}: ] |  0:Vow %> ( %^LCns:0 | %^WGStem: ) %^LVow: | %> %^LCns:0  ] ] ;

!! !!!Rules
Rules

"%{aäPar%}:o"
!! __@RULENAME@__
%{aäPar%}:o <=>   o: %> _ [# | %> ] ;
! o: %> _ ( %^WGStem:0 ) [ # | %>  [ n | t | %{uy%}: | m m %{oö%}: | t t %{oö%}: ]];


"%{aäPar%}:ö"
!! __@RULENAME@__
%{aäPar%}:ö <=>   ö:  %> _ [# | %> ] ;
! ö: %> _ ( %^WGStem:0 ) [ # | %>  [ n | t | %{uy%}: | m m %{oö%}: | t t %{oö%}: ]];


"%{aäDiph%}:o"
!! __@RULENAME@__
%{aäDiph%}:o <=> o: %> _ ( %^WGStem:0 ) [ # | %>  [ n | t | %{uy%}: | m m %{oö%}: | t t %{oö%}: ]];
         [ o: | u] %> _ [ # | %> ] ;

"%{aäDiph%}:ö"
!! __@RULENAME@__
%{aäDiph%}:ö <=> ö: %> _ ( %^WGStem:0 ) [ # | %>  [ n | t | %{uy%}: | m m %{oö%}: | t t %{oö%}: ]];
         [ ö: | y] %> _ [ # | %> ] ;

"%{aäDiph%}:e"
!! __@RULENAME@__
 %{aäDiph%}:e <=> [ e: | i ] %> _ [ # | %> ]  ;

"%{aäDiph%}:ä"
!! __@RULENAME@__
 %{aäDiph%}:ä <=> [ FTCons %> | FT %> [ d: | v v | j | h | t ]] _ [ ( %^WGStem:0 ) [ h | # | %> ] | i | %> n %{uy%}: | %> g i | # ] ;
   [ FTCons %> | FT ( %^WGStem:0 ) %> [ t (t) | s s ]] _ [  h | %> g | # ] ;
               Cns:  ä:  %> _ ;
   [ FTCons %> | [ FT ] ( %^WGStem:0 (%^E1:) ) %> [ (k) k (%{ui%}:i) |g (%{ui%}:)| m | d: | (t) t | s s ]] _ [  h | z |  s | ( n ) n | t t | m m | v |l | %> %{ijPRC%}: | %> g | # ] ;
!€# lʼykkä%^WGStem%>kk%{ui%}%{aäDiph%}mm%{oö%}
!€0 lʼyk0ä00kkiämmö
!€# lʼykkä%>%{aäDiph%}%>m%{aäDiph%}h
!€0 lʼykki0ä0mäh
!€# lʼykkä%>%{aäDiph%}%>%{ijPRC%}%{aäDiph%}%^A2U
!€0 lʼykki0ä0jy0

! Vowel harmony suffixes Back
"%{aäDiph%}:a"
!! __@RULENAME@__
 %{aäDiph%}:a <=> [ BTCons %> | BT %> [ d: | v v | h | j | t ]] _ [ ( %^WGStem:0 ) [ h | # | %> ] | i | %> n %{uy%}: | %> g i | # ] ;
   [ BTCons %> | BT ( %^WGStem:0 )  %> [ t (t) | s s ]] _ [  h | %> g | # ] ;
                 Cns:  a:  %> _ ;
   [ BTCons %> | [ BT ] ( %^WGStem:0 (%^E1:) ) %> [ (k) k (%{ui%}:u) |g (%{ui%}:) | m | d: | (t) t | s s ]] _ [  h | z | s | ( n ) n | t t | m m | v | l | %> %{ijPRC%}: | %> g | # ] ;
!€# kanda%>kk%{ui%}%{aäDiph%}mm%{oö%}
!€0 kanda0kkuammo
!€# pagis%>d%{aäDiph%}%^WGStemh
!€0 pa0is0ta0h

"%{aäDiph%}:o"
!! __@RULENAME@__
%{aäDiph%}:o <=> [ o: | u ] %> _ ( %^WGStem:0 ) [ # | %>  [ n | t | %{uy%}: | m m %{oö%}: | t t %{oö%}: ]];
         [ o: ] %> _ [# | %> ] ;

"%{aäDiph%}:ö"
!! __@RULENAME@__
%{aäDiph%}:ö <=> [ ö: | y ] %> _ ( %^WGStem:0 ) [ # | %>  [ n | t | %{uy%}: | m m %{oö%}: | t t %{oö%}: ]];
         Cns: [ ö: ] %> _ [# | %> ] ;

! Vowel harmony suffixes Back
"%{aäPar%}:a"
!! __@RULENAME@__
 %{aäPar%}:a <=>
  [ BTCons ( %> ) | BT %>  %{ijPRC%}:j %> | [ %{ui%}:u | %{uy%}:u | %{aäPar%}:u | a:u ] ( %> ) ] _ [ # | ( ( %^WGStem:0 ) %> )  [ Cns: | %{uy%}:u ] | %^WGStem:0 ] ;
   BT %>  %{ijPRC%}:j %> %{ui%}:u _ ;
! [ BTCons %> |  a:  %> | BT %> [ d: | j | t ]] _ [ ( %^WGStem:0 ) h | %^WGStem:0 [ %> | # ] | s | i | (n) n | %> [(n) %{uy%}: | g i | h ] | # ] ;
!   [ BTCons %> | [ BT ] ( %^WGStem:0 (%^E1:) ) %> [ (k) k (%{ui%}:u) | (t) t | s s ]] _ [  h | s | ( n ) n | t t | m m | v | %> g | # ] ;
  BT %> d: _  ;
!€# pagis%>d%{aäPar%}%^WGStemh
!€0 pa0is0ta0h

! Vowel harmony suffixes Front
"%{aäPar%}:ä"
!! __@RULENAME@__
 %{aäPar%}:ä <=>
  [ FTCons ( %> ) | FT %>  %{ijPRC%}:j %> | [ %{ui%}:i | %{uy%}:y | %{aäPar%}:i | ä:i ] ( %> ) ] _ [ # | ( ( %^WGStem:0 ) %> )  [ Cns: | %{uy%}:y ] | %^WGStem:0 ] ;



"%{aäPar%}:e"
!! __@RULENAME@__
 %{aäPar%}:e <=>  e:  %> _ [ # | (%^WGStem:) %> ] ;
![ e:i | i  ] %> _ [ # | %> %{uy%}: ] ;
!€# hävi%>%{aäPar%}%>%{uy%}
!€0 hävi0e0y
!€# hävi%>%{aäPar%}%>n
!€0 hävi0e0n

"%{aäPar%}:i"
!! __@RULENAME@__
%{aäPar%}:i <=> FT Cns: _ %> %{aäPar%}:ä ;
     Cns: i %> _ ;

"%{aäPar%}:u"
!! __@RULENAME@__
%{aäPar%}:u <=>
 [ u: | %{uy%}:u ] %> _ ( %^WGStem:0 ) [ # | %>  [ g i | n | t | %{uy%}: | m m %{oö%}: | t t %{oö%}: ]];
!€# luadi%^WGStem%^E1%>tt%{uy%}%>%{aäPar%}
!€0 luaji000ttu0u

         [ u ] %> _  ;
 	 BT [ Cns: | %{ijPRC%}: ] _ [ %^A2U:0 [ # | %> g i ] | %> %{aäPar%}:a ] ;

"%{aäPar%}:y"
!! __@RULENAME@__
%{aäPar%}:y <=> FT [ Cns: | %{ijPRC%}: ] _ %^A2U:0 [ # | %> g i ] ;
      y %> _ ;

"%{aäPar%}:0"
!! __@RULENAME@__
%{aäPar%}:0 <=> Cns:  _ %> i  ;
!            [ a | ä | i ] i _ %^A2U: ; 

!"%{aäPar%}:0 right"
!!! __@RULENAME@__
!%{aäPar%}:0 <=> _ %> i  ;

"e:0"
!! __@RULENAME@__
 e:0 <=> Cns: (%^VOICE:)  _ %> i  ;
         [ u | a | o | ä | ö | y ] %> %{ijPRC%}:  [%{oöØ%}:o|%{oöØ%}:ö] %> i %> _ n ;
!         [ u | a | o | ä | ö | y ] %> %{ijPRC%}:  %{oöØ%}: %> i %> _ n ;
 
"%{ui%}:u"
!! __@RULENAME@__
!! {{{
%{ui%}:u <=> BT [Cns: | %{ijPRC%}: ] ( %> ) _ ( %> ) [ %{aäPar%}: | %{aäDiph%}: ] ; !! * @CODE@ ! imperative forms
!€# kanda%>kk%{ui%}%{aäDiph%}mm%{oö%}
!€0 kanda0kkuammo
!! }}}

"%{ui%}:i"
!! __@RULENAME@__
!! {{{
%{ui%}:i <=> FT [ Cns: | %{ijPRC%}: ] ( %> ) _ ( %> ) [ %{aäPar%}: | %{aäDiph%}: ] ; !! * @CODE@ ! imperative forms
!€# lʼykkä%^WGStem%>kk%{ui%}%{aäDiph%}mm%{oö%}
!€0 lʼyk0ä00kkiämmö
!! }}}

"A2:a"
!! __@RULENAME@__
A2:a <=> [ a: ] %> _ ( %^WGStem:0 ) [ # | %>  [ n | t | %{uy%}: | m m %{oö%}: | t t %{oö%}: ]];

"A2:u"
!! __@RULENAME@__
A2:u <=>  u: %> _ ( %^WGStem:0 ) [ # | %>  ];

"A2:y"
!! __@RULENAME@__
A2:y <=> [ y: ] %> _ ( %^WGStem:0 ) [ # | %>  ];




"a:u before subsequent a"
!! __@RULENAME@__  Diphthong a+a => ua
a:u <=> ConsPalOrNot _ [ ( d:0 ) a  | %> [ %{aäPar%}: | %{aäDiph%}: | A2:a ] ]; 
        _ %^A2U:0 ;


"ä:i before subsequent A2"
!! __@RULENAME@__  Diphthong ä+ä => iä
ä:i <=>  Cns:  _ [ ä | %> [ %{aäPar%}:ä | %{aäDiph%}:ä| A2:ä ] ]; 

 
"o:u before subsequent o"
!! __@RULENAME@__ Diphthong o+o => uo erota:eruou
o:u <=> Cns _ %> [ %{aäPar%}: | %{aäDiph%}: ] ; 
!€# ero%>%{aäDiph%}%>n
!€0 eru0o0n
!€# runo%>%{aäPar%}
!€0 runu0o
                  _ %> v v ;
!€# jo%>vv%{aäDiph%}
!€0 ju0vva


"ö:y before subsequent ö"
!! __@RULENAME@__ Diphthong ö+ö, ö+%{aäPar%}:ö => yö
ö:y <=> Cns _ %> [ %{aäDiph%}:ö | %{aäPar%}:ö ] ; 
!€# sö%>%{aäDiph%}%>%{uy%}
!€0 sy0ö0y
!€# pölllö%>%{aäPar%}
!€0 pöllly0ö
          _ %> v v ;
!€# sö%>vv%{aäDiph%}
!€0 sy0vvä

"e:i before subsequent %{aäPar%}:e"
!! __@RULENAME@__ Diphthong o+o => uo erota:eruou
!! {{{
 e:i <=> Cns _ %> [ %{aäPar%}:e | %{aäDiph%}:e | e ] ;
!€# valge%>e%^WGStem%>t
!€0 valgi0e00t
!€# hävi%>%{aäPar%}%>n
!€0 hävi0e0n
        i _ d: o %^WGStem: ;
!€# tiedo%^WGStem%>n
!€# tiijo00n

         ä [ g: | d: ] _ %^WGStem:0 %> ;
!€# väge%^WGStem%>n
!€0 vä0i00n
         _ %> %{ijPRC%}: ;
!€# luge>%{ijPRC%}
!€0 lugi0j

!! }}}


"e:o"
!! __@RULENAME@__ Vowel harmony suffixes Back
!! {{{
 e:o <=>  BT  _ %> %{uy%}: ;
!€# luge>%{uy%}
!€0 lugo0u
!! }}}

"e:ö"
!! __@RULENAME@__ Vowel harmony 
!! {{{
 e:ö <=>  FT  _ %> %{uy%}: ;
!€# käske>%{uy%}
!€0 käskö0y
!! }}}

! Word final u, y
"ä:y word final"
!! __@RULENAME@__
ä:y <=> _ %^A2U:0 ;

! Word final ö
"ä:ö word final"
!! __@RULENAME@__
ä:ö <=> _ %^A2O:0 ;


"a:0 in suaja:sai"
!! __@RULENAME@__
!! {{{
a:0 <=>
 a _ ( %^VowRM: ) %> i ; !!= * @CODE@ Vowel shortening before subsequent "i"
[ # Cns:*  [[ u | o ] (i)| u o: ] [ Cns:+ |:Cns+] | # Cns:*  Vow:+ [ Cns:+ |:Cns+] [Vow:+ [ Cns:+ |:Cns+]]+ ] _ %> i: (%^WGStem:) [ # |  %> [i|Cns] | %> e [n|l]] ;
!€# koira%>i%>en
!€0 koir00i0en
!€$ koira0j0en
 _ %^VowRM: %> i ;
!€# vahna%^VowRM%>in
!€0 vahn000in

!! }}}

!! !!iToj between vowels
"i:j"
!! __@RULENAME@__
 i:j <=> :Vow ( %>  %^LCns:0 ) %> _ %> :Vow ;

"%{ijPRC%}:i"
!! __@RULENAME@__ 
!! {{{
!! * @CODE@ +ActPrsPrc
 %{ijPRC%}:i <=> :Vow %> _ [ # | %> [ g i | b o | h %{aäPar%}: i | g o ] ] ;
!! }}}



"a:o in the plural and preterite"
!! __@RULENAME@__
a:o <=> # Cns* [[ a | i | e ] (u|i)| u a ] [ Cns* (%^NONE:) Cns: ] _ %> i: ( %^WGStem:0 )  ;
       _ %^A2O: ;

"a:e"
!! __@RULENAME@__  pidiä
 a:e <=> _ (%^WGStem:0) %^E1:0 ;
!€# kanda%^WGStem%^E1%>tt%{aäDiph%}s
!€0 kanne000ttas
!€# kirjava%^E1%>m%^NONEb
!€0 kirjave00m0b

"ä:e piettih and in comparatives"
!! __@RULENAME@__  pidiä
ä:e <=> # Cns* [ i | e ] Cns: _ %^WGStem:0 %^E1:0 ;
      _ %^WGStem:0 %^E1:0 ;
!€# mečästä%^WGStem%^E1%>tt%{aäDiph%}s
!€0 mečäste000ttäs


"ä:0 pidiä:pidi"
!! __@RULENAME@__
ä:0 <=> Cns: _ (%^VowRM:|%^WGStem:) %> i:  ;

"i:0 reboi:reboloi"
!! __@RULENAME@__
i:0 <=> o _ %^ILoss: %> l %{oö%}: i ;
      [ e: | i ] %> %{ijPRC%}:  %{oöØ%}: %> i %> _ [ # | %> Cns ]  ;



"%{oö%}:o"
!! __@RULENAME@__ Vowel harmony suffixes Back
 %{oö%}:o <=>  BT (  %> ) _ ;

"%{oöØ%}:0"
!! __@RULENAME@__ Vowel harmony suffixes Back
 %{oöØ%}:0 <=> [ u | a | o | ä | ö | y ] %> %{ijPRC%}:  _  ;

"%{oöØ%}:o"
!! __@RULENAME@__ Vowel harmony suffixes Back
 %{oöØ%}:o <=>  BT  :Cns [ e: | i ] %> %{ijPRC%}: _  ;
!€# luge>%{ijPRC%}>%{oöØ%}>i>n
!€0 lugi0j0o0i0s

"%{oöØ%}:ö"
!! __@RULENAME@__ Vowel harmony suffixes Front
 %{oöØ%}:ö <=> FT :Cns [ e: | i ] %> %{ijPRC%}:  _  ;



"%{uy%}:u"
!! __@RULENAME@__ Vowel harmony suffixes Back
 %{uy%}:u <=>  [ BT | :VowBack Cns:* ] ( %> ) _ ;


"%{uy%}:y"
!! __@RULENAME@__ Vowel harmony suffixes Front
 %{uy%}:y <=>  [ FT | :VowFront Cns:* ] ( %> ) _ ;

!! !!Consonant change

!
"g:j"
!! __@RULENAME@__
!! {{{
g:j <=> i _ [ a: ] ( %> i ) %^WGStem:0 ;
!€# poiga%^WGStem%>n
!€0 poija00n
!! }}}

"g:v"
!! __@RULENAME@__
!! {{{
g:v <=> [ o | u ] _ e %^WGStem:0 ;
!€# joge%^WGStem%>n
!€0 jove00n
!! }}}


"g:l"
!! __@RULENAME@__  
g:l <=> l _ [[ a: ] ((%^A2O:) %> i )| i | e | u ] %^WGStem:0 ;
!€# sulga%^WGStem%>n
!€0 sulla00n
!€# jalga%^A2O%>i%^WGStem%>s
!€0 jallo00i00s

"g:r"
!! __@RULENAME@__  
!! {{{
g:r <=> r _ [[ a: ] ( %> i )| i | e ] %^WGStem:0 ;
!€# särge%^WGStem%>n
!€0 särre00n
!€# kergi%^WGStem%>t%{aäPar%}
!€0 kerri00tä
!! }}}

! lʼöydiä:lʼövvä
"d:v"
!! __@RULENAME@__  
!! {{{
d:v <=> [ ö y: | o u: ] _ [ ä: | a ] %^WGStem:0 ; !!= @CODE@
          u a           _   o       %^WGStem:0 ; !!= @CODE@
          a             _   u       %^WGStem:0 ; !!= @CODE@
        [ u o: | u: ]   _  [ e | a: ]  (%> i )    %^WGStem:0 ; !!= @CODE@ 
        !€# vuode%^WGStem%>n
        !€0 vuvve00n
        [ ä y: ]   _  [ e | y ]  (%> i )    %^WGStem:0 ; !!= @CODE@ 
!€# täydy%^WGStem
!€0 tävvy0

!! }}}

! luadie:luajitah
"d:j"
!! __@RULENAME@__  
d:j <=> [ u a ] _ [ i ] %^WGStem:0 ;
        [ i e: | a i ] _ o %^WGStem:0 ;
!€# maido%^WGStem%>n
!€0 maijo00n
!€# tiedo%^WGStem%>n
!€# tiijo00n


! lʼöydiä:lʼövvä
"y:v"
!! __@RULENAME@__
y:v <=>  [ ö | ä ] _ d: [ ä: | e | y ] %^WGStem:0 ;
!€# täydy%^WGStem
!€0 tävvy0

! soudua:sovva
"u:v"
!! __@RULENAME@__
u:v <=> [ o | u ] _ d: [ a | e | u ] %^WGStem:0 ;


"o:v Lengthening with Ut:vv weakening"
!! __@RULENAME@__  
!! {{{
o:v <=> u _ d: [ e | a: ] (%> i ) %^WGStem:0 ;
!! }}}

! vuozi:vuod
"d:z"
!! __@RULENAME@__  
d:z <=> [Vow | r | n ] _ %^D2Z:0 %> i ;

! vuozi:vuod+d%{uy%}, armas:armas+d%{uy%}
"d:t in partitive"
!! __@RULENAME@__  
d:t <=> [ d: | h | :t | s ] ( %^KS2S:0 ) %> _ ;
         [Vow | r | n ] _ %> d: %{uy%}: ;
!€# pagis%>d%{aäDiph%}%^WGStemh
!€0 pa0is0ta0h

"s:z"
!! __@RULENAME@__
!! {{{
s:z <=> _ %^VOICE:0 ; !!= * @CODE@ pestä: pezen
!! }}}

"k:g"
!! __@RULENAME@__ pestä: peskäh
k:g <=> [ n | l |r ] %> _ (%{ui%}:) %{aäPar%}: ; 


"rd:rr weakening"
!! __@RULENAME@__  
!! {{{
d:r <=>
     Vow: r _ Vow: ((%^A2O:) %> i) %^WGStem:0 [ (%^E1:) %> | # ] ;
   Vow r %> _ GRC ; !!= @CODE@ purra+Ind+Prs+ScPl3: purrah
!! }}}

"ld:ll weakening"
!! __@RULENAME@__  
!! {{{
d:l <=>
  Vow: l _ Vow: ((%^A2O:) %> i) %^WGStem:0 [ (%^E1:) %> | # ] ;
  Vow l %> _ GRC ; !!= @CODE@ tulla+Ind+Prs+ScPl3: tullah
!€# tul%>d%{aäDiph%}%^WGStem
!€0 tul0la0
!! }}}

"nd:nn weakening"
!! __@RULENAME@__  
!! {{{
d:n <=>
     Vow: n _ Vow: ((%^A2O:) %> i) %^WGStem:0 [ (%^E1:) %> | # ] ;
  Vow ( %^WGStem:0 %> ) n %> _ GRC ; !!= @CODE@ mennä+Ind+Prs+ScPl3: mennäh
!€# kanda%^WGStem%^E1%>tt%{aäDiph%}s
!€0 kanne000ttas
!! }}}

"rn:rr in prtprc"
!! __@RULENAME@__  
n:r <=> r %> _ (%{uy%}:) (%^WGStem:)  ;

"ln:ll in prtprc"
!! __@RULENAME@__  
n:l <=> l %> _ (%{uy%}:) (%^WGStem:)  ;

"sn:ss in prtprc"
!! __@RULENAME@__  
n:s <=> s %> _ (%{uy%}:) (%^WGStem:)  ;


!! !CONSONANT LOSS
 
"čToZero"
!! __@RULENAME@__  
!! {{{
č:0 <=> _ č [ (%>) Vow | Vow: %> i ] %^WGStem:0 ; !! * @CODE@ suvaija:suvaičen
!! }}}

"kToZero"
!! __@RULENAME@__  aika: ajan
!! {{{
k:0 <=> _ s %^KS2S:0 ;
        _ k [ Vow | Vow: %^A2O: %> i | e h ] %^WGStem:0 ;
!€# lʼykkä%^WGStem%>t%{AÄ%}
!€0 lʼy0kä00tä
!€# abuniekka%^A2O%>i%>l
!€0 abunie0ko00i0l
!€# liikkeh%^WGStem
!€0 lii0keh0

!! }}}

"gToZero"
!! __@RULENAME@__  aika: ajan
!! {{{
g:0 <=>
        [ Vow: - [ o | u ] ] _ i s %> d: [ %{aäPar%}: %^WGStem: | i h ] ;
        [ Vow - [ o | u ] ] _ e: %^WGStem:0 %> ;
!€# väge%^WGStem>n
!€0 vä0i00n
!€# pagis%>d%{aäDiph%}%^WGStemh
!€0 pa0is0ta0h
!! }}}


!2013-04-29! tToZero in weak
"tToZero in weak geminate"
 t:0 <=>
 Vow: _ t [ Vow: (%> i)| e h ] %^WGStem:0  ;
! [ # Cns* Vow | # Cns* Vow [ Vow - [ i | u | y ] ] | Vow h [ y | u ] ] _  GRC ;
!2013-04-29!        [ #: Cns*  Vow+ [ h | (r) t ] | Vow ( %^WGStem:0 %> ) t | Cns* Vow+ Cns+ Vow+ ( n ) t | Vow t ] _  [ GRC | %^TS:0 i %^WGStem:0 %> ] ;
!2013-04-29!        t _ Vow l %^WGStem:0 ; ! ottel- to otel-
!€# tavoitteh%^WGStem
!€0 tavoi0teh0

! luba : luvan
"b:v"
!! __@RULENAME@__  
b:v <=> [ #: ] Cns* Vow ( r | l | Vow ) _  [ GRC | %^TS:0 i %^WGStem:0 %> ] ;

"b:v"
!! __@RULENAME@__
!! {{{
b:m <=> m _  [ a | i ] %^WGStem:0 ;
!! }}}

!2013-04-29!"double pp to p"
!2013-04-29!p:0 <=> [ #: Cns* Vow ( m ) p | #: Cns* Vow Vow p | Cns* Vow+ Cns+ Vow+ p ] _ [ GRC | %^TS:0 i %^WGStem:0 %> ] ;
"p:0 in lapsi"
!! __@RULENAME@__  
p:0 <=> Vow _ s  %^KS2S:0  ;
 Vow: _ p Vow: (%> i) %^WGStem:0  ;

!2013-04-29! Vowel raising
!2013-04-29!"o:u ö:y in Inf"
!2013-04-29! Vx:Vy <=> _ Vx: %> v v %{aäPar%}: %^RVow:0 ;
!2013-04-29!           Vx: _ %> v v %{aäPar%}: %^RVow:0 ;
!2013-04-29!          where Vx in (o ö)
!2013-04-29!                Vy in (u y) matched ;

!! !!Consonant loss
"d:0"
!! __@RULENAME@__   pidiä:piän
d:0 <=>  # Cns* [ i | a: | o | ä ] _ [ ä: | a: | e: | i ] %^WGStem:0  ;
!€# pidä%^WGStem%>n
!€0 pi0ä00n

"s:0"
!! __@RULENAME@__   
 s:0 <=>  [ u | u a | e ] _ s [ ä: | a: (%^A2O: %> i) | e: | i ] %^WGStem:0  ;
!€# bussa%^WGStem%>l
!€0 bu0sa00l


!"Disallow V3 after two consecutive realized vowels"
! V3: /<= :Vow :0* :Vow :0* _ ;

"Disallow a:u before a and %> i"
a:u /<= _ a: %> i ;


