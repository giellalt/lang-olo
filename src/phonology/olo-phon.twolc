!! ================================================================ !
!! The Livvi (Olonets) Karelian morphophonological/twolc rules file !
!! ================================================================ !


Alphabet
a b č d e f g h i j k l m n o p r s š t u v z ž ü ä ö
A B Č D E F G H I J K L M N O P R S Š T U V Z Ž Ü Ä Ö
y
Y


! Vowel harmony with "(t)a/ä"
!A1:a
A1:ä
!A1:0

! Vowel harmony with "loi/löi" also kalatOÖ1in
!O1:o
O1:ö

! Vowel harmony with "ttu/tty"
!U1:u
U1:y

! this appears in the illative
V1:a
V1:e
V1:i
V1:o
V1:u
V1:y
V1:ä
V1:ö

! These appear with the inessive and adessive
V2:a
V2:e
V2:i
V2:o
V2:u
V2:y
V2:ä
V2:ö

! These reduplicate the preceding vowel if it in turn is preceded by a consonant
V3:a
V3:e
V3:i
V3:o
V3:u
V3:y
V3:ä
V3:ö
V3:0

! this k is not effected by gradation
K1:k
%^NoGrad:0 ! This will be placed after a stem to break Gradation

! this weakens the stem _ompel_ to _ommel_
%^WGStem:0

! The ti => si
%^TS:0

! Vowel raising
%^RVws:0

%- !Hyphen in  constructions 
%>
#   ! Word boundary for both lexicalised and dynamic compounds


%^LVws:0
%^LCns:0
%^WCns:0
%^AtoO:0
%^ÄtoÖ:0
%^OddSyll:0
%^StretchSyll2:0
%^SyllBr:0
%^E1:0


%^WEAK:0

;


Sets

VwsBack = a o u ;
VwsFront = ä ö y ü ;
VwsNeutral = e i ь ; 
VwsNonHigh = a o ä ö e ;
Vws = a o u ä ö y ü e i ь ;

Cns = b d f g h j k l m n p r s ş š t v z ƶ ž ;

Letters = Vws Cns ;



Definitions

! Front Trigger
FT =  [ #: Cns* [ VwsFront | VwsNeutral | i: ] | :VwsFront ] [ %> | VwsFront: | Cns | A1: | O1: | U1: ]*  ;


! Back Trigger 
BT =    #: Cns* ( [ VwsNeutral | i: ] \[ #: | VwsFront: | :VwsFront ]* )  [ VwsBack: | :VwsBack ]  \[ #: | VwsFront: | :VwsFront ]* ;

! Gradation strong to weak
!GS2W = [ p:v | m p:m | t:0 | l t:l | r t:r | n t:n | s t:s | k:0 | l k:0 | r k:0 | n k:g | s k:0 ] ;

! Short vowel
SV = [ [ #: | Cns+ ] :Vws ] ;

 ! If a vowel is preceeded by this, is can be long
!  in both the inessive and the adessive
! Inessive lengthening of vowel
IL = [ #: Cns* Vws : Cns:+ | Cns* Vws+ Cns:+  Vws:+ Cns:+  ] ;

!! Right context for gradation
GRC =  [ [ ( %>: ) A1: | O1: | U1: | Vws: | :Vws ]  ( %>: i ) |  %>:  i ] %^WEAK:0  ;

! oma:ommaa, 
! Subsequent Long Vowel
! preceeded by "Cns _"
! 2012-10-11 SLV =  [ :Vws [ ( %>: %^LCns:0 %^LVws:0 | %>: %^LCns:0 | %>: i %>: | %>: ) [ V1:Vws | A1: ] |  0:Vws %>: ( %^LCns:0 | %^WEAK: ) %^LVws: | %>: %^LCns:0  ] ] ;

Rules


! Stem vowel loss before plural "i"
"StemVowLoss"
 X:Y <=> X _  %>: i  %>: ;
!( %>:  %^WEAK:0 |  %>:  %^LCns:0 ) %>: [ i | i:j ] ;
          where X in (e o ö)
                Y in (0 0 0) matched ;

! A loss
"StemALoss"
 X:Y <=>  _   ( %>: %^LCns:0 ) %>: i: %>: ;
          where X in (a ä)
                Y in (0 0) matched ;

!€ osa%>^WCns%>i%>st
!€ os0000i0st

!%>%^LCnsi%>V1
! AÄ1 loss
"StemAÄLoss"
 A1:0 <=> [ A1: | Cns: | :Cns ] _ ( %>: %^LCns:0 ) %>: i %>: ;


! U loss [u:0 | y:0]
"StemULoss"
 X:Y <=> X _ %>:  i  %>: ;
! ( %>:  %^WEAK:0 |  %>:  %^LCns:0 ) %>: i %>: ;
         [ e | ä | a | o | ö ] _  [ t: | k: ] 0:v ;
          where X in (u y ü)
                Y in (0 0 0) matched ;
!Vws:0 %>:  %^WEAK:0 %>: i: %>: 

!€ maa%>i%>n
!€ ma00i0n

! Stem vowel "i" loss before plural "i"
"i:Zero"
 i:0 <=> i _ %>: i  %>: ;

! iToj between vowels
"i:j"
 i:j <=> :Vws ( %>:  %^LCns:0 ) %>: _ %>: :Vws ;
         Vws _ Cns:0 Vws: ;
!veitte%>%^WCnsttUY1%>%^LCns%>i%>AÄ1
!€ em0o0i%>%^LCnsAÄ1
!€ emmooj00a

!"a:o in the plural and preterite"
!a:o <=> _ ( %^WEAK:0 ) %>: i  ;

! a to e in comparatives
"a:e"
 a:e <=> _ %^WEAK:0 %^E1:0 ;

! ä to e in comparatives
"ä:e"
 ä:e <=> _ %^WEAK:0 %^E1:0 ;


!!
!! Vowel harmony suffixes Front
!"A1:ä"
! A1:ä <=> FT _  ( %>:  %^WEAK:0 ( %^LVws:0 ) |  %^LVws:0  |  %>:  %^LCns:0 ( %^LVws:0 ) ) [ ( %>: ) [ Cns | %^WEAK:0 %>: [ Cns ] | [[ Vws: ] - [ i | i:j ]]] | #: | A1: | %^StretchSyll2:0 | i | %>: ( %^WEAK:0 | %^LCns:0 ) [ V1: | V3: ] ]  ;

! mato : mattooa
! Vowel harmony suffixes Back
!2013-02-27!"A1:a"
!2013-02-27! A1:a <=> BT _ \[  ( %>: %^LCns:0 ) %>: i %>: ] ;
! BT _   ( %>:  %^WEAK:0 ( %^LVws:0 ) |  %^LVws:0  |  %>:  %^LCns:0 ( %^LVws:0 ) ) [ ( %>: ) [ Cns | %^WEAK:0 %>: [ Cns ] | [[ Vws: ] - [ i | i:j ]]] | #: | A1: | %^StretchSyll2:0 | i | %>: ( %^WEAK:0 | %^LCns:0 ) [ V1: | V3: ] ]  ;

!!
!! Vowel harmony suffixes Front
!"O1:ö"
! O1:ö <=>  FT _ ;

! Vowel harmony suffixes Back
"O1:o"
 O1:o <=>  BT _ ;

!!
!! Vowel harmony suffixes Front
!"U1:y"
! U1:y <=>  FT _ ;

! Vowel harmony suffixes Back
"U1:u"
 U1:u <=>  BT _ ;

! Vws %>: %^LCns: V1:
! Vowel agreement in illative 
"V1:aeouüäö"
 V1:Vx <=> :Vx %>: [ h | ( %^LCns: %>: ) ] _ ;
        where Vx in (a o u ä ö ü y) ;

! Vowel agreement in illative e
"V1:e"
 V1:e <=> [ e | i ] %>:  h  _ ;
          e ( %>: %^LCns: ) %>:  _ ;

! Illative i with no intermittent _h_
"V1:i"
 V1:i <=> i ( %>: %^LCns: ) %>: _ ;

!  _mato:maool_ not allowed
! Used with patsas type nominals
"V2:aeiouüäö"
 V2:Vx <=>  Vx ( h ( :h ) ) %>:  ( %^LCns:0 )  _ [ V2: | ( %>: ([ %^WEAK:0 | %^LCns:0 ] ( V3: ) ) )  Cns ] ;
            Vx ( h ( :h ) ) %>:  ( %^LCns:0 | %^WEAK:0 ) V2: _ [ ( %>: )  Cns ]  ;
        where Vx in (a o u ä ö ü e i y) ;


"V2:Zero"
 V2:0 <=> Vws %>: _ %>: i: %>: ;

!!!FROM HERE

! Vowel lengthening for the inesive and adessive with V3
!! This deals with secondary or perhaps pertary vowel lengthening
! e.g. gen plural, ine, ade
"V3:a"
 V3:a <=> :Cns :0* :a :0* _ ;

"V3:e"
 V3:e <=> :Cns :0* :e :0* _ ;

"V3:i"
 V3:i <=> :Cns :0* :i :0* _ ;

"V3:o"
 V3:o <=> :Cns :0* :o :0* _ ;

"V3:u"
 V3:u <=> :Cns :0* :u :0* _ ;

"V3:ä"
 V3:ä <=> :Cns :0* :ä :0* _ ;

"V3:ö"
 V3:ö <=> :Cns :0* :ö :0* _ ;

"V3:y"
 V3:y <=> :Cns :0* :y :0* _ ;

"V3:Zero"
 V3:0 <=> :Vws :0* :Vws :0* _ ;

! ! Vowel Lengthening
! mato : mattoon,
 "Zero:Vx"
 0:Vx <=> ( Vws+ :Cns+ ) Vx _ %>: ( %^LCns: | %^WEAK:0 ) %^LVws: ;
          #: Cns* Vws [ Cns 0: | 0:i j ] Vx _ Cns ( Cns:0 ) :Vws %^StretchSyll2:0 ; ! ommeena<omena
! 
         [ #: ] Cns* Vws Cns 0: Vx _ Cns :0 Vws [ Cns | [ #: ] ] ;
       where Vx in (a o u ä ö y e) ; 


! Secondary Consonant lengthening
! mato : mattoo, näytettävä : näytettävvää
!"even-syllable" This should be secondary
"consonant lengthening"
 0:Cx <=> 
         [ #: Cns* Vws+ Cns:+ Vws+ :0* Cns+ :Vws ( %>: %^LCns:0 ) | #: Cns*  Vws | %^OddSyll:0 ] Cx _ ( [ Vws: | :Vws ] ( :Vws ) ) %>: %^LCns:0 ; !
         #: Cns* Vws Cx _ [ Vws 0: Cns ( Cns:0 ) :Vws | Vws %^SyllBr:0 e ] %^StretchSyll2:0 ; ! ommeena<omena ; venät:vennäen
           where Cx in (m p t s n r l k g h) ;
!
! oja: oija
"j lengthening"
0:i <=> [ #: ] Cns* Vws _ j Vws ( 0: ) %>: %^LCns:0  ;
          [ [ :Cns | :Cns Vws:0 ] ( %>: ) | Cns: ] i _ %>: ( %^LCns: | %^WEAK:0 ) %^LVws: ;
         [ #: ] Cns* Vws Cns 0: i _ Cns :0 Vws [ Cns | [ #: ] ] ;
         #: Cns* Vws _ j [ Vws 0: Cns ( Cns:0 ) :Vws | Vws %^SyllBr:0 e ] %^StretchSyll2:0 ; ! ommeena<omena ; venät:vennäen, kaijaava


!!!TO HERE
!!!!!!!!!!!!!!!!!!!!!!!!!
!! Consonant weakening !!
!!!!!!!!!!!!!!!!!!!!!!!!!
! t:0, k:0 (suku:suun), p:v, ht:h, hk:h, lt:ll, rt:rr
! mb:mm, nd:nn, 
! it:ij, lk:l, rk:r, nk:ng, sk:s, tk:t, 
! kk:k, tt:t, pp:p
! ut:vv, yt:vv (uut:uuvv), uk:vv
! aika:ajan, poika:pojan, reikä:reijän, liika:liijan
! lp:lv, rp:rv



"Ut:vv Uk:vv weakening"
 Cx:Cy <=> [ #: ] Cns* Vws [ u: | y: ] _ 0:v [ GRC | %^TS:0 i %^WEAK:0 %>: ] ;
              where Cx in (k t)
                    Cy in (v v) matched ;

"0:v Lengthening with Ut:vv weakening"
 0:v <=> [ #: ] Cns* Vws [ u: | y: ] [ t: | k: ]  _ [ GRC | %^TS:0 i %^WEAK:0 %>: ] ;
         [ #: ][ Cns* Vws | Cns* Vws+ Cns:+ Vws+ :0* Cns+ :Vws ] v _  ( :Vws | A1: ) [ 0:Vws %>: %^LCns: | %>: %^LCns: :0* :Vws ] ; ! arvattava:arvattavvaa
        #: Cns* Vws v _ Vws [ Vws 0: Cns ( Cns:0 ) :Vws | Vws %^SyllBr:0 e ] %^StretchSyll2:0 ; ! kevät:kevväen, tavvaara




! viis: viijes
"it:j"
 t:j <=> [ i | o ] i _ [ e | o ] %^WEAK:0 %>: ;

"st:ss weakening"
t:s <=> Vws+ s _ ( %^TS:0 ) GRC ;
! last-minute verbs
! "ti:si in verbs"
   [ n | l | r | Vws Vws ] _ %^TS:0 ; !  pittää: piti 


"rd:rr weakening"
d:r <=>  Vws r _ GRC ;

"ld:ll weakening"
d:l <=>  Vws l _ GRC ;

"nd:nn weakening"
d:n <=>  Vws ( %^WEAK:0 %>: ) n _ GRC ;

!! p

"mp:mm weakening"
p:m <=> [ :Vws ( %^WEAK:0 %>: ) | [ a: | ä: ] %^WEAK:0 %>: %^E1:0 | A1: | Vws+ :Cns+ Vws  %^SyllBr:0 e %^StretchSyll2:0 ] m _ [ GRC | A1: %^WEAK:0 %>: ( V3: ) Cns | %^TS:0 i %^WEAK:0 %>: ] ;
        m _ Vws l %^WGStem:0 ; ! ompel- to ommel-
! [ ( %>: ) :Vws ( 0: ) %^WEAK:0 %>: | Vws:0 %>: i: %^WEAK:0 %>: | Vws:0 %>:  %^WEAK:0 %>: i: %>: ] ;
!%> %^WCns %^E1 mp
!!

"nk:ng weakening"
k:g <=> [ #: ] Cns* Vws n _ [ GRC | %^TS:0 i %^WEAK:0 %>: ] ;


! aika: ajan
"kToZero"
k:0 <=> [  #:  Cns* Vws (( r | i | VwsNonHigh ) k | t | h | l | r | s ) | Vws :Cns+ :Vws+ k ] _ [ GRC | %^TS:0 i %^WEAK:0 %>: ] ;
![ [ #: ] Cns* Vws (( r | i ) k | t | h | l | r | s ) | Vws :Cns+ :Vws+ k ] _ [ Vws: | :Vws | ( Vws ) %>: i ] ( %>: i: | %^StretchSyll2:0 ) %^WEAK:0 %>: ;
        [ #: ] Cns* VwsBack i: _ [ GRC | %^TS:0 i %^WEAK:0 %>: ] ;

! liika:liijan, reikä:reijän
"kToj"
 k:j <=> [ #: ] Cns* [ VwsFront | VwsNeutral ] i: _ [ GRC | %^TS:0 i %^WEAK:0 %>: ] ;

! mato : maot, ! mato : maool
! tToZero in weak
"Double stop:weakens tToZero"
t:0 <=> [ #: Cns* Vws | #: Cns* Vws [ Vws - [ i | u | y ] ] | Vws h [ y | u ] ] _  GRC ;
        [ #: Cns*  Vws+ [ h | (r) t ] | Vws ( %^WEAK:0 %>: ) t | Cns* Vws+ Cns+ Vws+ ( n ) t | Vws t ] _  [ GRC | %^TS:0 i %^WEAK:0 %>: ] ;
        t _ Vws l %^WGStem:0 ; ! ottel- to otel-

! lupa : luvan
"p:v"
p:v <=> [ #: ] Cns* Vws ( r | l | Vws ) _ [ GRC | %^TS:0 i %^WEAK:0 %>: ] ;

"double pp to p"
p:0 <=> [ #: Cns* Vws ( m ) p | #: Cns* Vws Vws p | Cns* Vws+ Cns+ Vws+ p ] _ [ GRC | %^TS:0 i %^WEAK:0 %>: ] ;

!! Vowel raising
"o:u ö:y in Inf"
 Vx:Vy <=> _ Vx: %>: v v A1: %^RVws:0 ;
           Vx: _ %>: v v A1: %^RVws:0 ;
          where Vx in (o ö)
                Vy in (u y) matched ;

!! Consonant loss
"d:0"
d:0 <=> Vws _ Vws: %^WEAK:0 %>: ;

!!
!"Disallow WLoss after non-v"
!WLoss:0 /<= [[ :* Letters ] - [ :* [ в л | в О1: л ] ] ] %>: _ ;

!"Disallow V3 after two consecutive realized vowels"
! V3: /<= :Vws :0* :Vws :0* _ ;

