!! !!!Livvi adjective inflection
! --------------------


!! !!Temporary lexica

LEXICON Q_X !!= * @CODE@ 
+TYÄ: # ;

LEXICON A_ !!= * @CODE@ 
+TYÄ+A: # ;

!! !!Somewhat open issues

LEXICON A_TULLUT !!= * @CODE@ IS THIS LIVVI? tullut:tullu
+A:%{back%} NMN_TULLUT ; 

LEXICON A_EX !!= * @CODE@ needs to be changed to @exclude=fst
+TYÄ+A: # ;

LEXICON A_BAHUV !!= @CODE@ 
+A+Bahuv: # ;

LEXICON A_UNDECL  !!= @CODE@ undeclinable fin: taipumaton
+A: # ;

LEXICON A_IS-N-PL-GEN !!= @CODE@ genitive plural attributes
+N+Pl+Gen+A: # ;

LEXICON A_IS-N-SG-GEN !!= @CODE@ genitive plural attributes
+N+Sg+Gen+A: # ;

LEXICON A-DEM_NÄMÄ !!= @CODE@ nämä:nämä
!+A+Dem+Pl+Abl:l K ;
!+A+Dem+Pl+Abl:lpäi K ;
+A+Dem+Pl+Ade:%^RmVow%>il K ;
+A+Dem+Pl+All:%^RmVow%>ile K ;
+A+Dem+Pl+Ess:%^RmVow%>inny K ;
+A+Dem+Pl+Gen:%^RmVow%>ien K ;
+A+Dem+Pl+Ill:%^RmVow%>ih K ;
+A+Dem+Pl+Ine:%^RmVow%>is K ;
+A+Dem+Pl+Nom: K ;
+A+Dem+Pl+Par:%^RmVow%>ii K ;
!+A+Dem+Pl+Tra:kse K ;



!! !ONE-SYLLABLE VOWEL-FINAL STEMS ENDING IN LONG VOWEL SEGMENT
LEXICON A_KUU !!= @CODE@ kuu:kuu
+A:%{back%} NMN_KUU ;

LEXICON A_MUA !!= @CODE@ mua:maa
+A:%{back%} NMN_MUA ;

LEXICON A_PIÄ !!= @CODE@ piä:piä
+A:%{front%} NMN_PIÄ ;


!! !!Ordinary inflection

!! !TWO-SYLLABLE VOWEL-FINAL STEMS
LEXICON A_HYVÄ !!= @CODE@ hyvä:hyvä
!! the comparatives and superlatives are supletive
+A:%{front%} NMN_OZA ;

LEXICON A_OZA !!= @CODE@ oza:oza
+A:%{back%} NMN_OZA ;
+A+Der/mbi+Comp:%{back%}%^E1%>mb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{back%}%^RmVow%>i NMN_ENIN        ;

LEXICON A_SÄYNÄ !!= @CODE@ säynä:säynä
+A:%{front%} NMN_SÄYNÄ ;
+A+Der/mbi+Comp:%{front%}%^E1%>mb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{front%}%^RmVow%>i NMN_ENIN        ;

LEXICON A_KALA !!= @CODE@ kala:kala
+A:%{back%} NMN_KALA ;
+A+Der/mbi+Comp:%{back%}%^E1%>mb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{back%}%^RmVow%>i NMN_ENIN        ;

LEXICON A_KOIVU !!= @CODE@ koivu:koivu
+A:%{back%}        NMN_KOIVU/HERY ;
+A+Der/mbi+Comp:%{back%}%>mb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{back%}vi NMN_ENIN        ;

LEXICON A_HERY !!= @CODE@ hery:hery
+A:%{front%}        NMN_KOIVU/HERY ;
+A+Der/mbi+Comp:%{front%}%>mb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{front%}vi NMN_ENIN        ;

LEXICON A_LUGU !!= @CODE@ lugu:lugu
+A:%{back%}        NMN_LUGU/IDY ;
+A+Der/mbi+Comp:%{back%}%^WGStem%>mb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{back%}vi NMN_ENIN        ;

LEXICON A_IDY !!= @CODE@ idy:idy
+A:%{front%}        NMN_LUGU/IDY ;
+A+Der/mbi+Comp:%{front%}%^WGStem%>mb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{front%}vi NMN_ENIN        ;

LEXICON A_RUNO
+A:%{back%}	NMN_RUNO/HÖRÖ ;
+A+Der/mbi+Comp:%{back%}%>mb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{back%}vi NMN_ENIN        ;

LEXICON A_RUADO
+A:%{back%}	NMN_RUADO/KYNDÖ ;
+A+Der/mbi+Comp:%{back%}%>mb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{back%}vi NMN_ENIN        ;

LEXICON A_PAPPI !!= @CODE@ pappi:pappi
+A:%{back%} NMN_PAPPI ;

LEXICON A_HÄKKI !!= @CODE@ häkki:häkki
+A:%{front%} NMN_HÄKKI ;

LEXICON A_ARTELI !!= @CODE@ 
+A:%{back%}     NMN_ARTELI ;

LEXICON A_ALUS !!= @CODE@ alus:aluks
+A:%{back%} NMN_ALUS/ILVES   ;
+A+Der/mbi+Comp:%{back%}e%>mb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{back%}i NMN_ENIN        ;

LEXICON A_SUALIŠ !!= @CODE@ suališ:suali
+A:%{back%}	 NMN_SUALIŠ ;
+A+Der/in+Superl:%{back%}vi NMN_ENIN        ;

LEXICON A_KUURNIS !!= @CODE@ kuurnis:kuurne
+A:%{back%}      NMN_KUURNIS ;

LEXICON A_PUHTAHUS !!= @CODE@ puhtahus:puhtahu
+A:%{back%}	NMN_PUHTAHUS/XX ;

LEXICON A_OLUT !!= @CODE@ olut:olu
+A:%{back%}	 NMN_OLUT ;

!! !TWO-SYLLABLE VOWEL-FINAL STEM WITH UNIQUE +Nom+Sg VOWEL
LEXICON A_PÄIVY !!= @CODE@ päivy:päivä
+A:%{front%} NMN_PÄIVY ;
+A+Der/mbi+Comp:%{front%}%^E1%>mb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{front%}%^RmVow%>i NMN_ENIN        ;

LEXICON A_MUARJU !!= @CODE@ muarju:muarja
+A:%{back%}	NMN_MUARJU ;
+A+Der/mbi+Comp:%{back%}%^E1%>mb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{back%}%^RmVow%>i NMN_ENIN        ;

LEXICON A_AKKU !!= @CODE@ akku:akka
+A:%{back%}	NMN_AKKU ;
+A+Der/mbi+Comp:%{back%}%^E1%>mb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{back%}%^RmVow%>i NMN_ENIN        ;

LEXICON A_KOIRU
+A:%{back%} NMN_KOIRU/PÄIVY ;
+A+Der/mbi+Comp:%{back%}%^E1%>mb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{back%}%^RmVow%>i NMN_ENIN        ;


LEXICON A_VALGEI !!= @CODE@ valgei:valge
+A:%{back%} NMN_VALGEI        ;
+A+Der/mbi+Comp:%{back%}%>emb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{back%}vi NMN_ENIN        ;

LEXICON A_RAHMANNOI !!= @CODE@ rahmannoi:rahmannoi
+A:%{back%} NMN_RAHMANNOI        ;


LEXICON A_HARDIE
+A:%{back%} NMN_HARDIE	;

LEXICON A_OSTAI
+A:%{back%}%>%{ijPRC%} NMN_OSTAI ;

LEXICON A_PEZII
+A:%{front%}%>%{ijPRC%} NMN_PEZII ;

LEXICON A_KONDII
+A:%{back%} NMN_KONDII ;

LEXICON A_KESTÄY !!= @CODE@ kestäy:kestä
+A:%{front%} NMN_KESTÄY ;

!! !TWO-SYLLABLE VOWEL-FINAL STEMS WITH CONSONANT-FINAL PARTITIVE STEM


!! !THREE-SYLLABLE VOWEL-FINAL STEMS
LEXICON A_PAREMBI !!= @CODE@ parembi:paremb
+A:%{back%} NMN_PAREMBI/ENÄMBI	  ;
+A+Der/MWN:%{back%} N_PAREMBI ;
!+N:%{aä%} Adj-NB-i ; !2012-09-27 unknown -loi vs -i

LEXICON A_JIÄTÖI !!= @CODE@ jiätöi:jiät
+A:%{front%} A_HUOLETOI/JIÄTÖI ;
LEXICON A_HUOLETOI !!= @CODE@ huoletoi:huolet
+A:%{back%} A_HUOLETOI/JIÄTÖI ;
LEXICON A_HUOLETOI/JIÄTÖI !!= @CODE@ huoletoi:huolet
+Der/Us+N:t%{oö%}m%{uy%} NMN_PUHTAHUS/XX ;
:	NMN_HUOLETOI/JIÄTÖI   ;
+Der/MWN: N_HUOLETOI ; 
+Der/MWN+N:t%{oö%}m%{aä%} Adj-NB-i ; !2012-09-27 unknown -loi vs -i
+Der/mbi+Comp:t%{oö%}memb NMN_PAREMBI/ENÄMBI        ;
+Der/in+Superl:t%{oö%}mi NMN_ENIN        ;

LEXICON A_KARJALAINE !!= @CODE@ karjalaine:karjala
+A+Der/Us:%{back%}z%{uy%} N_PUHTAHUS ;
+A:%{back%}	NMN_KARJALAINE/LIYGILÄINE	 ;
+A+Der/mbi+Comp:%{back%}zemb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{back%}zi NMN_ENIN        ;

LEXICON A_LIYGILÄINE !!= @CODE@ liygiläine:liygilä
+A+Der/Us:%{front%}z%{uy%} N_PUHTAHUS ;
+A:%{front%}	NMN_KARJALAINE/LIYGILÄINE ;
+A+Der/mbi+Comp:%{front%}zemb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{front%}zi NMN_ENIN        ;

LEXICON A_NAINE !!= @CODE@ naine:nai
+A:%{back%} A_NAINE/KIELINE_01 ;
LEXICON A_KIELINE !!= @CODE@ kieline:kieli
+A:%{front%} A_NAINE/KIELINE_01 ;
LEXICON A_NAINE/KIELINE_01 !!= @CODE@ kieline:kieli naine:nai
+Der/Us+N:z%{uy%} NMN_PUHTAHUS/XX ;
: NMN_NAINE/KIELINE ;
+Der/mbi+Comp:zemb NMN_PAREMBI/ENÄMBI        ;
+Der/in+Superl:zi NMN_ENIN        ;

LEXICON A_TOINE !!= @CODE@ toine:to
+A:%{back%} NMN_TOINE ;
+A+Der/mbi+Comp:%{back%}izemb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{back%}izi NMN_ENIN        ;
LEXICON A_TOINE-PL !!= @CODE@ toine:to
+A:%{back%} NMN_TOINE-PL ;

!! !THREE-SYLLABLE STEMS WITH TWO-SYLLABLE NOMINATIVE SINGULAR
LEXICON A_KARJAL !!= @CODE@ karjal:karjal
+A:%{back%}     NMN_KARJAL ;
+A+Der/mbi+Comp:%{back%}emb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{back%}i NMN_ENIN        ;

LEXICON A_MADAL !!= @CODE@ madal:madal
+A+Der/Us:%{back%}%{uy%} N_PUHTAHUS ;
+A:%{back%}	NMN_MADAL ;
+A+Der/mbi+Comp:%{back%}emb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{back%}i NMN_ENIN        ;

LEXICON A_PIIRAI !!= @CODE@ piirai:piira 
+A:%{back%}	NMN_PIIRAI   ;

LEXICON A_RAIŠ !!= @CODE@ raiš:ra
+A:%{back%}     NMN_RAIŠ   ;

LEXICON A_PEREH !!= @CODE@ pereh:pereh
+A:%{front%}	NMN_PEREH	;

LEXICON A_PESSYH !!= @CODE@ pessyh:pessy
+A:%{front%} NMN_PESSYH ;

LEXICON A_ARMAS !!= @CODE@ armas:arma
+A:%{back%} NMN_ARMAS        ;
+A+Der/mbi+Comp:%{back%}h%{aä%}mb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{back%}hi NMN_ENIN        ;

LEXICON A_VARVAS !!= @CODE@ varvas:varva
+A:%{back%} NMN_VARVAS        ;
+A+Der/mbi+Comp:%{back%}h%{aä%}mb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{back%}hi NMN_ENIN        ;

LEXICON A_TULLUH !!= @CODE@ tulluh:tullu
!!= * Yaml: __A-tulluh__
+A:%{back%} NMN_TULLUH ;

LEXICON A_TUORES !!= @CODE@ tuores:tuore
!!= * Yaml: __A-tuores__
+A:%{back%} NMN_TUORES ;
+A+Der/mbi+Comp:%{back%}hemb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{back%}hi NMN_ENIN        ;

LEXICON A_SUURIM !!= @CODE@ suurim:suurim
+A:%{back%}	NMN_SUURIM ;

LEXICON A_SUARI !!= @CODE@ suari:suar
+A:%{back%} A_SUARI/KIELI_01 ;

LEXICON A_KIELI !!= @CODE@ kieli:kiel
+A:%{front%} A_SUARI/KIELI_01 ;
LEXICON A_SUARI/KIELI_01 !!= @CODE@ suari:suar
:	NMN_SUARI/KIELI	;
+Der/mbi+Comp:emb NMN_PAREMBI/ENÄMBI        ;
+Der/in+Superl:i NMN_ENIN        ;

LEXICON A_VUOZI !!= @CODE@ vuozi:vuod
+A:%{back%}	NMN_VUOZI ;
+A+Der/mbi+Comp:%{back%}%^WGStem%>emb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{back%}%^WGStem%>i NMN_ENIN        ;

LEXICON A_VEZI !!= @CODE@ vezi:vet
+A:%{front%}     NMN_VEZI ;

LEXICON A_NIMI !!= @CODE@ nimi:nim
+A:%{front%}        NMN_HANGI/NIMI  ;
+A+Der/mbi+Comp:%{front%}emb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{front%}i NMN_ENIN        ;

LEXICON A_JÄLGI  !!= @CODE@ jälgi:jälg
!! front vowel
!! gradation Yes
+A:%{front%}        NMN_JÄLGI  ;
+A+Der/mbi+Comp:%{front%}emb NMN_PAREMBI/ENÄMBI        ;
+A+Der/in+Superl:%{front%}i NMN_ENIN        ;



!! !TWO-SYLLABLE WORD WITH CONSONANT-FINAL STEM
LEXICON A_VANUIN !!= @CODE@ vanuin:vanui
+A:%{back%} NMN_VANUIN	 ;

LEXICON A_VAŽEN !!= @CODE@ važen:važe
+A:%{back%} NMN_VAŽEN   ;


LEXICON A_LÄMMIN !!= @CODE@ lämmin:lämbi
+A:%{front%}	NMN_LÄMMIN   ;

LEXICON A_TAIGIN !!= @CODE@ taigin:taigin
+A:%{back%}	NMN_TAIGIN   ;

LEXICON A_SALBOIN !!= @CODE@ salboin:salboi
+A:%{back%} NMN_SALBOIN ;

LEXICON A_ENIN !!= @CODE@ enin:eni
+A:%{front%} NMN_ENIN-SG ;
+A:%{front%} NMN_ENIN-PL ;



LEXICON Adj-NB-i
! These cases are symmetrically marked for number
+Sg: Adj-NB-01 ;
+Pl:%>i Adj-NB-01 ;
! The next two share the same stem vowel
+Sg: SG-ACC/APR/COM/GENSUF ;
+Pl: PLNOMSUF ;

LEXICON Adj-NB-loi
! These cases are symmetrically marked for number
+Sg: Adj-NB-01 ;
+Pl:%>loi Adj-NB-01 ;
! The next two share the same stem vowel
+Sg: SG-ACC/APR/COM/GENSUF ;
+Pl: PLNOMSUF ;

LEXICON Adj-NB-01
+Ess:%^WGStem%>nn%{uy%} K ;
+Tra:%^WGStem%>kse K ;
+Ine:%^WGStem%>s K ;
+Ela:%^WGStem%>s K ;
+Ill:%>h K ;
+Ade:%^WGStem%>l K ;
+Abl:%^WGStem%>l K ;
+All:%^WGStem%>le K ;
+Abe:%^WGStem%>tt%{aä%}h K ;
+Err/Orth+Abe:%^WGStem%>tt%{aä%} K ;
+Err/Orth+Abe:%^WGStem%>t%{aä%} K ;
+Prl:%>či K ;
+Apr:%^WGStem%>ll%{uy%}%{oö%} K ;
+Ter:%^WGStem%>ss%{aä%}h K ;



! vim: set ft=xfst-lexc:

