!! !!!Livvi adjective inflection
! --------------------


!! !!Temporary lexica

LEXICON Q_X !!= * @CODE@ 
+TYÄ: # ;

LEXICON A_ !!= * @CODE@ 
+TYÄ+A: # ;

!! !!Somewhat open issues

LEXICON A_TULLUT !!= * @CODE@ IS THIS LIVVI? tullut:tullu
+A: NMN_TULLUT ; 

LEXICON A_EX !!= * @CODE@ needs to be changed to @exclude=fst
+TYÄ+A: # ;

LEXICON A_BAHUV !!= @CODE@ 
+A+Bahuv: # ;

LEXICON A_UNDECL  !!= @CODE@ undeclinable fin: taipumaton
+A: # ;

LEXICON A_IS-N-PL-GEN !!= @CODE@ genitive plural attributes
+N+Pl+Gen+A: # ;

LEXICON A_IS-N-SG-GEN !!= @CODE@ genitive plural attributes
+N+Sg+Gen+A: # ;

LEXICON A-DEM_NÄMÄ !!= @CODE@ nämä:nämä
!+A+Dem+Pl+Abl:l K ;
!+A+Dem+Pl+Abl:lpäi K ;
+A+Dem+Pl+Ade:%^VowRM%>il K ;
+A+Dem+Pl+All:%^VowRM%>ile K ;
+A+Dem+Pl+Ess:%^VowRM%>inny K ;
+A+Dem+Pl+Gen:%^VowRM%>ien K ;
+A+Dem+Pl+Ill:%^VowRM%>ih K ;
+A+Dem+Pl+Ine:%^VowRM%>is K ;
+A+Dem+Pl+Nom: K ;
+A+Dem+Pl+Par:%^VowRM%>ii K ;
!+A+Dem+Pl+Tra:kse K ;



!! !ONE-SYLLABLE VOWEL-FINAL STEMS ENDING IN LONG VOWEL SEGMENT
LEXICON A_KUU !!= @CODE@ kuu:kuu
+A: NMN_KUU ;

LEXICON A_MUA !!= @CODE@ mua:maa
+A: NMN_MUA ;

LEXICON A_PIÄ !!= @CODE@ piä:piä
+A: NMN_PIÄ ;


!! !!Ordinary inflection

!! !TWO-SYLLABLE VOWEL-FINAL STEMS
LEXICON A_HYVÄ !!= @CODE@ hyvä:hyvä
!! the comparatives and superlatives are supletive
+A: NMN_OZA ;

LEXICON A_OZA !!= @CODE@ oza:oza
+A: NMN_OZA ;
+A+Der/mbi+Comp:%^E1%>mb NMN_PAREMBI        ;
+A+Der/in+Superl:%^VowRM%>i NMN_ENIN        ;

LEXICON A_SÄYNÄ !!= @CODE@ säynä:säynä
+A: NMN_SÄYNÄ ;
+A+Der/mbi+Comp:%^E1%>mb NMN_PAREMBI        ;
+A+Der/in+Superl:%^VowRM%>i NMN_ENIN        ;

LEXICON A_KALA !!= @CODE@ kala:kala
+A: NMN_KALA ;
+A+Der/mbi+Comp:%^E1%>mb NMN_PAREMBI        ;
+A+Der/in+Superl:%^VowRM%>i NMN_ENIN        ;

LEXICON A_KOIVU !!= @CODE@ koivu:koivu
+A:        NMN_KOIVU ;
+A+Der/mbi+Comp:%>mb NMN_PAREMBI        ;
+A+Der/in+Superl:vi NMN_ENIN        ;

LEXICON A_HERY !!= @CODE@ hery:hery
+A:        NMN_HERY ;
+A+Der/mbi+Comp:%>mb NMN_PAREMBI        ;
+A+Der/in+Superl:vi NMN_ENIN        ;

LEXICON A_RUNO
+A:	NMN_RUNO ;
+A+Der/mbi+Comp:%>mb NMN_PAREMBI        ;
+A+Der/in+Superl:vi NMN_ENIN        ;

LEXICON A_RUADO
+A:	NMN_RUADO ;
+A+Der/mbi+Comp:%>mb NMN_PAREMBI        ;
+A+Der/in+Superl:vi NMN_ENIN        ;

LEXICON A_PAPPI !!= @CODE@ pappi:pappi
+A: NMN_PAPPI ;

LEXICON A_HÄKKI !!= @CODE@ häkki:häkki
+A: NMN_HÄKKI ;

LEXICON A_ARTELI !!= @CODE@ 
+A:     NMN_ARTELI ;

LEXICON A_ALUS !!= @CODE@ alus:aluks
+A: NMN_ALUS   ;
+A+Der/mbi+Comp:e%>mb NMN_PAREMBI        ;
+A+Der/in+Superl:i NMN_ENIN        ;

LEXICON A_SUALIŠ !!= @CODE@ suališ:suali
+A:	 NMN_SUALIŠ ;
+A+Der/in+Superl:vi NMN_ENIN        ;

LEXICON A_KUURNIS !!= @CODE@ kuurnis:kuurne
+A:      NMN_KUURNIS ;

LEXICON A_PUHTAHUS !!= @CODE@ puhtahus:puhtahu
+A:	NMN_PUHTAHUS ;

LEXICON A_OLUT !!= @CODE@ olut:olu
+A:	 NMN_OLUT ;

!! !TWO-SYLLABLE VOWEL-FINAL STEM WITH UNIQUE +Nom+Sg VOWEL
LEXICON A_PÄIVY !!= @CODE@ päivy:päivä
+A: NMN_PÄIVY ;
+A+Der/mbi+Comp:%^E1%>mb NMN_PAREMBI        ;
+A+Der/in+Superl:%^VowRM%>i NMN_ENIN        ;

LEXICON A_MUARJU !!= @CODE@ muarju:muarja
+A:	NMN_MUARJU ;
+A+Der/mbi+Comp:%^E1%>mb NMN_PAREMBI        ;
+A+Der/in+Superl:%^VowRM%>i NMN_ENIN        ;

LEXICON A_AKKU !!= @CODE@ akku:akka
+A:	NMN_AKKU ;
+A+Der/mbi+Comp:%^E1%>mb NMN_PAREMBI        ;
+A+Der/in+Superl:%^VowRM%>i NMN_ENIN        ;

LEXICON A_KOIRU
+A: NMN_PÄIVY ;
+A+Der/mbi+Comp:%^E1%>mb NMN_PAREMBI        ;
+A+Der/in+Superl:%^VowRM%>i NMN_ENIN        ;


LEXICON A_VALGEI !!= @CODE@ valgei:valge
+A: NMN_VALGEI        ;
+A+Der/mbi+Comp:%>emb NMN_PAREMBI        ;
+A+Der/in+Superl:vi NMN_ENIN        ;

LEXICON A_RAHMANNOI !!= @CODE@ rahmannoi:rahmannoi
+A: NMN_RAHMANNOI        ;


LEXICON A_HARDIE
+A: NMN_HARDIE	;

LEXICON A_OSTAI
+A:%>%{ijPRC%} NMN_OSTAI ;

LEXICON A_PEZII
+A:%>%{ijPRC%} NMN_PEZII ;

LEXICON A_KONDII
+A: NMN_KONDII ;

LEXICON A_KESTÄY !!= @CODE@ kestäy:kestä
+A: NMN_KESTÄY ;

!! !TWO-SYLLABLE VOWEL-FINAL STEMS WITH CONSONANT-FINAL PARTITIVE STEM


!! !THREE-SYLLABLE VOWEL-FINAL STEMS
LEXICON A_PAREMBI !!= @CODE@ parembi:paremb
+A: NMN_PAREMBI	  ;
+A+Der/MWN: N_PAREMBI ;
!+N:%{aä%} Adj-NB-i ; !2012-09-27 unknown -loi vs -i

LEXICON A_JIÄTÖI !!= @CODE@ jiätöi:jiät
 A_HUOLETOI ;
LEXICON A_HUOLETOI !!= @CODE@ huoletoi:huolet
+A+Der/Us:t%{oö%}m%{uy%} N_PUHTAHUS ;
+A:	NMN_HUOLETOI   ;
+A+Der/MWN: N_HUOLETOI ; 
+N:t%{oö%}m%{aä%} Adj-NB-i ; !2012-09-27 unknown -loi vs -i
+A+Der/mbi+Comp:t%{oö%}memb NMN_PAREMBI        ;
+A+Der/in+Superl:t%{oö%}mi NMN_ENIN        ;

LEXICON A_KARJALAINE !!= @CODE@ karjalaine:karjala
+A+Der/Us:z%{uy%} N_PUHTAHUS ;
+A:	NMN_KARJALAINE	 ;
+A+Der/mbi+Comp:zemb NMN_PAREMBI        ;
+A+Der/in+Superl:zi NMN_ENIN        ;

LEXICON A_LIYGILÄINE !!= @CODE@ liygiläine:liygilä
+A+Der/Us:z%{uy%} N_PUHTAHUS ;
+A:	NMN_LIYGILÄINE	 ;
+A+Der/mbi+Comp:zemb NMN_PAREMBI        ;
+A+Der/in+Superl:zi NMN_ENIN        ;

LEXICON A_NAINE !!= @CODE@ naine:nai
+A+Der/Us:z%{uy%} N_PUHTAHUS ;
+A: NMN_NAINE ;
+A+Der/mbi+Comp:zemb NMN_PAREMBI        ;
+A+Der/in+Superl:zi NMN_ENIN        ;

LEXICON A_KIELINE !!= @CODE@ kieline:kieli
+A+Der/Us:z%{uy%} N_PUHTAHUS ;
+A: NMN_KIELINE ;
+A+Der/mbi+Comp:zemb NMN_PAREMBI        ;
+A+Der/in+Superl:zi NMN_ENIN        ;

LEXICON A_TOINE !!= @CODE@ toine:to
+A: NMN_TOINE ;
+A+Der/mbi+Comp:izemb NMN_PAREMBI        ;
+A+Der/in+Superl:izi NMN_ENIN        ;
LEXICON A_TOINE-PL !!= @CODE@ toine:to
+A: NMN_TOINE-PL ;

!! !THREE-SYLLABLE STEMS WITH TWO-SYLLABLE NOMINATIVE SINGULAR
LEXICON A_KARJAL !!= @CODE@ karjal:karjal
+A:     NMN_KARJAL ;
+A+Der/mbi+Comp:emb NMN_PAREMBI        ;
+A+Der/in+Superl:i NMN_ENIN        ;

LEXICON A_MADAL !!= @CODE@ madal:madal
+A+Der/Us:%{uy%} N_PUHTAHUS ;
+A:	NMN_MADAL ;
+A+Der/mbi+Comp:emb NMN_PAREMBI        ;
+A+Der/in+Superl:i NMN_ENIN        ;

LEXICON A_PIIRAI !!= @CODE@ piirai:piira 
+A:	NMN_PIIRAI   ;

LEXICON A_RAIŠ !!= @CODE@ raiš:ra
+A:     NMN_RAIŠ   ;

LEXICON A_PEREH !!= @CODE@ pereh:pereh
+A:	NMN_PEREH	;

LEXICON A_PESSYH !!= @CODE@ pessyh:pessy
+A: NMN_PESSYH ;

LEXICON A_ARMAS !!= @CODE@ armas:arma
+A: NMN_ARMAS        ;
+A+Der/mbi+Comp:h%{aä%}mb NMN_PAREMBI        ;
+A+Der/in+Superl:hi NMN_ENIN        ;

LEXICON A_TUORES !!= @CODE@ tuores:tuore
!!= * Yaml: __A-tuores__
+A: NMN_TUORES ;
+A+Der/mbi+Comp:hemb NMN_PAREMBI        ;
+A+Der/in+Superl:hi NMN_ENIN        ;

LEXICON A_SUURIM !!= @CODE@ suurim:suurim
+A:	NMN_SUURIM ;

LEXICON A_KIELI !!= @CODE@ kieli:kiel
+A:	NMN_KIELI	;
+A+Der/mbi+Comp:emb NMN_PAREMBI        ;
+A+Der/in+Superl:i NMN_ENIN        ;

LEXICON A_VUOZI !!= @CODE@ vuozi:vuod
+A:	NMN_VUOZI ;
+A+Der/mbi+Comp:%^WGStem%>emb NMN_PAREMBI        ;
+A+Der/in+Superl:%^WGStem%>i NMN_ENIN        ;

LEXICON A_VEZI !!= @CODE@ vezi:vet
+A:     NMN_VEZI ;

LEXICON A_NIMI !!= @CODE@ nimi:nim
+A:        NMN_NIMI  ;
+A+Der/mbi+Comp:emb NMN_PAREMBI        ;
+A+Der/in+Superl:i NMN_ENIN        ;

LEXICON A_JÄLGI  !!= @CODE@ jälgi:jälg
!! front vowel
!! gradation Yes
+A:        NMN_JÄLGI  ;
+A+Der/mbi+Comp:emb NMN_PAREMBI        ;
+A+Der/in+Superl:i NMN_ENIN        ;



!! !TWO-SYLLABLE WORD WITH CONSONANT-FINAL STEM
LEXICON A_VANUIN !!= @CODE@ vanuin:vanui
+A: NMN_VANUIN	 ;

LEXICON A_VAŽEN !!= @CODE@ važen:važe
+A: NMN_VAŽEN   ;


LEXICON A_LÄMMIN !!= @CODE@ lämmin:lämbi
+A:	NMN_LÄMMIN   ;

LEXICON A_TAIGIN !!= @CODE@ taigin:taigin
+A:	NMN_TAIGIN   ;

LEXICON A_SALBOIN !!= @CODE@ salboin:salboi
+A: NMN_SALBOIN ;

LEXICON A_ENIN !!= @CODE@ enin:eni
+A: NMN_ENIN-SG ;
+A: NMN_ENIN-PL ;



LEXICON Adj-NB-i
! These cases are symmetrically marked for number
+Sg: Adj-NB-01 ;
+Pl:%>i Adj-NB-01 ;
! The next two share the same stem vowel
+Sg: SGGENSUF ; ! The singular comitative is declined here
+Pl: PLNOMSUF ;

LEXICON Adj-NB-loi
! These cases are symmetrically marked for number
+Sg: Adj-NB-01 ;
+Pl:%>loi Adj-NB-01 ;
! The next two share the same stem vowel
+Sg: SGGENSUF ; ! The singular comitative is declined here
+Pl: PLNOMSUF ;

LEXICON Adj-NB-01
+Ess:%^WGStem%>nn%{uy%} K ;
+Tra:%^WGStem%>kse K ;
+Ine:%^WGStem%>s K ;
+Ela:%^WGStem%>s K ;
+Ill:%>h K ;
+Ade:%^WGStem%>l K ;
+Abl:%^WGStem%>l K ;
+All:%^WGStem%>le K ;
+Abe:%^WGStem%>tt%{aä%}h K ;
+Err/Orth+Abe:%^WGStem%>tt%{aä%} K ;
+Err/Orth+Abe:%^WGStem%>t%{aä%} K ;
+Prl:%>či K ;
+Apr:%^WGStem%>ll%{uy%}%{oö%} K ;
+Ter:%^WGStem%>ss%{aä%}h K ;



! vim: set ft=xfst-lexc:

