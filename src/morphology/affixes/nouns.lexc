!! Noun inflection
!! ---------------
!! Livvi nouns inflect in cases.

LEXICON N_
+N+Sg+Nom+TYÄ: # ;



LEXICON N_HUOLETOI !!= @CODE@ huoletoi: huolet
+N:oi SGNOMSUF ;
+N:toma SGPARSUF_A2 ;
+N:tomie PLGENSUF ;
+N:tomi PLPARSUF_i ;
+N:tomi PLINSSUF ;
+N:toma Noun-NB-i ;

LEXICON N_KANDAI !!= @CODE@ kandai:kandai
+N: SGNOMSUF ;
+N:A1 SGPARSUF_A2 ;
+N:ie PLGENSUF ;
+N:i PLPARSUF_i ;
+N:i PLINSSUF ;
+N+Sg:A1 Noun-NB-01 ;
+N+Pl:i Noun-NB-01 ;

LEXICON N_PAGIZII !!= @CODE@ pagizii:pagizii
+N: SGNOMSUF ;
+N:A2 SGGENSUF ;
+N:A2 SGPARSUF_A2 ;
+N:%>O1i PLGENSUF ;
+N:%>O1i PLPARSUF_i ;
+N:%>O1i PLINSSUF ;
+N:A2 Noun-NB-01 ;
+N:%>O1 Noun-NB-01 ;

LEXICON N_ANUS !!= @CODE@ aigomus:aigomuks
+N: NMN_ANUS ;

LEXICON N_ANUS-PL !!= @CODE@ aigomus:aigomuks
+N: NMN_ANUS-PL ;

LEXICON N_KARJAL !!= @CODE@ karjal:karjal
+N: SGNOMSUF ;
+N:a SGPARSUF_dU1 ;
+N:a%>i PLGENSUF ;
+N:i PLPARSUF_i ;
+N:oi PLINSSUF ;
+N:A1 Noun-NB-i ;

LEXICON N_ARTELI
+N: NMN_ARTELI ;

LEXICON N_KARJALAINE !!= @CODE@ karjalaine:karjala
+N:    NMN_KARJALAINE	 ;

LEXICON N_KIELI !!= @CODE@ kieli:kiel
+N:i SGNOMSUF ;
+N:e SGGENSUF ;
+N: SGPARSUF_dU1 ;
+N+Sg:e Noun-NB-01 ;
+N:e PLNOMSUF ;
+N:ie PLGENSUF ;
+N:i PLPARSUF_i ;
+N:i PLINSSUF ;
+N+Pl:i Noun-NB-01 ;

LEXICON N_KUU !!= @CODE@ kuu:kuu
+N: NMN_KUU ;

LEXICON N_MUARJU !!= @CODE@ muarju:muarja
+N:%^A2U SGNOMSUF ;
+N: SGGENSUF ;
+N: SGPARSUF_A2 ;
+N+Sg: Noun-NB-01 ;
+N: PLNOMSUF ;
+N:%>oi PLGENSUF ; ! the plural comitative is declined here
+N:%>oi PLPARSUF_a ;
+N:%>oi PLINSSUF ;
+N+Pl:%>oi Noun-NB-01 ;

LEXICON N_KALA !!= @CODE@ kala:kala
+N: NMN_KALA ;

LEXICON N_OZA !!= @CODE@ oza:oza
+N: NMN_OZA ;

LEXICON N_ARMAS !!= @CODE@ armas:arma
+N:%^WEAK%>s SGNOMSUF ;
+N:%^WEAK%>s SGPARSUF_dU1 ;
+N:hie PLGENSUF ;
+N:hi PLPARSUF_i ;
+N:hi PLINSSUF ;
+N:ha Noun-NB-i ;

LEXICON N_PAREMBI !!= @CODE@ parembi:paremb
+N:i SGNOMSUF ;
+N:A1 SGPARSUF_A2 ;
+N:ie PLGENSUF ;
+N:i PLPARSUF_i ;
+N:i PLINSSUF ;
+N:A1 Noun-NB-i ;

LEXICON N_KOIRU !!= @CODE@ koiru:koira
 N_PÄIVY ;

LEXICON N_NIMI !!= @CODE@ 
+N:i SGNOMSUF ;
+N:e SGPARSUF_A2 ;
+N:%>ie PLGENSUF ; ! the plural comitative is declined here
+N:%>i PLPARSUF_i ;
+N:%>i PLINSSUF ;
+N:e Noun-NB-i ;
+N:iloi PLGENSUF ;
+N:iloi PLPARSUF_Zero ;
+N:iloi PLINSSUF ;
+N:i Noun-NB-loi ;

LEXICON N_LAPSI !!= @CODE@ lapsi:laps
+N:i SGNOMSUF ;
+N:e SGGENSUF ;
+N:%^KS2S SGPARSUF_dU1 ;
+N:e PLNOMSUF ;
+N:%>ie PLGENSUF ; ! the plural comitative is declined here
+N:%>i PLPARSUF_i ;
+N:%>i PLINSSUF ;
+N+Sg:e Noun-NB-01 ;
+N+Pl:i Noun-NB-01 ;

LEXICON N_VUOZI !!= @CODE@ vuozi:vuot
+N:%^T2Z%>i SGNOMSUF ;
+N:e SGGENSUF ;
+N: SGPARSUF_dU1 ;
+N:%^T2Z%>ie PLGENSUF ; ! the plural comitative is declined here
+N:%^T2Z%>i PLPARSUF_i ;
+N:%^T2Z%>i PLINSSUF ;
+N:e Noun-NB-01 ;
+N:%^T2Z%>i Noun-NB-01 ;

LEXICON N_PÄIVY !!= @CODE@ päivy:päivä
+N:%^A2U SGNOMSUF ;
+N: SGGENSUF ;
+N: SGPARSUF_A2 ;
+N: PLNOMSUF ;
+N:%>ie PLGENSUF ; ! the plural comitative is declined here
+N:%>i PLPARSUF_i ;
+N:%>i PLINSSUF ;
+N+Sg: Noun-NB-01 ;
+N+Pl:%>i Noun-NB-01 ;

LEXICON N_KOIVU !!= @CODE@ koivu:koivu
+N: SGNOMSUF ;
+N: SGGENSUF ;
+N: SGPARSUF_A2 ;
+N: PLNOMSUF ;
+N:%>lO1i PLGENSUF ; ! the plural comitative is declined here
+N:%>lO1i PLPARSUF_Zero ;
+N:%>lO1i PLINSSUF ;
+N+Sg: Noun-NB-01 ;
+N+Pl:%>lO1i Noun-NB-01 ;

LEXICON N_KIELINE !!= @CODE@ kieline:kieli
+N: NMN_KIELINE ;

LEXICON N_RUNO !!= @CODE@ runo:runo
+N: SGNOMSUF ;
+N: SGPARSUF_A2 ;
+N:loi PLGENSUF ;
+N:loi PLPARSUF_Zero ;
+N:loi PLINSSUF ;
+N: Noun-NB-loi ;

!! NMN
LEXICON NMN_ANUS !!= @CODE@ Anus:Anuks
	NMN_ANUS-SG ;
	NMN_ANUS-PL ;

LEXICON NMN_ANUS-SG
+N:%^KS2S SGNOMSUF ;
+N:e SGGENSUF ;
+N:%^KS2S SGPARSUF_dU1 ;
+N+Sg:e Noun-NB-01 ;

LEXICON NMN_ANUS-PL
+N:e PLNOMSUF ;
+N:%>ie PLGENSUF ; ! the plural comitative is declined here
+N:%>i PLPARSUF_i ;
+N:%>i PLINSSUF ;
+N+Pl:i Noun-NB-01 ;

LEXICON NMN_KUU
 SGNOMSUF ;
 SGPARSUF_dU1 ;
:%>lO1i PLGENSUF ;
:%>lO1i PLPARSUF_Zero ;
:%>lO1i PLINSSUF ;
+Sg: Noun-NB-01 ;
: Noun-NB-loi ;

LEXICON NMN_KALA !!= @CODE@ 
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
 PLNOMSUF ;
:i PLGENSUF ;
:i PLPARSUF_a ;
:i PLINSSUF ;
: Noun-NB-i ;

LEXICON NMN_KARJAL !!= @CODE@ karjal:karjal
 SGNOMSUF ;
:A2 SGGENSUF ;
:A2 SGPARSUF_A2 ;
:%>O1i PLGENSUF ;
:%>O1i PLPARSUF_Zero ;
:%>O1i PLINSSUF ;
+Sg:A2 Noun-NB-01 ;
+Pl:%>O1i Noun-NB-01 ;


LEXICON NMN_ARTELI !!= @CODE@ arteli:arteli
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
:i PLGENSUF ;
:%>lO1i PLPARSUF_Zero ;
:%>lO1i PLINSSUF ;
+Sg: Noun-NB-01 ;
+Pl:%>lO1i Noun-NB-01 ;

LEXICON NMN_OZA !!= @CODE@ oza:oza
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
:i PLGENSUF ;
:i PLPARSUF_a ;
:i PLINSSUF ;
+Sg: Noun-NB-01 ;
+Pl:i Noun-NB-01 ;

LEXICON NMN_KIELINE
:ne SGNOMSUF ;
:ze SGGENSUF ;
:s SGPARSUF_dU1 ;
:zie PLGENSUF ;
:zi PLPARSUF_i ;
:zi PLINSSUF ;
+Sg:ze Noun-NB-01 ;
+Pl:zi Noun-NB-01 ;

LEXICON NMN_KARJALAINE
:ine SGNOMSUF ;
:ze SGGENSUF ;
:s SGPARSUF_dU1 ;
:ze PLNOMSUF ;
:zie PLGENSUF ;
:zi PLPARSUF_i ;
:zi PLINSSUF ;
+Sg:ze Noun-NB-01 ;
+Pl:zi Noun-NB-01 ;


LEXICON Noun-NB-i
! These cases are symmetrically marked for number
+Sg: Noun-NB-01 ;
+Pl:%>i Noun-NB-01 ;
! The next two share the same stem vowel
 SGGENSUF ; ! The singular comitative is declined here
 PLNOMSUF ;
 
LEXICON Noun-NB-loi
! These cases are symmetrically marked for number
+Sg: Noun-NB-01 ;
+Pl:%>loi Noun-NB-01 ;
+Pl:%>loi ILLSUF_he ;
! The next two share the same stem vowel
 SGGENSUF ; ! The singular comitative is declined here
 PLNOMSUF ;
 

LEXICON SGNOMSUF
+Sg+Nom: K ;
+Sg+Acc: K ;

LEXICON SGPARSUF_dU1
+Sg+Par:%>dU1 K ;


LEXICON SGPARSUF_A2
+Sg+Par:%>A2 K ;

LEXICON SGGENSUF
+Sg+Gen:%^WEAK%>n K ;
+Sg+Acc:%^WEAK%>n K ;
+Sg+Com:%^WEAK%>nke K ;

LEXICON PLNOMSUF
+Pl+Nom:%^WEAK%>t K ;
+Pl+Acc:%^WEAK%>t K ;

LEXICON PLGENSUF
+Pl+Gen:%>n K ;
+Pl+Com:%^WEAK%>nke K ;

LEXICON ILLSUF_he
+Ill:%>he K ;

LEXICON PLINSSUF
+Pl+Ins:%>n K ;

LEXICON PLPARSUF_Zero
+Pl+Par: K ;

LEXICON PLPARSUF_a
+Pl+Par:%>A1 K ;

LEXICON PLPARSUF_i
+Pl+Par:%>i K ;

LEXICON Noun-NB-01
+Ess:%^WEAK%>nnU1 K ;
+Tra:%^WEAK%>kse K ;
+Ine:%^WEAK%>s K ;
+Ela:%^WEAK%>s K ;
+Ela:%^WEAK%>späi K ;
+Ill:%>h K ;
+Ade:%^WEAK%>l K ;
+Abl:%^WEAK%>l K ;
+Abl:%^WEAK%>lpäi K ;
+All:%^WEAK%>le K ;
+Abe:%^WEAK%>ttA1h K ;
+Abe:%^WEAK%>ttA1 K ;
+Abe:%^WEAK%>tA1 K ;
+Prl:%>či K ;
+Apr:%^WEAK%>llU1O1 K ;
+Ter:%^WEAK%>ssA1h K ;


! vim: set ft=xfst-lexc:

