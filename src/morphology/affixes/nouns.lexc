!! !!!Noun inflection
! ---------------
!! Livvi nouns inflect in cases.
!! Vowel harmony involves front and back
!! Gradation does not affect all consonants, therefore
!! there are three values: Yes, No and NA (not applicable)


!! !!The file proper

LEXICON N_
+N+Sg+Nom+TYÄ: # ;

!! !ONE-SYLLABLE VOWEL-FINAL STEMS ENDING IN LONG VOWEL SEGMENT
LEXICON N_SUO !!= @CODE@ suo:suo
!! Gradation: No
!! Harmony: Back
+N:%{back%} ATTESTED-NOUN-STEM ; 
+N:%{back%} NMN_KUU ;

LEXICON N_VYÖ !!= @CODE@ vyö:vyö
!! Gradation: No
!! Harmony: Front
+N: ATTESTED-NOUN-STEM ; 
+N:%{front%} NMN_VYÖ ;

LEXICON N_KUU !!= @CODE@ kuu:kuu
!! Gradation: No
!! Harmony: Back
+N:%{back%} ATTESTED-NOUN-STEM ; 
 N_KUU-SG ;
 N_KUU-PL ;
LEXICON N_KUU-SG
+N:%{back%} NMN_KUU-SG ;
LEXICON N_KUU-PL
+N:%{back%} NMN_KUU-PL ;

LEXICON N_MUA !!= @CODE@ mua:mua
!! Gradation: No
!! Harmony: Back
+N:%{back%} ATTESTED-NOUN-STEM ; 
+N:%{back%} NMN_MUA ;

LEXICON N_PIÄ !!= @CODE@ piä:piä
!! Gradation: No
!! Harmony: Front
+N: ATTESTED-NOUN-STEM ; 
 N_PIÄ-SG ;
 N_PIÄ-PL ;
LEXICON N_PIÄ-SG
+N:%{front%} NMN_PIÄ-SG ;
LEXICON N_PIÄ-PL
+N:%{front%} NMN_PIÄ-PL ;

!! !TWO-SYLLABLE VOWEL-FINAL STEMS

LEXICON N_PAPPI !!= @CODE@ pappi:pappi
!! Gradation: Yes
!! Harmony: Back
!! stem final i is retained
+N: ATTESTED-NOUN-STEM ; 
	N_PAPPI-SG ;
	N_PAPPI-PL ;
LEXICON N_PAPPI-SG
+N:%{back%} NMN_PAPPI-SG ;
LEXICON N_PAPPI-PL
+N:%{back%} NMN_PAPPI-PL ;

LEXICON N_LEIRI !!= @CODE@ leiri:leiri
!! Gradation NA
!! Harmony: Front
!! stem	final i is retained
+N: ATTESTED-NOUN-STEM ; 
	N_LEIRI-SG ;
	N_LEIRI-PL ;
LEXICON N_LEIRI-SG
+N:%{front%} NMN_LEIRI-SG ;
LEXICON N_LEIRI-PL
+N:%{front%} NMN_LEIRI-PL ;

LEXICON N_HÄKKI !!= @CODE@ häkki:häkki
!! Gradation: Yes
!! Harmony: Front
!! stem final i is retained
+N: ATTESTED-NOUN-STEM ; 
	N_HÄKKI-SG ;
	N_HÄKKI-PL ;
LEXICON N_HÄKKI-SG
+N:%{front%} NMN_HÄKKI-SG ;
LEXICON N_HÄKKI-PL
+N:%{front%} NMN_HÄKKI-PL ;

LEXICON N_PADA !!= @CODE@ pada:pada
!! Gradation Yes
!! Harmony: Back
!! stem final a changes to u in Sg Par
!! stem final a changes to o before i in Pl stem
+N: ATTESTED-NOUN-STEM ; 
	N_PADA-SG  ;
	N_PADA-PL  ;
LEXICON N_PADA-SG
+N:%{back%} NMN_PADA ;
LEXICON N_PADA-PL
+N:%{back%} NMN_PADA-PL ;


LEXICON N_KALA !!= @CODE@ kala:kala
!! Gradation NA
!! Harmony: Back
!! stem final a changes to u in Sg Par
!! stem final a changes to o before i in Pl stem
+N: ATTESTED-NOUN-STEM ; 
	N_KALA-SG  ;
	N_KALA-PL  ;
LEXICON N_KALA-SG
+N:%{back%} NMN_KALA ;
LEXICON N_KALA-PL
+N:%{back%} NMN_KALA-PL ;
LEXICON N-HUM_KALA
+N+Sem/Hum: NMN_KALA ;

LEXICON N_OZA !!= @CODE@ oza:oza
!! Gradation NA
!! Harmony: Back
!! stem final a changes to u in Sg Par
!! stem final a changes to 0 before i in Pl stem
+N: ATTESTED-NOUN-STEM ; 
	N_OZA-SG  ;
	N_OZA-PL  ;

LEXICON N_OZA-SG
+N:%{back%} NMN_OZA-SG ;

LEXICON N_OZA-PL
+N:%{back%} NMN_OZA-PL ;

LEXICON N_SÄYNÄ !!= @CODE@ säynä:säynä
!! Gradation NA
!! Harmony: Front
!! stem final ä changes to i in Sg Par
!! stem final ä changes to 0 before i in Pl stem

+N: ATTESTED-NOUN-STEM ; 
	N_SÄYNÄ-SG  ;
	N_SÄYNÄ-PL  ;

LEXICON N_SÄYNÄ-SG
+N:%{front%} NMN_SÄYNÄ-SG ;

LEXICON N_SÄYNÄ-PL
+N:%{front%} NMN_SÄYNÄ-PL ;

LEXICON N_KOIVU !!= @CODE@ koivu:koivu
!! Gradation NA
!! Harmony: Back
!! There are no changes in stem-final __u__
!! Plural stem in __loi__
+N: ATTESTED-NOUN-STEM ; 
        N_KOIVU-SG ;
        N_KOIVU-PL ;

LEXICON N_KOIVU-SG
+N:%{back%} NMN_KOIVU/HERY-SG        ;

LEXICON N_KOIVU-PL
+N:%{back%} NMN_KOIVU/HERY-PL        ;

LEXICON N_HERY !!= @CODE@ hery:hery
!! Gradation NA
!! Harmony: Front
!! There are no	changes	in stem-final __y__
!! Plural stem in __löi__
+N: ATTESTED-NOUN-STEM ; 
        N_HERY-SG ;
        N_HERY-PL ;

LEXICON N_HERY-SG
+N:%{front%} NMN_KOIVU/HERY-SG        ;

LEXICON N_HERY-PL
+N:%{front%} NMN_KOIVU/HERY-PL        ;
LEXICON N_IDY !!= @CODE@ idy:idy
!! Gradation Yes
!! Harmony: Front
!! There are no changes in stem-final __y__
!! Plural stem in __löi__
+N: ATTESTED-NOUN-STEM ;
        N_IDY-SG ;
        N_IDY-PL ;

LEXICON N_IDY-SG
+N:%{front%} NMN_LUGU/IDY-SG        ;

LEXICON N_IDY-PL
+N:%{front%} NMN_LUGU/IDY-PL        ;

LEXICON N_LUGU !!= @CODE@ lugu:lugu
!! Gradation Yes
!! Harmony: Back
!! There are no changes in stem-final __u__
!! Plural stem in __loi__
+N: ATTESTED-NOUN-STEM ;
        N_LUGU-SG ;
        N_LUGU-PL ;

LEXICON N_LUGU-SG
+N:%{back%} NMN_LUGU/IDY-SG        ;

LEXICON N_LUGU-PL
+N:%{back%} NMN_LUGU/IDY-PL        ;

LEXICON N_RUNO !!= @CODE@ runo:runo
!! Gradation NA
!! Harmony: Back
!! Stem-fianl o changes to u in Sg Par
!! Plural stem in __loi__
+N: ATTESTED-NOUN-STEM ;
	 N_RUNO-SG ;
	 N_RUNO-PL ;

LEXICON N_RUNO-SG
+N:%{back%} NMN_RUNO-SG   ;
LEXICON N_RUNO-PL
+N:%{back%} NMN_RUNO-PL   ;

LEXICON N_HÖRÖ !!= @CODE@ hörö:hörö
!! Gradation NA
!! Harmony: Front
+N: ATTESTED-NOUN-STEM ;
	 N_HÖRÖ-SG ;
	 N_HÖRÖ-PL ;

LEXICON N_HÖRÖ-SG
+N:%{front%} NMN_HÖRÖ-SG   ;
LEXICON N_HÖRÖ-PL
+N:%{front%} NMN_HÖRÖ-PL   ;

LEXICON N_RUADO !!= @CODE@ ruado:ruado
!! Gradation Yes
!! Harmony: Back
+N: ATTESTED-NOUN-STEM ;
	 N_RUADO-SG ;
	 N_RUADO-PL ;

LEXICON N_RUADO-SG
+N:%{back%} NMN_RUADO/KYNDÖ-SG   ;
LEXICON N_RUADO-PL
+N:%{back%} NMN_RUADO/KYNDÖ-PL   ;

LEXICON N_KYNDÖ !!= @CODE@ kyndö:kyndö
!! Gradation Yes
!! Harmony: Front
+N: ATTESTED-NOUN-STEM ;
	 N_KYNDÖ-SG ;
	 N_KYNDÖ-PL ;

LEXICON N_KYNDÖ-SG
+N:%{front%} NMN_RUADO/KYNDÖ-SG   ;
LEXICON N_KYNDÖ-PL
+N:%{front%} NMN_RUADO/KYNDÖ-PL   ;

!! !TWO-SYLLABLE VOWEL-FINAL STEM WITH UNIQUE +Nom+Sg VOWEL

LEXICON N_JÄLGI  !!= @CODE@ jälgi:jälg
!! Gradation Yes
!! Harmony: Front
+N:i ATTESTED-NOUN-STEM ;
 N_JÄLGI-SG ;
 N_JÄLGI-PL ;

LEXICON N_JÄLGI-SG
+N:%{front%} NMN_JÄLGI-SG ;
LEXICON N_JÄLGI-PL
+N:%{front%} NMN_JÄLGI-PL ;

LEXICON N_JOGI  !!= @CODE@ jogi:jog
!! Gradation Yes
!! Harmony: Back
+N:i ATTESTED-NOUN-STEM ;
 N_JOGI-SG ;
 N_JOGI-PL ;

LEXICON N_JOGI-SG
+N:%{back%} NMN_JOGI-SG ;
LEXICON N_JOGI-PL
+N:%{back%} NMN_JOGI-PL ;


LEXICON N_MUAMO !!= @CODE@ muamo:muama
!! Gradation NA
!! Harmony: Back
+N:%^A2O ATTESTED-NOUN-STEM ;
 N_MUAMO-SG ;
 N_MUAMO-PL ;

LEXICON N_MUAMO-SG
+N:%{back%} NMN_MUAMO-SG ;
LEXICON N_MUAMO-PL
+N:%{back%} NMN_MUAMO-PL ;


LEXICON N_TUATTO !!= @CODE@ tuatto:tuatta
!! Gradation Yes
!! Harmony: Back
+N:%^A2O ATTESTED-NOUN-STEM ;
 N_TUATTO-SG ;
 N_TUATTO-PL ;

LEXICON N_TUATTO-SG
+N:%{back%} NMN_TUATTO-SG ;
LEXICON N_TUATTO-PL
+N:%{back%} NMN_TUATTO-PL ;

LEXICON N_DIÄDÖ !!= @CODE@ diädö:diädä
!! Gradation No
!! Harmony: Front
+N:%^A2O ATTESTED-NOUN-STEM ;
 N_DIÄDÖ-SG ;
 N_DIÄDÖ-PL ;

LEXICON N_DIÄDÖ-SG
+N:%{front%} NMN_DIÄDÖ-SG ;
LEXICON N_DIÄDÖ-PL
+N:%{front%} NMN_DIÄDÖ-PL ;

LEXICON N_MUARJU !!= @CODE@ muarju:muarja
!! Gradation No
!! Harmony: Back
+N:%^A2UÄ2Y ATTESTED-NOUN-STEM ;
 N_MUARJU-SG ;
 N_MUARJU-PL ;

LEXICON N_MUARJU-SG
+N:%{back%} NMN_MUARJU-SG ;
LEXICON N_MUARJU-PL
+N:%{back%} NMN_MUARJU-PL ;


LEXICON N_AKKU !!= @CODE@ akku:akka
!! Gradation Yes
!! Harmony: Back
+N:%^A2UÄ2Y ATTESTED-NOUN-STEM ;
 N_AKKU-SG ;
 N_AKKU-PL ;

LEXICON N_AKKU-SG
+N:%{back%} NMN_AKKU-SG ;
LEXICON N_AKKU-PL
+N:%{back%} NMN_AKKU-PL ;

LEXICON N_KNIIGU !!= @CODE@ kniigu:kniiga
!! Gradation No
!! Harmony: Back
+N:%{back%}%^A2U ATTESTED-NOUN-STEM ;
 N_KNIIGU-SG ;
 N_KNIIGU-PL ;

LEXICON N_KNIIGU-SG
+N:%{back%} NMN_KNIIGU-SG ;
LEXICON N_KNIIGU-PL
+N:%{back%} NMN_KNIIGU-PL ;

LEXICON N_SULGU !!= @CODE@ sulgu:sulga
!! Gradation Yes
!! Harmony: Back
+N:%^A2U ATTESTED-NOUN-STEM ;
 N_SULGU-SG ;
 N_SULGU-PL ;

LEXICON N_SULGU-SG
+N:%{back%} NMN_SULGU-SG ;

LEXICON N_SULGU-PL
+N:%{back%} NMN_SULGU-PL ;



LEXICON N_KOIRU !!= @CODE@ koiru:koira
!! Gradation NA
!! Harmony: Back
+N:%^A2U ATTESTED-NOUN-STEM ;
 N_KOIRU-SG ;
 N_KOIRU-PL ;

LEXICON N_KOIRU-SG
+N:%{back%} NMN_KOIRU/PÄIVY-SG ;

LEXICON N_KOIRU-PL
+N:%{back%} NMN_KOIRU/PÄIVY-PL ;

LEXICON N_NIMI !!= @CODE@ nimi:nim
!! Gradation NA
!! Harmony: Front
+N:i ATTESTED-NOUN-STEM ;
	N_NIMI-SG  ;
	N_NIMI-PL  ;

LEXICON N_NIMI-SG
+N:%{front%} NMN_NIMI-SG ;

LEXICON N_NIMI-PL
+N:%{front%} NMN_NIMI-PL ;

LEXICON N_HANGI !!= @CODE@ hangi:hang
!! Gradation NA
!! Harmony: Back
+N:i ATTESTED-NOUN-STEM ;
	N_HANGI-SG  ;
	N_HANGI-PL  ;

LEXICON N_HANGI-SG
+N:%{back%} NMN_HANGI-SG ;

LEXICON N_HANGI-PL
+N:%{back%} NMN_HANGI-PL ;

LEXICON N_PÄIVY !!= @CODE@ päivy:päivä
!! Gradation NA
!! Harmony: Front
+N:%^A2U ATTESTED-NOUN-STEM ;
	N_PÄIVY-SG ;
	N_PÄIVY-PL ;

LEXICON N_PÄIVY-SG
+N:%{front%} NMN_KOIRU/PÄIVY-SG	;

LEXICON N_PÄIVY-PL
+N:%{front%} NMN_KOIRU/PÄIVY-PL	;

LEXICON N_MEČČY !!= @CODE@ meččy:meččä
!! Gradation Yes
!! Harmony: Front
+N:%^A2U ATTESTED-NOUN-STEM ;
 N_MEČČY-SG ;
 N_MEČČY-PL ;

LEXICON N_MEČČY-SG
+N:%{front%} NMN_MEČČY-SG ;
LEXICON N_MEČČY-PL
+N:%{front%} NMN_MEČČY-PL ;


LEXICON N_IŽÄNDY !!= @CODE@ ižändy:ižändä
!! Gradation No
!! Harmony: Front
+N:%^A2U ATTESTED-NOUN-STEM ;
	N_IŽÄNDY-SG ;
	N_IŽÄNDY-PL ;

LEXICON N_IŽÄNDY-SG
+N:%{front%} NMN_IŽÄNDY-SG	;

LEXICON N_IŽÄNDY-PL
+N:%{front%} NMN_IŽÄNDY-PL	;

LEXICON N_SIVE !!= @CODE@ sive:side
!! Gradation Yes
!! Harmony: Front
!+N: ATTESTED-NOUN-STEM ; Work needed
	N_SIVE-SG	;
	N_SIVE-PL	;
LEXICON N_SIVE-SG
+N:	NMN_SIVE-SG	;

LEXICON N_SIVE-PL
+N:	NMN_SIVE-PL	;

LEXICON N_HARDIE_SUB
+Err/Orth: N_HARDIE ;
LEXICON N_HARDIE !!= @CODE@ hardie:hardie
!! Gradation NA
!! Harmony: Back
+N: ATTESTED-NOUN-STEM ;
        N_HARDIE-SG ;
        N_HARDIE-PL ;

LEXICON N_HARDIE-SG
+N:%{back%} NMN_HARDIE-SG        ;

LEXICON N_HARDIE-PL
+N:%{back%} NMN_HARDIE-PL        ;

LEXICON N_KONDII !!= @CODE@ kondii:kondi
!! Gradation NA
!! Harmony: Back
+N:i ATTESTED-NOUN-STEM ;
        N_KONDII-SG ;
        N_KONDII-PL ;

LEXICON N_KONDII-SG
+N:%{back%} NMN_KONDII-SG        ;

LEXICON N_KONDII-PL
+N:%{back%} NMN_KONDII-PL        ;

LEXICON N_REBOI !!= @CODE@ reboi:reboi
!! Gradation No
!! Harmony: Back
+N: ATTESTED-NOUN-STEM ;
        N_REBOI-SG ;
        N_REBOI-PL ;
LEXICON N_REBOI-SG
+N:%{back%} NMN_REBOI-SG ;
LEXICON N_REBOI-PL
+N:%{back%} NMN_REBOI-PL ;

LEXICON N_PÖČÖI !!= @CODE@ pöčöi:pöččö
!! Gradation Yes
!! Harmony: Front
+N:%^WGStemi ATTESTED-NOUN-STEM ;
        N_PÖČÖI-SG ;
        N_PÖČÖI-PL ;
LEXICON N_PÖČÖI-SG
+N:%{front%} NMN_PÖČÖI-SG ;
LEXICON N_PÖČÖI-PL
+N:%{front%} NMN_PÖČÖI-PL ;

LEXICON N_VALGEI !!= @CODE@ valgei:valge
!! Gradation NA
!! Harmony: Back
+N:i ATTESTED-NOUN-STEM ;
        N_VALGEI-SG ;
        N_VALGEI-PL ;

LEXICON N_VALGEI-SG
+N:%{back%} NMN_VALGEI-SG        ;

LEXICON N_VALGEI-PL
+N:%{back%} NMN_VALGEI-PL        ;

LEXICON N_OSTAI !!= @CODE@ ostai:osta
!! Gradation NA
!! Harmony: Back
+N:i ATTESTED-NOUN-STEM ;
        N_OSTAI-SG ;
        N_OSTAI-PL ;

LEXICON N_OSTAI-SG
+N:%>%{ijPRC%} NMN_OSTAI-SG        ;

LEXICON N_OSTAI-PL
+N:%>%{ijPRC%} NMN_OSTAI-PL        ;

LEXICON N_PEZII !!= @CODE@ pezii:pezi
!! Gradation NA
!! Harmony: Front
+N:i ATTESTED-NOUN-STEM ;
        N_PEZII-SG ;
        N_PEZII-PL ;

LEXICON N_PEZII-SG
+N:%>%{ijPRC%} NMN_PEZII-SG        ;

LEXICON N_PEZII-PL
+N:%>%{ijPRC%} NMN_PEZII-PL        ;


LEXICON N_KESTÄY !!= @CODE@ kestäy:kestä
!! Gradation NA
!! Harmony: Front
+N:y ATTESTED-NOUN-STEM ;
        N_KESTÄY-SG ;
        N_KESTÄY-PL ;

LEXICON N_KESTÄY-SG
+N:%> NMN_KESTÄY-SG        ;

LEXICON N_KESTÄY-PL
+N:%> NMN_KESTÄY-PL        ;

!! !TWO-SYLLABLE VOWEL-FINAL STEMS WITH CONSONANT-FINAL PARTITIVE STEM
LEXICON N_UKSI !!= @CODE@ uksi:uks
!! Gradation NA
!! Harmony: Back
+N:i ATTESTED-NOUN-STEM ;
	N_UKSI-SG ;
	N_UKSI-PL ;
LEXICON N_UKSI-SG
+N:%{back%} NMN_UKSI-SG ;
LEXICON N_UKSI-PL
+N:%{back%} NMN_UKSI-PL ;

LEXICON N_SUARI !!= @CODE@ suari:suar
!! Harmony: Back
:%{back%} N_SUARI/KIELI_01 ;
LEXICON N_SUARI-PL !!= @CODE@ suari:suar
!! Harmony: Back
:%{back%} N_SUARI/KIELI-PL ;
LEXICON N_KIELI !!= @CODE@ kieli:kiel
!! Harmony: Front
:%{front%} N_SUARI/KIELI_01 ;
LEXICON N_SUARI/KIELI_01 !!= @CODE@ kieli:kiel
!! Gradation No
+N:i ATTESTED-NOUN-STEM ;
 N_SUARI/KIELI-SG ;
 N_SUARI/KIELI-PL ;

LEXICON N_SUARI/KIELI-SG
+N:%{front%} 	NMN_SUARI/KIELI-SG ;
LEXICON N_SUARI/KIELI-PL
+N:%{front%} 	NMN_SUARI/KIELI-PL ;

LEXICON N_LAPSI !!= @CODE@ lapsi:laps lapsi:laps
!! Gradation NA
!! Harmony: Back
+N:i ATTESTED-NOUN-STEM ;
        N_LAPSI-SG  ;
        N_LAPSI-PL  ;

LEXICON N_LAPSI-SG
+N:%{back%} NMN_LAPSI-SG ;

LEXICON N_LAPSI-PL
+N:%{back%} NMN_LAPSI-PL ;

LEXICON N_VEZI !!= @CODE@ vezi:ved
!! Gradation NA
!! Harmony: Front
+N:%^D2Z%>i ATTESTED-NOUN-STEM ;
 N_VEZI-SG ;
 N_VEZI-PL ;

LEXICON N_VEZI-SG
+N:%{front%} NMN_VEZI-SG ;

LEXICON N_VEZI-PL
+N:%{front%} NMN_VEZI-PL ;

LEXICON N_SUZI !!= @CODE@ suzi:su
!! Gradation NA
!! Harmony: Back
+N:zi ATTESTED-NOUN-STEM ;
 N_SUZI-SG ;
 N_SUZI-PL ;

LEXICON N_SUZI-SG
+N:%{back%} NMN_SUZI-SG ;

LEXICON N_SUZI-PL
+N:%{back%} NMN_SUZI-PL ;

LEXICON N_VUOZI !!= @CODE@ vuozi:vuod
!! Gradation NA
!! Harmony: Back
+N:%^D2Z%>i ATTESTED-NOUN-STEM ;
 N_VUOZI-SG        ;
 N_VUOZI-PL        ;

LEXICON N_VUOZI-SG
+N:%{back%} NMN_VUOZI-SG	;
LEXICON N_VUOZI-PL
+N:%{back%} NMN_VUOZI-PL	;

!! !THREE-SYLLABLE VOWEL-FINAL STEMS
LEXICON N_SYGYZY !!= @CODE@ sygyzy:sygyzy
!! Gradation NA
!! Harmony: Front
+N: ATTESTED-NOUN-STEM ;
        N_SYGYZY-SG ;
        N_SYGYZY-PL ;

LEXICON N_SYGYZY-SG
+N:%{front%} NMN_SYGYZY-SG        ;

LEXICON N_SYGYZY-PL
+N:%{front%} NMN_SYGYZY-PL        ;

LEXICON N_VASKIČČU !!= @CODE@ vaskičču:vaskičča
!! Gradation Yes
!! Harmony: Back
+N:%^A2U ATTESTED-NOUN-STEM ;
        N_VASKIČČU-SG ;
        N_VASKIČČU-PL ;

LEXICON N_VASKIČČU-SG
+N:%{back%} NMN_VASKIČČU-SG        ;

LEXICON N_VASKIČČU-PL
+N:%{back%} NMN_VASKIČČU-PL        ;

!! !THREE-SYLLABLE STEMS WITH TWO-SYLLABLE NOMINATIVE SINGULAR
LEXICON N_KARJAL !!= @CODE@ karjal:karjal
!! Gradation NA
!! Harmony: Back
+N: ATTESTED-NOUN-STEM ;
	N_KARJAL-SG ;
	N_KARJAL-PL ;
LEXICON N_KARJAL-SG !!= @CODE@ karjal:karjal
+N:	NMN_KARJAL-SG ;
LEXICON N_KARJAL-PL !!= @CODE@ karjal:karjal
+N:	NMN_KARJAL-PL ;

LEXICON N_MADAL !!= @CODE@ madal:madal
!! Gradation No
!! Harmony: Back
+N: ATTESTED-NOUN-STEM ;
	N_MADAL-SG ;
	N_MADAL-PL ;
LEXICON N_MADAL-SG !!= @CODE@ madal:madal
+N:	NMN_MADAL-SG ;
LEXICON N_MADAL-PL !!= @CODE@ madal:madal
+N:	NMN_MADAL-PL ;

LEXICON N_PIIRAI !!= @CODE@ piirai:piira CHECK THIS
!! Gradation NA
!! Harmony: Back
+N:i ATTESTED-NOUN-STEM ;
        N_PIIRAI-SG ;
        N_PIIRAI-PL ;

LEXICON N_PIIRAI-SG
+N:%{back%} NMN_PIIRAI-SG ;

LEXICON N_PIIRAI-PL
+N:%{back%} NMN_PIIRAI-PL ;


LEXICON N_KANDAI !!= @CODE@ kandai:kanda
!! Gradation NA
!! Harmony: Back
+N:i ATTESTED-NOUN-STEM ;
        N_KANDAI-SG ;
        N_KANDAI-PL ;

LEXICON N_KANDAI-SG
+N:%{back%} NMN_KANDAI-SG ;

LEXICON N_KANDAI-PL
+N:%{back%} NMN_KANDAI-PL ;

LEXICON  N_AVUAJU !!= @CODE@ 
!! Gradation NA
!! Harmony: Back
!!+N:%^A2U ATTESTED-NOUN-STEM ; Work needed
+N:%>%{ijPRC%}a NMN_KOIRU/PÄIVY ;

LEXICON  N_KERIÄJY !!= @CODE@ piästäjy:piästä
!! Gradation NA
!! Harmony: Front
!!+N:%^A2U ATTESTED-NOUN-STEM ; Work needed
+N:%>%{ijPRC%}ä NMN_KOIRU/PÄIVY ;


LEXICON N_PAGIZII !!= @CODE@ pagizii:pagizi
!! Gradation NA
!! Harmony: Back
+N:i ATTESTED-NOUN-STEM ;
 N_PAGIZII-SG ;
 N_PAGIZII-PL ;
LEXICON N_PAGIZII-SG !!= @CODE@ 
+N:%>%{ijPRC%} NMN_PAGIZII-SG ;
LEXICON N_PAGIZII-PL !!= @CODE@ 
+N:%>%{ijPRC%} NMN_PAGIZII-PL ;

LEXICON N_HUOLETOI !!= @CODE@ huoletoi: huolet
!! Gradation Yes
!! Harmony: Back
!+N:%^A2U ATTESTED-NOUN-STEM ; Work needed
 N_HUOLETOI-SG ;
 N_HUOLETOI-PL ;

LEXICON N_HUOLETOI-SG
+N:%{back%} NMN_HUOLETOI/JIÄTÖI-SG ;

LEXICON N_HUOLETOI-PL
+N:%{back%} NMN_HUOLETOI/JIÄTÖI-PL ;


LEXICON N_SAMMAL !!= @CODE@ sammal:sammal
!! Gradation No
!! Harmony: Back
+N: ATTESTED-NOUN-STEM ;
 N_SAMMAL-SG ;
 N_SAMMAL-PL ;

LEXICON N_SAMMAL-SG
+N:%{back%} NMN_SAMMAL-SG ;

LEXICON N_SAMMAL-PL
+N:%{back%} NMN_SAMMAL-PL ;

LEXICON N_TVER !!= @CODE@ tver:tver
 N_TVER-SG ;
 N_TVER-PL ;

LEXICON N_TVER-SG
+N:%{front%} NMN_TVER-SG ;

LEXICON N_TVER-PL
+N:%{front%} NMN_TVER-PL ;

LEXICON N_ŠOUFER !!= @CODE@ šoufer:šoufer
!! Vowel Harmony: Back
 N_ŠOUFER-SG ;
 N_ŠOUFER-PL ;

LEXICON N_ŠOUFER-SG
+N:%{back%} NMN_ŠOUFER-SG ;

LEXICON N_ŠOUFER-PL
+N:%{back%} NMN_ŠOUFER-PL ;

LEXICON N_VANUIN !!= @CODE@ vanuin:vanui
!! Gradation Yes
!! Harmony: Back
!! Stem	     consonant n/m
!! Singular stem vowel 0/0/e
!! Plural stem vowel i
+N:n ATTESTED-NOUN-STEM ;
 N_VANUIN-SG ;
 N_VANUIN-PL ;

LEXICON N_VANUIN-SG
+N:%{back%} NMN_VANUIN-SG ;

LEXICON N_VANUIN-PL
+N:%{back%} NMN_VANUIN-PL ;

LEXICON N_SAMMUTIN !!= @CODE@ sammutin:sammutti
!! Gradation Yes
!! Harmony: Back
!! Stem consonant n/m
!! Singular stem vowel 0/0/e
!! Plural stem vowel i
+N:%^WGStem%>n ATTESTED-NOUN-STEM ;
 N_SAMMUTIN-SG ;
 N_SAMMUTIN-PL ;

LEXICON N_SAMMUTIN-SG
+N:%{back%} NMN_SAMMUTIN-SG ;

LEXICON N_SAMMUTIN-PL
+N:%{back%} NMN_SAMMUTIN-PL ;

LEXICON N_KEITIN !!= @CODE@ keitin:sammutti
!! Gradation Yes
!! Harmony: Front
!! Stem consonant n/m
!! Singular stem vowel 0/0/e
!! Plural stem vowel i
+N:%^WGStem%>n ATTESTED-NOUN-STEM ;
 N_KEITIN-SG ;
 N_KEITIN-PL ;

LEXICON N_KEITIN-SG
+N:%{front%} NMN_KEITIN-SG ;

LEXICON N_KEITIN-PL
+N:%{front%} NMN_KEITIN-PL ;

LEXICON N_LÄMMIN !!= @CODE@ lämmin:lämbi
!! Gradation Yes
!! Harmony: Front
+N:%^WGStem%>n ATTESTED-NOUN-STEM ;
 N_LÄMMIN-SG ;
 N_LÄMMIN-PL ;

LEXICON N_LÄMMIN-SG
+N:%{front%} NMN_LÄMMIN-SG ;

LEXICON N_LÄMMIN-PL
+N:%{front%} NMN_LÄMMIN-PL ;

LEXICON N_TAIGIN !!= @CODE@ taigin:taigin
!! Gradation No
!! Harmony: Back
+N: ATTESTED-NOUN-STEM ;
 N_TAIGIN-SG ;
 N_TAIGIN-PL ;

LEXICON N_TAIGIN-SG
+N:%{back%} NMN_TAIGIN-SG ;

LEXICON N_TAIGIN-PL
+N:%{back%} NMN_TAIGIN-PL ;

LEXICON N_KARJALAINE !!= @CODE@ karjalaine:karjala
!! Gradation NA
!! Harmony: Back
+N:s ATTESTED-NOUN-STEM ;
 N_KARJALAINE-SG     ;
 N_KARJALAINE-PL     ;

LEXICON N_KARJALAINE-SG
+N:%{back%} NMN_KARJALAINE/LIYGILÄINE-SG	 ;

LEXICON N_KARJALAINE-PL
+N:%{back%} NMN_KARJALAINE/LIYGILÄINE-PL	 ;

LEXICON N_LIYGILÄINE !!= @CODE@ liygiläine:liygilä
!! Gradation NA
!! Harmony: Front
+N:s ATTESTED-NOUN-STEM ;
 N_LIYGILÄINE-SG     ;
 N_LIYGILÄINE-PL     ;

LEXICON N_LIYGILÄINE-SG
+N:%{front%} NMN_KARJALAINE/LIYGILÄINE-SG	 ;

LEXICON N_LIYGILÄINE-PL
+N:%{front%} NMN_KARJALAINE/LIYGILÄINE-PL	 ;

LEXICON N_NAINE !!= @CODE@ naine:nai
!! Gradation NA
!! Harmony: Back
+N:s ATTESTED-NOUN-STEM ;
 N_NAINE-SG     ;
 N_NAINE-PL     ;

LEXICON N_NAINE-SG
+N:%{back%}    NMN_NAINE-SG         ;
LEXICON N_NAINE-PL
+N:%{back%}    NMN_NAINE/KIELINE-PL         ;

LEXICON N_KIELINE !!= @CODE@ kieline:kieli
!! Gradation NA
!! Harmony: Front
+N:s ATTESTED-NOUN-STEM ;
 N_KIELINE-SG     ;
 N_KIELINE-PL     ;

LEXICON N_KIELINE-SG
+N:%{front%}    NMN_NAINE/KIELINE-SG         ;
LEXICON N_KIELINE-PL
+N:%{front%}    NMN_NAINE/KIELINE-PL         ;

LEXICON N_TOINE !!= @CODE@ toine:to
!! Gradation NA
!! Harmony: Back
!+N:%^A2U ATTESTED-NOUN-STEM ; Work needed
 N_TOINE-SG     ;
 N_TOINE-PL     ;

LEXICON N_TOINE-SG
+N:%{back%} NMN_TOINE-SG         ;

LEXICON N_TOINE-PL
+N:%{back%} NMN_TOINE-PL         ;



LEXICON N_RAIŠ !!= @CODE@ raiš:ra
!! Gradation Yes
!! Harmony: Back
!+N:%^A2U ATTESTED-NOUN-STEM ; Work needed
	N_RAIŠ-SG	;
	N_RAIŠ-PL	;
LEXICON N_RAIŠ-SG
+N:	NMN_RAIŠ-SG	;

LEXICON N_RAIŠ-PL
+N:	NMN_RAIŠ-PL	;

LEXICON N_PEREH !!= @CODE@ pereh:pereh
!! Gradation No
!! Harmony: Front
+N: ATTESTED-NOUN-STEM ;
	N_PEREH-SG	;
	N_PEREH-PL	;
LEXICON N_PEREH-SG
+N:	NMN_PEREH-SG	;

LEXICON N_PEREH-PL
+N:	NMN_PEREH-PL	;

LEXICON N_LIIKEH !!= @CODE@ liikeh:liikkeh
!! Gradation Yes
!! Harmony: Front
+N:%^WGStem ATTESTED-NOUN-STEM ;
	N_LIIKEH-SG	;
	N_LIIKEH-PL	;
LEXICON N_LIIKEH-SG
+N:	NMN_LIIKEH-SG	;

LEXICON N_LIIKEH-PL
+N:	NMN_LIIKEH-PL	;

LEXICON N_PENGER !!= @CODE@ penger:penger
!! Gradation No
!! Harmony: Front
+N: ATTESTED-NOUN-STEM ;
	N_PENGER-SG	;
	N_PENGER-PL	;
LEXICON N_PENGER-SG
+N:	NMN_PENGER-SG	;

LEXICON N_PENGER-PL
+N:	NMN_PENGER-PL	;

LEXICON N_ARTELI !!= @CODE@ arteli:arteli
!! Gradation No
!! Harmony: Back
+N:%{back%} ATTESTED-NOUN-STEM ;
	N_ARTELI-SG ;
	N_ARTELI-PL ;
LEXICON N_ARTELI-SG
+N:%{back%} NMN_ARTELI-SG ;
LEXICON N_ARTELI-PL
+N:%{back%} NMN_ARTELI-PL ;

LEXICON N_PUHTAHUS !!= @CODE@ puhtahus:puhtahu
!! Harmony: Back
!! Gradation NA
+N:%{back%}%>s ATTESTED-NOUN-STEM ;
	N_PUHTAHUS-SG ;
        N_PUHTAHUS-PL ;
LEXICON N_PUHTAHUS-SG
+N:%{back%} NMN_PUHTAHUS/XX-SG ;
LEXICON N_PUHTAHUS-PL
+N:%{back%} NMN_PUHTAHUS/XX-PL ;

LEXICON N_AIGOMUS
 N_ALUS ;

LEXICON N_ALUS !!= @CODE@ alus:aluks
!! Gradation No
!! Harmony: Back
+N:%^KS2S ATTESTED-NOUN-STEM ;
 N_ALUS-SG ;
 N_ALUS-PL ;

LEXICON N_ALUS-SG !!= @CODE@ alus:aluks
+N:%{back%} NMN_ALUS/ILVES-SG ;

LEXICON N_ALUS-PL !!= @CODE@ alus:aluks
+N:%{back%} NMN_ALUS/ILVES-PL ;

LEXICON N_ILVES !!= @CODE@ ilves:ilveks
!! Gradation No
!! Harmony: Front
+N:%^KS2S ATTESTED-NOUN-STEM ;
 N_ILVES-SG ;
 N_ILVES-PL ;

LEXICON N_ILVES-SG !!= @CODE@ ilves:ilveks
+N:%{front%} NMN_ALUS/ILVES-SG ;

LEXICON N_ILVES-PL !!= @CODE@ ilves:ilveks
+N:%{front%} NMN_ALUS/ILVES-PL ;

LEXICON N_MIES !!= @CODE@ mies:mie
!! Gradation No
!! Harmony: Front
+N:s ATTESTED-NOUN-STEM ;
 N_MIES-SG ;
 N_MIES-PL ;

LEXICON N_MIES-SG !!= @CODE@ mies:mie
+N:%{front%} NMN_MIES-SG ;

LEXICON N_MIES-PL !!= @CODE@ mies:mie
+N:%{front%} NMN_MIES-PL ;

LEXICON N_KUURNIS !!= @CODE@ kuurnis:kuurne
!! Gradation NA
!! Harmony: Back
+N:%>is ATTESTED-NOUN-STEM ;
	N_KUURNIS-SG ;
        N_KUURNIS-PL ;
LEXICON N_KUURNIS-SG
+N:%{back%} NMN_KUURNIS-SG ;
LEXICON N_KUURNIS-PL
+N:%{back%} NMN_KUURNIS-PL ;

LEXICON N_SUALIŠ !!= @CODE@ suališ:suali
!! Gradation NA
!! Harmony: Back
+N:š ATTESTED-NOUN-STEM ;
 N_SUALIŠ-SG ;
 N_SUALIŠ-PL ;

LEXICON N_SUALIŠ-SG !!= @CODE@ suališ:suali
+N:%{back%} NMN_SUALIŠ-SG ;

LEXICON N_SUALIŠ-PL !!= @CODE@ suališ:suali
+N:%{back%} NMN_SUALIŠ-PL ;

LEXICON N_OLUT !!= @CODE@ olut:olu
!! Gradation No
!! Harmony: Back
+N:t ATTESTED-NOUN-STEM ;
 N_OLUT-SG ;
 N_OLUT-PL ;

LEXICON N_OLUT-SG
+N:%{back%} NMN_OLUT-SG ;

LEXICON N_OLUT-PL
+N:%{back%} NMN_OLUT-PL ;

LEXICON N_KEVÄT !!= @CODE@ kevät:kevä
!! Gradation No
!! Harmony: Front
+N:t ATTESTED-NOUN-STEM ;
 N_KEVÄT-SG ;
 N_KEVÄT-PL ;

LEXICON N_KEVÄT-SG
+N:%{front%} NMN_KEVÄT-SG ;

LEXICON N_KEVÄT-PL
+N:%{front%} NMN_KEVÄT-PL ;

LEXICON N_TUHAT !!= @CODE@ tuhat:tuha
!! Gradation No
!! Harmony: Back
!+N: ATTESTED-NOUN-STEM ; Work needed
 N_TUHAT-SG ;
 N_TUHAT-PL ;

LEXICON N_TUHAT-SG
+N:%{back%} NMN_TUHAT-SG ;

LEXICON N_TUHAT-PL
+N:%{back%} NMN_TUHAT-PL ;

LEXICON N_ARMAS !!= @CODE@ armas:arma
!! Gradation NA
!! Harmony: Back
+N:s ATTESTED-NOUN-STEM ;
    N_ARMAS-SG        ;
    N_ARMAS-PL        ;
LEXICON N_ARMAS-SG
+N:%{back%} NMN_ARMAS-SG	;
LEXICON N_ARMAS-PL
+N:%{back%} NMN_ARMAS-PL	;

LEXICON N_VARVAS !!= @CODE@ varvas:varba
!! Gradation NA
!! Harmony: Back
+N:%^WGStem%>s ATTESTED-NOUN-STEM ;
    N_VARVAS-SG        ;
    N_VARVAS-PL        ;
LEXICON N_VARVAS-SG
+N:%{back%} NMN_VARVAS-SG	;
LEXICON N_VARVAS-PL
+N:%{back%} NMN_VARVAS-PL	;


LEXICON N_PAREMBI !!= @CODE@ parembi:paremb
!! Gradation NA
!! Harmony: Back
!+N:i ATTESTED-NOUN-STEM ; Work needed
 N_PAREMBI-SG	  ;
 N_PAREMBI-PL	  ;

LEXICON N_PAREMBI-SG
+N:%{back%} NMN_PAREMBI-SG   ;
LEXICON N_PAREMBI-PL
+N:%{back%} NMN_PAREMBI-PL   ;

LEXICON N_PESSYH !!= @CODE@ pessyh:pessy
!! Gradation NA
!! Harmony: Front
!+N: ATTESTED-NOUN-STEM ; Work needed
 N_PESSYH-SG ;
 N_PESSYH-PL ;
LEXICON N_PESSYH-SG
+N:%{front%} NMN_PESSYH-SG ;
LEXICON N_PESSYH-PL
+N:%{front%} NMN_PESSYH-PL ;

LEXICON N_TULLUH !!= @CODE@ tulluh
!! Gradation NA
!! Harmony: Front
!+N: ATTESTED-NOUN-STEM ; Work needed
 N_TULLUH-SG ;
 N_TULLUH-PL ;
LEXICON N_TULLUH-SG
+N:%{back%} NMN_TULLUH-SG ;
LEXICON N_TULLUH-PL
+N:%{back%} NMN_TULLUH-PL ;



LEXICON N_BEMMEL !!= @CODE@ bemmel:bembel
!! Gradation Yes
!! Harmony: Front
+N:%^WGStem ATTESTED-NOUN-STEM ;
    N_BEMMEL-SG        ;
    N_BEMMEL-PL        ;
LEXICON N_BEMMEL-SG
+N:%{front%} NMN_BEMMEL-SG	;
LEXICON N_BEMMEL-PL
+N:%{front%} NMN_BEMMEL-PL	;

LEXICON N_VIDEL !!= @CODE@ videl:videl
!! Gradation NA
!! Harmony: Front
+N: ATTESTED-NOUN-STEM ;
    N_VIDEL-SG        ;
    N_VIDEL-PL        ;
LEXICON N_VIDEL-SG
+N:%{front%} NMN_VIDEL-SG	;
LEXICON N_VIDEL-PL
+N:%{front%} NMN_VIDEL-PL	;



LEXICON N_SUURIM !!= @CODE@ suurim:suurim
!! Gradation NA
!! Harmony: Back
+N: ATTESTED-NOUN-STEM ;
        N_SUURIM-SG  ;
        N_SUURIM-PL  ;

LEXICON N_SUURIM-SG
+N:%{back%} NMN_SUURIM-SG ;

LEXICON N_SUURIM-PL
+N:%{back%} NMN_SUURIM-PL ;

LEXICON N_TUATINDAM !!= @CODE@ tuatindam:tuatindam
!! Gradation NA
!! Harmony: Back
+N: ATTESTED-NOUN-STEM ;
        N_TUATINDAM-SG  ;
        N_TUATINDAM-PL  ;

LEXICON N_TUATINDAM-SG
+N:%{back%} NMN_TUATINDAM-SG ;

LEXICON N_TUATINDAM-PL
+N:%{back%} NMN_TUATINDAM-PL ;


LEXICON N_TUATANDIM !!= @CODE@ tuatandim:tuatandim
!! Gradation NA
!! Harmony: Back
+N: ATTESTED-NOUN-STEM ;
        N_TUATANDIM-SG  ;
        N_TUATANDIM-PL  ;

LEXICON N_TUATANDIM-SG
+N:%{back%} NMN_TUATANDIM-SG ;

LEXICON N_TUATANDIM-PL
+N:%{back%} NMN_TUATANDIM-PL ;

LEXICON N_TYTÄRDIM !!= @CODE@ tytärdim:tytärdim
!! Gradation NA
!! Harmony: Front
+N: ATTESTED-NOUN-STEM ;
        N_TYTÄRDIM-SG  ;
        N_TYTÄRDIM-PL  ;

LEXICON N_TYTÄRDIM-SG
+N:%{front%} NMN_TYTÄRDIM-SG ;

LEXICON N_TYTÄRDIM-PL
+N:%{front%} NMN_TYTÄRDIM-PL ;

LEXICON N_SIEMEN !!= @CODE@ siemen:siemen
!! Gradation NA
!! Harmony: Front
+N: ATTESTED-NOUN-STEM ;
        N_SIEMEN-SG  ;
        N_SIEMEN-PL  ;

LEXICON N_SIEMEN-SG
        N_PAIMEN-SG  ;
LEXICON N_SIEMEN-PL
        N_PAIMEN-PL  ;


LEXICON N_PAIMEN !!= @CODE@ paimen:paimen
!! Gradation NA
!! Harmony: Back
+N: ATTESTED-NOUN-STEM ;
        N_PAIMEN-SG  ;
        N_PAIMEN-PL  ;

LEXICON N_PAIMEN-SG
+N:%{back%} NMN_PIENAR-SG ;

LEXICON N_PAIMEN-PL
+N:%{back%} NMN_PIENAR-PL ;


LEXICON N_SALBOIN !!= @CODE@ salboin:salboi
!! Gradation No
!! Harmony: Back
+N:n ATTESTED-NOUN-STEM ;
        N_SALBOIN-SG  ;
        N_SALBOIN-PL  ;

LEXICON N_SALBOIN-SG
+N:%{back%} NMN_SALBOIN-SG ;

LEXICON N_SALBOIN-PL
+N:%{back%} NMN_SALBOIN-PL ;


LEXICON N_ENIN !!= @CODE@ enin:eni
!! Gradation NA
!! Harmony: Front
+N:n ATTESTED-NOUN-STEM ;
        N_ENIN-SG  ;
        N_ENIN-PL  ;

LEXICON N_ENIN-SG
+N:%{front%} NMN_ENIN-SG ;

LEXICON N_ENIN-PL
+N:%{front%} NMN_ENIN-PL ;

LEXICON N_UDAR !!= @CODE@ udar:udar
!! Gradation No
!! Harmony: Back
+N: ATTESTED-NOUN-STEM ;
        N_UDAR-SG  ;
        N_UDAR-PL  ;

LEXICON N_UDAR-SG
+N:%{back%} NMN_UDAR-SG ;

LEXICON N_UDAR-PL
+N:%{back%} NMN_UDAR-PL ;


LEXICON N_PIENAR !!= @CODE@ pienar:piendar
!! Gradation Yes
!! Harmony: Back
+N:%^WGStem ATTESTED-NOUN-STEM ;
        N_PIENAR-SG  ;
        N_PIENAR-PL  ;

LEXICON N_PIENAR-SG
+N:%{back%} NMN_PIENAR-SG ;

LEXICON N_PIENAR-PL
+N:%{back%} NMN_PIENAR-PL ;

LEXICON N_TYTÄR !!= @CODE@ tytär:tyttär
!! Gradation Yes
!! Harmony: Front
+N:%^WGStem ATTESTED-NOUN-STEM ;
        N_TYTÄR-SG  ;
        N_TYTÄR-PL  ;

LEXICON N_TYTÄR-SG
+N:%{front%} NMN_TYTÄR-SG ;

LEXICON N_TYTÄR-PL
+N:%{front%} NMN_TYTÄR-PL ;

!! !NOMINAL DECLENSION BEGINS

LEXICON NMN_MUAMO !!= @CODE@ muamo:muama
!! Back vowel
!! gradation NA
 NMN_MUAMO-SG ;
 NMN_MUAMO-PL ;

LEXICON NMN_MUAMO-SG
:%^A2O SGNOMSUF ;
 SGNOM/PXSP3 ;
 SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01-USUALLY-WEAK ;
+Sg: Noun-NB-01-USUALLY-STRONG ;
LEXICON NMN_MUAMO-PL
 PLNOMSUF-USUALLY-WEAK ;
:%>i PL-GEN/COM/APRSUF_N ;			! muamoloin
:%>i PLPARSUF_Zero ;
:%>i PLINSSUF ;
+Pl:%>i Noun-NB-01-USUALLY-WEAK ;
+Pl:%>i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_TUATTO !!= @CODE@ tuatto:tuatta
!! Back vowel
!! gradation Yes
 NMN_TUATTO-SG ;
 NMN_TUATTO-PL ;

LEXICON NMN_TUATTO-SG
:%^A2O SGNOMSUF ;
 SGNOM/PXSP3 ;
:%^WGStem SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg:%^WGStem Noun-NB-01-USUALLY-WEAK ;
+Sg: Noun-NB-01-USUALLY-STRONG ;
LEXICON NMN_TUATTO-PL
:%^WGStem PLNOMSUF-USUALLY-WEAK ;
:%>i PL-GEN/COM/APRSUF_N ; 
:%>i PLPARSUF_Zero ;
:%>i%^WGStem PLINSSUF ;
+Pl:%>i%^WGStem Noun-NB-01-USUALLY-WEAK ;
+Pl:%>i Noun-NB-01-USUALLY-STRONG ;

:%^A2O%>l%{oö%}i PL-GEN/COM/APRSUF_N ;			! tuattol%{oö%}in
:%^A2O%>l%{oö%}i PLPARSUF_Zero ;
:%^A2O%>l%{oö%}i%^WGStem PLINSSUF ;
+Pl:%^A2O%>l%{oö%}i%^WGStem Noun-NB-01-USUALLY-WEAK ;
+Pl:%^A2O%>l%{oö%}i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_DIÄDÖ !!= @CODE@ diädö:diädä
 NMN_DIÄDÖ-SG ;
 NMN_DIÄDÖ-PL ;

LEXICON NMN_DIÄDÖ-SG
:%^A2O SGNOMSUF ;
 SGNOM/PXSP3 ;
 SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01-USUALLY-WEAK ;
+Sg: Noun-NB-01-USUALLY-STRONG ;
LEXICON NMN_DIÄDÖ-PL 
 PLNOMSUF-USUALLY-WEAK ;
:%>i PL-GEN/COM/APRSUF_N ; 
:%>i PLPARSUF_Zero ;
:%>i PLINSSUF ;
+Pl:%>i Noun-NB-01-USUALLY-WEAK ;
+Pl:%>i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_MUARJU !!= @CODE@ muarju:muarja
!! gradation NA
 NMN_MUARJU-SG ;
 NMN_MUARJU-PL ;

LEXICON NMN_MUARJU-SG
:%^A2UÄ2Y SGNOMSUF ;
 SGNOM/PXSP3 ;
 SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01-USUALLY-WEAK ;
+Sg: Noun-NB-01-USUALLY-STRONG ;
LEXICON NMN_MUARJU-PL
 PLNOMSUF-USUALLY-WEAK ;
:%^A2O%>i PL-GEN/COM/APRSUF_N ; 
:%^A2O%>i PLPARSUF_Zero ;
:%^A2O%>i PLINSSUF ;
+Pl:%^A2O%>i Noun-NB-01-USUALLY-WEAK ;
+Pl:%^A2O%>i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_AKKU !!= @CODE@ akku:akka
!! gradation Yes
 NMN_AKKU-SG ;
 NMN_AKKU-PL ;

LEXICON NMN_AKKU-SG
:%^A2UÄ2Y SGNOMSUF ;
 SGNOM/PXSP3 ;
:%^WGStem SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg:%^WGStem Noun-NB-01-USUALLY-WEAK ;
+Sg: Noun-NB-01-USUALLY-STRONG ;
LEXICON NMN_AKKU-PL
:%^WGStem PLNOMSUF-USUALLY-WEAK ;
:%^A2O%>i PL-GEN/COM/APRSUF_N ; 
:%^A2O%>i PLPARSUF_Zero ;
:%^A2O%>i%^WGStem PLINSSUF ;
+Pl:%^A2O%>i%^WGStem Noun-NB-01-USUALLY-WEAK ;
+Pl:%^A2O%>i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_KNIIGU !!= @CODE@ kniigu:kniiga
!! gradation No
 NMN_KNIIGU-SG ;
 NMN_KNIIGU-PL ;

LEXICON NMN_KNIIGU-SG
:%^A2UÄ2Y SGNOMSUF ;
 SGNOM/PXSP3 ;
 SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01-USUALLY-WEAK ;
+Sg: Noun-NB-01-USUALLY-STRONG ;
LEXICON NMN_KNIIGU-PL
 PLNOMSUF-USUALLY-WEAK ;
:%^A2O%>i PL-GEN/COM/APRSUF_N ; 
:%^A2O%>i PLPARSUF_Zero ;
:%^A2O%>i PLINSSUF ;
+Pl:%^A2O%>i Noun-NB-01-USUALLY-WEAK ;
+Pl:%^A2O%>i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_LAPSI !!= @CODE@ lapsi:laps lapsi:laps
        NMN_LAPSI-SG  ;
	NMN_LAPSI-PL  ;

LEXICON NMN_LAPSI-SG
:i SGNOMSUF ;
:e SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:%^KS2S SGPARSUF_dU ;
+Sg:e Noun-NB-01-USUALLY-WEAK ;
+Sg:e Noun-NB-01-USUALLY-STRONG ;
LEXICON NMN_LAPSI-PL
:e PLNOMSUF-USUALLY-WEAK ;
:%>i PL-GEN/COM/APRSUF_EN ; 
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_VEZI !!= @CODE@ vezi:ved
 NMN_VEZI-SG ;
 NMN_VEZI-PL ;

LEXICON NMN_VEZI-SG
:%^D2Z%>i SGNOMSUF ;
:e%^WGStem SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
: SGPARSUF_dU ;
+Sg:e%^WGStem Noun-NB-01-USUALLY-WEAK ;
+Sg:e Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_VEZI-PL
:e%^WGStem PLNOMSUF-USUALLY-WEAK ;
:%^D2Z%>i PL-GEN/COM/APRSUF_EN ;		! vezien
!:%^D2Z%>i PLPARSUF_Zero ;
:%^D2Z%>i PLINSSUF ;
+Pl:%^D2Z%>i Noun-NB-01-USUALLY-WEAK ;
+Pl:%^D2Z%>i Noun-NB-01-USUALLY-STRONG ;

:%^D2Z%>i%>löi PL-GEN/COM/APRSUF_EN ; 
:%^D2Z%>i%>löi PLPARSUF_Zero ;
:%^D2Z%>i%>löi PLINSSUF ;
+Pl:%^D2Z%>i%>löi Noun-NB-01-USUALLY-WEAK ;
+Pl:%^D2Z%>i%>löi Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_SUZI !!= @CODE@ suzi:su
 NMN_SUZI-SG ;
 NMN_SUZI-PL ;

LEXICON NMN_SUZI-SG
:z%>i SGNOMSUF ;
:ze SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:t SGPARSUF_dU ;
+Sg:ze Noun-NB-01-USUALLY-WEAK ;
+Sg:ze Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_SUZI-PL
:ze PLNOMSUF-USUALLY-WEAK ;
:z%>i%>l%{oö%}i PL-GEN/COM/APRSUF_EN ; 
:z%>i%>l%{oö%}i PLPARSUF_i ;
:z%>i%>l%{oö%}i PLINSSUF ;
+Pl:z%>i%>l%{oö%}i Noun-NB-01-USUALLY-WEAK ;
+Pl:z%>i%>l%{oö%}i Noun-NB-01-USUALLY-STRONG ;

!! !NMN = Nominals mutually shared case marking for nouns, adjectives, proper ouns, numerals
LEXICON NMN_NIMI
	NMN_NIMI-SG	;
	NMN_NIMI-PL	;

LEXICON NMN_NIMI-SG
:i SGNOMSUF ;
:e SGPARSUF_A2 ;
:e SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg:e Noun-NB-01-USUALLY-WEAK ;
+Sg:e Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_NIMI-PL
:e PLNOMSUF-USUALLY-WEAK	;
:%>i PL-GEN/COM/APRSUF_EN ; 
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;
:ilöi PL-GEN/COM/APRSUF_N ;
:ilöi PLPARSUF_Zero ;
:ilöi PLINSSUF ;
+Pl:ilöi Noun-NB-01-USUALLY-WEAK ;
+Pl:ilöi Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_HANGI
!! Gradation: No
!! Harmony: Back
	NMN_HANGI-SG	;
	NMN_HANGI-PL	;

LEXICON NMN_HANGI-SG
:i SGNOMSUF ;
:e SGPARSUF_A2 ;
:e SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg:e Noun-NB-01-USUALLY-WEAK ;
+Sg:e Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_HANGI-PL
:e PLNOMSUF-USUALLY-WEAK	;
:%>i PL-GEN/COM/APRSUF_EN ; 
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;
:il%{oö%}i PL-GEN/COM/APRSUF_N ;
:il%{oö%}i PLPARSUF_Zero ;
:il%{oö%}i PLINSSUF ;
+Pl:il%{oö%}i Noun-NB-01-USUALLY-WEAK ;
+Pl:il%{oö%}i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_JÄLGI
!! Front Vowel
!! Gradation Yes
	NMN_JÄLGI-SG	;
	NMN_JÄLGI-PL	;

LEXICON NMN_JÄLGI-SG
:i SGNOMSUF ;
:e SGPARSUF_A2 ;
:e%^WGStem SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg:e%^WGStem Noun-NB-01-USUALLY-WEAK ;
+Sg:e Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_JÄLGI-PL
:e%^WGStem PLNOMSUF-USUALLY-WEAK	;
:%>i PL-GEN/COM/APRSUF_EN ; 
:%>i PLPARSUF_i ;
:%>i%^WGStem PLINSSUF ;
+Pl:i%^WGStem Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;
:ilöi PL-GEN/COM/APRSUF_N ;
:ilöi PLPARSUF_Zero ;
:ilöi PLINSSUF ;
+Pl:ilöi Noun-NB-01-USUALLY-WEAK ;
+Pl:ilöi Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_JOGI !!= @CODE@ jogi:jog
!! Back Vowel
!! Gradation Yes
	NMN_JOGI-SG	;
	NMN_JOGI-PL	;

LEXICON NMN_JOGI-SG
:i SGNOMSUF ;
:e SGPARSUF_A2 ;
:e%^WGStem SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg:e%^WGStem Noun-NB-01-USUALLY-WEAK ;
+Sg:e Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_JOGI-PL
:e%^WGStem PLNOMSUF-USUALLY-WEAK	;
:%>i PL-GEN/COM/APRSUF_EN ; 
:%>i PLPARSUF_i ;    	 		   ! jogii
:%>i%^WGStem PLINSSUF ;			   ! jovin
+Pl:i%^WGStem Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;
:il%{oö%}i PL-GEN/COM/APRSUF_N ;
:il%{oö%}i PLPARSUF_Zero ;
:il%{oö%}i PLINSSUF ;
+Pl:il%{oö%}i Noun-NB-01-USUALLY-WEAK ;
+Pl:il%{oö%}i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_SUARI/KIELI
	NMN_SUARI/KIELI-SG ;
	NMN_SUARI/KIELI-PL ;
LEXICON NMN_SUARI/KIELI-SG
:i SGNOMSUF ;
:e SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
: SGPARSUF_dU ;
+Sg:e Noun-NB-01-USUALLY-WEAK ;
+Sg:e Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_SUARI/KIELI-PL
:e PLNOMSUF-USUALLY-WEAK ;
:i PL-GEN/COM/APRSUF_EN ;
:i PLPARSUF_i ;
:i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;
:il%{oö%}i PL-GEN/COM/APRSUF_N ;
:il%{oö%}i PLPARSUF_Zero ;
:il%{oö%}i PLINSSUF ;
+Pl:il%{oö%}i Noun-NB-01-USUALLY-WEAK ;
+Pl:il%{oö%}i Noun-NB-01-USUALLY-STRONG ;


LEXICON NMN_YKSI  !!= @CODE@ yksi:y
	NMN_YKSI-SG ;
	NMN_YKSI-PL ;

LEXICON NMN_YKSI-SG
:ksi SGNOMSUF ;
:hte SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:h SGPARSUF_dU ;
+Sg:hte Noun-NB-01-USUALLY-WEAK ;
+Sg:hte Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_YKSI-PL
:hte PLNOMSUF-USUALLY-WEAK ;
:ksi PL-GEN/COM/APRSUF_EN ; 
:ksi PLPARSUF_i ;
:ksi PLINSSUF ;
+Pl:ksi Noun-NB-01-USUALLY-WEAK ;
+Pl:ksi Noun-NB-01-USUALLY-STRONG ;


LEXICON NMN_NELLI  !!= @CODE@ nelli:nell
	NMN_NELLI-SG ;
	NMN_NELLI-PL ;

LEXICON NMN_NELLI-SG
:i SGNOMSUF ;
:%>ä SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:%>i SGPARSUF_A2 ;
+Sg:%>ä Noun-NB-01-USUALLY-WEAK ;
+Sg:%>ä Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_NELLI-PL
:%>ä PLNOMSUF-USUALLY-WEAK ;
:i PL-GEN/COM/APRSUF_EN ; 
:i PLPARSUF_i ;
:i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;


LEXICON NMN_VUOZI !!= @CODE@ vuozi:vuod
	NMN_VUOZI-SG ;
	NMN_VUOZI-PL ;

LEXICON NMN_VUOZI-SG
:%^D2Z%>i SGNOMSUF ;
:e%^WGStem SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
: SGPARSUF_dU ;
+Sg:e Noun-NB-01 ;

LEXICON NMN_VUOZI-PL
:e%^WGStem PLNOMSUF-USUALLY-WEAK ;
:%^D2Z%>i PL-GEN/COM/APRSUF_EN ; 
:%^D2Z%>i PLPARSUF_i ;
:%^D2Z%>i PLINSSUF ;
+Pl:%^D2Z%>i Noun-NB-01 ;

:%^D2Z%>i%>l%{oö%}i PL-GEN/COM/APRSUF_N ; 
:%^D2Z%>i%>%{oö%}i PLPARSUF_Zero ;
:%^D2Z%>i%>%{oö%}i PLINSSUF ;
+Pl:%^D2Z%>i%>%{oö%}i Noun-NB-01 ;

LEXICON NMN_PAREMBI !!= @CODE@ parembi:paremb
!!€gt-norm: Adjective comparative
!!€ tuorehembi: tuores+A+Der/mbi+Comp+Sg+Nom
!!$ tuorehembembannu: tuores+A+Der/mbi+Comp+Der/mbi+Comp+Sg+Ess
 NMN_PAREMBI-SG ;
 NMN_PAREMBI-PL ;

LEXICON NMN_PAREMBI-SG
:i SGNOMSUF ;
:%{aäPar%} SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:%{ui%} SGPARSUF_A2 ;
+Sg:%{aäPar%} Noun-NB-01-USUALLY-WEAK ;
+Sg:%{aäPar%} Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_PAREMBI-PL
:%{aäPar%} PLNOMSUF-USUALLY-WEAK ;
:i PL-GEN/COM/APRSUF_EN ;
:i PLPARSUF_i ;
:i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;

LEXICON	NMN_KUDAI !!= @CODE@ kudai:kuda
 NMN_KUDAI-SG ;
 NMN_KUDAI-PL ;
LEXICON NMN_KUDAI-SG
:i SGNOMSUF ;
:m%>%{aä%} SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:m%>%{ui%} SGPARSUF_A2 ;
+Sg:m%>%{aä%} Noun-NB-01-USUALLY-WEAK ;
+Sg:m%>%{aä%} Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_KUDAI-PL
:m%>%{aä%} PLNOMSUF-USUALLY-WEAK ;
:mi PL-GEN/COM/APRSUF_EN ;
:mi PLPARSUF_i ;
:mi PLINSSUF ;
+Pl:mi Noun-NB-01-USUALLY-WEAK ;
+Pl:mi Noun-NB-01-USUALLY-STRONG ;



LEXICON NMN_PIIRAI !!= @CODE@ piirai:piira
	NMN_PIIRAI-SG ;
	NMN_PIIRAI-PL ;

LEXICON NMN_PIIRAI-SG
:i SGNOMSUF ;
:a SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:a SGPARSUF_dU ;
+Sg:%{aäPar%} Noun-NB-01-USUALLY-WEAK ;
+Sg:%{aäPar%} Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_PIIRAI-PL
:a PLNOMSUF-USUALLY-WEAK ;
:al%{oö%}i PL-GEN/COM/APRSUF_N ;
:al%{oö%}i PLPARSUF_Zero ;
:al%{oö%}i PLINSSUF ;
+Pl:al%{oö%}i Noun-NB-01-USUALLY-WEAK ;
+Pl:al%{oö%}i Noun-NB-01-USUALLY-STRONG ;


LEXICON NMN_PAGIZII
 NMN_PAGIZII-SG ;
 NMN_PAGIZII-PL ;

LEXICON NMN_PAGIZII-SG !!= @CODE@ 
 SGNOMSUF ;
:%{aä%} SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:%{ui%} SGPARSUF_A2 ;

LEXICON NMN_PAGIZII-PL !!= @CODE@ 
:%>%{aä%} PLNOMSUF-USUALLY-WEAK ;
:%>%{oö%}i PL-GEN/COM/APRSUF_N ;
:%>%{oö%}i PLPARSUF_i ;
:%>%{oö%}i PLINSSUF ;
:A2 Noun-NB-01-USUALLY-WEAK ; !CHECKME 2016-08-18
:A2 Noun-NB-01-USUALLY-STRONG ; !CHECKME 2016-08-18
:%>%{oö%} Noun-NB-01-USUALLY-WEAK ;
:%>%{oö%} Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_HUOLETOI/JIÄTÖI !!= @CODE@ huoletoi: huolet
 NMN_HUOLETOI/JIÄTÖI-SG ;
 NMN_HUOLETOI/JIÄTÖI-PL ;

LEXICON NMN_HUOLETOI/JIÄTÖI-SG	
:%{oö%}i SGNOMSUF ;
:t%{oö%}m%{aäPar%} SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:t%{oö%}m%{ui%} SGPARSUF_A2 ;
+Sg:t%{oö%}m%{aäPar%}  Noun-NB-01-USUALLY-WEAK ;
+Sg:t%{oö%}m%{aäPar%} Noun-NB-01-USUALLY-STRONG ;
LEXICON NMN_HUOLETOI/JIÄTÖI-PL	
:t%{oö%}m%{aäPar%} PLNOMSUF-USUALLY-WEAK ;
:t%{oö%}mi PL-GEN/COM/APRSUF_EN ;
:t%{oö%}mi PLPARSUF_i ;
:t%{oö%}mi PLINSSUF ;
+Pl:t%{oö%}mi  Noun-NB-01-USUALLY-WEAK ;
+Pl:t%{oö%}mi Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_SULGU !!= @CODE@ sulgu:sulga
!! Gradation Yes
!! Vowel Harmony Back
 NMN_SULGU-SG ;
 NMN_SULGU-PL ;

LEXICON NMN_SULGU-SG
:%^A2UÄ2Y SGNOMSUF ;
:%^WGStem SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg:%^WGStem  Noun-NB-01-USUALLY-WEAK ;
+Sg:  Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_SULGU-PL
:%^WGStem PLNOMSUF-USUALLY-WEAK ;
:%^RmVow%>i PL-GEN/COM/APRSUF_EN ; 
:%^RmVow%>i PLPARSUF_i ;
:%^RmVow%>i%^WGStem PLINSSUF ;
+Pl:%^RmVow%>i%^WGStem  Noun-NB-01-USUALLY-WEAK ;
+Pl:%^RmVow%>i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_KOIRU/PÄIVY !!= @CODE@ koiru:koira
!! Gradation NA
!! Vowel Harmony Back
 NMN_KOIRU/PÄIVY-SG ;
 NMN_KOIRU/PÄIVY-PL ;

LEXICON NMN_KOIRU/PÄIVY-SG
:%^A2UÄ2Y SGNOMSUF ;
 SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg:  Noun-NB-01-USUALLY-WEAK ;
+Sg:  Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_KOIRU/PÄIVY-PL
 PLNOMSUF-USUALLY-WEAK ;
:%^RmVow%>i PL-GEN/COM/APRSUF_EN ; 
:%^RmVow%>i PLPARSUF_i ;
:%^RmVow%>i PLINSSUF ;
+Pl:%^RmVow%>i  Noun-NB-01-USUALLY-WEAK ;
+Pl:%^RmVow%>i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_PÄIVY !!= @CODE@ päivy:päivä
!! Gradation NA
 NMN_PÄIVY-SG ;
 NMN_PÄIVY-PL ;

LEXICON NMN_PÄIVY-SG
:%^A2UÄ2Y SGNOMSUF ;
 SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg:  Noun-NB-01-USUALLY-WEAK ;
+Sg: Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_PÄIVY-PL
 PLNOMSUF-USUALLY-WEAK ;
:%^RmVow%>i PL-GEN/COM/APRSUF_EN ; 
:%^RmVow%>i PLPARSUF_i ;
:%^RmVow%>i PLINSSUF ;
+Pl:%^RmVow%>i  Noun-NB-01-USUALLY-WEAK ;
+Pl:%^RmVow%>i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_MEČČY !!= @CODE@ meččy:meččä
!! gradation Yes
 NMN_MEČČY-SG ;
 NMN_MEČČY-PL ;

LEXICON NMN_MEČČY-SG
:%^A2UÄ2Y SGNOMSUF ;
 SGNOM/PXSP3 ;
:%^WGStem SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg:%^WGStem Noun-NB-01-USUALLY-WEAK ;
+Sg: Noun-NB-01-USUALLY-STRONG ;
LEXICON NMN_MEČČY-PL
:%^WGStem PLNOMSUF-USUALLY-WEAK ;
:%^RmVow%>i PL-GEN/COM/APRSUF_N ; 
:%^RmVow%>i PLPARSUF_Zero ;
:%^RmVow%>i%^WGStem PLINSSUF ;
+Pl:%^RmVow%>i%^WGStem Noun-NB-01-USUALLY-WEAK ;
+Pl:%^RmVow%>i Noun-NB-01-USUALLY-STRONG ;


LEXICON NMN_IŽÄNDY !!= @CODE@ ižändy:ižändä
!! Gradation No
!! VowelHarmony Front
 NMN_IŽÄNDY-SG ;
 NMN_IŽÄNDY-PL ;

LEXICON NMN_IŽÄNDY-SG
:%^A2UÄ2Y SGNOMSUF ;
 SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg:  Noun-NB-01-USUALLY-WEAK ;
+Sg: Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_IŽÄNDY-PL
 PLNOMSUF-USUALLY-WEAK ;
:%^RmVow%>i PL-GEN/COM/APRSUF_EN ; 
:%^RmVow%>i PLPARSUF_i ;
:%^RmVow%>i PLINSSUF ;
+Pl:%^RmVow%>i  Noun-NB-01-USUALLY-WEAK ;
+Pl:%^RmVow%>i Noun-NB-01-USUALLY-STRONG ;


LEXICON NMN_KOIVU/HERY !!= @CODE@ koivu:koivu
!! Gradation NA
!! Vowel Harmony Back
        NMN_KOIVU/HERY-SG ;
        NMN_KOIVU/HERY-PL ;

LEXICON NMN_KOIVU/HERY-SG
 SGNOMSUF ;
 SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
! SGPARSUF_A2 ;
 SGPARSUF_U ;
+Sg: Noun-NB-01 ;

LEXICON NMN_KOIVU/HERY-PL
 PLNOMSUF-USUALLY-WEAK ;
:%>l%{oö%}i PL-GEN/COM/APRSUF_N ; 
:%>l%{oö%}i PLPARSUF_Zero ;
:%>l%{oö%}i PLINSSUF ;
+Pl:%>l%{oö%}i Noun-NB-01 ;

LEXICON NMN_LUGU/IDY !!= @CODE@ lugu:lugu
!! Gradation Yes
!! Vowel Harmony Back
        NMN_LUGU/IDY-SG ;
        NMN_LUGU/IDY-PL ;

LEXICON NMN_LUGU/IDY-SG
 SGNOMSUF ;
:%^WGStem SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;

LEXICON NMN_LUGU/IDY-PL
:%^WGStem PLNOMSUF-USUALLY-WEAK ;
:%>l%{oö%}i PL-GEN/COM/APRSUF_N ; 
:%>l%{oö%}i PLPARSUF_Zero ;
:%>l%{oö%}i PLINSSUF ;
+Pl:%>l%{oö%}i Noun-NB-01 ;

LEXICON NMN_HERY !!= @CODE@ hery:hery
!! Gradation NA
!! Vowel Harmony Front
        NMN_HERY-SG ;
        NMN_HERY-PL ;

LEXICON NMN_HERY-SG
 SGNOMSUF ;
 SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
! SGPARSUF_A2 ;
 SGPARSUF_U ;
+Sg: Noun-NB-01 ;

LEXICON NMN_HERY-PL
 PLNOMSUF-USUALLY-WEAK ;
:%>löi PL-GEN/COM/APRSUF_N ; 
:%>löi PLPARSUF_Zero ;
:%>löi PLINSSUF ;
+Pl:%>löi Noun-NB-01 ;

LEXICON NMN_IDY !!= @CODE@ idy:idy
!! Gradation Yes
!! Vowel Harmony Front
        NMN_IDY-SG ;
        NMN_IDY-PL ;

LEXICON NMN_IDY-SG
 SGNOMSUF ;
:%^WGStem SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;

LEXICON NMN_IDY-PL
:%^WGStem PLNOMSUF-USUALLY-WEAK ;
:%>löi PL-GEN/COM/APRSUF_N ; 
:%>löi PLPARSUF_Zero ;
:%>löi PLINSSUF ;
+Pl:%>löi Noun-NB-01 ;


LEXICON NMN_VASKIČČU !!= @CODE@ vaskičču:vaskičč
        NMN_VASKIČČU-SG ;
        NMN_VASKIČČU-PL ;

LEXICON NMN_VASKIČČU-SG
:u SGNOMSUF ;
:a%^WGStem SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:%{ui%} SGPARSUF_A2 ;
+Sg:a Noun-NB-01 ;

LEXICON NMN_VASKIČČU-PL
:a%^WGStem PLNOMSUF-USUALLY-WEAK ;
:u%>l%{oö%}i PL-GEN/COM/APRSUF_N ; 
:u%>l%{oö%}i PLPARSUF_Zero ;
:u%>l%{oö%}i PLINSSUF ;
+Pl:u%>l%{oö%}i Noun-NB-01 ;

LEXICON NMN_SYGYZY !!= @CODE@ sygyzy:sygyzy
        NMN_SYGYZY-SG ;
        NMN_SYGYZY-PL ;

LEXICON NMN_SYGYZY-SG
 SGNOMSUF ;
 SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;

LEXICON NMN_SYGYZY-PL
 PLNOMSUF-USUALLY-WEAK ;
:%>löi PL-GEN/COM/APRSUF_N ; 
:%>löi PLPARSUF_Zero ;
:%>löi PLINSSUF ;
+Pl:%>löi Noun-NB-01 ;


LEXICON NMN_VALGEI !!= * __@CODE@__ valgei:valge
	NMN_VALGEI-SG	;
	NMN_VALGEI-PL	;

LEXICON NMN_VALGEI-SG
:i SGNOMSUF ;
:%>e SGPARSUF_dU ;
:%>e SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg:%>e Noun-NB-01 ;
LEXICON NMN_VALGEI-PL
:%>e PLNOMSUF-USUALLY-WEAK	;
:%>el%{oö%}i PL-GEN/COM/APRSUF_N ;
:%>el%{oö%}i PLPARSUF_Zero ;
:%>el%{oö%}i PLINSSUF ;
+Pl:%>el%{oö%}i Noun-NB-01 ;

LEXICON NMN_RAHMANNOI !!= @CODE@ rahmannoi:rahmannoi
	NMN_RAHMANNOI-SG	;
	NMN_RAHMANNOI-PL	;

LEXICON NMN_RAHMANNOI-SG
:i SGNOMSUF ;
:e SGPARSUF_A2 ;
:e SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_RAHMANNOI-PL
:e PLNOMSUF-USUALLY-WEAK	;
:%>i PL-GEN/COM/APRSUF_EN ; 
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;
:il%{oö%}i PL-GEN/COM/APRSUF_N ;
:il%{oö%}i PLPARSUF_Zero ;
:il%{oö%}i PLINSSUF ;
+Pl:il%{oö%}i Noun-NB-01 ;

LEXICON NMN_HARDIE !!= @CODE@ hardie:hardie
	NMN_HARDIE-SG	;
	NMN_HARDIE-PL	;

LEXICON NMN_HARDIE-SG
: SGNOMSUF ;
: SGPARSUF_dU ;
: SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg: Noun-NB-01 ;
LEXICON NMN_HARDIE-PL
: PLNOMSUF-USUALLY-WEAK	;
:%>l%{oö%}i PL-GEN/COM/APRSUF_N ;
:%>l%{oö%}i PLPARSUF_Zero ;
:%>l%{oö%}i PLINSSUF ;
+Pl:%>l%{oö%}i Noun-NB-01 ;

LEXICON NMN_KONDII !!= @CODE@ kondii:kondi
	NMN_KONDII-SG	;
	NMN_KONDII-PL	;

LEXICON NMN_KONDII-SG
:i SGNOMSUF ;
:e SGPARSUF_dU ;
:e SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_KONDII-PL
:e PLNOMSUF-USUALLY-WEAK	;
:e%>l%{oö%}i PL-GEN/COM/APRSUF_N ;
:e%>l%{oö%}i PLPARSUF_Zero ;
:e%>l%{oö%}i PLINSSUF ;
+Pl:e%>l%{oö%}i Noun-NB-01 ;

LEXICON NMN_SEIČČIE !!= @CODE@ seiččie:seičče
	NMN_SEIČČIE-SG	;
	NMN_SEIČČIE-PL	;

LEXICON NMN_SEIČČIE-SG
:%>e SGNOMSUF ;
:%^WGStemn SGPARSUF_dU ;
:me SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg:me Noun-NB-01 ;
LEXICON NMN_SEIČČIE-PL
:me PLNOMSUF-USUALLY-WEAK	;
:mi PL-GEN/COM/APRSUF_N ;
:mi PLPARSUF_i ;
:mi PLINSSUF ;
+Pl:mi Noun-NB-01 ;

LEXICON NMN_OSTAI !!= @CODE@ ostua:osta%>j
 NMN_OSTAI-SG ;
 NMN_OSTAI-PL ;

LEXICON NMN_OSTAI-SG
:%{uy%} SGNOMSUF ;
 SGNOMSUF ;
:%>%{aäPar%} SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:%>%{ui%} SGPARSUF_A2 ;
+Sg:%>%{aäPar%} Noun-NB-01 ;

LEXICON NMN_OSTAI-PL
:%>%{aäPar%} PLNOMSUF-USUALLY-WEAK ;
:%{oöØ%}%>i PL-GEN/COM/APRSUF_EN ; 
:%{oöØ%}%>i PLPARSUF_i ;
:%{oöØ%}%>i PLINSSUF ;
+Pl:%{oöØ%}%>i Noun-NB-01 ;

LEXICON NMN_KANDAI
!! Stem Vowel 0:a:0 kandai, kandajan, kandajua, kandajinnu
 NMN_KANDAI-SG ;
 NMN_KANDAI-PL ;

LEXICON NMN_KANDAI-SG
:%>%{ijPRC%}%>%{uy%} SGNOMSUF ;
:%>%{ijPRC%} SGNOMSUF ;
:%>%{ijPRC%}%>%{aäPar%} SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:%>%{ijPRC%}%>%{ui%} SGPARSUF_A2 ;
+Sg:%>%{ijPRC%}%>%{aäPar%} Noun-NB-01 ;
LEXICON NMN_KANDAI-PL
:%>%{ijPRC%}%>%{aäPar%} PLNOMSUF-USUALLY-WEAK ;
:%>%{ijPRC%}%>i PL-GEN/COM/APRSUF_EN ; 
:%>%{ijPRC%}%>i PLPARSUF_i ;
:%>%{ijPRC%}%>i PLINSSUF ;
+Pl:%>%{ijPRC%}%>i Noun-NB-01 ;

LEXICON NMN_PEZII !!= @CODE@ pestä:pezi%>j
 NMN_PEZII-SG ;
 NMN_PEZII-PL ;

LEXICON NMN_PEZII-SG
 SGNOMSUF ;
:%>%{aäPar%} SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:%>%{ui%} SGPARSUF_A2 ;
+Sg:%>%{aäPar%} Noun-NB-01 ;

LEXICON NMN_PEZII-PL
:%>%{aäPar%} PLNOMSUF-USUALLY-WEAK ;
:%>%{oö%}%>i PL-GEN/COM/APRSUF_N ; 
:%>%{oö%}%>i PLPARSUF_Zero ;
:%>%{oö%}%>i PLINSSUF ;
+Pl:%>%{oö%}%>i Noun-NB-01 ;

LEXICON NMN_KESTÄY !!= @CODE@ kestäy:kestä
 NMN_KESTÄY-SG ;
 NMN_KESTÄY-PL ;

LEXICON NMN_KESTÄY-SG
:%{uy%} SGNOMSUF ;
:v%>%{aäPar%} SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:v%>%{ui%} SGPARSUF_A2 ;
+Sg:v%>%{aäPar%} Noun-NB-01 ;

LEXICON NMN_KESTÄY-PL
 PLNOMSUF-USUALLY-WEAK ;
:v%>i PL-GEN/COM/APRSUF_EN ; 
:v%>i PLPARSUF_i ;
:v%>i PLINSSUF ;
+Pl:v%>i Noun-NB-01 ;

LEXICON NMN_RUNO !!= @CODE@ runo:runo
!! Gradation No
	NMN_RUNO-SG ;
	NMN_RUNO-PL ;

LEXICON NMN_RUNO-SG
	SGNOMSUF ;
 	SGPARSUF_A2 ;
	SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg: 	Noun-NB-01 ;
LEXICON NMN_RUNO-PL
 PLNOMSUF-USUALLY-WEAK ;
:l%{oö%}i PL-GEN/COM/APRSUF_N ;
:l%{oö%}i PLPARSUF_Zero ;
:l%{oö%}i PLINSSUF ;
+Pl:l%{oö%}i Noun-NB-01 ;

LEXICON NMN_HÖRÖ !!= @CODE@ hörö:hörö
!! Gradation No
	NMN_HÖRÖ-SG ;
	NMN_HÖRÖ-PL ;

LEXICON NMN_HÖRÖ-SG
	SGNOMSUF ;
 	SGPARSUF_A2 ;
	SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg: 	Noun-NB-01 ;
LEXICON NMN_HÖRÖ-PL
 PLNOMSUF-USUALLY-WEAK ;
:löi PL-GEN/COM/APRSUF_N ;
:löi PLPARSUF_Zero ;
:löi PLINSSUF ;
+Pl:löi Noun-NB-01 ;

LEXICON NMN_RUADO/KYNDÖ !!= @CODE@ ruado:ruado
!! Gradation Yes
	NMN_RUADO/KYNDÖ-SG ;
	NMN_RUADO/KYNDÖ-PL ;

LEXICON NMN_RUADO/KYNDÖ-SG
	SGNOMSUF ;
 	SGPARSUF_A2 ;
:%^WGStem	SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg:%^WGStem 	Noun-NB-01-USUALLY-WEAK ;
+Sg: 	Noun-NB-01-USUALLY-STRONG ;
LEXICON NMN_RUADO/KYNDÖ-PL
:%^WGStem PLNOMSUF-USUALLY-WEAK ;
:l%{oö%}i PL-GEN/COM/APRSUF_N ;
:l%{oö%}i PLPARSUF_Zero ;
:l%{oö%}i PLINSSUF ;
+Pl:l%{oö%}i Noun-NB-01-USUALLY-WEAK ;
+Pl:l%{oö%}i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_KYNDÖ !!= @CODE@ kyndö:kyndö
	NMN_KYNDÖ-SG ;
	NMN_KYNDÖ-PL ;

LEXICON NMN_KYNDÖ-SG
	SGNOMSUF ;
 	SGPARSUF_A2 ;
:%^WGStem	SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg: 	Noun-NB-01 ;
LEXICON NMN_KYNDÖ-PL
 PLNOMSUF-USUALLY-WEAK ;
:löi PL-GEN/COM/APRSUF_N ;
:löi PLPARSUF_Zero ;
:löi PLINSSUF ;
+Pl:löi Noun-NB-01 ;


LEXICON NMN_KUU !!= @CODE@ kuu:kuu
	NMN_KUU-SG	;
	NMN_KUU-PL	;

LEXICON NMN_KUU-SG
 SGNOMSUF ;
 SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_dU ;
+Sg: Noun-NB-01 ;

LEXICON NMN_KUU-PL
 PLNOMSUF-USUALLY-WEAK ;
:%>l%{oö%}i PL-GEN/COM/APRSUF_N ;
:%>l%{oö%}i PLPARSUF_Zero ;
:%>l%{oö%}i PLINSSUF ;
+Pl:%>l%{oö%}i Noun-NB-01 ;

LEXICON NMN_PIÄ !!= @CODE@ piä:piä
	NMN_PIÄ-SG	;
	NMN_PIÄ-PL	;

LEXICON NMN_PIÄ-SG
 SGNOMSUF ;
 SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_dU ;
+Sg: Noun-NB-01 ;

LEXICON NMN_PIÄ-PL
 PLNOMSUF-USUALLY-WEAK ;
:%>löi PL-GEN/COM/APRSUF_N ;
:%>löi PLPARSUF_Zero ;
:%>löi PLINSSUF ;
+Pl:%>löi Noun-NB-01 ;


LEXICON NMN_VYÖ !!= @CODE@ vyö:vyö
	NMN_VYÖ-SG	;
	NMN_VYÖ-PL	;

LEXICON NMN_VYÖ-SG
 SGNOMSUF ;
 SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_dU ;
+Sg: Noun-NB-01 ;

LEXICON NMN_VYÖ-PL
 PLNOMSUF-USUALLY-WEAK ;
:%>löi PL-GEN/COM/APRSUF_N ;
:%>löi PLPARSUF_Zero ;
:%>löi PLINSSUF ;
+Pl:%>löi Noun-NB-01 ;


LEXICON NMN_MUA !!= @CODE@ mua:mua
	NMN_MUA-SG	;
	NMN_MUA-PL	;

LEXICON NMN_MUA-SG
 SGNOMSUF ;
 SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_dU ;
+Sg: Noun-NB-01 ;

LEXICON NMN_MUA-PL
 PLNOMSUF-USUALLY-WEAK ;
:%>l%{oö%}i PL-GEN/COM/APRSUF_N ;
:%>l%{oö%}i PLPARSUF_Zero ;
:%>l%{oö%}i PLINSSUF ;
+Pl:%>l%{oö%}i Noun-NB-01 ;

LEXICON NMN_KALA !!= @CODE@ kala:kala
!! Gradation NA
	NMN_KALA-SG  ; 
	NMN_KALA-PL  ;
LEXICON NMN_KALA-SG
 SGNOMSUF ;
 SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;

LEXICON NMN_KALA-PL
 PLNOMSUF-USUALLY-WEAK ;
:%^A2O%>i PL-GEN/COM/APRSUF_EN ;
:%^A2O%>i PLPARSUF_Zero ;
:%^A2O%>i PLINSSUF ;
+Pl:%^A2O%>i Noun-NB-01 ;

LEXICON NMN_PADA !!= @CODE@ pada:pada
!! Yaml: __pada__
!! Gradation Yes
	NMN_PADA-SG  ; 
	NMN_PADA-PL  ;
LEXICON NMN_PADA-SG
 SGNOMSUF ;
:%^WGStem SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;

LEXICON NMN_PADA-PL
:%^WGStem PLNOMSUF-USUALLY-WEAK ;
:%^A2O%>i PL-GEN/COM/APRSUF_EN ;
:%^A2O%>i PLPARSUF_Zero ;
:%^A2O%>i PLINSSUF ;
+Pl:%^A2O%>i Noun-NB-01 ;

LEXICON NMN_PESSYH !!= @CODE@ pessyh
        NMN_PESSYH-SG ;
        NMN_PESSYH-PL ;

!! !Nominative singular in "h"
LEXICON NMN_PESSYH-SG
:h SGNOMSUF ;
+Err/Orth:t SGNOMSUF ;
:ö SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:t SGPARSUF_dU ;
+Sg:ö Noun-NB-01 ;

LEXICON NMN_PESSYH-PL
:ö PLNOMSUF-USUALLY-WEAK ;
:zi PL-GEN/COM/APRSUF_EN ; !!= * @CODE@ the +Pl+Com comes here
:zi PLPARSUF_i ;
:zi PLINSSUF ;
+Pl:zi Noun-NB-01 ;

LEXICON NMN_TULLUH !!= @CODE@ tulluh:tullu
        NMN_TULLUH-SG ;
        NMN_TULLUH-PL ;

!! !Nominative singular in "h"
LEXICON NMN_TULLUH-SG
:h SGNOMSUF ;
+Err/Orth:t SGNOMSUF ;
:o SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:t SGPARSUF_dU ;
+Sg:o Noun-NB-01 ;

LEXICON NMN_TULLUH-PL
:o PLNOMSUF-USUALLY-WEAK ;
:zi PL-GEN/COM/APRSUF_EN ; !!= * @CODE@ the +Pl+Com comes here
:zi PLPARSUF_i ;
:zi PLINSSUF ;
+Pl:zi Noun-NB-01 ;

LEXICON NMN_KARJAL !!= @CODE@ karjal:karjal
	NMN_KARJAL-SG ;
	NMN_KARJAL-PL ;

LEXICON NMN_KARJAL-SG !!= @CODE@ karjal:karjal
 SGNOMSUF ;
:a SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:u SGPARSUF_A2 ;
+Sg:a Noun-NB-01 ;
LEXICON NMN_KARJAL-PL !!= @CODE@ karjal:karjal
:a PLNOMSUF-USUALLY-WEAK ;
:oi PL-GEN/COM/APRSUF_N ;
:oi PLPARSUF_Zero ;
:oi PLINSSUF ;
+Pl:oi Noun-NB-01 ;

LEXICON NMN_MADAL !!= @CODE@ madal:madal
	NMN_MADAL-SG ;
	NMN_MADAL-PL ;

LEXICON NMN_MADAL-SG !!= @CODE@ madal:madal
 SGNOMSUF ;
:a SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:u SGPARSUF_A2 ;
+Sg:a Noun-NB-01 ;
LEXICON NMN_MADAL-PL !!= @CODE@ madal:madal
:a PLNOMSUF-USUALLY-WEAK ;
:%>i PL-GEN/COM/APRSUF_N ;
:%>ii PLPARSUF_Zero ;
:i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;

!! ! Nominative singular in "m"

LEXICON NMN_SUURIM !!= @CODE@ suurim
        NMN_SUURIM-SG   ;
        NMN_SUURIM-PL   ;

LEXICON NMN_SUURIM-SG
 SGNOMSUF ;
:u SGPARSUF_A2 ;

LEXICON NMN_SUURIM-PL
:a  PLNOMSUF-USUALLY-WEAK ;
:i PL-GEN/COM/APRSUF_EN ;
:i PLPARSUF_i ;

LEXICON NMN_TUATANDIM
        NMN_TUATANDIM-SG   ;
        NMN_TUATANDIM-PL   ;

LEXICON NMN_TUATANDIM-SG
 SGNOMSUF ;
:%{ui%} SGPARSUF_A2 ;

LEXICON NMN_TUATANDIM-PL
:a  PLNOMSUF-USUALLY-WEAK ;
:i PL-GEN/COM/APRSUF_EN ;
:i PLPARSUF_i ;

LEXICON NMN_TYTÄRDIM
        NMN_TYTÄRDIM-SG   ;
        NMN_TYTÄRDIM-PL   ;

LEXICON NMN_TYTÄRDIM-SG
 SGNOMSUF ;
:i SGPARSUF_A2 ;

LEXICON NMN_TYTÄRDIM-PL
:ä  PLNOMSUF-USUALLY-WEAK ;
:i PL-GEN/COM/APRSUF_EN ;
:i PLPARSUF_i ;

LEXICON NMN_TUATINDAM
        NMN_TUATINDAM-SG   ;
        NMN_TUATINDAM-PL   ;

LEXICON NMN_TUATINDAM-SG
 SGNOMSUF ;
:%{ui%} SGPARSUF_A2 ;

LEXICON NMN_TUATINDAM-PL
:a  PLNOMSUF-USUALLY-WEAK ;
:i PL-GEN/COM/APRSUF_EN ;
:i PLPARSUF_i ;

!! ! Nominative singular in "n"
LEXICON NMN_SALBOIN 
	NMN_SALBOIN-SG	;
	NMN_SALBOIN-PL	;

LEXICON NMN_SALBOIN-SG
:n SGNOMSUF ;
:n SGPARSUF_dU ;
:me SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg:me Noun-NB-01 ;
LEXICON NMN_SALBOIN-PL
:me PLNOMSUF-USUALLY-WEAK	;
:%>mi PL-GEN/COM/APRSUF_EN ; 
:%>mi PLPARSUF_i ;
:%>mi PLINSSUF ;
+Pl:mi Noun-NB-01-USUALLY-WEAK ;
+Pl:mi Noun-NB-01-USUALLY-STRONG ;
!:il%{oö%}i PL-GEN/COM/APRSUF_N ;
!:il%{oö%}i PLPARSUF_Zero ;
!:il%{oö%}i PLINSSUF ;

LEXICON NMN_ENIN !!= @CODE@ enin:eni
	NMN_ENIN-SG	;
	NMN_ENIN-PL	;

LEXICON NMN_ENIN-SG
:n SGNOMSUF ;
!:n SGPARSUF_dU ;
:m%>%{aäPar%} SGPARSUF_A2 ;
:m%>%{aäPar%} SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg:m%>%{aäPar%} Noun-NB-01 ;
LEXICON NMN_ENIN-PL
:m%>%{aäPar%} PLNOMSUF-USUALLY-WEAK	;
:mi PL-GEN/COM/APRSUF_EN ; 
:mi PLPARSUF_i ;
:mi PLINSSUF ;
+Pl:mi Noun-NB-01 ;


!! ! Nominative singular in "r"
LEXICON NMN_PIENAR
	NMN_PIENAR-SG	;
	NMN_PIENAR-PL	;

LEXICON NMN_PIENAR-SG
:%^WGStem SGNOMSUF ;
:%^WGStem SGPARSUF_dU ;
:e SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_PIENAR-PL
:e PLNOMSUF-USUALLY-WEAK	;
:%>i PL-GEN/COM/APRSUF_EN ; 
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;
:il%{oö%}i PL-GEN/COM/APRSUF_N ;
:il%{oö%}i PLPARSUF_Zero ;
:il%{oö%}i PLINSSUF ;
+Pl:il%{oö%}i Noun-NB-01 ;

LEXICON NMN_TYTÄR
	NMN_TYTÄR-SG	;
	NMN_TYTÄR-PL	;

LEXICON NMN_TYTÄR-SG
:%^WGStem SGNOMSUF ;
:%^WGStem SGPARSUF_dU ;
:e SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_TYTÄR-PL
:e PLNOMSUF-USUALLY-WEAK	;
:%>i PL-GEN/COM/APRSUF_EN ; 
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;
:ilöi PL-GEN/COM/APRSUF_N ;
:ilöi PLPARSUF_Zero ;
:ilöi PLINSSUF ;
+Pl:ilöi Noun-NB-01 ;

LEXICON NMN_UDAR
	NMN_UDAR-SG	;
	NMN_UDAR-PL	;

LEXICON NMN_UDAR-SG
:%^WGStem SGNOMSUF ;
:%^WGStem SGPARSUF_dU ;
:e SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_UDAR-PL
:e PLNOMSUF-USUALLY-WEAK	;
:%>i PL-GEN/COM/APRSUF_EN ; 
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;
:il%{oö%}i PL-GEN/COM/APRSUF_N ;
:il%{oö%}i PLPARSUF_Zero ;
:il%{oö%}i PLINSSUF ;
+Pl:il%{oö%}i Noun-NB-01 ;

!! ! Nominative singular in "s"
LEXICON NMN_PUHTAHUS/XX !!= @CODE@ puhtahus:puhtahu
	NMN_PUHTAHUS/XX-SG ;
	NMN_PUHTAHUS/XX-PL ;

LEXICON NMN_PUHTAHUS/XX-SG
:s SGNOMSUF ;
:%{oö%} SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:t SGPARSUF_dU ;
+Sg:%{oö%} Noun-NB-01 ;

LEXICON NMN_PUHTAHUS/XX-PL
:%{oö%} PLNOMSUF-USUALLY-WEAK ;
:z%>i PL-GEN/COM/APRSUF_EN ; 
:z%>i PLPARSUF_i ;
:z%>i PLINSSUF ;
+Pl:zi Noun-NB-01 ;


LEXICON NMN_ALUS/ILVES !!= @CODE@ alus:aluks
	NMN_ALUS/ILVES-SG ;
	NMN_ALUS/ILVES-PL ;

LEXICON NMN_ALUS/ILVES-SG
:%^KS2S SGNOMSUF ;
:e SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:%^KS2S SGPARSUF_dU ;
+Sg:e Noun-NB-01 ;

LEXICON NMN_ALUS/ILVES-PL
:e PLNOMSUF-USUALLY-WEAK ;
:%>i PL-GEN/COM/APRSUF_EN ; 
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_ILVES !!= @CODE@ ilves:ilveks
	NMN_ILVES-SG ;
	NMN_ILVES-PL ;

LEXICON NMN_ILVES-SG
:%^KS2S SGNOMSUF ;
:e SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:%^KS2S SGPARSUF_dU ;
+Sg:e Noun-NB-01 ;

LEXICON NMN_ILVES-PL
:e PLNOMSUF-USUALLY-WEAK ;
:%>i PL-GEN/COM/APRSUF_EN ; 
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_MIES !!= @CODE@ mies:mie
	NMN_MIES-SG ;
	NMN_MIES-PL ;

LEXICON NMN_MIES-SG
:s SGNOMSUF ;
:he SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:s SGPARSUF_dU ;
+Sg:he Noun-NB-01 ;

LEXICON NMN_MIES-PL
:he PLNOMSUF-USUALLY-WEAK ;
:%>hi PL-GEN/COM/APRSUF_EN ; 
:%>hi PLPARSUF_i ;
:%>hi PLINSSUF ;
+Pl:hi Noun-NB-01 ;

LEXICON NMN_ARMAS !!= @CODE@ armas:arma
 NMN_ARMAS-SG        ;
 NMN_ARMAS-PL        ;

LEXICON NMN_ARMAS-SG
:%^WGStem%>s SGNOMSUF ;
:%^WGStem%>s SGPARSUF_dU ;
:h%>%{aäPar%} SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg:h%>%{aäPar%} Noun-NB-01 ;
LEXICON NMN_ARMAS-PL
:h%>%{aäPar%} PLNOMSUF-USUALLY-WEAK ;
:hi PL-GEN/COM/APRSUF_EN ;
:hi PLPARSUF_i ;
:hi PLINSSUF ;
+Pl:hi Noun-NB-01 ;

LEXICON NMN_VARVAS !!= @CODE@ varvas:varba
 NMN_VARVAS-SG        ;
 NMN_VARVAS-PL        ;

LEXICON NMN_VARVAS-SG
:%^WGStem%>s SGNOMSUF ;
:%^WGStem%>s SGPARSUF_dU ;
:h%>%{aäPar%} SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg:h%>%{aäPar%} Noun-NB-01 ;
LEXICON NMN_VARVAS-PL
:h%>%{aäPar%} PLNOMSUF-USUALLY-WEAK ;
:hi PL-GEN/COM/APRSUF_EN ;
:hi PLPARSUF_i ;
:hi PLINSSUF ;
+Pl:hi Noun-NB-01 ;

LEXICON NMN_BEMMEL !!= @CODE@ bemmel:bembel
 NMN_BEMMEL-SG        ;
 NMN_BEMMEL-PL        ;

LEXICON NMN_BEMMEL-SG
:%^WGStem SGNOMSUF ;
:%^WGStem SGPARSUF_dU ;
:e SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_BEMMEL-PL
:e PLNOMSUF-USUALLY-WEAK ;
:i PL-GEN/COM/APRSUF_EN ;
:i PLPARSUF_i ;
:i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_VIDEL !!= @CODE@ videl:videl
!! Gradation No
 NMN_VIDEL-SG        ;
 NMN_VIDEL-PL        ;

LEXICON NMN_VIDEL-SG
:%^WGStem SGNOMSUF ;
:%^WGStem SGPARSUF_dU ;
:e SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_VIDEL-PL
:e PLNOMSUF-USUALLY-WEAK ;
:i PL-GEN/COM/APRSUF_EN ;
:i PLPARSUF_i ;
:i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_TUORES !!= @CODE@ tuores:tuore
!!= * Yaml: __A-tuores__
 NMN_TUORES-SG        ;
 NMN_TUORES-PL        ;

LEXICON NMN_TUORES-SG
:%^WGStem%>s SGNOMSUF ;
:%^WGStem%>s SGPARSUF_dU ;
:he SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
+Sg:he Noun-NB-01 ;
LEXICON NMN_TUORES-PL
:he PLNOMSUF-USUALLY-WEAK ;
:hi PL-GEN/COM/APRSUF_EN ;
:hi PLPARSUF_i ;
:hi PLINSSUF ;
+Pl:hi Noun-NB-01 ;


LEXICON NMN_SUALIŠ !!= @CODE@ suališ:suali
	NMN_SUALIŠ-SG ;
	NMN_SUALIŠ-PL ;

LEXICON NMN_SUALIŠ-SG
:s SGNOMSUF ;
:i SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:s SGPARSUF_dU ;
+Sg:i Noun-NB-01-USUALLY-WEAK ;
+Sg:i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_SUALIŠ-PL
:i PLNOMSUF-USUALLY-WEAK ;
!:%>i PL-GEN/COM/APRSUF_EN ; 
!:%>i PLPARSUF_i ;
!:%>i PLINSSUF ;
!+Pl:i Noun-NB-01 ;


LEXICON NMN_RAIŠ !!= @CODE@ raiš:ra
	NMN_RAIŠ-SG	;
	NMN_RAIŠ-PL	;
LEXICON NMN_RAIŠ-SG
:iš SGNOMSUF ;
:gehe SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:iš SGPARSUF_dU ;
+Sg:gehe Noun-NB-01 ;
LEXICON NMN_RAIŠ-PL
:gehe PLNOMSUF-USUALLY-WEAK ;
:gehi PL-GEN/COM/APRSUF_EN ;
:gehi PLPARSUF_i ;
:gehi PLINSSUF ;
+Pl:gehi Noun-NB-01 ;

LEXICON NMN_KUURNIS !!= @CODE@ kuurnis:kuurne
	NMN_KUURNIS-SG ;
	NMN_KUURNIS-PL ;

LEXICON NMN_KUURNIS-SG
:%>is SGNOMSUF ;
:i SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:s SGPARSUF_dU ;
+Sg:i Noun-NB-01-USUALLY-WEAK ;
+Sg:i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_KUURNIS-PL
:i PLNOMSUF-USUALLY-WEAK ;
!:%>i PL-GEN/COM/APRSUF_EN ; 
!:%>i PLPARSUF_i ;
!:%>i PLINSSUF ;
!+Pl:i Noun-NB-01 ;

LEXICON NMN_OLUT !!= * @CODE@ olut:olu
 NMN_OLUT-SG ;
 NMN_OLUT-PL ;

LEXICON NMN_OLUT-SG
:t SGNOMSUF ;
:e SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:t SGPARSUF_dU ;
+Sg:e Noun-NB-01 ;

LEXICON NMN_OLUT-PL
:e  PLNOMSUF-USUALLY-WEAK ;
:je PL-GEN/COM/APRSUF_N ;
:i PLPARSUF_dU ;
:i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_KEVÄT !!= * @CODE@ kevät:kevä
 NMN_KEVÄT-SG ;
 NMN_KEVÄT-PL ;

LEXICON NMN_KEVÄT-SG
:t SGNOMSUF ;
:ä SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:t SGPARSUF_dU ;
+Sg:ä Noun-NB-01 ;

LEXICON NMN_KEVÄT-PL
:ä PLNOMSUF-USUALLY-WEAK ;
:älöi PL-GEN/COM/APRSUF_N ;
:älöi PLPARSUF_Zero ;
:älöi PLINSSUF ;
+Pl:älöi Noun-NB-01 ;

LEXICON NMN_TUHAT !!= * @CODE@ tuhat:tuha
!! Gradation None
 NMN_TUHAT-SG ;
 NMN_TUHAT-PL ;

LEXICON NMN_TUHAT-SG
:t SGNOMSUF ;
:nde SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:t SGPARSUF_dU ;
+Sg:nde Noun-NB-01-USUALLY-WEAK ;
+Sg:nde Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_TUHAT-PL
:nde  PLNOMSUF-USUALLY-WEAK ;
:nzi PL-GEN/COM/APRSUF_EN ;
:nzi PLPARSUF_dU ;
:nzi PLINSSUF ;
+Pl:nzi Noun-NB-01 ;

LEXICON NMN_TULLUT !!= * @CODE@ tullut:tullu
+Use/NG: NMN_TULLUT-SG ;
+Use/NG: NMN_TULLUT-PL ;

LEXICON NMN_TULLUT-SG
:t SGNOMSUF ;
:%{oö%} SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:t SGPARSUF_dU ;
+Sg:%{oö%} Noun-NB-01 ;

LEXICON NMN_TULLUT-PL
:%{oö%} PLNOMSUF-USUALLY-WEAK ;
:z%>i PL-GEN/COM/APRSUF_EN ;
:z%>i PLPARSUF_i ;
:z%>i PLINSSUF ;
+Pl:zi Noun-NB-01 ;


LEXICON NMN_PEREH !!= @CODE@ pereh:pereh
	NMN_PEREH-SG	;
	NMN_PEREH-PL	;
LEXICON NMN_PEREH-SG
 SGNOMSUF ;
:e SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_dU ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_PEREH-PL
:e PLNOMSUF-USUALLY-WEAK ;
:i PL-GEN/COM/APRSUF_EN ;
:i PLPARSUF_i ;
:i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_LIIKEH !!= @CODE@ liikeh:liikkeh
	NMN_LIIKEH-SG	;
	NMN_LIIKEH-PL	;
LEXICON NMN_LIIKEH-SG
:%^WGStem SGNOMSUF ;
:e SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:%^WGStem SGPARSUF_dU ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_LIIKEH-PL
:e PLNOMSUF-USUALLY-WEAK ;
:i PL-GEN/COM/APRSUF_EN ;
:i PLPARSUF_i ;
:i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_SIVE !!= @CODE@ sive:side
	NMN_SIVE-SG	;
	NMN_SIVE-PL	;
LEXICON NMN_SIVE-SG
 SGNOMSUF ;
:e SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_tty ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_SIVE-PL
:e  PLNOMSUF-USUALLY-WEAK ;
:i PL-GEN/COM/APRSUF_EN ;
:i PLPARSUF_i ;
:i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_PENGER !!= @CODE@ penger:penger
	NMN_PENGER-SG	;
	NMN_PENGER-PL	;
LEXICON NMN_PENGER-SG
 SGNOMSUF ;
:e SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_dU ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_PENGER-PL
:e  PLNOMSUF-USUALLY-WEAK ;
:i PL-GEN/COM/APRSUF_EN ;
:i PLPARSUF_i ;
:i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_ARTELI !!= @CODE@ arteli:arteli
	NMN_ARTELI-SG ;
	NMN_ARTELI-PL ;
LEXICON NMN_ARTELI-SG
 SGNOMSUF ;
 SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;
LEXICON NMN_ARTELI-PL
: PLNOMSUF-USUALLY-WEAK ;
: PL-GEN/COM/APRSUF_EN ;
:%>l%{oö%}i PLPARSUF_Zero ;
:%>l%{oö%}i PLINSSUF ;
+Pl:%>l%{oö%}i Noun-NB-01 ;


LEXICON NMN_PAPPI !!= @CODE@ pappi:pappi
	NMN_PAPPI-SG ;
	NMN_PAPPI-PL ;
LEXICON NMN_PAPPI-SG
 SGNOMSUF ;
:%^WGStem SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;
LEXICON NMN_PAPPI-PL
:%^WGStem PLNOMSUF-USUALLY-WEAK ;
: PL-GEN/COM/APRSUF_EN ;
: PLPARSUF_i ;
:%^WGStem PLINSSUF ;
+Pl:%^WGStem Noun-NB-01-USUALLY-WEAK ;
+Pl: Noun-NB-01-USUALLY-STRONG ;
:%>l%{oö%}i PL-GEN/COM/APRSUF_N ;
:%>l%{oö%}i PLPARSUF_Zero ;
:%>l%{oö%}i PLINSSUF ;
+Pl:%>l%{oö%}i Noun-NB-01-USUALLY-WEAK ;
+Pl:%>l%{oö%}i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_HÄKKI !!= @CODE@ häkki:häkki
	NMN_HÄKKI-SG ;
	NMN_HÄKKI-PL ;
LEXICON NMN_HÄKKI-SG
 SGNOMSUF ;
:%^WGStem SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;
LEXICON NMN_HÄKKI-PL
:%^WGStem PLNOMSUF-USUALLY-WEAK ;
: PL-GEN/COM/APRSUF_EN ;
:%>löi PL-GEN/COM/APRSUF_N ;
:%>löi PLPARSUF_Zero ;
:%>löi PLINSSUF ;
+Pl:%>löi Noun-NB-01 ;


LEXICON NMN_LEIRI !!= @CODE@ leiri:leiri
!! Gradation NA
	NMN_LEIRI-SG ;
	NMN_LEIRI-PL ;
LEXICON NMN_LEIRI-SG
 SGNOMSUF ;
 SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;
LEXICON NMN_LEIRI-PL
: PLNOMSUF-USUALLY-WEAK ;
: PL-GEN/COM/APRSUF_EN ;
: PLPARSUF_i ;
: PLINSSUF ;
+Pl: Noun-NB-01-USUALLY-WEAK ;
+Pl: Noun-NB-01-USUALLY-STRONG ;
:%>löi PL-GEN/COM/APRSUF_N ;
:%>löi PLPARSUF_Zero ;
:%>löi PLINSSUF ;
+Pl:%>löi Noun-NB-01-USUALLY-WEAK ;
+Pl:%>löi Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_UKSI !!= @CODE@ uksi:uks
	NMN_UKSI-SG ;
	NMN_UKSI-PL ;

LEXICON NMN_UKSI-SG
:i SGNOMSUF ;
:e SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg:e Noun-NB-01 ;

LEXICON NMN_UKSI-PL
:e PLNOMSUF-USUALLY-WEAK ;
:i PL-GEN/COM/APRSUF_EN ;
:i PLPARSUF_Zero ;
:i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_REBOI !!= @CODE@ reboi:reboi
        NMN_REBOI-SG ;
        NMN_REBOI-PL ;
LEXICON NMN_REBOI-SG
 SGNOMSUF ;
 SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_dU ;
+Sg: Noun-NB-01 ;
LEXICON NMN_REBOI-PL
:	PLNOMSUF-USUALLY-WEAK ;
:%^ILoss%>l%{oö%}i PL-GEN/COM/APRSUF_N ;
:%^ILoss%>l%{oö%}i PLPARSUF_Zero ;
:%^ILoss%>l%{oö%}i PLINSSUF ;
+Pl:%^ILoss%>l%{oö%}i Noun-NB-01 ;

LEXICON NMN_PÖČÖI !!= @CODE@ pöčöi:pöččö
        NMN_PÖČÖI-SG ;
        NMN_PÖČÖI-PL ;
LEXICON NMN_PÖČÖI-SG
:%^WGStemi SGNOMSUF ;
:%^WGStemi SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:i SGPARSUF_dU ;
+Sg:%^WGStemi Noun-NB-01-USUALLY-WEAK ;
+Sg:i Noun-NB-01-USUALLY-STRONG ;
LEXICON NMN_PÖČÖI-PL
:%^WGStemi	PLNOMSUF-USUALLY-WEAK ;
:%>löi PL-GEN/COM/APRSUF_N ;
:%>löi PLPARSUF_Zero ;
:%>löi PLINSSUF ;
+Pl:%>löi Noun-NB-01 ;

LEXICON NMN_OZA !!= @CODE@ oza:oza
	NMN_OZA-SG  ;
	NMN_OZA-PL  ;

LEXICON NMN_OZA-SG
 SGNOMSUF ;
 SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;

LEXICON NMN_OZA-PL
: PLNOMSUF-USUALLY-WEAK ;
:%^RmVow%>i PL-GEN/COM/APRSUF_EN ;
:%^RmVow%>i PLPARSUF_i ;
:%^RmVow%>i PLINSSUF ;
+Pl:%^RmVow%>i Noun-NB-01 ;

LEXICON NMN_SÄYNÄ !!= @CODE@ säynä:säynä
	NMN_SÄYNÄ-SG  ;
	NMN_SÄYNÄ-PL  ;

LEXICON NMN_SÄYNÄ-SG
 SGNOMSUF ;
 SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;

LEXICON NMN_SÄYNÄ-PL
 PLNOMSUF-USUALLY-WEAK ;
:%^RmVow%>i PL-GEN/COM/APRSUF_EN ;
:%^RmVow%>i PLPARSUF_i ;
:%^RmVow%>i PLINSSUF ;
+Pl:%^RmVow%>i Noun-NB-01 ;

LEXICON NMN_SAMMAL !!= @CODE@ sammal:sammal
 NMN_SAMMAL-SG ;
 NMN_SAMMAL-PL ;

LEXICON NMN_SAMMAL-SG	
 SGNOMSUF ;
:e SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
: SGPARSUF_dU ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_SAMMAL-PL	
:e PLNOMSUF-USUALLY-WEAK ;
:i PL-GEN/COM/APRSUF_EN ;
:i PLPARSUF_i ;
:i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_TVER !!= @CODE@ tver:tver
!! Gradation NA
!! Front	 Vowel
!! Singular stem vowel 0/i
!! Plural stem vowel ilöi
 NMN_TVER-SG ;
 NMN_TVER-PL ;

LEXICON NMN_TVER-SG	
 SGNOMSUF ;
:i SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
: SGPARSUF_dU ;
+Sg:i Noun-NB-01-USUALLY-WEAK ;
+Sg:i Noun-NB-01-USUALLY-STRONG ;
LEXICON NMN_TVER-PL	
:i PLNOMSUF-USUALLY-WEAK ;
:i%>löi PL-GEN/COM/APRSUF_EN ;
:i%>löi PLPARSUF_i ;
:i%>löi PLINSSUF ;
+Pl:i%>löi Noun-NB-01-USUALLY-WEAK ;
+Pl:i%>löi Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_ŠOUFER !!= @CODE@ šoufer:šoufer
!! Gradation NA
!! Back	 Vowel
!! Singular stem vowel 0/i
!! Plural stem vowel ilöi
 NMN_ŠOUFER-SG ;
 NMN_ŠOUFER-PL ;

LEXICON NMN_ŠOUFER-SG	
 SGNOMSUF ;
:i SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
: SGPARSUF_dU ;
+Sg:i Noun-NB-01-USUALLY-WEAK ;
+Sg:i Noun-NB-01-USUALLY-STRONG ;
LEXICON NMN_ŠOUFER-PL	
:i PLNOMSUF-USUALLY-WEAK ;
:i%>l%{oö%}i PL-GEN/COM/APRSUF_EN ;
:i%>l%{oö%}i PLPARSUF_i ;
:i%>l%{oö%}i PLINSSUF ;
+Pl:i%>l%{oö%}i Noun-NB-01-USUALLY-WEAK ;
+Pl:i%>l%{oö%}i Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_VANUIN !!= @CODE@ vanuin:vanui
!! Gradation NA
!! Back	     Vowel
!! Stem	     consonant n/m
!! Singular stem vowel 0/0/e
!! Plural stem vowel i
 NMN_VANUIN-SG ;
 NMN_VANUIN-PL ;

LEXICON NMN_VANUIN-SG	
:n SGNOMSUF ;
:me SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:n SGPARSUF_A2 ;
+Sg:me Noun-NB-01 ;
LEXICON NMN_VANUIN-PL	
:me PLNOMSUF-USUALLY-WEAK ;
:mi PL-GEN/COM/APRSUF_EN ;
:mi PLPARSUF_i ;
:mi PLINSSUF ;
+Pl:mi Noun-NB-01 ;

LEXICON NMN_SAMMUTIN !!= @CODE@ sammutin:sammutti
!! Gradation Yes
!! (Nominative and partitive singular forms are weak grade)
!! Back	     Vowel
!! Stem	     consonant n/m
!! Singular stem vowel 0/0/e
!! Plural stem vowel i
 NMN_SAMMUTIN-SG ;
 NMN_SAMMUTIN-PL ;

LEXICON NMN_SAMMUTIN-SG	
:%^WGStemn SGNOMSUF ;
:me SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:%^WGStemn SGPARSUF_A2 ;
+Sg:me Noun-NB-01 ;
LEXICON NMN_SAMMUTIN-PL	
:me PLNOMSUF-USUALLY-WEAK ;
:mi PL-GEN/COM/APRSUF_EN ;
:mi PLPARSUF_i ;
:mi PLINSSUF ;
+Pl:mi Noun-NB-01 ;

LEXICON NMN_KEITIN !!= @CODE@ keitin:keitti
!! Gradation Yes
!! (Nominative and partitive singular forms are weak grade)
!! Front	     Vowel
!! Stem	     consonant n/m
!! Singular stem vowel 0/0/e
!! Plural stem vowel i
 NMN_KEITIN-SG ;
 NMN_KEITIN-PL ;

LEXICON NMN_KEITIN-SG	
:%^WGStemn SGNOMSUF ;
:me SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:%^WGStemn SGPARSUF_A2 ;
+Sg:me Noun-NB-01 ;
LEXICON NMN_KEITIN-PL	
:me PLNOMSUF-USUALLY-WEAK ;
:mi PL-GEN/COM/APRSUF_EN ;
:mi PLPARSUF_i ;
:mi PLINSSUF ;
+Pl:mi Noun-NB-01 ;

LEXICON NMN_VAŽEN !!= @CODE@ važen:važe
 NMN_VAŽEN-SG ;
 NMN_VAŽEN-PL ;

LEXICON NMN_VAŽEN-SG	
:n SGNOMSUF ;
:me SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:n SGPARSUF_A2 ;
+Sg:me Noun-NB-01 ;
LEXICON NMN_VAŽEN-PL	
:me PLNOMSUF-USUALLY-WEAK ;
:mi PL-GEN/COM/APRSUF_EN ;
:mi PLPARSUF_i ;
:mi PLINSSUF ;
+Pl:mi Noun-NB-01 ;

LEXICON NMN_LÄMMIN !!= @CODE@ lämmin:lämbi
 NMN_LÄMMIN-SG ;
 NMN_LÄMMIN-PL ;

LEXICON NMN_LÄMMIN-SG	
:%^WGStemn SGNOMSUF ;
:mä SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:%^WGStemn SGPARSUF_dU ;
+Sg:mä Noun-NB-01 ;
LEXICON NMN_LÄMMIN-PL	
:mä PLNOMSUF-USUALLY-WEAK ;
:mi PL-GEN/COM/APRSUF_EN ;
:mi PLPARSUF_i ;
:mi PLINSSUF ;
+Pl:mi Noun-NB-01 ;

LEXICON NMN_TAIGIN !!= @CODE@ taigin:taigin
 NMN_TAIGIN-SG ;
 NMN_TAIGIN-PL ;

LEXICON NMN_TAIGIN-SG	
: SGNOMSUF ;
:a SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:%{ui%} SGPARSUF_A2 ;
+Sg:a Noun-NB-01 ;
LEXICON NMN_TAIGIN-PL	
:a PLNOMSUF-USUALLY-WEAK ;
:oi PL-GEN/COM/APRSUF_N ;
:oi PLPARSUF_Zero ;
:oi PLINSSUF ;
+Pl:oi Noun-NB-01 ;

LEXICON NMN_KARJALAINE/LIYGILÄINE
	NMN_KARJALAINE/LIYGILÄINE-SG ;
	NMN_KARJALAINE/LIYGILÄINE-PL ;

LEXICON NMN_KARJALAINE/LIYGILÄINE-SG
:ine SGNOMSUF ;
:ze SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:s SGPARSUF_dU ;
+Sg:ze Noun-NB-01 ;

LEXICON NMN_KARJALAINE/LIYGILÄINE-PL
:ze PLNOMSUF-USUALLY-WEAK ;
:zi PL-GEN/COM/APRSUF_EN ;
:zi PLPARSUF_i ;
:zi PLINSSUF ;
+Pl:zi Noun-NB-01-USUALLY-WEAK ;
+Pl:zi Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_LIYGILÄINE
	NMN_LIYGILÄINE-SG ;
	NMN_LIYGILÄINE-PL ;

LEXICON NMN_LIYGILÄINE-SG
:ine SGNOMSUF ;
:ze SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:s SGPARSUF_dU ;
+Sg:ze Noun-NB-01-USUALLY-WEAK ;
+Sg:ze Noun-NB-01-USUALLY-STRONG ;
LEXICON NMN_LIYGILÄINE-PL
:ze PLNOMSUF-USUALLY-WEAK ;
:zi PL-GEN/COM/APRSUF_EN ;
:zi PLPARSUF_i ;
:zi PLINSSUF ;
+Pl:zi Noun-NB-01-USUALLY-WEAK ;
+Pl:zi Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_NAINE/KIELINE
	NMN_NAINE/KIELINE-SG ;
	NMN_NAINE/KIELINE-PL ;

LEXICON NMN_NAINE/KIELINE-SG
:ne SGNOMSUF ;
:ze SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:s SGPARSUF_dU ;
+Sg:ze  Noun-NB-01-USUALLY-WEAK ;
+Sg:ze  Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_NAINE/KIELINE-PL
:ze PLNOMSUF-USUALLY-WEAK ;
:zi PL-GEN/COM/APRSUF_EN ;
:zi PLPARSUF_i ;
:zi PLINSSUF ;
+Pl:zi Noun-NB-01-USUALLY-WEAK ;
+Pl:zi Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_KIELINE
	NMN_KIELINE-SG ;
	NMN_KIELINE-PL ;

LEXICON NMN_KIELINE-SG
:ne SGNOMSUF ;
:ze SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:s SGPARSUF_dU ;
+Sg:ze Noun-NB-01-USUALLY-WEAK ;
+Sg:ze Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_KIELINE-PL
:ze PLNOMSUF-USUALLY-WEAK ;
:zi PL-GEN/COM/APRSUF_EN ;
:zi PLPARSUF_i ;
:zi PLINSSUF ;
+Pl:zi Noun-NB-01-USUALLY-WEAK ;
+Pl:zi Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_TOINE
	NMN_TOINE-SG ;
	NMN_TOINE-PL ;

LEXICON NMN_TOINE-SG
:ine SGNOMSUF ;
:ize SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:s SGPARSUF_dU ;
+Sg:ize Noun-NB-01-USUALLY-WEAK ;
+Sg:ize Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_TOINE-PL
:ize PLNOMSUF-USUALLY-WEAK ;
:izi PL-GEN/COM/APRSUF_EN ;
:izi PLPARSUF_i ;
:izi PLINSSUF ;
+Pl:izi Noun-NB-01-USUALLY-WEAK ;
+Pl:izi Noun-NB-01-USUALLY-STRONG ;


!! ! Nominative singular in "v"
LEXICON NMN_ABRAMOV !!= * @CODE@ Abramov:Abramov
	NMN_ABRAMOV-SG ;
	NMN_ABRAMOV-PL ;

LEXICON NMN_ABRAMOV-SG
 SGNOMSUF ;
:%>%{aäPar%} SG-ACC/APR/COM/GENSUF-USUALLY-WEAK ;
:%>%{ui%} SGPARSUF_A2 ;
+Sg:%>%{aäPar%} Noun-NB-01-USUALLY-WEAK ;
+Sg:%>%{aäPar%} Noun-NB-01-USUALLY-STRONG ;

LEXICON NMN_ABRAMOV-PL
:%>%{aäPar%} PLNOMSUF-USUALLY-WEAK ;
:i PL-GEN/COM/APRSUF_EN ;
:i PLPARSUF_i ;
:i PLINSSUF ;
+Pl:i Noun-NB-01-USUALLY-WEAK ;
+Pl:i Noun-NB-01-USUALLY-STRONG ;



!! !Singular suffixes
!! 
LEXICON SG-ACC/APR/COM/GENSUF-USUALLY-WEAK
+Sg+Gen:%>n K ;
+Sg+Acc:%>n K ;
+Sg+Com:%>nke K ;
+Sg+Apr:%>ll%{uy%}%{oö%} K ;
+Sg+Ins:%>n K ;

LEXICON SG-ACC/APR/COM/GENSUF
+Sg+Gen:%>n K ;
+Sg+Acc:%>n K ;
+Sg+Com:%>nke K ;
+Sg+Apr:%>ll%{uy%}%{oö%} K ;

 SGINSSUF ;
 
LEXICON SGINSSUF
+Sg+Ins:%^WGStem%>n K ;

LEXICON SGNOMSUF
+Sg+Nom: K ;
+Sg+Acc: K ;

LEXICON SGPARSUF_dU
+Sg+Par:%>d%{uy%} K ;
LEXICON SGPARSUF_du
+Sg+Par:%>du K ;
LEXICON SGPARSUF_dy
+Sg+Par:%>dy K ;

LEXICON SGPARSUF_tu
+Sg+Par:%>tu K ;
LEXICON SGPARSUF_ty
+Sg+Par:%>ty K ;

LEXICON SGPARSUF_ttu
+Sg+Par:%>ttu K ;
LEXICON SGPARSUF_tty
+Sg+Par:%>tty K ;


LEXICON SGPARSUF_A2
+Sg+Par:%>%{aäoeöuiy%} K ;
!!€# luadi%^WGStem%^E1%>tt%{uy%}%>%{aäoeöuiy%}
!!€0 luaji000ttu0u

LEXICON SGPARSUF_U
+Sg+Par:%>%{uy%} K ;


!! !Plural suffixes
LEXICON PL-GEN/COM/APRSUF_EN
+Pl+Gen:%>en K ;
+Pl+Com:%>enke K ;
+Pl+Apr:%>ell%{uy%}%{oö%} K ;

LEXICON PL-GEN/COM/APRSUF_N
+Pl+Gen:%>n K ;
+Pl+Com:%>nke K ;
+Pl+Apr:%>ll%{uy%}%{oö%} K ;

LEXICON PLINSSUF
+Pl+Ins:%>n K ;

LEXICON PLNOMSUF-USUALLY-WEAK
+Pl+Nom:%>t K ;
+Pl+Acc:%>t K ;

LEXICON PLNOMSUF
+Pl+Nom:%^WGStem%>t K ;
+Pl+Acc:%^WGStem%>t K ;

LEXICON PLPARSUF_Zero
+Pl+Par: K ;

LEXICON PLPARSUF_a
+Pl+Par:%>%{aäPar%} K ;

LEXICON PLPARSUF_i
+Pl+Par:%>i K ;

LEXICON PLPARSUF_dU
+Pl+Par:%>d%{uy%} K ;

LEXICON ILLSUF_he
+Ill:%>he K ;

LEXICON Noun-NB-01
+Ade:%^WGStem%>l K ;
+Abl:%^WGStem%>l K ;
+Abl+Use/NG:%^WGStem%>lpäi K ;
+All:%^WGStem%>le K ;
+Abe:%^WGStem%>tt%{aäPar%}h K ;
+Abe+Err/Orth:%^WGStem%>tt%{aäPar%} K ;
+Abe:%^WGStem%>t%{aäPar%} K ;
+Ela:%^WGStem%>s K ;
+Ela+Use/NG:%^WGStem%>späi K ;
+Ess:%^WGStem%>nn%{uy%} K ;
+Ine:%^WGStem%>s K ;
!+Ins:%^WGStem%>n K ; 
+Ill:%>h K ;
+Tra:%^WGStem%>kse K ;
+Prl:%^WGStem%>či K ;
+Ter:%>ss%{aäPar%}h K ;

LEXICON Noun-NB-01-USUALLY-WEAK
+Ade:%>l K ;
+Abl:%>l K ;
+Abl+Use/NG:%>lpäi K ;
+All:%>le K ;
+All+PxSg3:%>lleh K ; ! More PX is needed 2015-11
+Abe:%>tt%{aäPar%}h K ;
+Abe:%>t%{aäPar%} K ;
+Abe+Err/Orth:%>tt%{aäPar%} K ;
+Ela:%>s K ;
+Ela+Use/NG:%>späi K ;
+Ess:%>nn%{uy%} K ;
+Ine:%>s K ;
!+Ins:%>n K ; 
+Tra:%>kse K ;
+Prl:%>či K ;

LEXICON Noun-NB-01-USUALLY-STRONG
+Ill:%>h K ;
+Ter:%>ss%{aäPar%}h K ;

!! !SINGULAR POSSESSA

LEXICON SGNOM/PXSP3 !!= @CODE@ adding -h
+Sg+Nom+PxSP3:%>h K ;

! vim: set ft=xfst-lexc:

