!! Noun inflection
!! ---------------
!! Livvi nouns inflect in cases.

LEXICON N_
+N+Sg+Nom+TYÄ: # ;



LEXICON N_KARJAL !!= @CODE@ karjal:karjal
+N: SGNOMSUF ;
+N:a SGGENSUF ;
+N:a SGPARSUF_A2 ;
+N:a%>i PLGENSUF ;
+N:i PLPARSUF_i ;
+N:%{oö%}i PLINSSUF ;
+N:%{aä%} Noun-NB-i ;

LEXICON N_KANDAI !!= @CODE@ kandai:kandai
+N: SGNOMSUF ;
+N:%{aä%} SGPARSUF_A2 ;
+N:ie PLGENSUF ;
+N:i PLPARSUF_i ;
+N:i PLINSSUF ;
+N+Sg:%{aä%} Noun-NB-01 ;
+N+Pl:i Noun-NB-01 ;

LEXICON N_PAGIZII !!= @CODE@ pagizii:pagizii
+N: SGNOMSUF ;
+N:A2 SGGENSUF ;
+N:A2 SGPARSUF_A2 ;
+N:%>%{oö%}i PLGENSUF ;
+N:%>%{oö%}i PLPARSUF_i ;
+N:%>%{oö%}i PLINSSUF ;
+N:A2 Noun-NB-01 ;
+N:%>%{oö%} Noun-NB-01 ;

LEXICON N_HUOLETOI !!= @CODE@ huoletoi: huolet
+N:%{oö%}i SGNOMSUF ;
+N:t%{oö%}ma SGPARSUF_A2 ;
+N:t%{oö%}mie PLGENSUF ;
+N:t%{oö%}mi PLPARSUF_i ;
+N:t%{oö%}mi PLINSSUF ;
+N:t%{oö%}ma Noun-NB-i ;

LEXICON N_KARJALAINE !!= @CODE@ karjalaine:karjala
 N_KARJALAINE-SG     ;
 N_KARJALAINE-PL     ;

LEXICON N_KARJALAINE-SG
+N:    NMN_KARJALAINE-SG	 ;

LEXICON N_KARJALAINE-PL
+N:    NMN_KARJALAINE-PL	 ;

LEXICON N_PEREH !!= @CODE@ pereh:pereh
+N: SGNOMSUF ;
+N:e SGGENSUF ;
+N: SGPARSUF_dU ;
+N+Sg:e Noun-NB-01 ;
+N:ie PLGENSUF ;
+N:i PLPARSUF_i ;
+N:i PLINSSUF ;
+N+Pl:i Noun-NB-01 ;

LEXICON N_ARTELI !!= @CODE@ arteli:arteli
+N: NMN_ARTELI ;

LEXICON N_KIELI !!= @CODE@ kieli:kiel
+N:i SGNOMSUF ;
+N:e SGGENSUF ;
+N: SGPARSUF_dU ;
+N+Sg:e Noun-NB-01 ;
+N:e PLNOMSUF ;
+N:ie PLGENSUF ;
+N:i PLPARSUF_i ;
+N:i PLINSSUF ;
+N+Pl:i Noun-NB-01 ;

LEXICON N_ANUS !!= @CODE@ aigomus:aigomuks
+N: NMN_ANUS ;

LEXICON N_ANUS-PL !!= @CODE@ aigomus:aigomuks
+N: NMN_ANUS-PL ;

LEXICON N_OLUT !!= @CODE@ olut:olu
+N:t SGNOMSUF ;
+N:e SGGENSUF ;
+N:t SGPARSUF_dU ;
+N+Sg:e Noun-NB-01 ;
+N:je PLGENSUF ;
+N:i PLPARSUF_dU ;
+N:i PLINSSUF ;
+N+Pl:i Noun-NB-01 ;

LEXICON N_KUU !!= @CODE@ kuu:kuu
+N: NMN_KUU ;

LEXICON N_MUARJU !!= @CODE@ muarju:muarja
+N: NMN_MUARJU ;

LEXICON NMN_MUARJU
:%^A2U SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;
 PLNOMSUF ;
:%>oi PLGENSUF ; ! the plural comitative is declined here
:%>oi PLPARSUF_a ;
:%>oi PLINSSUF ;
+Pl:%>oi Noun-NB-01 ;

LEXICON N_KALA !!= @CODE@ kala:kala
+N: NMN_KALA ;

LEXICON N_OZA !!= @CODE@ oza:oza
+N: NMN_OZA ;

LEXICON N_ARMAS !!= @CODE@ armas:arma
+N:%^WEAK%>s SGNOMSUF ;
+N:%^WEAK%>s SGPARSUF_dU ;
+N:hie PLGENSUF ;
+N:hi PLPARSUF_i ;
+N:hi PLINSSUF ;
+N:ha Noun-NB-i ;

LEXICON N_PAREMBI !!= @CODE@ parembi:paremb
+N:i SGNOMSUF ;
+N:%{aä%} SGPARSUF_A2 ;
+N:ie PLGENSUF ;
+N:i PLPARSUF_i ;
+N:i PLINSSUF ;
+N:%{aä%} Noun-NB-i ;

LEXICON N_KOIRU !!= @CODE@ koiru:koira
 N_PÄIVY ;

LEXICON N_NIMI !!= @CODE@ nimi:nim
	N_NIMI-SG  ;
	N_NIMI-PL  ;

LEXICON N_NIMI-SG
+N: NMN_NIMI-SG ;

LEXICON N_NIMI-PL
+N: NMN_NIMI-PL ;

LEXICON N_LAPSI !!= @CODE@ lapsi:laps lapsi:laps
+N:i SGNOMSUF ;
+N:e SGGENSUF ;
+N:%^KS2S SGPARSUF_dU ;
+N:e PLNOMSUF ;
+N:%>ie PLGENSUF ; ! the plural comitative is declined here
+N:%>i PLPARSUF_i ;
+N:%>i PLINSSUF ;
+N+Sg:e Noun-NB-01 ;
+N+Pl:i Noun-NB-01 ;

LEXICON N_VUOZI !!= @CODE@ vuozi:vuot
+N:%^T2Z%>i SGNOMSUF ;
+N:e SGGENSUF ;
+N: SGPARSUF_dU ;
+N+Sg:e Noun-NB-01 ;
+N:%^T2Z%>ie PLGENSUF ; ! the plural comitative is declined here
+N:%^T2Z%>i PLPARSUF_i ;
+N:%^T2Z%>i PLINSSUF ;
+N+Pl:%^T2Z%>i Noun-NB-01 ;

LEXICON N_PÄIVY !!= @CODE@ päivy:päivä
+N:%^A2U SGNOMSUF ;
+N: SGGENSUF ;
+N: SGPARSUF_A2 ;
+N: PLNOMSUF ;
+N:%>ie PLGENSUF ; ! the plural comitative is declined here
+N:%>i PLPARSUF_i ;
+N:%>i PLINSSUF ;
+N+Sg: Noun-NB-01 ;
+N+Pl:%>i Noun-NB-01 ;

LEXICON N_KOIVU !!= @CODE@ koivu:koivu
+N: SGNOMSUF ;
+N: SGGENSUF ;
+N: SGPARSUF_A2 ;
+N: PLNOMSUF ;
+N:%>l%{oö%}i PLGENSUF ; ! the plural comitative is declined here
+N:%>l%{oö%}i PLPARSUF_Zero ;
+N:%>l%{oö%}i PLINSSUF ;
+N+Sg: Noun-NB-01 ;
+N+Pl:%>lO1i Noun-NB-01 ;

LEXICON N_KIELINE !!= @CODE@ kieline:kieli
+N: NMN_KIELINE ;

LEXICON N_RUNO !!= @CODE@ runo:runo
+N: SGNOMSUF ;
+N: SGPARSUF_A2 ;
+N:l%{oö%}i PLGENSUF ;
+N:l%{oö%}i PLPARSUF_Zero ;
+N:l%{oö%}i PLINSSUF ;
+N: Noun-NB-loi ;

!! NMN
LEXICON NMN_NIMI
	NMN_NIMI-SG	;
	NMN_NIMI-PL	;

LEXICON NMN_NIMI-SG
:i SGNOMSUF ;
:e SGPARSUF_A2 ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_NIMI-PL
:%>ie PLGENSUF ; ! the plural comitative is declined here
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
:il%{oö%}i PLGENSUF ;
:il%{oö%}i PLPARSUF_Zero ;
:il%{oö%}i PLINSSUF ;
:i Noun-NB-loi ;


LEXICON NMN_ANUS !!= @CODE@ Anus:Anuks
	NMN_ANUS-SG ;
	NMN_ANUS-PL ;

LEXICON NMN_ANUS-SG
+N:%^KS2S SGNOMSUF ;
+N:e SGGENSUF ;
+N:%^KS2S SGPARSUF_dU ;
+N+Sg:e Noun-NB-01 ;

LEXICON NMN_ANUS-PL
+N:e PLNOMSUF ;
+N:%>ie PLGENSUF ; ! the plural comitative is declined here
+N:%>i PLPARSUF_i ;
+N:%>i PLINSSUF ;
+N+Pl:i Noun-NB-01 ;

LEXICON NMN_KUU
 SGNOMSUF ;
 SGPARSUF_dU ;
:%>l%{oö%}i PLGENSUF ;
:%>l%{oö%}i PLPARSUF_Zero ;
:%>l%{oö%}i PLINSSUF ;
+Sg: Noun-NB-01 ;
: Noun-NB-loi ;

LEXICON NMN_KALA !!= @CODE@ 
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
 PLNOMSUF ;
:i PLGENSUF ;
:i PLPARSUF_a ;
:i PLINSSUF ;
: Noun-NB-i ;

LEXICON NMN_KARJAL !!= @CODE@ karjal:karjal
 SGNOMSUF ;
:A2 SGGENSUF ;
:A2 SGPARSUF_A2 ;
:%>%{oö%}i PLGENSUF ;
:%>%{oö%}i PLPARSUF_Zero ;
:%>%{oö%}i PLINSSUF ;
+Sg:A2 Noun-NB-01 ;
+Pl:%>%{oö%}i Noun-NB-01 ;


LEXICON NMN_ARTELI !!= @CODE@ arteli:arteli
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
:i PLGENSUF ;
:%>l%{oö%}i PLPARSUF_Zero ;
:%>l%{oö%}i PLINSSUF ;
+Sg: Noun-NB-01 ;
+Pl:%>l%{oö%}i Noun-NB-01 ;

LEXICON NMN_OZA !!= @CODE@ oza:oza
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
:i PLGENSUF ;
:i PLPARSUF_a ;
:i PLINSSUF ;
+Sg: Noun-NB-01 ;
+Pl:i Noun-NB-01 ;

LEXICON NMN_KIELINE
:ne SGNOMSUF ;
:ze SGGENSUF ;
:s SGPARSUF_dU ;
:zie PLGENSUF ;
:zi PLPARSUF_i ;
:zi PLINSSUF ;
+Sg:ze Noun-NB-01 ;
+Pl:zi Noun-NB-01 ;

LEXICON NMN_KARJALAINE
	NMN_KARJALAINE-SG ;
	NMN_KARJALAINE-PL ;

LEXICON NMN_KARJALAINE-SG
:ine SGNOMSUF ;
:ze SGGENSUF ;
:s SGPARSUF_dU ;
+Sg:ze Noun-NB-01 ;

LEXICON NMN_KARJALAINE-PL
:ze PLNOMSUF ;
:zie PLGENSUF ;
:zi PLPARSUF_i ;
:zi PLINSSUF ;
+Pl:zi Noun-NB-01 ;


LEXICON Noun-NB-i
! These cases are symmetrically marked for number
+Sg: Noun-NB-01 ;
+Pl:%>i Noun-NB-01 ;
! The next two share the same stem vowel
 SGGENSUF ; ! The singular comitative is declined here
 PLNOMSUF ;
 
LEXICON Noun-NB-loi
! These cases are symmetrically marked for number
+Sg: Noun-NB-01 ;
+Pl:%>l%{oö%}i Noun-NB-01 ;
+Pl:%>loi ILLSUF_he ;
! The next two share the same stem vowel
 SGGENSUF ; ! The singular comitative is declined here
 PLNOMSUF ;
 

LEXICON SGNOMSUF
+Sg+Nom: K ;
+Sg+Acc: K ;

LEXICON SGPARSUF_dU
+Sg+Par:%>d%{uy%} K ;


LEXICON SGPARSUF_A2
+Sg+Par:%>A2 K ;

LEXICON SGGENSUF
+Sg+Gen:%^WEAK%>n K ;
+Sg+Acc:%^WEAK%>n K ;
+Sg+Com:%^WEAK%>nke K ;

LEXICON PLNOMSUF
+Pl+Nom:%^WEAK%>t K ;
+Pl+Acc:%^WEAK%>t K ;

LEXICON PLGENSUF
+Pl+Gen:%>n K ;
+Pl+Com:%^WEAK%>nke K ;

LEXICON ILLSUF_he
+Ill:%>he K ;

LEXICON PLINSSUF
+Pl+Ins:%>n K ;

LEXICON PLPARSUF_Zero
+Pl+Par: K ;

LEXICON PLPARSUF_a
+Pl+Par:%>%{aä%} K ;

LEXICON PLPARSUF_i
+Pl+Par:%>i K ;

LEXICON PLPARSUF_dU
+Pl+Par:%>d%{uy%} K ;

LEXICON Noun-NB-01
+Ess:%^WEAK%>nn%{uy%} K ;
+Tra:%^WEAK%>kse K ;
+Ine:%^WEAK%>s K ;
+Ela:%^WEAK%>s K ;
+Ela:%^WEAK%>späi K ;
+Ill:%>h K ;
+Ade:%^WEAK%>l K ;
+Abl:%^WEAK%>l K ;
+Abl:%^WEAK%>lpäi K ;
+All:%^WEAK%>le K ;
+Abe:%^WEAK%>tt%{aä%}h K ;
+Abe:%^WEAK%>tt%{aä%} K ;
+Abe:%^WEAK%>t%{aä%} K ;
+Prl:%>či K ;
+Apr:%^WEAK%>ll%{uy%}%{oö%} K ;
+Ter:%^WEAK%>ss%{aä%}h K ;


! vim: set ft=xfst-lexc:

