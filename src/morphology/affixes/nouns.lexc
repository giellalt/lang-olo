!! Noun inflection
!! ---------------
!! Livvi nouns inflect in cases.

LEXICON N_
+N+Sg+Nom+TYÄ: # ;

!! !ONE-SYLLABLE VOWEL-FINAL STEMS ENDING IN LONG VOWEL SEGMENT
LEXICON N_SUO !!= @CODE@ suo:suo
+N: NMN_KUU ;

LEXICON N_KUU !!= @CODE@ kuu:kuu
+N: NMN_KUU ;

LEXICON N_MUA !!= @CODE@ mua:maa
+N: NMN_MUA ;

!! !TWO-SYLLABLE VOWEL-FINAL STEMS
LEXICON N_PAPPI !!= @CODE@ pappi:pappi
	N_PAPPI-SG ;
	N_PAPPI-PL ;
LEXICON N_PAPPI-SG
+N: NMN_PAPPI-SG ;
LEXICON N_PAPPI-PL
+N: NMN_PAPPI-PL ;

LEXICON N_VIGA !!= @CODE@ viga:viga
 N_KALA ;
LEXICON N-HUM_KALA
+N+Hum: NMN_KALA ;

LEXICON N_KALA !!= @CODE@ kala:kala
	N_KALA-SG  ;
	N_KALA-PL  ;
LEXICON N_KALA-SG
+N: NMN_KALA ;
LEXICON N_KALA-PL
+N: NMN_KALA-PL ;

LEXICON N_OZA !!= @CODE@ oza:oza
	N_OZA-SG  ;
	N_OZA-PL  ;

LEXICON N_OZA-SG
+N: NMN_OZA-SG ;

LEXICON N_OZA-PL
+N: NMN_OZA-PL ;

LEXICON N_SÄYNÄ !!= @CODE@ säynä:säynä
	N_SÄYNÄ-SG  ;
	N_SÄYNÄ-PL  ;

LEXICON N_SÄYNÄ-SG
+N: NMN_SÄYNÄ-SG ;

LEXICON N_SÄYNÄ-PL
+N: NMN_SÄYNÄ-PL ;

LEXICON N_KOIVU !!= @CODE@ koivu:koivu
        N_KOIVU-SG ;
        N_KOIVU-PL ;

LEXICON N_KOIVU-SG
+N: NMN_KOIVU-SG        ;

LEXICON N_KOIVU-PL
+N: NMN_KOIVU-PL        ;

LEXICON N_RUNO !!= @CODE@ runo:runo
	 N_RUNO-SG ;
	 N_RUNO-PL ;

LEXICON N_RUNO-SG
+N: NMN_RUNO-SG   ;
LEXICON N_RUNO-PL
+N: NMN_RUNO-PL   ;

LEXICON N_KYNDÖ !!= @CODE@ kyndö:kyndö
	 N_KYNDÖ-SG ;
	 N_KYNDÖ-PL ;

LEXICON N_KYNDÖ-SG
+N: NMN_KYNDÖ-SG   ;
LEXICON N_KYNDÖ-PL
+N: NMN_KYNDÖ-PL   ;

!! !TWO-SYLLABLE VOWEL-FINAL STEM WITH UNIQUE +Nom+Sg VOWEL

LEXICON N_JÄLGI  !!= @CODE@ jälgi:jälg
 N_NIMI ;

LEXICON N_MUARJU !!= @CODE@ muarju:muarja
 N_MUARJU-SG ;
 N_MUARJU-PL ;

LEXICON N_MUARJU-SG
+N: NMN_MUARJU-SG ;
LEXICON N_MUARJU-PL
+N: NMN_MUARJU-PL ;

LEXICON N_SULGU !!= @CODE@ sulgu:sulga
 N_KOIRU ;

LEXICON N_KOIRU !!= @CODE@ koiru:koira
 N_KOIRU-SG ;
 N_KOIRU-PL ;

LEXICON N_KOIRU-SG
 N_PÄIVY-SG ;

LEXICON N_KOIRU-PL
 N_PÄIVY-PL ;

LEXICON N_NIMI !!= @CODE@ nimi:nim
	N_NIMI-SG  ;
	N_NIMI-PL  ;

LEXICON N_NIMI-SG
+N: NMN_NIMI-SG ;

LEXICON N_NIMI-PL
+N: NMN_NIMI-PL ;

LEXICON N_PÄIVY !!= @CODE@ päivy:päivä
	N_PÄIVY-SG ;
	N_PÄIVY-PL ;

LEXICON N_PÄIVY-SG
+N: NMN_PÄIVY-SG	;

LEXICON N_PÄIVY-PL
+N: NMN_PÄIVY-PL	;

 LEXICON N_SIVE !!= @CODE@ sive:sive
	N_SIVE-SG	;
	N_SIVE-PL	;
LEXICON N_SIVE-SG
+N:	NMN_SIVE-SG	;

LEXICON N_SIVE-PL
+N:	NMN_SIVE-PL	;

LEXICON N_HARDIE !!= @CODE@ hardie:hardie
        N_HARDIE-SG ;
        N_HARDIE-PL ;

LEXICON N_HARDIE-SG
+N: NMN_HARDIE-SG        ;

LEXICON N_HARDIE-PL
+N: NMN_HARDIE-PL        ;

LEXICON N_REBOI !!= @CODE@ reboi:reboi
        N_REBOI-SG ;
        N_REBOI-PL ;
LEXICON N_REBOI-SG
+N: NMN_REBOI-SG ;
LEXICON N_REBOI-PL
+N: NMN_REBOI-PL ;

LEXICON N_VALGEI !!= @CODE@ valgei:valge
        N_VALGEI-SG ;
        N_VALGEI-PL ;

LEXICON N_VALGEI-SG
+N: NMN_VALGEI-SG        ;

LEXICON N_VALGEI-PL
+N: NMN_VALGEI-PL        ;

LEXICON N_OSTAI !!= @CODE@ ostai:osta
        N_OSTAI-SG ;
        N_OSTAI-PL ;

LEXICON N_OSTAI-SG
+N:%>j NMN_OSTAI-SG        ;

LEXICON N_OSTAI-PL
+N:%> NMN_OSTAI-PL        ;

LEXICON N_PEZII !!= @CODE@ pezii:pezi
        N_PEZII-SG ;
        N_PEZII-PL ;

LEXICON N_PEZII-SG
+N:%>j NMN_PEZII-SG        ;

LEXICON N_PEZII-PL
+N:%> NMN_PEZII-PL        ;


!! !TWO-SYLLABLE VOWEL-FINAL STEMS WITH CONSONANT-FINAL PARTITIVE STEM
LEXICON N_UKSI !!= @CODE@ uksi:uks
	N_UKSI-SG ;
	N_UKSI-PL ;
LEXICON N_UKSI-SG
+N: NMN_UKSI-SG ;
LEXICON N_UKSI-PL
+N: NMN_UKSI-PL ;

LEXICON N_KIELI !!= @CODE@ kieli:kiel
 N_KIELI-SG ;
 N_KIELI-PL ;

LEXICON N_KIELI-SG
+N: 	NMN_KIELI-SG ;
LEXICON N_KIELI-PL
+N: 	NMN_KIELI-PL ;

LEXICON N_LAPSI !!= @CODE@ lapsi:laps lapsi:laps
        N_LAPSI-SG  ;
        N_LAPSI-PL  ;

LEXICON N_LAPSI-SG
+N: NMN_LAPSI-SG ;

LEXICON N_LAPSI-PL
+N: NMN_LAPSI-PL ;

LEXICON N_VEZI !!= @CODE@ vezi:ved
 N_VEZI-SG ;
 N_VEZI-PL ;

LEXICON N_VEZI-SG
+N: NMN_VEZI-SG ;

LEXICON N_VEZI-PL
+N: NMN_VEZI-PL ;

LEXICON N_SUZI !!= @CODE@ suzi:su
 N_SUZI-SG ;
 N_SUZI-PL ;

LEXICON N_SUZI-SG
+N: NMN_SUZI-SG ;

LEXICON N_SUZI-PL
+N: NMN_SUZI-PL ;

LEXICON N_VUOZI !!= @CODE@ vuozi:vuod
 N_VUOZI-SG        ;
 N_VUOZI-PL        ;

LEXICON N_VUOZI-SG
+N: NMN_VUOZI-SG	;
LEXICON N_VUOZI-PL
+N: NMN_VUOZI-PL	;

!! !THREE-SYLLABLE VOWEL-FINAL STEMS
LEXICON N_SYGYZY !!= @CODE@ sygyzy:sygyzy
        N_SYGYZY-SG ;
        N_SYGYZY-PL ;

LEXICON N_SYGYZY-SG
+N: NMN_SYGYZY-SG        ;

LEXICON N_SYGYZY-PL
+N: NMN_SYGYZY-PL        ;

LEXICON N_VASKIČČU !!= @CODE@ vaskičču:vaskičča
        N_VASKIČČU-SG ;
        N_VASKIČČU-PL ;

LEXICON N_VASKIČČU-SG
+N: NMN_VASKIČČU-SG        ;

LEXICON N_VASKIČČU-PL
+N: NMN_VASKIČČU-PL        ;

!! !THREE-SYLLABLE STEMS WITH TWO-SYLLABLE NOMINATIVE SINGULAR
LEXICON N_KARJAL !!= @CODE@ karjal:karjal
	N_KARJAL-SG ;
	N_KARJAL-PL ;
LEXICON N_KARJAL-SG !!= @CODE@ karjal:karjal
+N:	NMN_KARJAL-SG ;
LEXICON N_KARJAL-PL !!= @CODE@ karjal:karjal
+N:	NMN_KARJAL-PL ;

LEXICON N_MADAL !!= @CODE@ madal:madal
	N_MADAL-SG ;
	N_MADAL-PL ;
LEXICON N_MADAL-SG !!= @CODE@ madal:madal
+N:	NMN_MADAL-SG ;
LEXICON N_MADAL-PL !!= @CODE@ madal:madal
+N:	NMN_MADAL-PL ;

LEXICON N_PIIRAI !!= @CODE@ piirai:piira CHECK THIS
        N_PIIRAI-SG ;
        N_PIIRAI-PL ;

LEXICON N_PIIRAI-SG
+N: NMN_PIIRAI-SG ;

LEXICON N_PIIRAI-PL
+N: NMN_PIIRAI-PL ;


LEXICON N_KANDAI !!= @CODE@ kandai:kandai
+N: SGNOMSUF ;
+N:%{ui%} SGPARSUF_A2 ;
+N:ie PLGENSUF_N ;
+N:i PLPARSUF_i ;
+N:i PLINSSUF ;
+N+Sg:%{aä%} Noun-NB-01 ;
+N+Pl:i Noun-NB-01 ;

LEXICON N_PAGIZII !!= @CODE@ pagizii:pagizii
 N_PAGIZII-SG ;
 N_PAGIZII-PL ;
LEXICON N_PAGIZII-SG !!= @CODE@ 
+N: NMN_PAGIZII-SG ;
LEXICON N_PAGIZII-PL !!= @CODE@ 
+N: NMN_PAGIZII-PL ;

LEXICON N_HUOLETOI !!= @CODE@ huoletoi: huolet
 N_HUOLETOI-SG ;
 N_HUOLETOI-PL ;

LEXICON N_HUOLETOI-SG
+N: NMN_HUOLETOI-SG ;

LEXICON N_HUOLETOI-PL
+N: NMN_HUOLETOI-PL ;

LEXICON N_VANUIN !!= @CODE@ vanuin:vanui
 N_VANUIN-SG ;
 N_VANUIN-PL ;

LEXICON N_VANUIN-SG
+N: NMN_VANUIN-SG ;

LEXICON N_VANUIN-PL
+N: NMN_VANUIN-PL ;

LEXICON N_TAIGIN !!= @CODE@ taigin:taigin
 N_TAIGIN-SG ;
 N_TAIGIN-PL ;

LEXICON N_TAIGIN-SG
+N: NMN_TAIGIN-SG ;

LEXICON N_TAIGIN-PL
+N: NMN_TAIGIN-PL ;

LEXICON NMN_VANUIN !!= @CODE@ vanuin:vanui
 NMN_VANUIN-SG ;
 NMN_VANUIN-PL ;

LEXICON NMN_VANUIN-SG	
:n SGNOMSUF ;
:me SGGENSUF ;
:n SGPARSUF_A2 ;
+Sg:me Noun-NB-01 ;
LEXICON NMN_VANUIN-PL	
:me PLNOMSUF ;
:mie PLGENSUF_N ;
:mi PLPARSUF_i ;
:mi PLINSSUF ;
+Pl:mi Noun-NB-01 ;

LEXICON NMN_VAŽEN !!= @CODE@ važen:važe
 NMN_VAŽEN-SG ;
 NMN_VAŽEN-PL ;

LEXICON NMN_VAŽEN-SG	
:n SGNOMSUF ;
:me SGGENSUF ;
:n SGPARSUF_A2 ;
+Sg:me Noun-NB-01 ;
LEXICON NMN_VAŽEN-PL	
:me PLNOMSUF ;
:mie PLGENSUF_N ;
:mi PLPARSUF_i ;
:mi PLINSSUF ;
+Pl:mi Noun-NB-01 ;

LEXICON NMN_LÄMMIN !!= @CODE@ lämmin:lämbi
 NMN_LÄMMIN-SG ;
 NMN_LÄMMIN-PL ;

LEXICON NMN_LÄMMIN-SG	
:%^WGStemn SGNOMSUF ;
:mä SGGENSUF ;
:%^WGStemn SGPARSUF_dU ;
+Sg:mä Noun-NB-01 ;
LEXICON NMN_LÄMMIN-PL	
:mä PLNOMSUF ;
:mie PLGENSUF_N ;
:mi PLPARSUF_i ;
:mi PLINSSUF ;
+Pl:mi Noun-NB-01 ;

LEXICON NMN_TAIGIN !!= @CODE@ taigin:taigin
 NMN_TAIGIN-SG ;
 NMN_TAIGIN-PL ;

LEXICON NMN_TAIGIN-SG	
: SGNOMSUF ;
:a SGGENSUF ;
:%{ui%} SGPARSUF_A2 ;
+Sg:a Noun-NB-01 ;
LEXICON NMN_TAIGIN-PL	
:a PLNOMSUF ;
:oi PLGENSUF_N ;
:oi PLPARSUF_i ;
:oi PLINSSUF ;
+Pl:oi Noun-NB-01 ;

LEXICON N_KARJALAINE !!= @CODE@ karjalaine:karjala
 N_KARJALAINE-SG     ;
 N_KARJALAINE-PL     ;

LEXICON N_KARJALAINE-SG
+N:    NMN_KARJALAINE-SG	 ;

LEXICON N_KARJALAINE-PL
+N:    NMN_KARJALAINE-PL	 ;

LEXICON N_KIELINE !!= @CODE@ kieline:kieli
 N_KIELINE-SG     ;
 N_KIELINE-PL     ;

LEXICON N_KIELINE-SG
+N:    NMN_KIELINE-SG         ;

LEXICON N_KIELINE-PL
+N:    NMN_KIELINE-PL         ;

LEXICON N_TOINE !!= @CODE@ toine:to
 N_TOINE-SG     ;
 N_TOINE-PL     ;

LEXICON N_TOINE-SG
+N:    NMN_TOINE-SG         ;

LEXICON N_TOINE-PL
+N:    NMN_TOINE-PL         ;



LEXICON N_RAIŠ !!= @CODE@ raiš:ra
	N_RAIŠ-SG	;
	N_RAIŠ-PL	;
LEXICON N_RAIŠ-SG
+N:	NMN_RAIŠ-SG	;

LEXICON N_RAIŠ-PL
+N:	NMN_RAIŠ-PL	;

LEXICON N_PEREH !!= @CODE@ pereh:pereh
	N_PEREH-SG	;
	N_PEREH-PL	;
LEXICON N_PEREH-SG
+N:	NMN_PEREH-SG	;

LEXICON N_PEREH-PL
+N:	NMN_PEREH-PL	;

LEXICON N_PENGER !!= @CODE@ penger:penger
	N_PENGER-SG	;
	N_PENGER-PL	;
LEXICON N_PENGER-SG
+N:	NMN_PENGER-SG	;

LEXICON N_PENGER-PL
+N:	NMN_PENGER-PL	;

LEXICON N_ARTELI !!= @CODE@ arteli:arteli
	N_ARTELI-SG ;
	N_ARTELI-PL ;
LEXICON N_ARTELI-SG
+N: NMN_ARTELI-SG ;
LEXICON N_ARTELI-PL
+N: NMN_ARTELI-PL ;

LEXICON N_PUHTAHUS !!= @CODE@ puhtahus:puhtahu
+N: NMN_PUHTAHUS ;

LEXICON N_AIGOMUS
 N_ALUS ;

LEXICON N_ALUS !!= @CODE@ alus:aluks
 N_ALUS-SG ;
 N_ALUS-PL ;

LEXICON N_ALUS-SG !!= @CODE@ alus:aluks
+N: NMN_ALUS-SG ;

LEXICON N_ALUS-PL !!= @CODE@ alus:aluks
+N: NMN_ALUS-PL ;

LEXICON N_MIES !!= @CODE@ mies:mie
 N_MIES-SG ;
 N_MIES-PL ;

LEXICON N_MIES-SG !!= @CODE@ mies:mie
+N: NMN_MIES-SG ;

LEXICON N_MIES-PL !!= @CODE@ mies:mie
+N: NMN_MIES-PL ;

LEXICON N_SUALIŠ !!= @CODE@ suališ:suali
 N_SUALIŠ-SG ;
 N_SUALIŠ-PL ;

LEXICON N_SUALIŠ-SG !!= @CODE@ suališ:suali
+N: NMN_SUALIŠ-SG ;

LEXICON N_SUALIŠ-PL !!= @CODE@ suališ:suali
+N: NMN_SUALIŠ-PL ;

LEXICON N_OLUT !!= @CODE@ olut:olu
 N_OLUT-SG ;
 N_OLUT-PL ;

LEXICON N_OLUT-SG
+N: NMN_OLUT-SG ;

LEXICON N_OLUT-PL
+N: NMN_OLUT-PL ;

LEXICON NMN_MUARJU !!= @CODE@ muarju:muarja
 NMN_MUARJU-SG ;
 NMN_MUARJU-PL ;

LEXICON NMN_MUARJU-SG
:%^A2U SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;
LEXICON NMN_MUARJU-PL
 PLNOMSUF ;
:%>i PLGENSUF_N ; ! the plural comitative is declined here
:%>i PLPARSUF_Zero ;
:%>i PLINSSUF ;
+Pl:%>i Noun-NB-01 ;

LEXICON N_ARMAS !!= @CODE@ armas:arma
    N_ARMAS-SG        ;
    N_ARMAS-PL        ;
LEXICON N_ARMAS-SG
+N: NMN_ARMAS-SG	;
LEXICON N_ARMAS-PL
+N: NMN_ARMAS-PL	;


LEXICON N_PAREMBI !!= @CODE@ parembi:paremb
 N_PAREMBI-SG	  ;
 N_PAREMBI-PL	  ;

LEXICON N_PAREMBI-SG
+N: NMN_PAREMBI-SG   ;
LEXICON N_PAREMBI-PL
+N: NMN_PAREMBI-PL   ;

LEXICON N_SUURIM !!= @CODE@ suurim:suurim
        N_SUURIM-SG  ;
        N_SUURIM-PL  ;

LEXICON N_SUURIM-SG
+N: NMN_SUURIM-SG ;

LEXICON N_SUURIM-PL
+N:     NMN_SUURIM ;

LEXICON N_TUATINDAM !!= @CODE@ tuatindam:tuatindam
        N_TUATINDAM-SG  ;
        N_TUATINDAM-PL  ;

LEXICON N_TUATINDAM-SG
+N: NMN_TUATINDAM-SG ;

LEXICON N_TUATINDAM-PL
+N:     NMN_TUATINDAM ;

LEXICON N_TUATANDIM !!= @CODE@ tuatandim:tuatandim
        N_TUATANDIM-SG  ;
        N_TUATANDIM-PL  ;

LEXICON N_TUATANDIM-SG
+N: NMN_TUATANDIM-SG ;

LEXICON N_TUATANDIM-PL
+N:     NMN_TUATANDIM ;

LEXICON N_SIEMEN !!= @CODE@ siemen:siemen
        N_SIEMEN-SG  ;
        N_SIEMEN-PL  ;

LEXICON N_SIEMEN-SG
        N_PAIMEN-SG  ;
LEXICON N_SIEMEN-PL
        N_PAIMEN-PL  ;

LEXICON N_PAIMEN !!= @CODE@ paimen:paimen
        N_PAIMEN-SG  ;
        N_PAIMEN-PL  ;

LEXICON N_PAIMEN-SG
+N: NMN_PIENAR-SG ;

LEXICON N_PAIMEN-PL
+N: NMN_PIENAR-PL ;

LEXICON N_SALBOIN !!= @CODE@ salboin:salboi
        N_SALBOIN-SG  ;
        N_SALBOIN-PL  ;

LEXICON N_SALBOIN-SG
+N: NMN_SALBOIN-SG ;

LEXICON N_SALBOIN-PL
+N: NMN_SALBOIN-PL ;

LEXICON N_ENIN !!= @CODE@ enin:eni
        N_ENIN-SG  ;
        N_ENIN-PL  ;

LEXICON N_ENIN-SG
+N: NMN_ENIN-SG ;

LEXICON N_ENIN-PL
+N: NMN_ENIN-PL ;

LEXICON N_UDAR !!= @CODE@ udar:udar
        N_UDAR-SG  ;
        N_UDAR-PL  ;

LEXICON N_UDAR-SG
+N: NMN_UDAR-SG ;

LEXICON N_UDAR-PL
+N: NMN_UDAR-PL ;

LEXICON N_PIENAR !!= @CODE@ pienar:piendar
        N_PIENAR-SG  ;
        N_PIENAR-PL  ;

LEXICON N_PIENAR-SG
+N: NMN_PIENAR-SG ;

LEXICON N_PIENAR-PL
+N: NMN_PIENAR-PL ;


LEXICON NMN_LAPSI !!= @CODE@ lapsi:laps lapsi:laps
        NMN_LAPSI-SG  ;
	NMN_LAPSI-PL  ;

LEXICON NMN_LAPSI-SG
:i SGNOMSUF ;
:e SGGENSUF ;
:%^KS2S SGPARSUF_dU ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_LAPSI-PL
:e PLNOMSUF ;
:%>ie PLGENSUF_N ; ! the plural comitative is declined here
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
+Pl:i Noun-NB-01 ;

LEXICON NMN_VEZI !!= @CODE@ vezi:vet
 NMN_VEZI-SG ;
 NMN_VEZI-PL ;

LEXICON NMN_VEZI-SG
+N:%^D2Z%>i SGNOMSUF ;
+N:e SGGENSUF ;
+N: SGPARSUF_dU ;
+N+Sg:e Noun-NB-01 ;

LEXICON NMN_VEZI-PL
+N:%^D2Z%>ie PLGENSUF_N ; ! the plural comitative is declined here
+N:%^D2Z%>i PLPARSUF_i ;
+N:%^D2Z%>i PLINSSUF ;
+N+Pl:%^D2Z%>i Noun-NB-01 ;

LEXICON NMN_SUZI !!= @CODE@ suzi:su
 NMN_SUZI-SG ;
 NMN_SUZI-PL ;

LEXICON NMN_SUZI-SG
+N:z%>i SGNOMSUF ;
+N:ze SGGENSUF ;
+N:t SGPARSUF_dU ;
+N+Sg:ze Noun-NB-01 ;

LEXICON NMN_SUZI-PL
+N:z%>ie PLGENSUF_N ; ! the plural comitative is declined here
+N:z%>i PLPARSUF_i ;
+N:z%>i PLINSSUF ;
+N+Pl:z%>i Noun-NB-01 ;

!! !NMN = Nominals mutually shared case marking for nouns, adjectives, proper ouns, numerals
LEXICON NMN_NIMI
	NMN_NIMI-SG	;
	NMN_NIMI-PL	;

LEXICON NMN_NIMI-SG
:i SGNOMSUF ;
:e SGPARSUF_A2 ;
:e SGGENSUF ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_NIMI-PL
:e PLNOMSUF	;
:%>ie PLGENSUF_N ; ! the plural comitative is declined here
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
:il%{oö%}i PLGENSUF_N ;
:il%{oö%}i PLPARSUF_Zero ;
:il%{oö%}i PLINSSUF ;
:i Noun-NB-loi ;

LEXICON NMN_KIELI
	NMN_KIELI-SG ;
	NMN_KIELI-PL ;
LEXICON NMN_KIELI-SG
:i SGNOMSUF ;
:e SGGENSUF ;
: SGPARSUF_dU ;
+Sg:e Noun-NB-01 ;

LEXICON NMN_KIELI-PL
:e PLNOMSUF ;
:ie PLGENSUF_N ;
:i PLPARSUF_i ;
:i PLINSSUF ;
+Pl:i Noun-NB-01 ;
:il%{oö%}i PLGENSUF_N ;
:il%{oö%}i PLPARSUF_Zero ;
:il%{oö%}i PLINSSUF ;
+Pl:il%{oö%}i Noun-NB-01 ;

LEXICON NMN_YKSI  !!= @CODE@ yksi:y
	NMN_YKSI-SG ;
	NMN_YKSI-PL ;

LEXICON NMN_YKSI-SG
:ksi SGNOMSUF ;
:hte SGGENSUF ;
:h SGPARSUF_dU ;
+Sg:hte Noun-NB-01 ;

LEXICON NMN_YKSI-PL
:ksie PLGENSUF_N ; ! the plural comitative is declined here
:ksi PLPARSUF_i ;
:ksi PLINSSUF ;
+Pl:ksi Noun-NB-01 ;


LEXICON NMN_VUOZI !!= @CODE@ vuozi:vuod
	NMN_VUOZI-SG ;
	NMN_VUOZI-PL ;

LEXICON NMN_VUOZI-SG
:%^D2Z%>i SGNOMSUF ;
:e SGGENSUF ;
: SGPARSUF_dU ;
+Sg:e Noun-NB-01 ;

LEXICON NMN_VUOZI-PL
:e PLNOMSUF ;
:%^D2Z%>ie PLGENSUF_N ; ! the plural comitative is declined here
:%^D2Z%>i PLPARSUF_i ;
:%^D2Z%>i PLINSSUF ;
+Pl:%^D2Z%>i Noun-NB-01 ;

LEXICON NMN_PAREMBI !!= @CODE@ parembi:paremb
 NMN_PAREMBI-SG ;
 NMN_PAREMBI-PL ;

LEXICON NMN_PAREMBI-SG
:i SGNOMSUF ;
:%{aä%} SGGENSUF ;
:%{ui%} SGPARSUF_A2 ;
+Sg:%{aä%} Noun-NB-01 ;

LEXICON NMN_PAREMBI-PL
:%{aä%} PLNOMSUF ;
:ie PLGENSUF_N ;
:i PLPARSUF_i ;
:i PLINSSUF ;
+Pl: Noun-NB-01 ;

LEXICON	NMN_KUDAI !!= @CODE@ kudai:kuda
 NMN_KUDAI-SG ;
 NMN_KUDAI-PL ;
LEXICON NMN_KUDAI-SG
:i SGNOMSUF ;
:m%>%{aä%} SGGENSUF ;
:m%>%{ui%} SGPARSUF_A2 ;
+Sg:m%>%{aä%} Noun-NB-01 ;

LEXICON NMN_KUDAI-PL
:m%>%{aä%} PLNOMSUF ;
:mie PLGENSUF_N ;
:mi PLPARSUF_i ;
:mi PLINSSUF ;
+Pl:mi Noun-NB-01 ;



LEXICON NMN_PIIRAI !!= @CODE@ piirai:piira
	NMN_PIIRAI-SG ;
	NMN_PIIRAI-PL ;

LEXICON NMN_PIIRAI-SG
:i SGNOMSUF ;
:a SGGENSUF ;
:a SGPARSUF_dU ;
+Sg:%{aä%} Noun-NB-01 ;

LEXICON NMN_PIIRAI-PL
:a PLNOMSUF ;
+N:al%{oö%}i PLGENSUF_N ;
+N:al%{oö%}i PLPARSUF_Zero ;
+N:al%{oö%}i PLINSSUF ;
+Pl:al%{oö%}i Noun-NB-01 ;


LEXICON NMN_PAGIZII
 NMN_PAGIZII-SG ;
 NMN_PAGIZII-PL ;

LEXICON NMN_PAGIZII-SG !!= @CODE@ 
 SGNOMSUF ;
:%{aä%} SGGENSUF ;
:%{ui%} SGPARSUF_A2 ;

LEXICON NMN_PAGIZII-PL !!= @CODE@ 
:%{aä%} PLNOMSUF ;
:%>%{oö%}i PLGENSUF_N ;
:%>%{oö%}i PLPARSUF_i ;
:%>%{oö%}i PLINSSUF ;
:A2 Noun-NB-01 ;
:%>%{oö%} Noun-NB-01 ;

LEXICON NMN_HUOLETOI !!= @CODE@ huoletoi: huolet
 NMN_HUOLETOI-SG ;
 NMN_HUOLETOI-PL ;

LEXICON NMN_HUOLETOI-SG	
:%{oö%}i SGNOMSUF ;
:t%{oö%}m%{aä%} SGGENSUF ;
:t%{oö%}m%{ui%} SGPARSUF_A2 ;
+Sg:t%{oö%}m%{aä%} Noun-NB-01 ;
LEXICON NMN_HUOLETOI-PL	
:t%{oö%}m%{aä%} PLNOMSUF ;
:t%{oö%}mie PLGENSUF_N ;
:t%{oö%}mi PLPARSUF_i ;
:t%{oö%}mi PLINSSUF ;
+Pl:t%{oö%}mi Noun-NB-01 ;

LEXICON NMN_PÄIVY !!= @CODE@ päivy:päivä
 NMN_PÄIVY-SG ;
 NMN_PÄIVY-PL ;

LEXICON NMN_PÄIVY-SG
:%^A2U SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;

LEXICON NMN_PÄIVY-PL
 PLNOMSUF ;
:%>ie PLGENSUF_N ; ! the plural comitative is declined here
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
+Pl:%>i Noun-NB-01 ;

LEXICON NMN_KOIVU !!= @CODE@ koivu:koivu
        NMN_KOIVU-SG ;
        NMN_KOIVU-PL ;

LEXICON NMN_KOIVU-SG
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;

LEXICON NMN_KOIVU-PL
 PLNOMSUF ;
:%>l%{oö%}i PLGENSUF_N ; ! the plural comitative is declined here
:%>l%{oö%}i PLPARSUF_Zero ;
:%>l%{oö%}i PLINSSUF ;
+Pl:%>l%{oö%}i Noun-NB-01 ;

LEXICON NMN_VASKIČČU !!= @CODE@ vaskičču:vaskičč
        NMN_VASKIČČU-SG ;
        NMN_VASKIČČU-PL ;

LEXICON NMN_VASKIČČU-SG
:u SGNOMSUF ;
:a SGGENSUF ;
:%{ui%} SGPARSUF_A2 ;
+Sg:a Noun-NB-01 ;

LEXICON NMN_VASKIČČU-PL
:a PLNOMSUF ;
:u%>l%{oö%}i PLGENSUF_N ; ! the plural comitative is declined here
:u%>l%{oö%}i PLPARSUF_Zero ;
:u%>l%{oö%}i PLINSSUF ;
+Pl:u%>l%{oö%}i Noun-NB-01 ;

LEXICON NMN_SYGYZY !!= @CODE@ sygyzy:sygyzy
        NMN_SYGYZY-SG ;
        NMN_SYGYZY-PL ;

LEXICON NMN_SYGYZY-SG
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;

LEXICON NMN_SYGYZY-PL
 PLNOMSUF ;
:%>l%{oö%}i PLGENSUF_N ; ! the plural comitative is declined here
:%>l%{oö%}i PLPARSUF_Zero ;
:%>l%{oö%}i PLINSSUF ;
+Pl:%>l%{oö%}i Noun-NB-01 ;


LEXICON NMN_VALGEI
	NMN_VALGEI-SG	;
	NMN_VALGEI-PL	;

LEXICON NMN_VALGEI-SG
:i SGNOMSUF ;
:e SGPARSUF_A2 ;
:e SGGENSUF ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_VALGEI-PL
:e PLNOMSUF	;
:%>ie PLGENSUF_N ; ! the plural comitative is declined here
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
:il%{oö%}i PLGENSUF_N ;
:il%{oö%}i PLPARSUF_Zero ;
:il%{oö%}i PLINSSUF ;
:i Noun-NB-loi ;

LEXICON NMN_RAHMANNOI !!= @CODE@ rahmannoi:rahmannoi
	NMN_RAHMANNOI-SG	;
	NMN_RAHMANNOI-PL	;

LEXICON NMN_RAHMANNOI-SG
:i SGNOMSUF ;
:e SGPARSUF_A2 ;
:e SGGENSUF ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_RAHMANNOI-PL
:e PLNOMSUF	;
:%>ie PLGENSUF_N ; ! the plural comitative is declined here
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
:il%{oö%}i PLGENSUF_N ;
:il%{oö%}i PLPARSUF_Zero ;
:il%{oö%}i PLINSSUF ;
:i Noun-NB-loi ;

LEXICON NMN_HARDIE !!= @CODE@ hardie:hardie
	NMN_HARDIE-SG	;
	NMN_HARDIE-PL	;

LEXICON NMN_HARDIE-SG
: SGNOMSUF ;
: SGPARSUF_dU ;
: SGGENSUF ;
+Sg: Noun-NB-01 ;
LEXICON NMN_HARDIE-PL
: PLNOMSUF	;
:%>l%{oö%}i PLGENSUF_N ;
:%>l%{oö%}i PLPARSUF_Zero ;
:%>l%{oö%}i PLINSSUF ;
+Pl:%>l%{oö%}i Noun-NB-01 ;

LEXICON NMN_OSTAI !!= @CODE@ ostua:osta%>j
 NMN_OSTAI-SG ;
 NMN_OSTAI-PL ;

LEXICON NMN_OSTAI-SG
:%{aä%}%^A2U SGNOMSUF ;
 SGNOMSUF ;
:%>%{aä%} SGGENSUF ;
:%>%{ui%} SGPARSUF_A2 ;
+Sg:%>%{aä%} Noun-NB-01 ;

LEXICON NMN_OSTAI-PL
:%>%{aä%} PLNOMSUF ;
:%>ie PLGENSUF_N ; ! the plural comitative is declined here
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
+Pl:%>i Noun-NB-01 ;

LEXICON NMN_PEZII !!= @CODE@ pestä:pezi%>j
 NMN_PEZII-SG ;
 NMN_PEZII-PL ;

LEXICON NMN_PEZII-SG
 SGNOMSUF ;
:%>%{aä%} SGGENSUF ;
:%>%{ui%} SGPARSUF_A2 ;
+Sg:%>%{aä%} Noun-NB-01 ;

LEXICON NMN_PEZII-PL
:%>%{aä%} PLNOMSUF ;
:%>%{oö%}%>i PLGENSUF_N ; ! the plural comitative is declined here
:%>%{oö%}%>i PLPARSUF_Zero ;
:%>%{oö%}%>i PLINSSUF ;
+Pl:%>%{oö%}%>i Noun-NB-01 ;

LEXICON NMN_KESTÄY !!= @CODE@ kestäy:kestä
 NMN_KESTÄY-SG ;
 NMN_KESTÄY-PL ;

LEXICON NMN_KESTÄY-SG
:%{uy%} SGNOMSUF ;
:v%>%{aä%} SGGENSUF ;
:v%>%{ui%} SGPARSUF_A2 ;
+Sg:v%>%{aä%} Noun-NB-01 ;

LEXICON NMN_KESTÄY-PL
 PLNOMSUF ;
:v%>ie PLGENSUF_N ; ! the plural comitative is declined here
:v%>i PLPARSUF_i ;
:v%>i PLINSSUF ;
+Pl:v%>i Noun-NB-01 ;

LEXICON NMN_RUNO !!= @CODE@ runo:runo
	NMN_RUNO-SG ;
	NMN_RUNO-PL ;

LEXICON NMN_RUNO-SG
	SGNOMSUF ;
 	SGPARSUF_A2 ;
	SGGENSUF ;
+Sg: 	Noun-NB-01 ;
LEXICON NMN_RUNO-PL
:l%{oö%}i PLGENSUF_N ;
:l%{oö%}i PLPARSUF_Zero ;
:l%{oö%}i PLINSSUF ;
 	  Noun-NB-loi ;

LEXICON NMN_KYNDÖ !!= @CODE@ kyndö:kyndö
	NMN_KYNDÖ-SG ;
	NMN_KYNDÖ-PL ;

LEXICON NMN_KYNDÖ-SG
	SGNOMSUF ;
 	SGPARSUF_A2 ;
	SGGENSUF ;
+Sg: 	Noun-NB-01 ;
LEXICON NMN_KYNDÖ-PL
:l%{oö%}i PLGENSUF_N ;
:l%{oö%}i PLPARSUF_Zero ;
:l%{oö%}i PLINSSUF ;
 	  Noun-NB-loi ;


LEXICON NMN_KUU !!= @CODE@ kuu:kuu
	NMN_KUU-SG	;
	NMN_KUU-PL	;

LEXICON NMN_KUU-SG
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_dU ;
+Sg: Noun-NB-01 ;

LEXICON NMN_KUU-PL
 PLNOMSUF ;
:%>l%{oö%}i PLGENSUF_N ;
:%>l%{oö%}i PLPARSUF_Zero ;
:%>l%{oö%}i PLINSSUF ;
: Noun-NB-loi ;


LEXICON NMN_MUA !!= @CODE@ mua:maa
	NMN_MUA-SG	;
	NMN_MUA-PL	;

LEXICON NMN_MUA-SG
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_dU ;
+Sg: Noun-NB-01 ;

LEXICON NMN_MUA-PL
 PLNOMSUF ;
:%>l%{oö%}i PLGENSUF_N ;
:%>l%{oö%}i PLPARSUF_Zero ;
:%>l%{oö%}i PLINSSUF ;
: Noun-NB-loi ;

LEXICON NMN_KALA !!= @CODE@ kala:kala
!! **Yaml: __pada__
	NMN_KALA-SG  ; 
	NMN_KALA-PL  ;
LEXICON NMN_KALA-SG
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;

LEXICON NMN_KALA-PL
 PLNOMSUF ;
:%>i PLGENSUF_EN ;
:%>i PLPARSUF_Zero ;
:%>i PLINSSUF ;
+Pl:%>i Noun-NB-01 ;

LEXICON NMN_PESSYH !!= @CODE@ pessyh
        NMN_PESSYH-SG ;
        NMN_PESSYH-PL ;

!! ! Nominative singular in "h"
LEXICON NMN_PESSYH-SG
:h SGNOMSUF ;
:%{oö%} SGGENSUF ;
:t SGPARSUF_dU ;
+Sg:%{oö%} Noun-NB-01 ;

LEXICON NMN_PESSYH-PL
:%{oö%} PLNOMSUF ;
:zi PLGENSUF_EN ; !! * @CODE@ the +Pl+Com comes here
:zi PLPARSUF_i ;
:zi PLINSSUF ;
+Pl:zi Noun-NB-01 ;

LEXICON NMN_KARJAL !!= @CODE@ karjal:karjal
	NMN_KARJAL-SG ;
	NMN_KARJAL-PL ;

LEXICON NMN_KARJAL-SG !!= @CODE@ karjal:karjal
 SGNOMSUF ;
:%>%{aä%} SGGENSUF ;
:%>%{ui%} SGPARSUF_A2 ;
+Sg:%>%{aä%} Noun-NB-01 ;
LEXICON NMN_KARJAL-PL !!= @CODE@ karjal:karjal
:%>%{aä%} PLNOMSUF ;
:%>%{oö%}i PLGENSUF_N ;
:%>%{oö%}i PLPARSUF_Zero ;
:%{oö%}i PLINSSUF ;
+Pl:%{oö%}i Noun-NB-01 ;

LEXICON NMN_MADAL !!= @CODE@ madal:madal
	NMN_MADAL-SG ;
	NMN_MADAL-PL ;

LEXICON NMN_MADAL-SG !!= @CODE@ madal:madal
 SGNOMSUF ;
:%>%{aä%} SGGENSUF ;
:%>%{ui%} SGPARSUF_A2 ;
+Sg:%>%{aä%} Noun-NB-01 ;
LEXICON NMN_MADAL-PL !!= @CODE@ madal:madal
:%>%{aä%} PLNOMSUF ;
:%>i PLGENSUF_N ;
:%>ii PLPARSUF_Zero ;
:i PLINSSUF ;
+Pl:i Noun-NB-01 ;

!! ! Nominative singular in "m"
LEXICON NMN_SUURIM
        NMN_SUURIM-SG   ;
        NMN_SUURIM-PL   ;

LEXICON NMN_SUURIM-SG
 SGNOMSUF ;
:%{ui%} SGPARSUF_A2 ;

LEXICON NMN_SUURIM-PL
:ie PLGENSUF_N ;
:i PLPARSUF_i ;

LEXICON NMN_TUATANDIM
        NMN_TUATANDIM-SG   ;
        NMN_TUATANDIM-PL   ;

LEXICON NMN_TUATANDIM-SG
 SGNOMSUF ;
:%{ui%} SGPARSUF_A2 ;

LEXICON NMN_TUATANDIM-PL
:ie PLGENSUF_N ;
:i PLPARSUF_i ;

LEXICON NMN_TUATINDAM
        NMN_TUATINDAM-SG   ;
        NMN_TUATINDAM-PL   ;

LEXICON NMN_TUATINDAM-SG
 SGNOMSUF ;
:%{ui%} SGPARSUF_A2 ;

LEXICON NMN_TUATINDAM-PL
:ie PLGENSUF_N ;
:i PLPARSUF_i ;

!! ! Nominative singular in "n"
LEXICON NMN_SALBOIN
	NMN_SALBOIN-SG	;
	NMN_SALBOIN-PL	;

LEXICON NMN_SALBOIN-SG
:n SGNOMSUF ;
:n SGPARSUF_dU ;
:me SGGENSUF ;
+Sg:me Noun-NB-01 ;
LEXICON NMN_SALBOIN-PL
:me PLNOMSUF	;
:%>mie PLGENSUF_N ; ! the plural comitative is declined here
:%>mi PLPARSUF_i ;
:%>mi PLINSSUF ;
!:il%{oö%}i PLGENSUF_N ;
!:il%{oö%}i PLPARSUF_Zero ;
!:il%{oö%}i PLINSSUF ;
!:i Noun-NB-loi ;

LEXICON NMN_ENIN !!= @CODE@ enin:eni
	NMN_ENIN-SG	;
	NMN_ENIN-PL	;

LEXICON NMN_ENIN-SG
:n SGNOMSUF ;
:n SGPARSUF_dU ;
:m%>%{AÄ%} SGGENSUF ;
+Sg:m%>%{AÄ%} Noun-NB-01 ;
LEXICON NMN_ENIN-PL
:m%>%{AÄ%} PLNOMSUF	;
:mie PLGENSUF_N ; ! the plural comitative is declined here
:mi PLPARSUF_i ;
:mi PLINSSUF ;



!! ! Nominative singular in "r"
LEXICON NMN_PIENAR
	NMN_PIENAR-SG	;
	NMN_PIENAR-PL	;

LEXICON NMN_PIENAR-SG
:%^WGStem SGNOMSUF ;
:%^WGStem SGPARSUF_dU ;
:e SGGENSUF ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_PIENAR-PL
:e PLNOMSUF	;
:%>ie PLGENSUF_N ; ! the plural comitative is declined here
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
:il%{oö%}i PLGENSUF_N ;
:il%{oö%}i PLPARSUF_Zero ;
:il%{oö%}i PLINSSUF ;
:i Noun-NB-loi ;

LEXICON NMN_UDAR
	NMN_UDAR-SG	;
	NMN_UDAR-PL	;

LEXICON NMN_UDAR-SG
:%^WGStem SGNOMSUF ;
:%^WGStem SGPARSUF_dU ;
:e SGGENSUF ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_UDAR-PL
:e PLNOMSUF	;
:%>ie PLGENSUF_N ; ! the plural comitative is declined here
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
:il%{oö%}i PLGENSUF_N ;
:il%{oö%}i PLPARSUF_Zero ;
:il%{oö%}i PLINSSUF ;
:i Noun-NB-loi ;

!! ! Nominative singular in "s"
LEXICON NMN_PUHTAHUS !!= @CODE@ puhtahus:puhtahu
	NMN_PUHTAHUS-SG ;
	NMN_PUHTAHUS-PL ;

LEXICON NMN_PUHTAHUS-SG
:s SGNOMSUF ;
:%{oö%} SGGENSUF ;
:t SGPARSUF_dU ;
+Sg:%{oö%} Noun-NB-01 ;

LEXICON NMN_PUHTAHUS-PL
:%{oö%} PLNOMSUF ;
:z%>ie PLGENSUF_N ; ! the plural comitative is declined here
:z%>i PLPARSUF_i ;
:z%>i PLINSSUF ;
+Pl:zi Noun-NB-01 ;


LEXICON NMN_ALUS !!= @CODE@ alus:aluks
	NMN_ALUS-SG ;
	NMN_ALUS-PL ;

LEXICON NMN_ALUS-SG
:%^KS2S SGNOMSUF ;
:e SGGENSUF ;
:%^KS2S SGPARSUF_dU ;
+Sg:e Noun-NB-01 ;

LEXICON NMN_ALUS-PL
:e PLNOMSUF ;
:%>ie PLGENSUF_N ; ! the plural comitative is declined here
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
+Pl:i Noun-NB-01 ;

LEXICON NMN_MIES !!= @CODE@ mies:mie
	NMN_MIES-SG ;
	NMN_MIES-PL ;

LEXICON NMN_MIES-SG
+N:s SGNOMSUF ;
+N:he SGGENSUF ;
+N:s SGPARSUF_dU ;
+N+Sg:he Noun-NB-01 ;

LEXICON NMN_MIES-PL
+N:he PLNOMSUF ;
+N:%>hie PLGENSUF_N ; ! the plural comitative is declined here
+N:%>hi PLPARSUF_i ;
+N:%>hi PLINSSUF ;
+N+Pl:hi Noun-NB-01 ;

LEXICON NMN_ARMAS !!= @CODE@ armas:arma
 NMN_ARMAS-SG        ;
 NMN_ARMAS-PL        ;

LEXICON NMN_ARMAS-SG
:%^WGStem%>s SGNOMSUF ;
:%^WGStem%>s SGPARSUF_dU ;
:ha SGGENSUF ;
+Sg:ha Noun-NB-01 ;
LEXICON NMN_ARMAS-PL
:ha PLNOMSUF ;
:hie PLGENSUF_N ;
:hi PLPARSUF_i ;
:hi PLINSSUF ;
+Pl:hi Noun-NB-01 ;

LEXICON NMN_TUORES !!= @CODE@ tuores:tuore
!!= * Yaml: __A-tuores__
 NMN_TUORES-SG        ;
 NMN_TUORES-PL        ;

LEXICON NMN_TUORES-SG
:%^WGStem%>s SGNOMSUF ;
:%^WGStem%>s SGPARSUF_dU ;
:he SGGENSUF ;
+Sg:he Noun-NB-01 ;
LEXICON NMN_TUORES-PL
:he PLNOMSUF ;
:hie PLGENSUF_N ;
:hi PLPARSUF_i ;
:hi PLINSSUF ;
+Pl:hi Noun-NB-01 ;


LEXICON NMN_SUALIŠ !!= @CODE@ suališ:suali
	NMN_SUALIŠ-SG ;
	NMN_SUALIŠ-PL ;

LEXICON NMN_SUALIŠ-SG
+N:s SGNOMSUF ;
+N:i SGGENSUF ;
+N:s SGPARSUF_dU ;
+N+Sg:i Noun-NB-01 ;

LEXICON NMN_SUALIŠ-PL
+N:i PLNOMSUF ;
!+N:%>ie PLGENSUF_N ; ! the plural comitative is declined here
!+N:%>i PLPARSUF_i ;
!+N:%>i PLINSSUF ;
!+N+Pl:i Noun-NB-01 ;


LEXICON NMN_RAIŠ !!= @CODE@ raiš:ra
	NMN_RAIŠ-SG	;
	NMN_RAIŠ-PL	;
LEXICON NMN_RAIŠ-SG
:iš SGNOMSUF ;
:gehe SGGENSUF ;
:iš SGPARSUF_dU ;
+Sg:gehe Noun-NB-01 ;
LEXICON NMN_RAIŠ-PL
:gehe PLNOMSUF ;
:gehie PLGENSUF_N ;
:gehi PLPARSUF_i ;
:gehi PLINSSUF ;
+Pl:gehi Noun-NB-01 ;

LEXICON NMN_KUURNIS !!= @CODE@ kuurniš:kuurne
	NMN_KUURNIS-SG ;
	NMN_KUURNIS-PL ;

LEXICON NMN_KUURNIS-SG
+N:s SGNOMSUF ;
+N:i SGGENSUF ;
+N:s SGPARSUF_dU ;
+N+Sg:i Noun-NB-01 ;

LEXICON NMN_KUURNIS-PL
+N:i PLNOMSUF ;
!+N:%>ie PLGENSUF_N ; ! the plural comitative is declined here
!+N:%>i PLPARSUF_i ;
!+N:%>i PLINSSUF ;
!+N+Pl:i Noun-NB-01 ;

LEXICON NMN_OLUT !!= * @CODE@ olut:olu
 NMN_OLUT-SG ;
 NMN_OLUT-PL ;

LEXICON NMN_OLUT-SG
:t SGNOMSUF ;
:e SGGENSUF ;
:t SGPARSUF_dU ;
+Sg:e Noun-NB-01 ;

LEXICON NMN_OLUT-PL
:je PLGENSUF_N ;
:i PLPARSUF_dU ;
:i PLINSSUF ;
+Pl:i Noun-NB-01 ;

LEXICON NMN_TULLUT !!= * @CODE@ tullut:tullu
 NMN_TULLUT-SG ;
 NMN_TULLUT-PL ;

LEXICON NMN_TULLUT-SG
:t SGNOMSUF ;
:%{oö%} SGGENSUF ;
:t SGPARSUF_dU ;
+Sg:%{oö%} Noun-NB-01 ;

LEXICON NMN_TULLUT-PL
:%{oö%} PLNOMSUF ;
:z%>ie PLGENSUF_N ;
:z%>i PLPARSUF_i ;
:z%>i PLINSSUF ;
+Pl:zi Noun-NB-01 ;



LEXICON NMN_TUHAT !!= * @CODE@ tuhat:tuha
 NMN_TUHAT-SG ;
 NMN_TUHAT-PL ;

LEXICON NMN_TUHAT-SG
:t SGNOMSUF ;
:nde SGGENSUF ;
:t SGPARSUF_dU ;
+Sg:nde Noun-NB-01 ;

LEXICON NMN_TUHAT-PL
:nde PLNOMSUF ;
!:ie PLGENSUF_N ;
!:i PLPARSUF_i ;
!:i PLINSSUF ;
!+Pl:i Noun-NB-01 ;


LEXICON NMN_PEREH !!= @CODE@ pereh:pereh
	NMN_PEREH-SG	;
	NMN_PEREH-PL	;
LEXICON NMN_PEREH-SG
 SGNOMSUF ;
:e SGGENSUF ;
 SGPARSUF_dU ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_PEREH-PL
:e PLNOMSUF ;
:ie PLGENSUF_N ;
:i PLPARSUF_i ;
:i PLINSSUF ;
+Pl:i Noun-NB-01 ;

LEXICON NMN_SIVE !!= @CODE@ sive:sive
	NMN_SIVE-SG	;
	NMN_SIVE-PL	;
LEXICON NMN_SIVE-SG
 SGNOMSUF ;
:e SGGENSUF ;
 SGPARSUF_dU ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_SIVE-PL
:ie PLGENSUF_N ;
:i PLPARSUF_i ;
:i PLINSSUF ;
+Pl:i Noun-NB-01 ;

LEXICON NMN_PENGER !!= @CODE@ penger:penger
	NMN_PENGER-SG	;
	NMN_PENGER-PL	;
LEXICON NMN_PENGER-SG
 SGNOMSUF ;
:e SGGENSUF ;
 SGPARSUF_dU ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_PENGER-PL
:ie PLGENSUF_N ;
:i PLPARSUF_i ;
:i PLINSSUF ;
+Pl:i Noun-NB-01 ;

LEXICON NMN_ARTELI
 !!= @CODE@ arteli:arteli
	NMN_ARTELI-SG ;
	NMN_ARTELI-PL ;
LEXICON NMN_ARTELI-SG
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;
LEXICON NMN_ARTELI-PL
: PLNOMSUF ;
: PLGENSUF_EN ;
:%>l%{oö%}i PLPARSUF_Zero ;
:%>l%{oö%}i PLINSSUF ;
+Pl:%>l%{oö%}i Noun-NB-01 ;


LEXICON NMN_PAPPI !!= @CODE@ pappi:pappi
	NMN_PAPPI-SG ;
	NMN_PAPPI-PL ;
LEXICON NMN_PAPPI-SG
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;
LEXICON NMN_PAPPI-PL
: PLNOMSUF ;
: PLGENSUF_EN ;
:%>l%{oö%}i PLPARSUF_Zero ;
:%>l%{oö%}i PLINSSUF ;
+Pl:%>l%{oö%}i Noun-NB-01 ;

LEXICON NMN_UKSI !!= @CODE@ uksi:uks
	NMN_UKSI-SG ;
	NMN_UKSI-PL ;

LEXICON NMN_UKSI-SG
:i SGNOMSUF ;
:e SGGENSUF ;
 SGPARSUF_A2 ;
+Sg:e Noun-NB-01 ;

LEXICON NMN_UKSI-PL
:e SGNOMSUF ;
:i PLGENSUF_EN ;
:i PLPARSUF_Zero ;
:i PLINSSUF ;
+Pl:i Noun-NB-01 ;

LEXICON NMN_REBOI !!= @CODE@ reboi:reboi
        NMN_REBOI-SG ;
        NMN_REBOI-PL ;
LEXICON NMN_REBOI-SG
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_dU ;
+Sg: Noun-NB-01 ;
LEXICON NMN_REBOI-PL
:	PLNOMSUF ;
:%^ILoss%>l%{oö%}i PLGENSUF_N ;
:%^ILoss%>l%{oö%}i PLPARSUF_Zero ;
:%^ILoss%>l%{oö%}i PLINSSUF ;
+Pl:%^ILoss%>l%{oö%}i Noun-NB-01 ;

LEXICON NMN_OZA !!= @CODE@ oza:oza
	NMN_OZA-SG  ;
	NMN_OZA-PL  ;

LEXICON NMN_OZA-SG
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;

LEXICON NMN_OZA-PL
: PLNOMSUF ;
:%>ie PLGENSUF_N ;
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
+Pl:%>i Noun-NB-01 ;

LEXICON NMN_SÄYNÄ !!= @CODE@ säynä:säynä
	NMN_SÄYNÄ-SG  ;
	NMN_SÄYNÄ-PL  ;

LEXICON NMN_SÄYNÄ-SG
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;

LEXICON NMN_SÄYNÄ-PL
 PLNOMSUF ;
:%>i PLGENSUF_EN ;
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
+Pl:%>i Noun-NB-01 ;

LEXICON NMN_KARJALAINE
	NMN_KARJALAINE-SG ;
	NMN_KARJALAINE-PL ;

LEXICON NMN_KARJALAINE-SG
:ine SGNOMSUF ;
:ze SGGENSUF ;
:s SGPARSUF_dU ;
+Sg:ze Noun-NB-01 ;

LEXICON NMN_KARJALAINE-PL
:ze PLNOMSUF ;
:zie PLGENSUF_N ;
:zi PLPARSUF_i ;
:zi PLINSSUF ;
+Pl:zi Noun-NB-01 ;

LEXICON NMN_KIELINE
	NMN_KIELINE-SG ;
	NMN_KIELINE-PL ;

LEXICON NMN_KIELINE-SG
:ne SGNOMSUF ;
:ze SGGENSUF ;
:s SGPARSUF_dU ;
+Sg:ze Noun-NB-01 ;

LEXICON NMN_KIELINE-PL
:ze PLNOMSUF ;
:zie PLGENSUF_N ;
:zi PLPARSUF_i ;
:zi PLINSSUF ;
+Pl:zi Noun-NB-01 ;

LEXICON NMN_TOINE
	NMN_TOINE-SG ;
	NMN_TOINE-PL ;

LEXICON NMN_TOINE-SG
:ine SGNOMSUF ;
:ize SGGENSUF ;
:s SGPARSUF_dU ;
+Sg:ize Noun-NB-01 ;

LEXICON NMN_TOINE-PL
:ize PLNOMSUF ;
:izie PLGENSUF_N ;
:izi PLPARSUF_i ;
:izi PLINSSUF ;
+Pl:izi Noun-NB-01 ;


!! ! Nominative singular in "v"
LEXICON NMN_ABRAMOV !! * @CODE@ Abramov:Abramov
	NMN_ABRAMOV-SG ;
	NMN_ABRAMOV-PL ;

LEXICON NMN_ABRAMOV-SG
 SGNOMSUF ;
:%>%{aä%} SGGENSUF ;
:%>%{ui%} SGPARSUF_A2 ;
+Sg:%>%{aä%} Noun-NB-01 ;

LEXICON NMN_ABRAMOV-PL
:%>%{aä%} PLNOMSUF ;
:ie PLGENSUF_N ;
:i PLPARSUF_i ;
:i PLINSSUF ;
+Pl:i Noun-NB-01 ;



LEXICON Noun-NB-i
! These cases are symmetrically marked for number
+Sg: Noun-NB-01 ;
+Pl:%>i Noun-NB-01 ;
! The next two share the same stem vowel
 SGGENSUF ; ! The singular comitative is declined here
 PLNOMSUF ;
 
LEXICON Noun-NB-loi
! These cases are symmetrically marked for number
+Sg: Noun-NB-01 ;
+Pl:%>l%{oö%}i Noun-NB-01 ;
+Pl:%>loi ILLSUF_he ;
! The next two share the same stem vowel
 SGGENSUF ; ! The singular comitative is declined here
 PLNOMSUF ;
 
!! !Singular suffixes
LEXICON SGGENSUF
+Sg+Gen:%^WGStem%>n K ;
+Sg+Acc:%^WGStem%>n K ;
+Sg+Com:%^WGStem%>nke K ;

LEXICON SGNOMSUF
+Sg+Nom: K ;
+Sg+Acc: K ;

LEXICON SGPARSUF_dU
+Sg+Par:%>d%{uy%} K ;

LEXICON SGPARSUF_A2
+Sg+Par:%>%{aä%} K ;

!! !Plural suffixes
LEXICON PLGENSUF_EN
+Pl+Gen:%>en K ;
+Pl+Com:%>enke K ;

LEXICON PLGENSUF_N
+Pl+Gen:%>n K ;
+Pl+Com:%>nke K ;

LEXICON PLINSSUF
+Pl+Ins:%>n K ;

LEXICON PLNOMSUF
+Pl+Nom:%^WGStem%>t K ;
+Pl+Acc:%^WGStem%>t K ;

LEXICON PLPARSUF_Zero
+Pl+Par: K ;

LEXICON PLPARSUF_a
+Pl+Par:%>%{aä%} K ;

LEXICON PLPARSUF_i
+Pl+Par:%>i K ;

LEXICON PLPARSUF_dU
+Pl+Par:%>d%{uy%} K ;

LEXICON ILLSUF_he
+Ill:%>he K ;

LEXICON Noun-NB-01
+Ade:%^WGStem%>l K ;
+Abl:%^WGStem%>l K ;
+Abl:%^WGStem%>lpäi K ;
+All:%^WGStem%>le K ;
+Abe:%^WGStem%>tt%{aä%}h K ;
+Abe:%^WGStem%>tt%{aä%} K ;
+Abe:%^WGStem%>t%{aä%} K ;
+Apr:%^WGStem%>ll%{uy%}%{oö%} K ;
+Ela:%^WGStem%>s K ;
+Ela:%^WGStem%>späi K ;
+Ess:%^WGStem%>nn%{uy%} K ;
+Ine:%^WGStem%>s K ;
+Ins:%^WGStem%>n K ;
+Ill:%>h K ;
+Tra:%^WGStem%>kse K ;
+Prl:%^WGStem%>či K ;
+Ter:%^WGStem%>ss%{aä%}h K ;


! vim: set ft=xfst-lexc:

