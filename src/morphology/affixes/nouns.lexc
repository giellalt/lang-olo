!! Noun inflection
!! ---------------
!! Livvi nouns inflect in cases.

LEXICON N_
+N+Sg+Nom+TYÄ: # ;



LEXICON N_KARJAL !!= @CODE@ karjal:karjal
+N: SGNOMSUF ;
+N:a SGGENSUF ;
+N:a SGPARSUF_A2 ;
+N:a%>i PLGENSUF ;
+N:i PLPARSUF_i ;
+N:%{oö%}i PLINSSUF ;
+N:%{aä%} Noun-NB-i ;

LEXICON N_PIIRAI !!= @CODE@ piirai:piira CHECK THIS
        N_PIIRAI-SG ;
        N_PIIRAI-PL ;

LEXICON N_PIIRAI-SG
+N: NMN_PIIRAI-SG ;

LEXICON N_PIIRAI-PL
+N: NMN_PIIRAI-PL ;

LEXICON NMN_PIIRAI !!= @CODE@ piirai:piira
	NMN_PIIRAI-SG ;
	NMN_PIIRAI-PL ;

LEXICON NMN_PIIRAI-SG
:i SGNOMSUF ;
:a SGGENSUF ;
:a SGPARSUF_dU ;
+Sg:%{aä%} Noun-NB-01 ;

LEXICON NMN_PIIRAI-PL
:a PLNOMSUF ;
+N:al%{oö%}i PLGENSUF ;
+N:al%{oö%}i PLPARSUF_Zero ;
+N:al%{oö%}i PLINSSUF ;
+Pl:al%{oö%}i Noun-NB-01 ;


LEXICON N_KANDAI !!= @CODE@ kandai:kandai
+N: SGNOMSUF ;
+N:%{aä%} SGPARSUF_A2 ;
+N:ie PLGENSUF ;
+N:i PLPARSUF_i ;
+N:i PLINSSUF ;
+N+Sg:%{aä%} Noun-NB-01 ;
+N+Pl:i Noun-NB-01 ;

LEXICON N_PAGIZII !!= @CODE@ pagizii:pagizii
+N: SGNOMSUF ;
+N:A2 SGGENSUF ;
+N:A2 SGPARSUF_A2 ;
+N:%>%{oö%}i PLGENSUF ;
+N:%>%{oö%}i PLPARSUF_i ;
+N:%>%{oö%}i PLINSSUF ;
+N:A2 Noun-NB-01 ;
+N:%>%{oö%} Noun-NB-01 ;

LEXICON N_HUOLETOI !!= @CODE@ huoletoi: huolet
 N_HUOLETOI-SG ;
 N_HUOLETOI-PL ;

LEXICON N_HUOLETOI-SG
+N: NMN_HUOLETOI-SG ;

LEXICON N_HUOLETOI-PL
+N: NMN_HUOLETOI-PL ;

LEXICON NMN_HUOLETOI !!= @CODE@ huoletoi: huolet
 NMN_HUOLETOI-SG ;
 NMN_HUOLETOI-PL ;

LEXICON NMN_HUOLETOI-SG	
:%{oö%}i SGNOMSUF ;
:t%{oö%}ma SGGENSUF ;
:t%{oö%}ma SGPARSUF_A2 ;
+Sg:t%{oö%}ma Noun-NB-01 ;
LEXICON NMN_HUOLETOI-PL	
:t%{oö%}m%{aä%} PLNOMSUF ;
:t%{oö%}mie PLGENSUF ;
:t%{oö%}mi PLPARSUF_i ;
:t%{oö%}mi PLINSSUF ;
+Pl:t%{oö%}mi Noun-NB-01 ;

LEXICON N_VANUIN !!= @CODE@ vanuin:vanui
 N_VANUIN-SG ;
 N_VANUIN-PL ;

LEXICON N_VANUIN-SG
+N: NMN_VANUIN-SG ;

LEXICON N_VANUIN-PL
+N: NMN_VANUIN-PL ;

LEXICON NMN_VANUIN !!= @CODE@ vanuin:vanui
 NMN_VANUIN-SG ;
 NMN_VANUIN-PL ;

LEXICON NMN_VANUIN-SG	
:n SGNOMSUF ;
:me SGGENSUF ;
:n SGPARSUF_A2 ;
+Sg:me Noun-NB-01 ;
LEXICON NMN_VANUIN-PL	
:me PLNOMSUF ;
:mie PLGENSUF ;
:mi PLPARSUF_i ;
:mi PLINSSUF ;
+Pl:mi Noun-NB-01 ;

LEXICON N_KARJALAINE !!= @CODE@ karjalaine:karjala
 N_KARJALAINE-SG     ;
 N_KARJALAINE-PL     ;

LEXICON N_KARJALAINE-SG
+N:    NMN_KARJALAINE-SG	 ;

LEXICON N_KARJALAINE-PL
+N:    NMN_KARJALAINE-PL	 ;

LEXICON N_RAIŠ !!= @CODE@ raiš:ra
	N_RAIŠ-SG	;
	N_RAIŠ-PL	;
LEXICON N_RAIŠ-SG
+N:	NMN_RAIŠ-SG	;

LEXICON N_RAIŠ-PL
+N:	NMN_RAIŠ-PL	;

LEXICON N_PEREH !!= @CODE@ pereh:pereh
	N_PEREH-SG	;
	N_PEREH-PL	;
LEXICON N_PEREH-SG
+N:	NMN_PEREH-SG	;

LEXICON N_PEREH-PL
+N:	NMN_PEREH-PL	;

LEXICON N_PENGER !!= @CODE@ penger:penger
	N_PENGER-SG	;
	N_PENGER-PL	;
LEXICON N_PENGER-SG
+N:	NMN_PENGER-SG	;

LEXICON N_PENGER-PL
+N:	NMN_PENGER-PL	;

LEXICON N_ARTELI
 N_PAPPI ;
LEXICON N_PAPPI !!= @CODE@ arteli:arteli
	N_PAPPI-SG ;
	N_PAPPI-PL ;
LEXICON N_PAPPI-SG
+N: NMN_PAPPI-SG ;
LEXICON N_PAPPI-PL
+N: NMN_PAPPI-PL ;

LEXICON N_REBOI !!= @CODE@ reboi:reboi
        N_REBOI-SG ;
        N_REBOI-PL ;
LEXICON N_REBOI-SG
+N: NMN_REBOI-SG ;
LEXICON N_REBOI-PL
+N: NMN_REBOI-PL ;

LEXICON N_KIELI !!= @CODE@ kieli:kiel
+N:i SGNOMSUF ;
+N:e SGGENSUF ;
+N: SGPARSUF_dU ;
+N+Sg:e Noun-NB-01 ;
+N:e PLNOMSUF ;
+N:ie PLGENSUF ;
+N:i PLPARSUF_i ;
+N:i PLINSSUF ;
+N+Pl:i Noun-NB-01 ;

LEXICON N_PUHTAHUS !!= @CODE@ puhtahus:puhtahu
+N: NMN_PUHTAHUS ;

LEXICON N_ALUS !!= @CODE@ aigomus:aigomuks
+N: NMN_ALUS ;

LEXICON N_ALUS-PL !!= @CODE@ aigomus:aigomuks
+N: NMN_ALUS-PL ;

LEXICON N_OLUT !!= @CODE@ olut:olu
+N:t SGNOMSUF ;
+N:e SGGENSUF ;
+N:t SGPARSUF_dU ;
+N+Sg:e Noun-NB-01 ;
+N:je PLGENSUF ;
+N:i PLPARSUF_dU ;
+N:i PLINSSUF ;
+N+Pl:i Noun-NB-01 ;

LEXICON N_KUU !!= @CODE@ kuu:kuu
+N: NMN_KUU ;

LEXICON N_MUARJU !!= @CODE@ muarju:muarja
 N_MUARJU-SG ;
 N_MUARJU-PL ;

LEXICON N_MUARJU-SG
+N: NMN_MUARJU-SG ;
LEXICON N_MUARJU-PL
+N: NMN_MUARJU-PL ;

LEXICON NMN_MUARJU !!= @CODE@ muarju:muarja
 NMN_MUARJU-SG ;
 NMN_MUARJU-PL ;

LEXICON NMN_MUARJU-SG
:%^A2U SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;
LEXICON NMN_MUARJU-PL
 PLNOMSUF ;
:%>oi PLGENSUF ; ! the plural comitative is declined here
:%>oi PLPARSUF_a ;
:%>oi PLINSSUF ;
+Pl:%>oi Noun-NB-01 ;

LEXICON N-HUM_KALA
+N+Hum: NMN_KALA ;

LEXICON N_KALA !!= @CODE@ kala:kala
	N_KALA-SG  ;
	N_KALA-PL  ;
LEXICON N_KALA-SG
+N: NMN_KALA ;
LEXICON N_KALA-PL
+N: NMN_KALA-PL ;

LEXICON N_OZA !!= @CODE@ oza:oza
	N_OZA-SG  ;
	N_OZA-PL  ;

LEXICON N_OZA-SG
+N: NMN_OZA-SG ;

LEXICON N_OZA-PL
+N: NMN_OZA-PL ;

LEXICON N_ARMAS !!= @CODE@ armas:arma
    N_ARMAS-SG        ;
    N_ARMAS-PL        ;
LEXICON N_ARMAS-SG
+N: NMN_ARMAS-SG	;
LEXICON N_ARMAS-PL
+N: NMN_ARMAS-PL	;

LEXICON NMN_ARMAS !!= @CODE@ armas:arma
 NMN_ARMAS-SG        ;
 NMN_ARMAS-PL        ;

LEXICON N_PAREMBI !!= @CODE@ parembi:paremb
 N_PAREMBI-SG	  ;
 N_PAREMBI-PL	  ;

LEXICON N_PAREMBI-SG
+N: NMN_PAREMBI-SG   ;
LEXICON N_PAREMBI-PL
+N: NMN_PAREMBI-PL   ;

LEXICON NMN_PAREMBI !!= @CODE@ parembi:paremb
 NMN_PAREMBI-SG	  ;
 NMN_PAREMBI-PL	  ;

LEXICON NMN_PAREMBI-SG
:i SGNOMSUF ;
:%{aä%} SGGENSUF ;
:%{aä%} SGPARSUF_A2 ;
+Sg:%{aä%} Noun-NB-01 ;

LEXICON NMN_PAREMBI-PL
:%{aä%} PLNOMSUF ;
:ie PLGENSUF ;
:i PLPARSUF_i ;
:i PLINSSUF ;
+Pl: Noun-NB-01 ;

LEXICON N_KOIRU !!= @CODE@ koiru:koira
 N_PÄIVY ;

LEXICON N_NIMI !!= @CODE@ nimi:nim
	N_NIMI-SG  ;
	N_NIMI-PL  ;

LEXICON N_NIMI-SG
+N: NMN_NIMI-SG ;

LEXICON N_NIMI-PL
+N: NMN_NIMI-PL ;

LEXICON N_PIENAR !!= @CODE@ pienar:piendar
        N_PIENAR-SG  ;
        N_PIENAR-PL  ;

LEXICON N_PIENAR-SG
+N: NMN_PIENAR-SG ;

LEXICON N_PIENAR-PL
+N: NMN_PIENAR-PL ;


LEXICON N_LAPSI !!= @CODE@ lapsi:laps lapsi:laps
+N:i SGNOMSUF ;
+N:e SGGENSUF ;
+N:%^KS2S SGPARSUF_dU ;
+N:e PLNOMSUF ;
+N:%>ie PLGENSUF ; ! the plural comitative is declined here
+N:%>i PLPARSUF_i ;
+N:%>i PLINSSUF ;
+N+Sg:e Noun-NB-01 ;
+N+Pl:i Noun-NB-01 ;

LEXICON N_VEZI !!= @CODE@ vezi:vet
+N:%^T2Z%>i SGNOMSUF ;
+N:e SGGENSUF ;
+N: SGPARSUF_dU ;
+N+Sg:e Noun-NB-01 ;
+N:%^T2Z%>ie PLGENSUF ; ! the plural comitative is declined here
+N:%^T2Z%>i PLPARSUF_i ;
+N:%^T2Z%>i PLINSSUF ;
+N+Pl:%^T2Z%>i Noun-NB-01 ;

LEXICON N_VUOZI !!= @CODE@ vuozi:vuot
+N: NMN_VUOZI-SG	;
+N: NMN_VUOZI-PL	;

LEXICON N_PÄIVY !!= @CODE@ päivy:päivä
	N_PÄIVY-SG ;
	N_PÄIVY-PL ;

LEXICON N_PÄIVY-SG
+N: NMN_PÄIVY-SG	;

LEXICON N_PÄIVY-PL
+N: NMN_PÄIVY-PL	;

LEXICON N_KOIVU !!= @CODE@ koivu:koivu
        N_KOIVU-SG ;
        N_KOIVU-PL ;

LEXICON N_KOIVU-SG
+N: NMN_KOIVU-SG        ;

LEXICON N_KOIVU-PL
+N: NMN_KOIVU-PL        ;

LEXICON N_VALGEI !!= @CODE@ valgei:valge
        N_VALGEI-SG ;
        N_VALGEI-PL ;

LEXICON N_VALGEI-SG
+N: NMN_VALGEI-SG        ;

LEXICON N_VALGEI-PL
+N: NMN_VALGEI-PL        ;

LEXICON N_KIELINE !!= @CODE@ kieline:kieli
+N: NMN_KIELINE ;

LEXICON N_RUNO !!= @CODE@ runo:runo
	 N_RUNO-SG ;
	 N_RUNO-PL ;

LEXICON N_RUNO-SG
+N: NMN_RUNO-SG   ;
LEXICON N_RUNO-PL
+N: NMN_RUNO-PL   ;

!! NMN
LEXICON NMN_NIMI
	NMN_NIMI-SG	;
	NMN_NIMI-PL	;

LEXICON NMN_NIMI-SG
:i SGNOMSUF ;
:e SGPARSUF_A2 ;
:e SGGENSUF ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_NIMI-PL
:e PLNOMSUF	;
:%>ie PLGENSUF ; ! the plural comitative is declined here
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
:il%{oö%}i PLGENSUF ;
:il%{oö%}i PLPARSUF_Zero ;
:il%{oö%}i PLINSSUF ;
:i Noun-NB-loi ;


LEXICON NMN_VUOZI
	NMN_VUOZI-SG ;
	NMN_VUOZI-PL ;

LEXICON NMN_VUOZI-SG
:%^T2Z%>i SGNOMSUF ;
:e SGGENSUF ;
: SGPARSUF_dU ;
+Sg:e Noun-NB-01 ;

LEXICON NMN_VUOZI-PL
:%^T2Z%>ie PLGENSUF ; ! the plural comitative is declined here
:%^T2Z%>i PLPARSUF_i ;
:%^T2Z%>i PLINSSUF ;
+Pl:%^T2Z%>i Noun-NB-01 ;

LEXICON NMN_PÄIVY
 NMN_PÄIVY-SG ;
 NMN_PÄIVY-PL ;

LEXICON NMN_PÄIVY-SG
:%^A2U SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;

LEXICON NMN_PÄIVY-PL
 PLNOMSUF ;
:%>ie PLGENSUF ; ! the plural comitative is declined here
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
+Pl:%>i Noun-NB-01 ;

LEXICON NMN_KOIVU !!= @CODE@ koivu:koivu
        NMN_KOIVU-SG ;
        NMN_KOIVU-PL ;

LEXICON NMN_KOIVU-SG
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;

LEXICON NMN_KOIVU-PL
 PLNOMSUF ;
:%>l%{oö%}i PLGENSUF ; ! the plural comitative is declined here
:%>l%{oö%}i PLPARSUF_Zero ;
:%>l%{oö%}i PLINSSUF ;
+Pl:%>lO1i Noun-NB-01 ;


LEXICON NMN_VALGEI
	NMN_VALGEI-SG	;
	NMN_VALGEI-PL	;

LEXICON NMN_VALGEI-SG
:i SGNOMSUF ;
:e SGPARSUF_A2 ;
:e SGGENSUF ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_VALGEI-PL
:e PLNOMSUF	;
:%>ie PLGENSUF ; ! the plural comitative is declined here
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
:il%{oö%}i PLGENSUF ;
:il%{oö%}i PLPARSUF_Zero ;
:il%{oö%}i PLINSSUF ;
:i Noun-NB-loi ;


LEXICON NMN_PIENAR
	NMN_PIENAR-SG	;
	NMN_PIENAR-PL	;

LEXICON NMN_PIENAR-SG
:%^WEAK SGNOMSUF ;
:%^WEAK SGPARSUF_dU ;
:e SGGENSUF ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_PIENAR-PL
:e PLNOMSUF	;
:%>ie PLGENSUF ; ! the plural comitative is declined here
:%>i PLPARSUF_i ;
:%>i PLINSSUF ;
:il%{oö%}i PLGENSUF ;
:il%{oö%}i PLPARSUF_Zero ;
:il%{oö%}i PLINSSUF ;
:i Noun-NB-loi ;

LEXICON NMN_RUNO !!= @CODE@ runo:runo
	NMN_RUNO-SG ;
	NMN_RUNO-PL ;

LEXICON NMN_RUNO-SG
	SGNOMSUF ;
 	SGPARSUF_A2 ;
	SGGENSUF ;
+Sg: 	Noun-NB-01 ;
LEXICON NMN_RUNO-PL
:l%{oö%}i PLGENSUF ;
:l%{oö%}i PLPARSUF_Zero ;
:l%{oö%}i PLINSSUF ;
 	  Noun-NB-loi ;


LEXICON NMN_PUHTAHUS !!= @CODE@ puhtahus:puhtahu
	NMN_PUHTAHUS-SG ;
	NMN_PUHTAHUS-PL ;

LEXICON NMN_PUHTAHUS-SG
+N:s SGNOMSUF ;
+N:%{oö%} SGGENSUF ;
+N:t SGPARSUF_dU ;
+N+Sg:%{oö%} Noun-NB-01 ;

LEXICON NMN_PUHTAHUS-PL
+N:%{oö%} PLNOMSUF ;
+N:z%>ie PLGENSUF ; ! the plural comitative is declined here
+N:z%>i PLPARSUF_i ;
+N:z%>i PLINSSUF ;
+N+Pl:zi Noun-NB-01 ;


LEXICON NMN_ALUS !!= @CODE@ alus:aluks
	NMN_ALUS-SG ;
	NMN_ALUS-PL ;

LEXICON NMN_ALUS-SG
+N:%^KS2S SGNOMSUF ;
+N:e SGGENSUF ;
+N:%^KS2S SGPARSUF_dU ;
+N+Sg:e Noun-NB-01 ;

LEXICON NMN_ALUS-PL
+N:e PLNOMSUF ;
+N:%>ie PLGENSUF ; ! the plural comitative is declined here
+N:%>i PLPARSUF_i ;
+N:%>i PLINSSUF ;
+N+Pl:i Noun-NB-01 ;

LEXICON NMN_ARMAS-SG
:%^WEAK%>s SGNOMSUF ;
:%^WEAK%>s SGPARSUF_dU ;
:ha SGGENSUF ;
:ha Noun-NB-01 ;
LEXICON NMN_ARMAS-PL
:hie PLGENSUF ;
:hi PLPARSUF_i ;
:hi PLINSSUF ;
:hi Noun-NB-01 ;

LEXICON NMN_KUU !!= @CODE@ kuu:kuu
	NMN_KUU-SG	;
	NMN_KUU-PL	;

LEXICON NMN_KUU-SG
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_dU ;
+Sg: Noun-NB-01 ;

LEXICON NMN_KUU-PL
 PLNOMSUF ;
:%>l%{oö%}i PLGENSUF ;
:%>l%{oö%}i PLPARSUF_Zero ;
:%>l%{oö%}i PLINSSUF ;
: Noun-NB-loi ;

LEXICON NMN_KALA !!= @CODE@
	NMN_KALA-SG  ; 
	NMN_KALA-PL  ;
LEXICON NMN_KALA-SG
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;

LEXICON NMN_KALA-PL
 PLNOMSUF ;
:%>i PLGENSUF ;
:%>i PLPARSUF_a ;
:%>i PLINSSUF ;
+Pl:%>i Noun-NB-01 ;

LEXICON NMN_KARJAL !!= @CODE@ karjal:karjal
 SGNOMSUF ;
:A2 SGGENSUF ;
:A2 SGPARSUF_A2 ;
:%>%{oö%}i PLGENSUF ;
:%>%{oö%}i PLPARSUF_Zero ;
:%>%{oö%}i PLINSSUF ;
+Sg:A2 Noun-NB-01 ;
+Pl:%>%{oö%}i Noun-NB-01 ;


LEXICON NMN_RAIŠ !!= @CODE@ raiš:ra
	NMN_RAIŠ-SG	;
	NMN_RAIŠ-PL	;
LEXICON NMN_RAIŠ-SG
:iš SGNOMSUF ;
:gehe SGGENSUF ;
:iš SGPARSUF_dU ;
+Sg:gehe Noun-NB-01 ;
LEXICON NMN_RAIŠ-PL
:gehe PLNOMSUF ;
:gehie PLGENSUF ;
:gehi PLPARSUF_i ;
:gehi PLINSSUF ;
+Pl:gehi Noun-NB-01 ;


LEXICON NMN_PEREH !!= @CODE@ pereh:pereh
	NMN_PEREH-SG	;
	NMN_PEREH-PL	;
LEXICON NMN_PEREH-SG
 SGNOMSUF ;
:e SGGENSUF ;
 SGPARSUF_dU ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_PEREH-PL
:ie PLGENSUF ;
:i PLPARSUF_i ;
:i PLINSSUF ;
+Pl:i Noun-NB-01 ;

LEXICON NMN_PENGER !!= @CODE@ penger:penger
	NMN_PENGER-SG	;
	NMN_PENGER-PL	;
LEXICON NMN_PENGER-SG
 SGNOMSUF ;
:e SGGENSUF ;
 SGPARSUF_dU ;
+Sg:e Noun-NB-01 ;
LEXICON NMN_PENGER-PL
:ie PLGENSUF ;
:i PLPARSUF_i ;
:i PLINSSUF ;
+Pl:i Noun-NB-01 ;

LEXICON NMN_ARTELI
	NMN_PAPPI ;

LEXICON NMN_PAPPI !!= @CODE@ arteli:arteli
	NMN_PAPPI-SG ;
	NMN_PAPPI-PL ;
LEXICON NMN_PAPPI-SG
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;
LEXICON NMN_PAPPI-PL
:i PLGENSUF ;
:%>l%{oö%}i PLPARSUF_Zero ;
:%>l%{oö%}i PLINSSUF ;
+Pl:%>l%{oö%}i Noun-NB-01 ;

LEXICON NMN_REBOI !!= @CODE@ reboi:reboi
        NMN_REBOI-SG ;
        NMN_REBOI-PL ;
LEXICON NMN_REBOI-SG
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;
LEXICON NMN_REBOI-PL
:i PLGENSUF ;
:%>l%{oö%}i PLPARSUF_Zero ;
:%>l%{oö%}i PLINSSUF ;
+Pl:%>l%{oö%}i Noun-NB-01 ;

LEXICON NMN_OZA !!= @CODE@ oza:oza
	NMN_OZA-SG  ;
	NMN_OZA-PL  ;

LEXICON NMN_OZA-SG
 SGNOMSUF ;
 SGGENSUF ;
 SGPARSUF_A2 ;
+Sg: Noun-NB-01 ;

LEXICON NMN_OZA-PL
:%>i PLGENSUF ;
:%>i PLPARSUF_a ;
:%>i PLINSSUF ;
+Pl:%>i Noun-NB-01 ;

LEXICON NMN_KIELINE
:ne SGNOMSUF ;
:ze SGGENSUF ;
:s SGPARSUF_dU ;
:zie PLGENSUF ;
:zi PLPARSUF_i ;
:zi PLINSSUF ;
+Sg:ze Noun-NB-01 ;
+Pl:zi Noun-NB-01 ;

LEXICON NMN_KARJALAINE
	NMN_KARJALAINE-SG ;
	NMN_KARJALAINE-PL ;

LEXICON NMN_KARJALAINE-SG
:ine SGNOMSUF ;
:ze SGGENSUF ;
:s SGPARSUF_dU ;
+Sg:ze Noun-NB-01 ;

LEXICON NMN_KARJALAINE-PL
:ze PLNOMSUF ;
:zie PLGENSUF ;
:zi PLPARSUF_i ;
:zi PLINSSUF ;
+Pl:zi Noun-NB-01 ;


LEXICON Noun-NB-i
! These cases are symmetrically marked for number
+Sg: Noun-NB-01 ;
+Pl:%>i Noun-NB-01 ;
! The next two share the same stem vowel
 SGGENSUF ; ! The singular comitative is declined here
 PLNOMSUF ;
 
LEXICON Noun-NB-loi
! These cases are symmetrically marked for number
+Sg: Noun-NB-01 ;
+Pl:%>l%{oö%}i Noun-NB-01 ;
+Pl:%>loi ILLSUF_he ;
! The next two share the same stem vowel
 SGGENSUF ; ! The singular comitative is declined here
 PLNOMSUF ;
 

LEXICON SGNOMSUF
+Sg+Nom: K ;
+Sg+Acc: K ;

LEXICON SGPARSUF_dU
+Sg+Par:%>d%{uy%} K ;


LEXICON SGPARSUF_A2
+Sg+Par:%>A2 K ;

LEXICON SGGENSUF
+Sg+Gen:%^WEAK%>n K ;
+Sg+Acc:%^WEAK%>n K ;
+Sg+Com:%^WEAK%>nke K ;

LEXICON PLNOMSUF
+Pl+Nom:%^WEAK%>t K ;
+Pl+Acc:%^WEAK%>t K ;

LEXICON PLGENSUF
+Pl+Gen:%>n K ;
+Pl+Com:%^WEAK%>nke K ;

LEXICON ILLSUF_he
+Ill:%>he K ;

LEXICON PLINSSUF
+Pl+Ins:%>n K ;

LEXICON PLPARSUF_Zero
+Pl+Par: K ;

LEXICON PLPARSUF_a
+Pl+Par:%>%{aä%} K ;

LEXICON PLPARSUF_i
+Pl+Par:%>i K ;

LEXICON PLPARSUF_dU
+Pl+Par:%>d%{uy%} K ;

LEXICON Noun-NB-01
+Ess:%^WEAK%>nn%{uy%} K ;
+Tra:%^WEAK%>kse K ;
+Ine:%^WEAK%>s K ;
+Ela:%^WEAK%>s K ;
+Ela:%^WEAK%>späi K ;
+Ill:%>h K ;
+Ade:%^WEAK%>l K ;
+Abl:%^WEAK%>l K ;
+Abl:%^WEAK%>lpäi K ;
+All:%^WEAK%>le K ;
+Abe:%^WEAK%>tt%{aä%}h K ;
+Abe:%^WEAK%>tt%{aä%} K ;
+Abe:%^WEAK%>t%{aä%} K ;
+Prl:%>či K ;
+Apr:%^WEAK%>ll%{uy%}%{oö%} K ;
+Ter:%^WEAK%>ss%{aä%}h K ;


! vim: set ft=xfst-lexc:

