!! Noun inflection
!! ---------------
!! Livvi nouns inflect in cases.

LEXICON N_
+N+Sg+Nom+TYÄ: # ;



LEXICON N_HUOLETOI !!= @CODE@ huoletoi: huolet
+N:oi SGNOMSUF ;
+N:toma SGPARSUF_A1 ;
+N:tomie PLGENSUF ;
+N:tomi PLPARSUF_i ;
+N:tomi PLINSSUF ;
+N:toma Noun-NB-i ;

LEXICON N_KANDAI !!= @CODE@ kandai:kandai
+N: SGNOMSUF ;
+N:A1 SGPARSUF_A1 ;
+N:ie PLGENSUF ;
+N:i PLPARSUF_i ;
+N:i PLINSSUF ;
+N:A1 Noun-NB-i ;

LEXICON N_KARJAL !!= @CODE@ karjal:karjal
+N: SGNOMSUF ;
+N:a SGPARSUF_dU1 ;
+N:a%>i PLGENSUF ;
+N:i PLPARSUF_i ;
+N:oi PLINSSUF ;
+N:A1 Noun-NB-i ;

LEXICON N_KARJALAINE !!= @CODE@ karjalaine:karjala
+N:    NMN_KARJALAINE	 ;

LEXICON N_KIELI !!= @CODE@ kieli:kiel
+N:i SGNOMSUF ;
+N: SGPARSUF_dU1 ;
+N:ie PLGENSUF ;
+N:i PLPARSUF_i ;
+N:i PLINSSUF ;
+N:e Noun-NB-i ;

LEXICON N_KUU !!= @CODE@ kuu:kuu
+N: SGNOMSUF ;
+N: SGPARSUF_dU1 ;
+N:loi PLGENSUF ;
+N:loi PLPARSUF_Zero ;
+N:loi PLINSSUF ;
+N+Sg: Noun-NB-01 ;
+N: Noun-NB-loi ;

LEXICON N_MUARJU !!= @CODE@ muarju:muarj
+N:U1 SGNOMSUF ;
+N:A1 SGPARSUF_A1 ;
+N:%>oi PLGENSUF ; ! the plural comitative is declined here
+N:%>oi PLPARSUF_a ;
+N:%>oi PLINSSUF ;
+N:A1 Noun-NB-i ;

LEXICON N_PADA !!= @CODE@ 
+N: SGNOMSUF ;
+N: SGPARSUF_A1 ;
+N:i PLGENSUF ;
+N:i PLPARSUF_a ;
+N:i PLINSSUF ;
+N: Noun-NB-i ;

LEXICON N_ARMAS !!= @CODE@ armas:arma
+N:%^WEAK%>s SGNOMSUF ;
+N:%^WEAK%>s SGPARSUF_dU1 ;
+N:hie PLGENSUF ;
+N:hi PLPARSUF_i ;
+N:hi PLINSSUF ;
+N:ha Noun-NB-i ;

LEXICON N_PAREMBI !!= @CODE@ parembi:paremb
+N:i SGNOMSUF ;
+N:A1 SGPARSUF_A1 ;
+N:ie PLGENSUF ;
+N:i PLPARSUF_i ;
+N:i PLINSSUF ;
+N:A1 Noun-NB-i ;

LEXICON N_KOIRU
 N_PÄIVY ;

LEXICON N_NIMI !!= @CODE@ 
+N:i SGNOMSUF ;
+N:e SGPARSUF_A1 ;
+N:%>ie PLGENSUF ; ! the plural comitative is declined here
+N:%>i PLPARSUF_i ;
+N:%>i PLINSSUF ;
+N:e Noun-NB-i ;
+N:iloi PLGENSUF ;
+N:iloi PLPARSUF_Zero ;
+N:iloi PLINSSUF ;
+N:i Noun-NB-loi ;

LEXICON N_VUOZI !!= @CODE@ 
+N:%^T2Z%>i SGNOMSUF ;
+N:e SGGENSUF ;
+N: SGPARSUF_dU1 ;
+N:%^T2Z%>ie PLGENSUF ; ! the plural comitative is declined here
+N:%^T2Z%>i PLPARSUF_i ;
+N:%^T2Z%>i PLINSSUF ;
+N:e Noun-NB-01 ;
+N:%^T2Z%>i Noun-NB-01 ;

LEXICON N_PÄIVY !!= @CODE@ päivy:päiv
+N:U1 SGNOMSUF ;
+N:A1 SGGENSUF ;
+N:A1 SGPARSUF_A1 ;
+N:A1 PLNOMSUF ;
+N:%>ie PLGENSUF ; ! the plural comitative is declined here
+N:%>i PLPARSUF_i ;
+N:%>i PLINSSUF ;
+N+Sg:A1 Noun-NB-01 ;
+N+Pl:%>i Noun-NB-01 ;

LEXICON N_RUNO !!= @CODE@ runo:runo
+N: SGNOMSUF ;
+N: SGPARSUF_A1 ;
+N:loi PLGENSUF ;
+N:loi PLPARSUF_Zero ;
+N:loi PLINSSUF ;
+N: Noun-NB-loi ;

!! NMN
LEXICON NMN_KARJALAINE
:ine SGNOMSUF ;
:s SGPARSUF_dU1 ;
:zie PLGENSUF ;
:zi PLPARSUF_i ;
:zi PLINSSUF ;
:ze Noun-NB-i ;


LEXICON Noun-NB-i
! These cases are symmetrically marked for number
+Sg: Noun-NB-01 ;
+Pl:%>i Noun-NB-01 ;
! The next two share the same stem vowel
 SGGENSUF ; ! The singular comitative is declined here
 PLNOMSUF ;
 
LEXICON Noun-NB-loi
! These cases are symmetrically marked for number
+Sg: Noun-NB-01 ;
+Pl:%>loi Noun-NB-01 ;
+Pl:%>loi ILLSUF_he ;
! The next two share the same stem vowel
 SGGENSUF ; ! The singular comitative is declined here
 PLNOMSUF ;
 

LEXICON SGNOMSUF
+Sg+Nom: K ;
+Sg+Acc: K ;

LEXICON SGPARSUF_dU1
+Sg+Par:%>dU1 K ;


LEXICON SGPARSUF_A1
+Sg+Par:%>A1 K ;

LEXICON SGGENSUF
+Sg+Gen:%^WEAK%>n K ;
+Sg+Acc:%^WEAK%>n K ;
+Sg+Com:%^WEAK%>nke K ;

LEXICON PLNOMSUF
+Pl+Nom:%^WEAK%>t K ;
+Pl+Acc:%^WEAK%>t K ;

LEXICON PLGENSUF
+Pl+Gen:%>n K ;
+Pl+Com:%^WEAK%>nke K ;

LEXICON ILLSUF_he
+Ill:%>he K ;

LEXICON PLINSSUF
+Pl+Ins:%>n K ;

LEXICON PLPARSUF_Zero
+Pl+Par: K ;

LEXICON PLPARSUF_a
+Pl+Par:%>A1 K ;

LEXICON PLPARSUF_i
+Pl+Par:%>i K ;

LEXICON Noun-NB-01
+Ess:%^WEAK%>nnU1 K ;
+Tra:%^WEAK%>kse K ;
+Ine:%^WEAK%>s K ;
+Ela:%^WEAK%>s K ;
+Ela:%^WEAK%>späi K ;
+Ill:%>h K ;
+Ade:%^WEAK%>l K ;
+Abl:%^WEAK%>l K ;
+Abl:%^WEAK%>lpäi K ;
+All:%^WEAK%>le K ;
+Abe:%^WEAK%>ttA1h K ;
+Abe:%^WEAK%>ttA1 K ;
+Abe:%^WEAK%>tA1 K ;
+Prl:%>či K ;
+Apr:%^WEAK%>llU1O1 K ;
+Ter:%^WEAK%>ssA1h K ;


! vim: set ft=xfst-lexc:

