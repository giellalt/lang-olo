!! Verb inflection
!! ---------------
!! Verbs in the Livvi (Olonets) Karelian language inflect for mood, number and persons.


LEXICON V_
+TYÄ+V: K ;

LEXICON V-NEG !!= @CODE@ ei:e
+V+Neg+ScSg1:n K ;
+V+Neg+ScSg2:t K ;
+V+Neg+ScSg3:i K ;
+V+Neg+ScPl1:mmo K ;
+V+Neg+ScPl2:tto K ;
!+V+Neg+ScPl3:i K ;

LEXICON V_OLLA !!= @CODE@ olla:o
+V+Ind+Prs+ScSg1:le%^WGStem%>n K ;
+V+Ind+Prs+ScSg2:le%^WGStem%>t K ;
+V+Ind+Prs+ScSg3:n K ;
+V+Ind+Prs+ScPl1:le%^WGStem%>mm%{oö%} K ;
+V+Ind+Prs+ScPl2:le%^WGStem%>tt%{oö%} K ;
+V:l INDPRSPL3_dAh ;
+V+Ind+Prs+ConNeg:le%^WGStem K ;

+V+Ind+Prt+ScSg1:l%^WGStem%>in K ;
+V+Ind+Prt+ScSg2:l%^WGStem%>it K ;
+V+Ind+Prt+ScSg3:l%>i K ;
+V+Ind+Prt+ScPl1:l%^WGStem%>imm%{oö%} K ;
+V+Ind+Prt+ScPl2:l%^WGStem%>itt%{oö%} K ;
+V:l INDPRTPL3_dih     ;
+V+Ind+Prt+ConNeg:l%>n%{uy%}%^WGStemh K ;
+V+Ind+Prt+ConNeg+ScPl3:l%>t%{uy%} K ;
+V:l CONDPRS_IS  ;
+V:le M-INF ;
+V:li ACTPRSPRC_PEZII ;


LEXICON V_KANDUA !!= @CODE@ kandua:kanda
!Verbs of the Finnish type 1
!V1a
+V: V-VV_01 ;

LEXICON V_OTTUA !!= @CODE@ ottua:otta
!Verbs of the Finnish type 1
!V1a
+V: V-VV_01 ;

LEXICON V_PUNUO !!= @CODE@ punuo:puno
!V1o
+V: V-VV_01 ;

LEXICON V_VALUO !!= @CODE@ valuo:valu
!V1u
+V: V-VV_01 ;



!V1i
LEXICON V_POTKIE !!= @CODE@ potkie:potki
+V: V-VV_01 ;


!V1i
LEXICON V_IMIE !!= @CODE@ imie:ime
+V: V-VV_01 ;


LEXICON  V_PIDIÄ !!= @CODE@ pidiä:pidä
+V: V-VV_01 ;

!! !VERBS OF FINNISH TYPE 2 in dA

LEXICON V_ROIJA !!= @CODE@ roija:roi
!Verbs of the Finnish type 2
+V: V-VV_02 ;

LEXICON V_SUAJA !!= @CODE@ suaja:saa
!Verbs of the Finnish type 2
+V: V-VV_02 ;

LEXICON V_JUVVA !!= @CODE@ juvva:juo
!Verbs of the Finnish type 2
+V: V-VV_02 ;

LEXICON V_NÄHTÄ !!= @CODE@ nähtä:nä
+TYÄ+V:	V-VV_02	 ;

LEXICON V_KUULTA !!= @CODE@ kuulta:kuul
!! !Verbs of the Finnish type 3 except for the infinitive that makes no sense
!! I should be kuulda perhaps 2013-11-14
+V: V-VV_03 ;


LEXICON V_TULLA !!= @CODE@ tulla:tul
!! !Verbs of the Finnish type 3
+V: V-VV_03 ;

LEXICON V_PESTÄ !!= @CODE@ pestä:pes
!! !Verbs of the Finnish type 3
+V: V-VV_03 ;

LEXICON V_NOSTA !!= @CODE@ nosta:no
!! !Verbs of the Finnish type 3
+V: V-VV_03 ;

LEXICON V_KUULLA !!= @CODE@ kuulla:kuul
!! !Verbs of the Finnish type 3 but not identical
+V: V-VV_KUULLA ;


LEXICON V_TYPÄTÄ !!= @CODE@ typätä:typpä
+V: #;

LEXICON V_KÄITÄ !!= @CODE@ käitä:käge
+V: V-VV_04 ;

LEXICON V_SUVATA !!= @CODE@ suvata:suva
+V: V-VV_04 ;

LEXICON V_AVATA !!= @CODE@ avata:ava
!Verbs of the Finnish type 4
+V: V-VV_04 ;



LEXICON V_SUVAIJA !!= @CODE@ suvaija:suvai
!Verbs of the Finnish type 5
+V: V-VV_05_SUVAIJA ;

LEXICON V_MERKITÄ !!= @CODE@ merkitä:merki
!Verbs of the Finnish type 5
+V: V-VV_05_MERKITÄ ;

LEXICON V_ALETA !!= @CODE@ aleta:ale (alenov)
+V: V-VV_06 ;
+V:ne INDPRSSG1 ;
+V:ne INDPRSSG2 ;
+V:n%{oö%} INDPRSSG3 ;
+V:ne INDPRSPL1 ;
+V:ne INDPRSPL2 ;
+V: INDPRSPL3_tAh ;
+V:ne INDPRSCONNEG ;
+V:n INDPRTSG1     ;
+V:n INDPRTSG2     ;
+V:n INDPRTSG3     ;
+V:n INDPRTPL1     ;
+V:n INDPRTPL2     ;
+V: INDPRTPL3_ttih     ;
+V+Ind+PrfPrc+Use/Sub:%>nn%{uy%}t K ;
+V+Ind+PrfPrc:%>nn%{uy%}h K ;
+V+Ind+PrfPrcPl3:%>tt%{uy%} K ;
+V: INDPRTCONNEG_nnUh ; !!
+V: INDPRTCONNEGPL3_ttU ;
+V:n CONDPRS_IS ;
!+V:%>d CONDPRSPL3 ;
!+V:%>d%{uy%}%^WGStem CONDPRT ;
!+V:%>d%{aä%} CONDPRTPL3_DAnUs ;

LEXICON V_SUATA !!= @CODE@ suata:suva (suvaiččov)
+V: #;

LEXICON VR_AMBUO !!= @CODE@ ambuokseh:ambu
	VR_01 ;

LEXICON VR_VALUO !!= @CODE@ valuokseh:valu
	VR_01 ;

LEXICON VR_PUNUO !!= @CODE@ punuokseh:puno
	VR_01 ;

LEXICON VR_IMIE !!= @CODE@ imiekseh:ime
	VR_01 ;

LEXICON VR_POTKIE !!= @CODE@ potkiekseh:potki
	VR_01 ;

LEXICON VR_KANDUA !!= @CODE@ kanduakseh:kanda
  VR_01 ;

LEXICON VR_OTTUA !!= @CODE@ ottuakseh:otta
	VR_01 ;

LEXICON VR_RODIEKSEH !!= @CODE@ rodiekseh:rodi
! Reflexive verbs
 VR_01 ;

LEXICON VR_ROIJA !!= @CODE@ roijakseh:roi
!
        VR_ ;

LEXICON VR_ROITA !!= @CODE@ roita:roi 
!
        VR_ ;

LEXICON VR_JUVVA !!= @CODE@ juvva:juo
	VR_	 ;

LEXICON VR_NÄHTÄ !!= @CODE@ nähtä:nä
	VR_	 ;

LEXICON VR_TULLA !!= @CODE@ tuliekseh:tul !this will need work 2013-10-23
	VR_03 ;

LEXICON VR_PESTÄ !!= @CODE@ tuliekseh:tul !this will need work 2013-10-23
	VR_03 ;

LEXICON VR_AVATA !!= @CODE@ avatakseh:ava (typätäkseh:typpä)
 VR_ ;

LEXICON VR_SUVAIJA !!= @CODE@ emändöijäkseh:emändöi
!Verbs of the Finnish type 5
:čče VR-LONGSTEM_ ;
 VR-SHORTSTEM_ ;

LEXICON VR_MERKITÄ !!= @CODE@ 
!Verbs of the Finnish type 5
:čče VR-LONGSTEM_ ;
 VR-SHORTSTEM_ ;



LEXICON VR_
	VR-LONGSTEM_ ;
	VR-SHORTSTEM_ ;
LEXICON VR-LONGSTEM_
+V+Ind+Prs+RcSg1:%^WGStem%>mm%{oö%}s	K	;
+V+Ind+Prs+RcSg2:%^WGStem%>tt%{oö%}s	K	;
+V+Ind+Prs+RcSg3:%^WGStem%>h	K	;
+V+Ind+Prs+RcPl1:%^WGStem%>mm%{oö%}kseh	K	;
+V+Ind+Prs+RcPl2:%^WGStem%>tt%{oö%}kseh	K	;

+V+Ind+Prt+RcSg1:%>mm%{oö%}s	K	;
+V+Ind+Prt+RcSg2:%>tt%{oö%}s	K	;
+V+Ind+Prt+RcSg3:%>h	K	;
+V+Ind+Prt+RcPl1:%>mm%{oö%}kseh	K	;
+V+Ind+Prt+RcPl2:%>tt%{oö%}kseh	K	;

LEXICON VR-SHORTSTEM_
+V+Ind+Prs+RcPl3:%^WGStem%>j%{aä%}h	K	;
+V+Ind+Prs+ConNeg+RcSg:%^WGStem%>	K	;
+V+Ind+Prs+ConNeg+RcPl1:%^WGStem%>	K	;
+V+Ind+Prs+ConNeg+RcPl2:%^WGStem%>	K	;

+V+Ind+Prs+ConNeg+RcPl3:%^WGStem%>j%{aä%}	K	;


+V+Ind+Prt+RcPl3:%>ttih	K	;
+V+Ind+Prt+ConNeg+RcSg:%>n%{uy%}h	K	;
+V+Ind+Prt+ConNeg+RcPl1:%>n%{uy%}h	K	;
+V+Ind+Prt+ConNeg+RcPl2:%>n%{uy%}h	K	;
+V+Ind+Prt+ConNeg+RcPl3:%>tt%{uy%}	K	;


LEXICON V-VV_01
! V1
! This verb type has two final vowels in the first infinitive
 INDPRSSG1 ;
 INDPRSSG2 ;
 INDPRSSG3 ;
 INDPRSPL1 ;
 INDPRSPL2 ;
 INDPRSPL3_EtAh ;
 INDPRSCONNEG_WEAK ;

 INDPRTSG1 ;
 INDPRTSG2 ;
 INDPRTSG3 ;
 INDPRTPL1 ;
 INDPRTPL2 ;
 INDPRTPL3_Ettih ;
!+Ind+Prt+ConNeg:%>n%{uy%}h K ;
 INDPRTCONNEG_nUh ;
 INDPRTCONNEGPL3_EttU ;

 CONDPRS_S  ;
 CONDPRSPL3_EttAs ;
 CONDPRT_nUs ;
 CONDPRTPL3_EtAnnUs ;

 POT_N ;
 POTPL3_EtAnneh ;

 IMPRT_kkA ;
 IMPRTPL3_EttAhes ;

 ACTPRSPRC_OSTAI ;
 PSSPRSPRC_EttAvU ;
 ACTPRFPRC_nUH ;
 PSSPRFPRC_EttU ;

 M-INF        ;
+Inf:%>%{aäInf%} K ;



LEXICON V-VV_02
 INDPRSSG1 ;
 INDPRSSG2 ;
 INDPRSSG3 ;
 INDPRSPL1 ;
 INDPRSPL2 ;
 INDPRSPL3_jAh ;
 INDPRSCONNEG ;
 INDPRTSG1     ;
 INDPRTSG2     ;
 INDPRTSG3     ;
 INDPRTPL1     ;
 INDPRTPL2     ;
 INDPRTPL3_dih     ;
 INDPRTCONNEG_nnUh	   ;
 INDPRTCONNEGPL3_DU	   ;
 CONDPRS_S ;
 CONDPRSPL3_DAs ;
 CONDPRT_nnUs ;
 CONDPRTPL3_DAnUs ;
 POT_nnE ;
 POTPL3_DAneh ;
 IMPRT_GA ;
 IMPRTPL3_DAhes ;

 ACTPRSPRC_AVUAJU ;
 ACTPRFPRC_nnUH ;
 PSSPRSPRC_DAvU ;
 PSSPRFPRC_DU ;

 M-INF	      ;
+Inf:%>j%{aäInf%} K ;


LEXICON V-VV_03 !!= @CODE@ tulla:tul
:%^VOICEe INDPRSSG1 ;
:%^VOICEe INDPRSSG2 ;
:%^VOICE%{oö%} INDPRSSG3 ;
:%^VOICEe INDPRSPL1 ;
:%^VOICEe INDPRSPL2 ;
 INDPRSPL3_dAh ;
:%^VOICEe INDPRSCONNEG_WEAK ;

:%^VOICE INDPRTSG1     ;
:%^VOICE INDPRTSG2     ;
:%^VOICE INDPRTSG3     ;
:%^VOICE INDPRTPL1     ;
:%^VOICE INDPRTPL2     ;
: INDPRTPL3_dih     ;
 INDPRTCONNEG_nUh ;
 INDPRTCONNEGPL3_DU ;

:%^VOICE CONDPRS_IS ;
: CONDPRSPL3_DAs ;
: CONDPRT_nUs ;
: CONDPRTPL3_DAnnUs ;
: POT_N ;
: POTPL3_DAnneh ;
: IMPRT_KA ;
: IMPRTPL3_DAhes ;

!:%^VOICEe ACTPRSPRC_OSTAI ;
:%^VOICEi ACTPRSPRC_PEZII ;
: ACTPRFPRC_nUH ;
: PSSPRSPRC_DAvU ;
: PSSPRFPRC_DU ;
:%^VOICEe M-INF ;
+Inf:%>d%{aä%}%^WGStem K ;

LEXICON V-VV_KUULLA !!= @CODE@ kuulla:kuul
:%^VOICEe INDPRSSG1 ;
:%^VOICEe INDPRSSG2 ;
:%^VOICE%{oö%} INDPRSSG3 ;
:%^VOICEe INDPRSPL1 ;
:%^VOICEe INDPRSPL2 ;
 INDPRSPL3_tAh ;
:%^VOICEe INDPRSCONNEG_WEAK ;

:%^VOICE INDPRTSG1     ;
:%^VOICE INDPRTSG2     ;
:%^VOICE INDPRTSG3     ;
:%^VOICE INDPRTPL1     ;
:%^VOICE INDPRTPL2     ;
: INDPRTPL3_tih     ;
 INDPRTCONNEG_nUh ;
 INDPRTCONNEGPL3_tU ;

:%^VOICE CONDPRS_IS ;
: CONDPRSPL3_tAs ;
: CONDPRT_nUs ;
: CONDPRTPL3_tAnnUs ;
: POT_N ;
: POTPL3_tAnneh ;
: IMPRT_KA ;
: IMPRTPL3_tAhes ;

:%^VOICEi ACTPRSPRC_PEZII ;
: ACTPRFPRC_nUH ;
: PSSPRSPRC_tAvU ;
: PSSPRFPRC_tU ;
:%^VOICEe M-INF ;
+Inf:%>t%{aä%}%^WGStem K ;

LEXICON V-VV_04
:%>%{aä%} INDPRSSG1 ;
:%>%{aä%} INDPRSSG2 ;
:%>%{aä%} INDPRSSG3 ;
:%>%{aä%} INDPRSPL1 ;
:%>%{aä%} INDPRSPL2 ;
 INDPRSPL3_tAh ;
:%>%{aä%} INDPRSCONNEG ;
:%^SyllBound INDPRTSG1     ;
:%^SyllBound INDPRTSG2     ;
:%^SyllBound INDPRTSG3     ;
:%^SyllBound INDPRTPL1     ;
:%^SyllBound INDPRTPL2     ;
 INDPRTPL3_ttih     ; 
 INDPRTCONNEG_nnUh ; !!
 INDPRTCONNEGPL3_ttU ;
:%>%{aä%} CONDPRS_S ;
 CONDPRSPL3_ttAs ;
 CONDPRT_nnUs ;
 CONDPRTPL3_tAnnUs ;

 POT_nnE ;
 POTPL3_tAnneh ;
 IMPRT_WEAK-kkA ;
 IMPRTPL3_ttAhes ;
!:%>%{aä%} ACTPRSPRC_vU ;
:%>%{aä%} ACTPRSPRC_AVUAJU ;
 PSSPRSPRC_ttAvU ;
 ACTPRFPRC_nnUH ;
 PSSPRFPRC_ttU ;
+Inf:%^WGStem%>t%{aä%} K ;
:%>%{aä%} M-INF ; 

LEXICON V-VV_05_SUVAIJA !! * @CODE@ suvaija:suvai
:čče INDPRSSG1 ;
:čče INDPRSSG2 ;
:čč%{oö%} INDPRSSG3 ;
:čče INDPRSPL1 ;
:čče INDPRSPL2 ;
 INDPRSPL3_jAh ;
:čče INDPRSCONNEG_WEAK ;
:čč INDPRTSG1     ;
:čč INDPRTSG2     ;
:čč INDPRTSG3     ;
:čč INDPRTPL1     ;
:čč INDPRTPL2     ;
 INDPRTPL3_ttih     ;
 INDPRTCONNEG_nnUh	   ;
 INDPRTCONNEGPL3_ttU	   ;
:čč CONDPRS_IS ;
 CONDPRSPL3_ttAs ;
 CONDPRT_nnUs ;
 CONDPRTPL3_tAnnUs ;
 POT_nnE ;
 POTPL3_tAnneh ;
 IMPRT_kkA ;
 IMPRTPL3_ttAhes ;

:čči ACTPRSPRC_PEZII ;
 ACTPRFPRC_nnUH ;
 PSSPRSPRC_ttAvU ;
 PSSPRFPRC_ttU ;

:čče M-INF	      ;
+Inf:%>j%{aäInf%} K ;


LEXICON V-VV_05_MERKITÄ !! * @CODE@ merkitä:merki
:čče INDPRSSG1 ;
:čče INDPRSSG2 ;
:čč%{oö%} INDPRSSG3 ;
:čče INDPRSPL1 ;
:čče INDPRSPL2 ;
 INDPRSPL3_tAh ;
:čče INDPRSCONNEG_WEAK ;
:čč INDPRTSG1     ;
:čč INDPRTSG2     ;
:čč INDPRTSG3     ;
:čč INDPRTPL1     ;
:čč INDPRTPL2     ;
 INDPRTPL3_ttih     ;
 INDPRTCONNEG_nnUh	   ;
 INDPRTCONNEGPL3_ttU	   ;
:čč CONDPRS_IS ;
 CONDPRSPL3_ttAs ;
 CONDPRT_nnUs ;
 CONDPRTPL3_tAnnUs ;
 POT_nnE ;
 POTPL3_tAnneh ;
 IMPRT_kkA ;
 IMPRTPL3_ttAhes ;

:čči ACTPRSPRC_PEZII ;
 ACTPRFPRC_nnUH ;
 PSSPRSPRC_ttAvU ;
 PSSPRFPRC_ttU ;

:čče M-INF	      ;
+Inf:%>t%{aäInf%} K ;


LEXICON V-VV_06
:ne INDPRSSG1 ;
:ne INDPRSSG2 ;
:n%{oö%} INDPRSSG3 ;
:ne INDPRSPL1 ;
:ne INDPRSPL2 ;
 INDPRSPL3_jAh ;
:ne INDPRSCONNEG ;
:n INDPRTSG1     ;
:n INDPRTSG2     ;
:n INDPRTSG3     ;
:n INDPRTPL1     ;
:n INDPRTPL2     ;
 INDPRTPL3_tih     ;
 INDPRTCONNEG_nnUh	   ;
 INDPRTCONNEGPL3_ttU	   ;
:n CONDPRS_IS ;
 CONDPRSPL3_ttAs ;
 CONDPRT_nnUs ;
 CONDPRTPL3_tAnnUs ;
 POT_nnE ;
 POTPL3_tAnneh ;
 IMPRT_kkA ;
 IMPRTPL3_ttAhes ;

 ACTPRSPRC_AVUAJU ;
 ACTPRFPRC_nnUH ;
 PSSPRSPRC_ttAvU ;
 PSSPRFPRC_ttU ;

:ne M-INF	      ;
+Inf:%>t%{aäInf%} K ;


!! !REFLEXIVE CONJUGATION

LEXICON VR_01
+V:	VR_01-LONGSTEM_ ;
+V:	VR_01-SHORTSTEM_ ;
LEXICON VR_01-LONGSTEM_
 VR_INDPRSSG1 ;
 VR_INDPRSSG2 ;
 VR_INDPRSSG3 ;
 VR_INDPRSPL1 ;
 VR_INDPRSPL2 ;

 VR_INDPRTSG1 ;
 VR_INDPRTSG2 ;
 VR_INDPRTSG3 ;
 VR_INDPRTPL1 ;
 VR_INDPRTPL2 ;

 VR_CONDPRS_IZ ;
 VR_INDPRSCONNEG ;

LEXICON VR_01-SHORTSTEM_
 VR_INDPRSPL3_EtAhes ;

 VR_INDPRTPL3_Ettihes ;
 VR_INDPRTCONNEG_nUhes ;
 VR_INDPRTCONNEGPL3_EttUhes ;

 VR_CONDPRSPL3_EttAzihes ;
 VR_CONDPRT_nUs ;
 VR_CONDPRTPL3_EtAhUzihes ;
 VR_POT_N ;
 VR_POTPL3_EtAnehes ;
 VR_IMPRT_kkA ;
 VR_IMPRTPL3_EttAhes ;

 RCPRFPRC_nUHes ;
+Inf+Rc:%>t%{aäInf%}kseh K ;

!! HOW WILL THESE WORK
! ACTPRSPRC_OSTAI ;
! PSSPRSPRC_EttAvU ;
! ACTPRFPRC_nUH ;
! PSSPRFPRC_EttU ;

! M-INF        ;
!+Inf:%>%{aäInf%} K ;



LEXICON VR_03
+V:%^VOICEe	VR_03-LONGSTEM_ ;
+V:	VR_03-SHORTSTEM_ ;
LEXICON VR_03-LONGSTEM_
 VR_INDPRSSG1 ;
 VR_INDPRSSG2 ;
 VR_INDPRSSG3 ;
 VR_INDPRSPL1 ;
 VR_INDPRSPL2 ;

 VR_INDPRTSG1 ;
 VR_INDPRTSG2 ;
 VR_INDPRTSG3 ;
 VR_INDPRTPL1 ;
 VR_INDPRTPL2 ;

 VR_CONDPRS_IZ ;
 VR_INDPRSCONNEG_WEAK ;

LEXICON VR_03-SHORTSTEM_
 VR_INDPRSPL3_dAhes ;

 VR_INDPRTPL3_tihes ;
 VR_INDPRTCONNEG_nUhes ;
 VR_INDPRTCONNEGPL3_DUhes ;

 VR_CONDPRSPL3_DAzihes ;
 VR_CONDPRT_nUs ;
 VR_CONDPRTPL3_DAhUzihes ;
 VR_POT_N ;
 VR_POTPL3_DAnehes ;
 VR_IMPRT_KA ;
 VR_IMPRTPL3_DAhes ;

 RCPRFPRC_nUHes ;
+Inf+Rc:%>t%{aäInf%}kseh K ;


!! Nonfinites

!! Finites
!! !INDICATIVE PRESENT
LEXICON INDPRSSG1
+Ind+Prs+ScSg1:%^WGStem%>n K ;
LEXICON INDPRSSG2
+Ind+Prs+ScSg2:%^WGStem%>t K ;
LEXICON INDPRSSG3
+Ind+Prs+ScSg3:%>%{uy%} K ;
LEXICON INDPRSPL1
+Ind+Prs+ScPl1:%^WGStem%>mm%{oö%} K ;
LEXICON INDPRSPL2
+Ind+Prs+ScPl2:%^WGStem%>tt%{oö%} K ;
LEXICON INDPRSPL3_jAh
+Ind+Prs+ScPl3:%>j%{aä%}h K ;
+Ind+Prs+ConNeg+ScPl3:%>j%{aä%} K ;
LEXICON INDPRSPL3_dAh
+Ind+Prs+ScPl3:%>d%{aä%}%^WGStemh K ;
+Ind+Prs+ConNeg+ScPl3:%>d%{aä%}%^WGStem K ;
LEXICON INDPRSPL3_tAh
+Ind+Prs+ScPl3:%^WGStem%>t%{aä%}h K ;
+Ind+Prs+ConNeg+ScPl3:%^WGStem%>t%{aä%} K ;
LEXICON INDPRSPL3_EtAh
+Ind+Prs+ScPl3:%^WGStem%^E1%>t%{aä%}h K ;
+Ind+Prs+ConNeg+ScPl3:%^WGStem%^E1%>t%{aä%} K ;
LEXICON INDPRSCONNEG_WEAK
+Ind+Prs+ConNeg:%^WGStem K ;
+Imprt+ScSg2:%^WGStem K ;
+Imprt+ConNeg+ScSg2:%^WGStem K ;
LEXICON INDPRSCONNEG !! * @CODE@ type 04
+Ind+Prs+ConNeg: K ;
+Imprt+ScSg2: K ;
+Imprt+ConNeg+ScSg2: K ;

!! !INDICATIVE PRESENT REFLEXIVE
LEXICON VR_INDPRSSG1
+Ind+Prs+RcSg1:%^WGStem%>mm%{oö%}s K ;
LEXICON VR_INDPRSSG2
+Ind+Prs+RcSg2:%^WGStem%>tt%{oö%}s K ;
LEXICON VR_INDPRSSG3
+Ind+Prs+RcSg3:%^WGStem%>h K ;
+Ind+Prs+RcSg3:%^WGStem%>hes K ;
LEXICON VR_INDPRSPL1
+Ind+Prs+RcPl1:%^WGStem%>mm%{oö%}kseh K ;
LEXICON VR_INDPRSPL2
+Ind+Prs+RcPl2:%^WGStem%>tt%{oö%}kseh K ;
LEXICON VR_INDPRSPL3_jAhes
+Ind+Prs+RcPl3:%>j%{aä%}hes K ;
+Ind+Prs+ConNeg+RcPl3:%>j%{aä%}hes K ;
LEXICON VR_INDPRSPL3_dAhes
+Ind+Prs+RcPl3:%>d%{aä%}%^WGStemhes K ;
+Ind+Prs+ConNeg+RcPl3:%>d%{aä%}%^WGStemhes K ;
!LEXICON VR_INDPRSPL3_tAhes
!+Ind+Prs+RcPl3:%^WGStem%>t%{aä%}hes K ;
!+Ind+Prs+ConNeg+RcPl3:%^WGStem%>t%{aä%}hes K ;
LEXICON VR_INDPRSPL3_EtAhes
+Ind+Prs+RcPl3:%^WGStem%^E1%>t%{aä%}hes K ;
+Ind+Prs+ConNeg+RcPl3:%^WGStem%^E1%>t%{aä%}hes K ;

LEXICON VR_INDPRSCONNEG_WEAK
+Ind+Prs+ConNeg+RcSg:%^WGStemi K ;
+Imprt+RcSg2:%^WGStemi K ;
+Imprt+ConNeg+RcSg2:%^WGStemi K ;
+Ind+Prs+ConNeg+RcPl1:%^WGStemi K ;
+Ind+Prs+ConNeg+RcPl2:%^WGStemi K ;

LEXICON VR_INDPRSCONNEG !! * @CODE@ 
+Ind+Prs+ConNeg+RcSg:i K ;
+Imprt+RcSg2:i K ;
+Imprt+ConNeg+RcSg2:i K ;
+Ind+Prs+ConNeg+RcPl1:i K ;
+Ind+Prs+ConNeg+RcPl2:i K ;

!! !INDICATIVE PRETERITE
LEXICON INDPRTSG1
+Ind+Prt+ScSg1:%>i%^WGStem%>n K ;
LEXICON INDPRTSG2
+Ind+Prt+ScSg2:%>i%^WGStem%>t K ;
LEXICON INDPRTSG3
+Ind+Prt+ScSg3:%>i K ;
LEXICON INDPRTPL1
+Ind+Prt+ScPl1:%>i%^WGStem%>mm%{oö%} K ;
LEXICON INDPRTPL2
+Ind+Prt+ScPl2:%>i%^WGStem%>tt%{oö%} K ;
LEXICON INDPRTPL3_dih
+Ind+Prt+ScPl3:%>dih K ;
LEXICON INDPRTPL3_tih
+Ind+Prt+ScPl3:%^WGStem%>tih K ;
!LEXICON INDPRTPL3_Etih
!+Ind+Prt+ScPl3:%^WGStem%^E1%>tih K ;
LEXICON INDPRTPL3_ttih
+Ind+Prt+ScPl3:%^WGStem%>ttih K ;
LEXICON INDPRTPL3_Ettih
+Ind+Prt+ScPl3:%^WGStem%^E1%>ttih K ;
!LEXICON INDPRTSGCONNEG
!+Ind+Prt+ConNeg+Sg:%^WGStem K ;
LEXICON INDPRTCONNEG_nUh
+Ind+Prt+ConNeg:%>n%{uy%}h K ;
LEXICON INDPRTCONNEG_nnUh
+Ind+Prt+ConNeg:%^WGStem%>nn%{uy%}h K ;
LEXICON INDPRTCONNEGPL3_DU
+Ind+Prt+ConNeg+ScPl3:%>d%{uy%} K ;
LEXICON INDPRTCONNEGPL3_tU
+Ind+Prt+ConNeg+ScPl3:%>t%{uy%} K ;
LEXICON INDPRTCONNEGPL3_ttU
+Ind+Prt+ConNeg+ScPl3:%^WGStem%>tt%{uy%} K ;
LEXICON INDPRTCONNEGPL3_EttU
+Ind+Prt+ConNeg+ScPl3:%^WGStem%^E1%>tt%{uy%} K ;

!! !INDICATIVE PRETERITE REFLEXIVE
LEXICON VR_INDPRTSG1
+Ind+Prt+RcSg1:%>i%^WGStem%>mm%{oö%}s K ;
LEXICON VR_INDPRTSG2
+Ind+Prt+RcSg2:%>i%^WGStem%>tt%{oö%}s K ;
LEXICON VR_INDPRTSG3
+Ind+Prt+RcSg3:%>i%>h K ;
+Ind+Prt+RcSg3:%>i%>hes K ;
LEXICON VR_INDPRTPL1
+Ind+Prt+RcPl1:%>i%^WGStem%>mm%{oö%}kseh K ;
LEXICON VR_INDPRTPL2
+Ind+Prt+RcPl2:%>i%^WGStem%>tt%{oö%}kseh K ;
LEXICON VR_INDPRTPL3_dihes
+Ind+Prt+RcPl3:%>dihes K ;
LEXICON VR_INDPRTPL3_tihes
+Ind+Prt+RcPl3:%^WGStem%>tihes K ;
!LEXICON VR_INDPRTPL3_Etihes
!+Ind+Prt+RcPl3:%^WGStem%^E1%>tihes K ;
!LEXICON VR_INDPRTPL3_ttihes
!+Ind+Prt+RcPl3:%^WGStem%>ttihes K ;
LEXICON VR_INDPRTPL3_Ettihes
+Ind+Prt+RcPl3:%^WGStem%^E1%>ttihes K ;
!LEXICON VR_INDPRTSGCONNEG
!+Ind+Prt+ConNeg+Sg:%^WGStem K ;
LEXICON VR_INDPRTCONNEG_nUhes
+Ind+Prt+ConNeg+RcSg:%>n%{uy%}%^WGStemhes K ;
+Ind+Prt+ConNeg+RcPl1:%>n%{uy%}%^WGStemhes K ;
+Ind+Prt+ConNeg+RcPl2:%>n%{uy%}%^WGStemhes K ;
!LEXICON VR_INDPRTCONNEG_nnUhes
!+Ind+Prt+ConNeg:%^WGStem%>nn%{uy%}hes K ;
LEXICON VR_INDPRTCONNEGPL3_DUhes
+Ind+Prt+ConNeg+RcPl3:%>d%{uy%}hes K ;
!LEXICON VR_INDPRTCONNEGPL3_ttUhes
!+Ind+Prt+ConNeg+RcPl3:%^WGStem%>tt%{uy%}hes K ;
LEXICON VR_INDPRTCONNEGPL3_EttUhes
+Ind+Prt+ConNeg+RcPl3:%^WGStem%^E1%>tt%{uy%}hes K ;


!! Conditional
LEXICON CONDPRS_IS
+Cond+Prs+ScSg1:%>izin K ;
+Cond+Prs+ScSg2:%>izit K ;
+Cond+Prs+ScSg3:%>is K ;
+Cond+Prs+ScSg3:%>iz K ;
+Cond+Prs+ScPl1:%>izimm%{oö%} K ;
+Cond+Prs+ScPl2:%>izitt%{oö%} K ;
+Cond+Prs+ConNeg:%>is K ;

LEXICON CONDPRS_S
+Cond+Prs+ScSg1:%>zin K ;
+Cond+Prs+ScSg2:%>zit K ;
+Cond+Prs+ScSg3:%>s K ;
+Cond+Prs+ScSg3:%>z K ;
+Cond+Prs+ScPl1:%>zimm%{oö%} K ;
+Cond+Prs+ScPl2:%>zitt%{oö%} K ;
+Cond+Prs+ConNeg:%>s K ;

LEXICON CONDPRSPL3
+Cond+Prs+ScPl3:%{aä%}s K ;
+Cond+Prs+ConNeg+ScPl3:%{aä%}s K ;

LEXICON CONDPRSPL3_DAs
+Cond+Prs+ScPl3:%>d%{aä%}s K ;
+Cond+Prs+ConNeg+ScPl3:%>d%{aä%}s K ;

LEXICON CONDPRSPL3_tAs
+Cond+Prs+ScPl3:%>t%{aä%}s K ;
+Cond+Prs+ConNeg+ScPl3:%>t%{aä%}s K ;

LEXICON CONDPRSPL3_ttAs
+Cond+Prs+ScPl3:%^WGStem%>tt%{aä%}s K ;
+Cond+Prs+ConNeg+ScPl3:%^WGStem%>tt%{aä%}s K ;

LEXICON CONDPRSPL3_EttAs
+Cond+Prs+ScPl3:%^WGStem%^E1%>tt%{aä%}s K ;
+Cond+Prs+ConNeg+ScPl3:%^WGStem%^E1%>tt%{aä%}s K ;

LEXICON VR_CONDPRS_IZ
+Cond+Prs+RcSg1:%>i%>zimm%{oö%}s K ;
+Cond+Prs+RcSg2:%>izitt%{oö%}s K ;
+Cond+Prs+RcSg3:%>izihes K ;
+Cond+Prs+RcPl1:%>izimm%{oö%}kseh K ;
+Cond+Prs+RcPl2:%>izitt%{oö%}kseh K ;
+Cond+Prs+ConNeg+RcSg:%>izihes K ;
+Cond+Prs+ConNeg+RcPl1:%>izihes K ;
+Cond+Prs+ConNeg+RcPl2:%>izihes K ;

LEXICON VR_CONDPRSPL3_DAzihes
+Cond+Prs+RcPl3:%>d%{aä%}zihes K ;
+Cond+Prs+ConNeg+RcPl3:%>d%{aä%}zihes K ;

LEXICON VR_CONDPRSPL3_EttAzihes
+Cond+Prs+RcPl3:%^WGStem%^E1%>tt%{aä%}zihes K ;
+Cond+Prs+ConNeg+RcPl3:%^WGStem%^E1%>tt%{aä%}zihes K ;

!! !CONDITIONAL PRETERITE
LEXICON CONDPRT
+Cond+Prt+ScSg1:%>zin K ;
+Cond+Prt+ScSg2:%>zit K ;
+Cond+Prt+ScSg3:%>s K ;
+Cond+Prt+ScSg3:%>z K ;
+Cond+Prt+ScPl1:%>zimm%{oö%} K ;
+Cond+Prt+ScPl2:%>zitt%{oö%} K ;

LEXICON CONDPRT_nUs
+Cond+Prt+ScSg1:%>n%{uy%}%^WGStem%>zin K ;
+Cond+Prt+ScSg2:%>n%{uy%}%^WGStem%>zit K ;
+Cond+Prt+ScSg3:%>n%{uy%}%^WGStem%>s K ;
+Cond+Prt+ScSg3:%>n%{uy%}%^WGStem%>z K ;
+Cond+Prt+ScPl1:%>n%{uy%}%^WGStem%>zimm%{oö%} K ;
+Cond+Prt+ScPl2:%>n%{uy%}%^WGStem%>zitt%{oö%} K ;
+Cond+Prt+ConNeg:%>n%{uy%}%^WGStem%>s K ;

LEXICON CONDPRT_nnUs
+Cond+Prt+ScSg1:%^WGStem%>nn%{uy%}%>zin K ;
+Cond+Prt+ScSg2:%^WGStem%>nn%{uy%}%>zit K ;
+Cond+Prt+ScSg3:%^WGStem%>nn%{uy%}%>s K ;
+Cond+Prt+ScSg3:%^WGStem%>nn%{uy%}%>z K ;
+Cond+Prt+ScPl1:%^WGStem%>nn%{uy%}%>zimm%{oö%} K ;
+Cond+Prt+ScPl2:%^WGStem%>nn%{uy%}%>zitt%{oö%} K ;
+Cond+Prt+ConNeg:%^WGStem%>nn%{uy%}%>s K ;

LEXICON CONDPRTPL3_DAnUs
+Cond+Prt+ScPl3:%>d%{aä%}n%{uy%}s K ;
+Cond+Prt+ConNeg+ScPl3:%>d%{aä%}n%{uy%}s K ;

LEXICON CONDPRTPL3_DAnnUs
+Cond+Prt+ScPl3:%>d%{aä%}nn%{uy%}s K ;
+Cond+Prt+ConNeg+ScPl3:%>d%{aä%}nn%{uy%}s K ;

LEXICON CONDPRTPL3_tAnnUs
+Cond+Prt+ScPl3:%^WGStem%>t%{aä%}nn%{uy%}s K ;
+Cond+Prt+ConNeg+ScPl3:%^WGStem%>t%{aä%}nn%{uy%}s K ;

LEXICON CONDPRTPL3_EtAnnUs
+Cond+Prt+ScPl3:%^WGStem%^E1%>t%{aä%}nn%{uy%}s K ;
+Cond+Prt+ConNeg+ScPl3:%^WGStem%^E1%>t%{aä%}nn%{uy%}s K ;

LEXICON VR_CONDPRT_nUs
+Cond+Prt+RcSg1:%>n%{uy%}%^WGStem%>zimm%{oö%}s K ;
+Cond+Prt+RcSg2:%>n%{uy%}%^WGStem%>zitt%{oö%}s K ;
+Cond+Prt+RcSg3:%>n%{uy%}%^WGStem%>zihes K ;
+Cond+Prt+RcPl1:%>n%{uy%}%^WGStem%>zimm%{oö%}kseh K ;
+Cond+Prt+RcPl2:%>n%{uy%}%^WGStem%>zitt%{oö%}kseh K ;
+Cond+Prt+ConNeg+RcSg:%>n%{uy%}%^WGStem%>zihes K ;
+Cond+Prt+ConNeg+RcPl1:%>n%{uy%}%^WGStem%>zihes K ;
+Cond+Prt+ConNeg+RcPl2:%>n%{uy%}%^WGStem%>zihes K ;

LEXICON VR_CONDPRTPL3_DAhUzihes
+Cond+Prt+RcPl3:%>d%{aä%}h%{uy%}zihes K ;
+Cond+Prt+ConNeg+RcPl3:%>d%{aä%}h%{uy%}zihes K ;

LEXICON VR_CONDPRTPL3_EtAhUzihes
+Cond+Prt+ScPl3:%^WGStem%^E1%>t%{aä%}h%{uy%}zihes K ;
+Cond+Prt+ConNeg+ScPl3:%^WGStem%^E1%>t%{aä%}h%{uy%}zihes K ;



LEXICON POT_N
+Pot+ScSg1:%>n%^WGStemen K ;
+Pot+ScSg2:%>n%^WGStemet K ;
+Pot+ScSg3:%>n%^WGStem%{oö%}%{uy%} K ;
+Pot+ScPl1:%>n%^WGStememm%{oö%} K ;
+Pot+ScPl2:%>n%^WGStemett%{oö%} K ;
+Pot+ConNeg:%>n%^WGSteme K ;

LEXICON POT_nnE
+Pot+ScSg1:%^WGStem%>nnen K ;
+Pot+ScSg2:%^WGStem%>nnet K ;
+Pot+ScSg3:%^WGStem%>nn%{oö%}%{uy%} K ;
+Pot+ScPl1:%^WGStem%>nnemm%{oö%} K ;
+Pot+ScPl2:%^WGStem%>nnett%{oö%} K ;
+Pot+ConNeg:%^WGStem%>nne K ;

LEXICON POTPL3_DAnneh
+Pot+ScPl3:%>d%{aä%}%>nneh K ;
+Pot+ConNeg+ScPl3:%>d%{aä%}%>nne K ;

LEXICON POTPL3_DAneh
+Pot+ScPl3:%>d%{aä%}%>neh K ;
+Pot+ConNeg+ScPl3:%>d%{aä%}%>ne K ;

LEXICON POTPL3_tAnneh
+Pot+ScPl3:%^WGStem%>t%{aä%}nneh K ;
+Pot+ConNeg+ScPl3:%^WGStem%>t%{aä%}nne K ;

LEXICON POTPL3_EtAnneh
+Pot+ScPl3:%^WGStem%^E1%>t%{aä%}nneh K ;
+Pot+ConNeg+ScPl3:%^WGStem%^E1%>t%{aä%}nne K ;

LEXICON VR_POT_N
+Pot+RcSg1:%>n%^WGStememm%{oö%}s K ;
+Pot+RcSg2:%>n%^WGStemett%{oö%}s K ;
+Pot+RcSg3:%>n%^WGStemehes K ;
+Pot+RcPl1:%>n%^WGStememm%{oö%}kseh K ;
+Pot+RcPl2:%>n%^WGStemett%{oö%}kseh K ;
+Pot+ConNeg+RcSg:%>n%^WGStemehes K ;
+Pot+ConNeg+RcPl1:%>n%^WGStemehes K ;
+Pot+ConNeg+RcPl2:%>n%^WGStemehes K ;

LEXICON VR_POTPL3_DAnehes
+Pot+RcPl3:%>d%{aä%}%>hes K ;
!+Pot+RcPl3:%>d%{aä%}%>nehes K ; !!= @CODE@ Shouldn't THIS be the right form
+Pot+ConNeg+RcPl3:%>d%{aä%}%>nehes K ;

LEXICON VR_POTPL3_EtAnehes
+Pot+RcPl3:%^WGStem%^E1%>t%{aä%}nehes K ;
+Pot+ConNeg+RcPl3:%^WGStem%^E1%>t%{aä%}nehes K ;

!! !IMPERATIVE
LEXICON IMPRT_GA
+Imprt+ScSg3:%>g%{aä%}h K ;
+Imprt+ScPl1:%>g%{ui%}%{aä%}mm%{oö%} K ;
+Imprt+ScPl2:%>g%{ui%}%{aä%} K ;
+Imprt+ScPl2:%>g%{ui%}%{aä%}tt%{oö%} K ;

+Imprt+ConNeg+ScSg3:%>g%{aä%}h K ;
+Imprt+ConNeg+ScPl1:%>g%{ui%}%{aä%}mm%{oö%} K ;
+Imprt+ConNeg+ScPl2:%>g%{ui%}%{aä%} K ;
+Imprt+ConNeg+ScPl2:%>g%{ui%}%{aä%}tt%{oö%} K ;

LEXICON IMPRT_KA
+Imprt+ScSg3:%>k%{aä%}h K ;
+Imprt+ScPl1:%>k%{ui%}%{aä%}mm%{oö%} K ;
+Imprt+ScPl2:%>k%{ui%}%{aä%} K ;
+Imprt+ScPl2:%>k%{ui%}%{aä%}tt%{oö%} K ;

+Imprt+ConNeg+ScSg3:%>k%{aä%}h K ;
+Imprt+ConNeg+ScPl1:%>k%{ui%}%{aä%}mm%{oö%} K ;
+Imprt+ConNeg+ScPl2:%>k%{ui%}%{aä%} K ;
+Imprt+ConNeg+ScPl2:%>k%{ui%}%{aä%}tt%{oö%} K ;

LEXICON IMPRT_kkA
+Imprt+ScSg3:%>kk%{aä%}h K ;
+Imprt+ScPl1:%>kk%{ui%}%{aä%}mm%{oö%} K ;
+Imprt+ScPl2:%>kk%{ui%}%{aä%} K ;
+Imprt+ScPl2:%>kk%{ui%}%{aä%}tt%{oö%} K ;

+Imprt+ConNeg+ScSg3:%>kk%{aä%}h K ;
+Imprt+ConNeg+ScPl1:%>kk%{ui%}%{aä%}mm%{oö%} K ;
+Imprt+ConNeg+ScPl2:%>kk%{ui%}%{aä%} K ;
+Imprt+ConNeg+ScPl2:%>kk%{ui%}%{aä%}tt%{oö%} K ;

LEXICON IMPRT_WEAK-kkA
+Imprt+ScSg3:%^WGStem%>kk%{aä%}h K ;
+Imprt+ScPl1:%^WGStem%>kk%{ui%}%{aä%}mm%{oö%} K ;
+Imprt+ScPl2:%^WGStem%>kk%{ui%}%{aä%} K ;
+Imprt+ScPl2:%^WGStem%>kk%{ui%}%{aä%}tt%{oö%} K ;

+Imprt+ConNeg+ScSg3:%^WGStem%>kk%{aä%}h K ;
+Imprt+ConNeg+ScPl1:%^WGStem%>kk%{ui%}%{aä%}mm%{oö%} K ;
+Imprt+ConNeg+ScPl2:%^WGStem%>kk%{ui%}%{aä%} K ;
+Imprt+ConNeg+ScPl2:%^WGStem%>kk%{ui%}%{aä%}tt%{oö%} K ;

LEXICON IMPRTPL3_DAhes
+Imprt+ScPl3:%>d%{aä%}hes K ;
+Imprt+ConNeg+ScPl3:%>d%{aä%}hes K ;

LEXICON IMPRTPL3_tAhes
+Imprt+ScPl3:%>t%{aä%}hes K ;
+Imprt+ConNeg+ScPl3:%>t%{aä%}hes K ;

LEXICON IMPRTPL3_ttAhes
+Imprt+ScPl3:%^WGStem%>tt%{aä%}hes K ;
+Imprt+ConNeg+ScPl3:%^WGStem%>tt%{aä%}hes K ;

LEXICON IMPRTPL3_EttAhes
+Imprt+ScPl3:%^WGStem%^E1%>tt%{aä%}hes K ;
+Imprt+ConNeg+ScPl3:%^WGStem%^E1%>tt%{aä%}hes K ;

!! !IMPERATIVE REFLEXIVE
LEXICON VR_IMPRT_GA
+Imprt+RcSg3:%>g%{aä%}hes K ;
+Imprt+RcPl1:%>g%{ui%}%{aä%}mm%{oö%}kseh K ;
+Imprt+RcPl2:%>g%{ui%}%{aä%}tt%{oö%}kseh K ;

+Imprt+ConNeg+RcSg3:%>g%{aä%}hes K ;
+Imprt+ConNeg+RcPl1:%>g%{ui%}%{aä%}mm%{oö%}kseh K ;
+Imprt+ConNeg+RcPl2:%>g%{ui%}%{aä%}tt%{oö%}kseh K ;

LEXICON VR_IMPRT_kkA
+Imprt+RcSg3:%>kk%{aä%}hes K ;
+Imprt+RcPl1:%>kk%{ui%}%{aä%}mm%{oö%}kseh K ;
+Imprt+RcPl2:%>kk%{ui%}%{aä%}tt%{oö%}kseh K ;

+Imprt+ConNeg+RcSg3:%>kk%{aä%}hes K ;
+Imprt+ConNeg+RcPl1:%>kk%{ui%}%{aä%}mm%{oö%}kseh K ;
+Imprt+ConNeg+RcPl2:%>kk%{ui%}%{aä%}tt%{oö%}kseh K ;

!LEXICON VR_IMPRT_WEAK-kkA
!+Imprt+RcSg3:%^WGStem%>kk%{aä%}hes K ;
!+Imprt+RcPl1:%^WGStem%>kk%{ui%}%{aä%}mm%{oö%}kseh K ;
!+Imprt+RcPl2:%^WGStem%>kk%{ui%}%{aä%}tt%{oö%}kseh K ;

!+Imprt+ConNeg+RcSg3:%^WGStem%>kk%{aä%}hes K ;
!+Imprt+ConNeg+RcPl1:%^WGStem%>kk%{ui%}%{aä%}mm%{oö%}kseh K ;
!+Imprt+ConNeg+RcPl2:%^WGStem%>kk%{ui%}%{aä%}tt%{oö%}kseh K ;

LEXICON VR_IMPRT_KA
+Imprt+RcSg3:%>k%{aä%}hes K ;
+Imprt+RcPl1:%>k%{ui%}%{aä%}mm%{oö%}kseh K ;
+Imprt+RcPl2:%>k%{ui%}%{aä%}tt%{oö%}kseh K ;

+Imprt+ConNeg+RcSg3:%>k%{aä%}hes K ;
+Imprt+ConNeg+RcPl1:%>k%{ui%}%{aä%}mm%{oö%}kseh K ;
+Imprt+ConNeg+RcPl2:%>k%{ui%}%{aä%}tt%{oö%}kseh K ;

LEXICON VR_IMPRTPL3_DAhes
+Imprt+RcPl3:%>d%{aä%}hes K ;
+Imprt+ConNeg+RcPl3:%>d%{aä%}hes K ;

!LEXICON VR_IMPRTPL3_ttAhes
!+Imprt+RcPl3:%^WGStem%>tt%{aä%}hes K ;
!+Imprt+ConNeg+RcPl3:%^WGStem%>tt%{aä%}hes K ;

LEXICON VR_IMPRTPL3_EttAhes
+Imprt+RcPl3:%^WGStem%^E1%>tt%{aä%}hes K ;
+Imprt+ConNeg+RcPl3:%^WGStem%^E1%>tt%{aä%}hes K ;



!!
!LEXICON  ACTPRSPRC_vU
!+ActPrsPrc:%>v%{uy%} K ;
!+ActPrsPrc:%>v%{aä%} NMN_PÄIVY ;

LEXICON  ACTPRSPRC_IU
+ActPrsPrc:%>%{ijPRC%} NMN_OSTAI ;

LEXICON  ACTPRSPRC_OSTAI
+ActPrsPrc:%>%{ijPRC%} NMN_OSTAI ;

LEXICON  ACTPRSPRC_PEZII
+ActPrsPrc:%>%{ijPRC%} NMN_PEZII ;

LEXICON  ACTPRSPRC_AVUAJU
+ActPrsPrc:%>%{ijPRC%}%{aä%} NMN_PÄIVY ;

LEXICON  PSSPRSPRC_DAvU
+PssPrsPrc:%>d%{aä%}v%{uy%} K ;
+PssPrsPrc:%>d%{aä%}v%{aä%} NMN_PÄIVY        ;

LEXICON  PSSPRSPRC_tAvU
+PssPrsPrc:%>t%{aä%}v%{uy%} K ;
+PssPrsPrc:%>t%{aä%}v%{aä%} NMN_PÄIVY        ;

!LEXICON  PSSPRSPRC_EtAvU
!+PssPrsPrc:%^WGStem%^E1%>t%{aä%}v%{uy%} K ;
!+PssPrsPrc:%^WGStem%^E1%>t%{aä%}v%{aä%} NMN_PÄIVY        ;

LEXICON  PSSPRSPRC_ttAvU
+PssPrsPrc:%^WGStem%>tt%{aä%}v%{uy%} K	;
+PssPrsPrc:%^WGStem%>tt%{aä%}v%{aä%} NMN_PÄIVY	;

LEXICON  PSSPRSPRC_EttAvU
+PssPrsPrc:%^WGStem%^E1%>tt%{aä%}v%{uy%} K	;
+PssPrsPrc:%^WGStem%^E1%>tt%{aä%}v%{aä%} NMN_PÄIVY	;

LEXICON ACTPRFPRC_nUH
+ActPrfPrc:%>n%{uy%}h K ;
+ActPrfPrc+Use/Sub:%>n%{uy%}t K ;
+ActPrfPrc:%>n%{uy%} NMN_PESSYH ;

LEXICON ACTPRFPRC_nnUH
+ActPrfPrc:%^WGStem%>nn%{uy%}h K ;
+ActPrfPrc+Use/Sub:%^WGStem%>nn%{uy%}h K ;
+ActPrfPrc:%^WGStem%>nn%{uy%} NMN_PESSYH ;

LEXICON  PSSPRFPRC_DU
+PssPrfPrc:%>d%{uy%} K ;
+PssPrfPrc:%>d%{uy%} A_KOIVU ;

LEXICON  PSSPRFPRC_tU
+PssPrfPrc:%>t%{uy%} K ;
+PssPrfPrc:%>t%{uy%} A_KOIVU ;

LEXICON  PSSPRFPRC_ttU
+PssPrfPrc:%^WGStem%>tt%{uy%} K ;
+PssPrfPrc:%^WGStem%>tt%{uy%} A_KOIVU ;

LEXICON  PSSPRFPRC_EttU
+PssPrfPrc:%^WGStem%^E1%>tt%{uy%} K ;
+PssPrfPrc:%^WGStem%^E1%>tt%{uy%} A_KOIVU ;

!!
LEXICON RCPRFPRC_nUHes
+RcPrfPrc:%>n%{uy%}hes K ;
!+RcPrfPrc+Use/Sub:%>n%{uy%}tes K ;

!LEXICON RCPRFPRC_nnUHes
!+RcPrfPrc:%^WGStem%>nn%{uy%}hes K ;
!+RcPrfPrc+Use/Sub:%^WGStem%>nn%{uy%}tes K ;

LEXICON  RCPRFPRC_DUhes
+RcPrfPrc+Pl3:%>d%{uy%}hes K ;

!LEXICON  RCPRFPRC_ttUhes
!+RcPrfPrc+Pl3:%^WGStem%>tt%{uy%} K ;

!LEXICON  RCPRFPRC_EttUhes
!+RcPrfPrc+Pl3:%^WGStem%^E1%>tt%{uy%} K ;

LEXICON M-INF
+Der/mine:%>mi NMN_KIELINE ;
+Der/mA+Inf+Abe:%>m%{aä%}tt%{aä%}h K ;
+Der/mA+Inf+Abe+Use/Sub:%>m%{aä%}tt%{aä%} K ;
+Der/mA+Inf+Abe:%>m%{aä%}t%{aä%} K ;
+Der/mA+Inf+Ade:%>m%{aä%}l K ;
+Der/mA+Inf+Ela:%>m%{aä%}späi K ;
+Der/mA+Inf+Ill:%>m%{aä%}h K ;
+Der/mA+Inf+Ine:%>m%{aä%}s K ;
+Der/mA+Inf+Par:%>m%{ui%}%{aä%} K ;


! vim: set ft=xfst-lexc:

