!! !!!Verb inflection
! ---------------

!! !!Temporary lexica


LEXICON V_ !!= * __@CODE@__ is for ad-hoc incoming verbs, not in xml
+TYÄ+V: K ;


!! !!Auxiliary verbs

LEXICON V-NEG !!= * __@CODE@__ ei:e
+V+Neg+ScSg1:n K ;
+V+Neg+ScSg2:t K ;
+V+Neg+ScSg3:i K ;
+V+Neg+ScPl1:mmo K ;
+V+Neg+ScPl2:tto K ;
!+V+Neg+ScPl3:i K ;

LEXICON V-PROH !!= * __@CODE@__ älä:äl
+V+Neg+Imprt+ScSg2:ä K ;
+V+Neg+Imprt+ScSg3:gäh K ;
+V+Neg+Imprt+ScPl1:giämmö K ;
+V+Neg+Imprt+ScPl2:giä K ;
!+V+Neg+ScPl3:i K ;

LEXICON V_OLLA !!= * __@CODE@__ olla:o
+V+Ind+Prs+ScSg1:le%^WGStem%>n K ;
+V+Ind+Prs+ScSg2:le%^WGStem%>t K ;
+V+Ind+Prs+ScSg3:n K ;
+V+Ind+Prs+ScPl1:le%^WGStem%>mm%{oö%} K ;
+V+Ind+Prs+ScPl2:le%^WGStem%>tt%{oö%} K ;
+V:l INDPRSPL3_dAh ;
+V+Ind+Prs+ConNeg:le%^WGStem K ;

+V+Ind+Prt+ScSg1:l%^WGStem%>in K ;
+V+Ind+Prt+ScSg2:l%^WGStem%>it K ;
+V+Ind+Prt+ScSg3:l%>i K ;
+V+Ind+Prt+ScPl1:l%^WGStem%>imm%{oö%} K ;
+V+Ind+Prt+ScPl2:l%^WGStem%>itt%{oö%} K ;
+V:l INDPRTPL3_dih     ;
+V+Ind+Prt+ConNeg:l%>n%{uy%}%^WGStemh K ;
+V+Ind+Prt+ConNeg+ScPl3:l%>t%{uy%} K ;
+V:l CONDPRS_IS  ;
+V:l CONDPRSPL3_DAs ;
+V:l CONDPRT_nUs ;
+V:l CONDPRTPL3_DAnnUs ;
+V:l POT_N ;
+V:l POTPL3_DAnneh ;
+V:l IMPRT_GA ;
+V:l IMPRTPL3_DAhes ;
+V:li ACT-PRSPRC_PEZII ;
+V:l ACT-PRFPRC_nUH ;
+V:l PSS-PRSPRC_DAvU ;
+V:l PSS-PRFPRC_DU ;
+V:le M-INF ;
+Ger+Manner:%>de%^WGStemn K ;
+Ger+Temp:%>de%^WGStems K ;



!! !!Regular verbs

!! !Verbs of the Finnish type 1

!! V1a

LEXICON V_ELLENDIÄ !!= * __@CODE@__ ellendiä:ellendä
!! Gradation No
+V: V-VV_01-NO-GRADATION ;

LEXICON V_KANDUA !!= * __@CODE@__ kandua:kanda
!! Gradation Yes
!! Back Vowel
+V: V-VV_01 ;

LEXICON V_KANDUA-SG3 !!= * __@CODE@__ kandua:kanda
+V: V-VV_01 ;
+V:  INDPRSSG3 ;
+V: INDPRSCONNEG_WEAK ;
+V: INDPRTSG3 ;
+V: INDPRTCONNEG_nUh ;

LEXICON V_KANDUA-3 !!= * __@CODE@__ kandua:kanda
+V: V-VV_01 ;

LEXICON V_HEITTIÄ !!= * __@CODE@__ heittä:heittä
!! Gradation Yes
!! Front vowel
+V: V-VV_01 ;

LEXICON V_HEITTIÄ-SG3 !!= * __@CODE@__ heittä:heittä
+V: V-VV_01 ;
+V:  INDPRSSG3 ;
+V: INDPRSCONNEG_WEAK ;
+V: INDPRTSG3 ;
+V: INDPRTCONNEG_nUh ;

LEXICON V_HEITTIÄ-3 !!= * __@CODE@__ heittä:tiedä
+V: V-VV_01 ;

!! V1o

LEXICON V_PUNUO !!= * __@CODE@__ punuo:puno
!! Gradation No
!! Back vowel
+V: V-VV_01 ;

LEXICON V_PUNUO-SG3 !!= * __@CODE@__ punuo:puno
+V: V-VV_01 ;

!! V1u

LEXICON V_VALUO !!= * __@CODE@__ valuo:valu
!! Gradation No
!! Back vowel
+V: V-VV_01 ;

LEXICON V_VALUO-SG3 !!= * __@CODE@__ valuo:valu
+V: V-VV_01 ;

LEXICON V_VALUO-3 !!= * __@CODE@__ valuo:valu
+V: V-VV_01 ;


!! V1i
LEXICON V_POTKIE !!= * __@CODE@__ potkie:potki
!! Gradation No
!! Back vowel
+V: V-VV_01 ;

LEXICON V_POTKIE-SG3 !!= * __@CODE@__ potkie:potki
+V: V-VV_01 ;

LEXICON V_IMIE !!= * __@CODE@__ imie:ime
!! Gradation No
!! Front vowel
+V: V-VV_01 ;
+V:%>e INDPRTPL3_ttih ; !!= * __@CODE@__ 
!!€# koske%>e%^WGStem%>ttih
!!€0 koski0e00ttih
+V:%>e INDPRSPL3_tAh ; !!= * __@CODE@__ 
!!€# koske%>e%^WGStem%>t%{aäDiph%}h
!!€0 koski0e00tah
+V:%>e PSS-PRFPRC_ttU ;
!!€# koske%>e%^WGStem%>tt%{uy%}
!!€0 koski0e00tah

LEXICON V_IMIE-SG3 !!= * __@CODE@__ imie:ime
+V: V-VV_01 ;


LEXICON  V_PIDIÄ !!= * __@CODE@__ pidiä:pidä
!! Gradation Yes
!! Front vowel
+V: V-VV_01 ;


LEXICON  V_PIDIÄ-SG3 !!= * __@CODE@__ pidiä:pidä
+V: V-VV_01 ;

LEXICON V_KIRJUTTUA !!= * __@CODE@__ kirjuttua:kirjutta
!! 3-syllable stem
!! Gradation Yes
!! Back vowel

 V-3SYLL_KIRJUTTUA ;

!! !VERBS OF FINNISH TYPE 2 in dA

LEXICON V_VOIJA !!= * __@CODE@__ voija:voi
+TYÄ+V: V-VV_02 ;
+V+Ind+Prs+ScSg1:n K ;
+V+Ind+Prs+ScSg3:bi K ;
+V+Ind+Prs+ScPl3:ijah K ;

LEXICON V_ROIJA !!= * __@CODE@__ roija:roi
+V: V-VV_02 ;


LEXICON V_SUAJA !!= * __@CODE@__ suaja:saa jiäjä:jää
+V: V-VV_02 ;
+V+Inf:%>h%{aäDiph%} K ; !!= * __@CODE@__ type 2 also suaha jiäjä, jiähä
+V: INDPRSPL3_hAh ;

LEXICON V_JUVVA !!= * __@CODE@__ juvva:juo
+V: V-VV_JUVVA_02 ;

LEXICON V_NÄHTÄ !!= * __@CODE@__ nähtä:nä
!!	+TYÄ+V: V-VV_02	 ;
+V+Ind+Prs+ScSg1:en K ;
+V+Ind+Prs+ScSg2:et K ;
+V+Ind+Prs+ScSg3:göy K ;
+V+Ind+Prs+ScPl:emmö K ;
+V+Ind+Prs+ScPl2:ettö K ;
+V:h INDPRSPL3_tAh ;

+V+Ind+Prs+ScSg1:in K ;
+V+Ind+Prs+ScSg2:it K ;
+V+Ind+Prs+ScSg3:gi K ;
+V+Ind+Prs+ScPl:immö K ;
+V+Ind+Prs+ScPl2:ittö K ;

+V:h INDPRTPL3_tih ;
+V:h INDPRTCONNEG_nUh ;
+V:h INDPRTCONNEGPL3_tU ;
+V:g CONDPRS_IS  ;
+V:h CONDPRSPL3_tAs ;
+V:h CONDPRT_nUs ;
+V:h CONDPRTPL3_DAnUs ;

+V:h POT_N ;
+V:h POTPL3_DAneh ;

+V:h IMPRT_KA ;
+V:h IMPRTPL3_tAhes ;

+V:gi ACT-PRSPRC_OSTAI ;
+V:h PSS-PRSPRC_tAvU ;
+V:h ACT-PRFPRC_nUH ;
+V:h PSS-PRFPRC_tU ;

+V:ge M-INF        ;
+V+Inf:h%>%{aäDiph%} K ;
+V:h GER_ten ;
+V:h GER-TEMP_tes ;

!+V+ACTPRSPRC:gevä A_PÄIVY ;

LEXICON V_KÄVVÄ !!= * __@CODE@__ kävvä:käv
+TYÄ+V: V-VV_02  ;
+V+Ind+Prs+ScSg1:yn K ;
+V+Ind+Prs+ScSg3:yy K ;
+V+Ind+Prs+ScPl3:väh K	;


!! !Verbs of the Finnish type 3

LEXICON V_TULLA !!= * __@CODE@__ tulla:tul
+V: V-VV_03 ;

LEXICON V_PESTÄ !!= * __@CODE@__ pestä:pes
+V: V-VV_03 ;

LEXICON V_NOSTA !!= * __@CODE@__ nosta:no
+V: V-VV_03 ;
+V+Ind+Prs+ScSg1:uzen K ;
+V+Ind+Prs+ScSg3:uzou K ;
+V+Ind+Prs+ScPl3:stah K  ;

!! Verbs of the Finnish type 3 but not identical

LEXICON V_KUULTA !!= * __@CODE@__ kuulta:kuul
+V: V-VV_KUULLA ;

LEXICON V_KUULTA-SG3 !!= * __@CODE@__ kuulta:kuul
+V: V-VV_KUULLA ;


LEXICON V_TYPÄTÄ !!= * __@CODE@__ typätä:typpä
+V: #;

LEXICON V_KÄITÄ !!= * __@CODE@__ käitä:käge
+V: V-VV_04 ;

LEXICON V_SUVATA !!= * __@CODE@__ suvata:suva
+V: V-VV_04 ;


!! !Verbs of the Finnish type 4

LEXICON V_AVATA !!= * __@CODE@__ avata:ava
+V: V-VV_04 ;

LEXICON V_AVATA-SG3 !!= * __@CODE@__ avata:ava
+V: V-VV_04 ;


!! !Verbs of the Finnish type 5

LEXICON V_SUVAIJA !!= * __@CODE@__ suvaija:suvai
+V: V-VV_05_SUVAIJA ;

LEXICON V_SUVAIJA-SG3 !!= * __@CODE@__ suvaija:suvai
+V: V-VV_05_SUVAIJA ;

LEXICON V_SUVAIJA-3 !!= * __@CODE@__ suvaija:suvai
+V: V-VV_05_SUVAIJA ;

LEXICON V_EMÄNDÖIJÄ !!= * __@CODE@__ emändöijä:emändöi
+V: V-VV_05_EMÄNDÖIJÄ ;

LEXICON V_EMÄNDÖIJÄ-SG3 !!= * __@CODE@__ emändöijä:emändöi
+V: V-VV_05_EMÄNDÖIJÄ ;

LEXICON V_EMÄNDÖIJÄ-3 !!= * __@CODE@__ emändöijä:emändöi
+V: V-VV_05_EMÄNDÖIJÄ ;

LEXICON V_MERKITÄ !!= * __@CODE@__ merkitä:merki
+V: V-VV_05_MERKITÄ ;
LEXICON V_MAINITA !!= * __@CODE@__ mainita:maini
+V: V-VV_05_MERKITÄ ;
!!= *  Incorrect forms are generated here

LEXICON V_ALETA !!= * __@CODE@__ aleta:ale (alenov)
+V: V-VV_06 ;
+V:ne INDPRSSG1 ;
+V:ne INDPRSSG2 ;
+V:n%{oö%} INDPRSSG3 ;
+V:ne INDPRSPL1 ;
+V:ne INDPRSPL2 ;
+V: INDPRSPL3_tAh ;
+V:ne INDPRSCONNEG ;
+V:n INDPRTSG1     ;
+V:n INDPRTSG2     ;
+V:n INDPRTSG3     ;
+V:n INDPRTPL1     ;
+V:n INDPRTPL2     ;
+V: INDPRTPL3_ttih     ;
+V+Ind+PrfPrc+Err/Orth:%>nn%{uy%}t K ;
+V+Ind+PrfPrc:%>nn%{uy%}h K ;
+V+Ind+PrfPrcPl3:%>tt%{uy%} K ;
+V: INDPRTCONNEG_nnUh ; !!
+V: INDPRTCONNEGPL3_ttU ;
+V:n CONDPRS_IS ;
!+V:%>d CONDPRSPL3 ;
!+V:%>d%{uy%}%^WGStem CONDPRT ;
!+V:%>d%{aäPar%} CONDPRTPL3_DAnUs ;

LEXICON V_ALETA-SG3 !!= * __@CODE@__ aleta:ale (alenov)
+V: V-VV_06 ;


LEXICON V_SUATA !!= * __@CODE@__ suata:suva (suvaiččov)
+V: #;

LEXICON VR_AMBUO !!= * __@CODE@__ ambuokseh:ambu
	VR_01 ;

LEXICON VR_VALUO !!= * __@CODE@__ valuokseh:valu
	VR_01 ;

LEXICON VR_PUNUO !!= * __@CODE@__ punuokseh:puno
	VR_01 ;

LEXICON VR_IMIE !!= * __@CODE@__ imiekseh:ime
	VR_01 ;

LEXICON VR_POTKIE !!= * __@CODE@__ potkiekseh:potki
	VR_01 ;

LEXICON VR_PIDIÄ !!= * __@CODE@__ pidiäkseh:pidä
  VR_01 ;

LEXICON VR_KANDUA !!= * __@CODE@__ kanduakseh:kanda
  VR_01 ;

LEXICON VR_KANDUA-SG3 !!= * __@CODE@__ kanduakseh:kanda
  VR_01 ;


!! !! Reflexive verbs

LEXICON VR_RODIEKSEH !!= * __@CODE@__ rodiekseh:rodi
 VR_01 ;
+V+VR: VR_INDPRTCONNEG_nnUhes ;

LEXICON VR_ROIJA !!= * __@CODE@__ roijakseh:roi
        VR_ ;

LEXICON VR_VOIJA !!= * __@CODE@__ voijakseh:voi
        VR_ ;

LEXICON VR_ROITA !!= * __@CODE@__ roita:roi 
        VR_ ;

LEXICON VR_JUVVA !!= * __@CODE@__ juvva:juo
	VR_	 ;

LEXICON VR_NÄHTÄ !!= * __@CODE@__ nähtä:nä
	VR_	 ;

LEXICON VR_TULLA !!= * __@CODE@__ tuliekseh:tul !this will need work 2013-10-23
	VR_03 ;

LEXICON VR_PESTÄ !!= * __@CODE@__ tuliekseh:tul !this will need work 2013-10-23
	VR_03 ;

LEXICON VR_AVATA !!= * __@CODE@__ avatakseh:ava (typätäkseh:typpä)
 VR_ ;

LEXICON VR_SUVAIJA !!= * __@CODE@__ suvaijakseh:suvai
!Verbs of the Finnish type 5
:čče VR-LONGSTEM_ ;
 VR-SHORTSTEM_ ;

LEXICON VR_EMÄNDÖIJÄ !!= * __@CODE@__ emändöijäkseh:emändöi
!Verbs of the Finnish type 5
:čče VR-LONGSTEM_ ;
 VR-SHORTSTEM_ ;

LEXICON VR_MERKITÄ !!= * __@CODE@__ merkitäkseh:merki
!Verbs of the Finnish type 5
:čče VR-LONGSTEM_ ;
 VR-SHORTSTEM_ ;

LEXICON VR_MAINITA !!= * __@CODE@__ mainitakseh:maini
!Verbs of the Finnish type 5
:čče VR-LONGSTEM_ ;
 VR-SHORTSTEM_ ;



LEXICON VR_
	VR-LONGSTEM_ ;
	VR-SHORTSTEM_ ;
LEXICON VR-LONGSTEM_
+V+Ind+Prs+RcSg1:%^WGStem%>mm%{oö%}s	K	;
+V+Ind+Prs+RcSg2:%^WGStem%>tt%{oö%}s	K	;
+V+Ind+Prs+RcSg3:%^WGStem%>h	K	;
+V+Ind+Prs+RcPl1:%^WGStem%>mm%{oö%}kseh	K	;
+V+Ind+Prs+RcPl2:%^WGStem%>tt%{oö%}kseh	K	;

+V+Ind+Prt+RcSg1:%>mm%{oö%}s	K	;
+V+Ind+Prt+RcSg2:%>tt%{oö%}s	K	;
+V+Ind+Prt+RcSg3:%>h	K	;
+V+Ind+Prt+RcPl1:%>mm%{oö%}kseh	K	;
+V+Ind+Prt+RcPl2:%>tt%{oö%}kseh	K	;

LEXICON VR-SHORTSTEM_
+V+Ind+Prs+RcPl3:%^WGStem%>j%{aäPar%}h	K	;
+V+Ind+Prs+ConNeg+RcSg:%^WGStem%>	K	;
+V+Ind+Prs+ConNeg+RcPl1:%^WGStem%>	K	;
+V+Ind+Prs+ConNeg+RcPl2:%^WGStem%>	K	;

+V+Ind+Prs+ConNeg+RcPl3:%^WGStem%>j%{aäPar%}	K	;


+V+Ind+Prt+RcPl3:%>ttih	K	;
+V+Ind+Prt+ConNeg+RcSg:%>n%{uy%}h	K	;
+V+Ind+Prt+ConNeg+RcPl1:%>n%{uy%}h	K	;
+V+Ind+Prt+ConNeg+RcPl2:%>n%{uy%}h	K	;
+V+Ind+Prt+ConNeg+RcPl3:%>tt%{uy%}	K	;


LEXICON V-VV_01
! V1
! This verb type has two final vowels in the first infinitive
:%^WGStem INDPRSSG1 ;
:%^WGStem INDPRSSG2 ;
 INDPRSSG3 ;
:%^WGStem INDPRSPL1 ;
:%^WGStem INDPRSPL2 ;
:%^WGStem INDPRSPL3_EtAh ;
:%^WGStem INDPRSCONNEG_WEAK ;

:%^WGStem INDPRTSG1 ;
:%^WGStem INDPRTSG2 ;
 INDPRTSG3 ;
:%^WGStem INDPRTPL1 ;
:%^WGStem INDPRTPL2 ;
:%^WGStem INDPRTPL3_Ettih ;
!+Ind+Prt+ConNeg:%>n%{uy%}h K ;
 INDPRTCONNEG_nUh ;
:%^WGStem INDPRTCONNEGPL3_EttU ;

 CONDPRS_S  ;
:%^WGStem CONDPRSPL3_EttAs ;
 CONDPRT_nUs ;
 CONDPRTPL3_EtAnnUs ;

 POT_N ;
 POTPL3_EtAnneh ;

 IMPRT_kkA ;
 IMPRTPL3_EttAhes ;

 ACT-PRSPRC_OSTAI ;
 PSS-PRSPRC_EttAvU ;
 ACT-PRFPRC_nUH ;
 PSS-PRFPRC_EttU ;

 M-INF        ;
+Inf:%>%{aäDiph%} K ;
 GER_jen ;
 GER-TEMP_jes ;

LEXICON V-VV_01-NO-GRADATION
! V1 ellendiä:ellendä, ellendän
!! No Gradation
! This verb type has two final vowels in the first infinitive
 INDPRSSG1 ;
 INDPRSSG2 ;
 INDPRSSG3 ;
 INDPRSPL1 ;
 INDPRSPL2 ;
 INDPRSPL3_EtAh ;
 INDPRSCONNEG_WEAK ;

 INDPRTSG1 ;
 INDPRTSG2 ;
 INDPRTSG3 ;
 INDPRTPL1 ;
 INDPRTPL2 ;
 INDPRTPL3_Ettih ;
!+Ind+Prt+ConNeg:%>n%{uy%}h K ;
 INDPRTCONNEG_nUh ;
 INDPRTCONNEGPL3_EttU ;

 CONDPRS_S  ;
 CONDPRSPL3_EttAs ;
 CONDPRT_nUs ;
 CONDPRTPL3_EtAnnUs ;

 POT_N ;
 POTPL3_EtAnneh ;

 IMPRT_kkA ;
 IMPRTPL3_EttAhes ;

 ACT-PRSPRC_OSTAI ;
 PSS-PRSPRC_EttAvU ;
 ACT-PRFPRC_nUH ;
 PSS-PRFPRC_EttU ;

 M-INF        ;
+Inf:%>%{aäDiph%} K ;
 GER_jen ;
 GER-TEMP_jes ;

LEXICON V-3SYLL_KIRJUTTUA !!= __@CODE@__  kirjuttua:kirjutta
! V1 3-syllable stem 
! This verb type has two final vowels in the first infinitive
 INDPRSSG1 ;
 INDPRSSG2 ;
 INDPRSSG3 ;
 INDPRSPL1 ;
 INDPRSPL2 ;
 INDPRSPL3_EtAh ;
 INDPRSCONNEG_WEAK ;

 INDPRTSG1 ;
 INDPRTSG2 ;
 INDPRTSG3 ;
 INDPRTPL1 ;
 INDPRTPL2 ;
 INDPRTPL3_Ettih ;
!+Ind+Prt+ConNeg:%>n%{uy%}h K ;
 INDPRTCONNEG_nUh ;
 INDPRTCONNEG_nnUh ;
 INDPRTCONNEGPL3_EttU ;

 CONDPRS_S  ;
 CONDPRSPL3_EttAs ;
 CONDPRT_nUs ;
 CONDPRTPL3_EtAnnUs ;

 POT_N ;
 POTPL3_EtAnneh ;

 IMPRT_kkA ;
 IMPRTPL3_EttAhes ;

 ACT-PRSPRC_OSTAI ;
 PSS-PRSPRC_EttAvU ;
 ACT-PRFPRC_nUH ;
 ACT-PRFPRC_nnUH ;
 PSS-PRFPRC_EttU ;

 M-INF        ;
+Inf:%>%{aäDiph%} K ;



LEXICON V-VV_02
 INDPRSSG1 ;
 INDPRSSG2 ;
 INDPRSSG3 ;
 INDPRSPL1 ;
 INDPRSPL2 ;
 INDPRSPL3_jAh ;
 INDPRSCONNEG ;
 INDPRTSG1     ;
 INDPRTSG2     ;
 INDPRTSG3     ;
 INDPRTPL1     ;
 INDPRTPL2     ;
 INDPRTPL3_dih     ;
 INDPRTCONNEG_nnUh	   ;
 INDPRTCONNEGPL3_DU	   ;
 CONDPRS_S ;
 CONDPRSPL3_DAs ;
 CONDPRT_nnUs ;
 CONDPRTPL3_DAnUs ;
 POT_nnE ;
 POTPL3_DAneh ;
 IMPRT_GA ;
 IMPRTPL3_DAhes ;

 ACT-PRSPRC_AVUAJU ;
 ACT-PRFPRC_nnUH ;
 PSS-PRSPRC_DAvU ;
 PSS-PRFPRC_DU ;

 M-INF	      ;
+Inf:%>j%{aäDiph%} K ;

LEXICON V-VV_JUVVA_02
:%>%{aäDiph%} INDPRSSG1 ;
:%>%{aäDiph%} INDPRSSG2 ;
:%>%{aäDiph%} INDPRSSG3 ;
:%>%{aäDiph%} INDPRSPL1 ;
:%>%{aäDiph%} INDPRSPL2 ;
 INDPRSPL3_vvAh ;
 INDPRSCONNEG ;
 INDPRTSG1     ;
 INDPRTSG2     ;
 INDPRTSG3     ;
 INDPRTPL1     ;
 INDPRTPL2     ;
:%>%{aäDiph%} INDPRTPL3_dih     ;
:%>%{aäDiph%} INDPRTCONNEG_nnUh	   ;
:%>%{aäDiph%} INDPRTCONNEGPL3_DU	   ;
 CONDPRS_S ;
:%>%{aäDiph%} CONDPRSPL3_DAs ;
:%>%{aäDiph%} CONDPRT_nnUs ;
 CONDPRTPL3_DAnUs ;
:%>%{aäDiph%} POT_nnE ;
 POTPL3_DAneh ;
 IMPRT_GA ;
 IMPRTPL3_DAhes ;

 ACT-PRSPRC_AVUAJU ;
:%>%{aäDiph%} ACT-PRFPRC_nnUH ;
 PSS-PRSPRC_DAvU ;
 PSS-PRFPRC_DU ;

:%>%{aäDiph%} M-INF	      ;
+Inf:%>vv%{aäDiph%} K ;


LEXICON V-VV_03 !!= * __@CODE@__ tulla:tul
:%^VOICEe INDPRSSG1 ;
:%^VOICEe INDPRSSG2 ;
:%^VOICE%{oö%} INDPRSSG3 ;
:%^VOICEe INDPRSPL1 ;
:%^VOICEe INDPRSPL2 ;
 INDPRSPL3_dAh ;
:%^VOICEe INDPRSCONNEG_WEAK ;

:%^VOICE INDPRTSG1     ;
:%^VOICE INDPRTSG2     ;
:%^VOICE INDPRTSG3     ;
:%^VOICE INDPRTPL1     ;
:%^VOICE INDPRTPL2     ;
: INDPRTPL3_dih     ;
 INDPRTCONNEG_nUh ;
 INDPRTCONNEGPL3_DU ;

:%^VOICE CONDPRS_IS ;
: CONDPRSPL3_DAs ;
: CONDPRT_nUs ;
: CONDPRTPL3_DAnnUs ;
: POT_N ;
: POTPL3_DAnneh ;
: IMPRT_GA ;
: IMPRTPL3_DAhes ;

:%^VOICEi ACT-PRSPRC_PEZII ;
: ACT-PRFPRC_nUH ;
: PSS-PRSPRC_DAvU ;
: PSS-PRFPRC_DU ;
:%^VOICEe M-INF ;
+Inf:%>d%{aäDiph%}%^WGStem K ;
+Ger+Temp:%>de%^WGStems K ;

LEXICON V-VV_KUULLA !!= * __@CODE@__ kuulla:kuul
:%^VOICEe INDPRSSG1 ;
:%^VOICEe INDPRSSG2 ;
:%^VOICE%{oö%} INDPRSSG3 ;
:%^VOICEe INDPRSPL1 ;
:%^VOICEe INDPRSPL2 ;
 INDPRSPL3_tAh ;
:%^VOICEe INDPRSCONNEG_WEAK ;

:%^VOICE INDPRTSG1     ;
:%^VOICE INDPRTSG2     ;
:%^VOICE INDPRTSG3     ;
:%^VOICE INDPRTPL1     ;
:%^VOICE INDPRTPL2     ;
: INDPRTPL3_tih     ;
 INDPRTCONNEG_nUh ;
 INDPRTCONNEGPL3_tU ;

:%^VOICE CONDPRS_IS ;
: CONDPRSPL3_tAs ;
: CONDPRT_nUs ;
: CONDPRTPL3_tAnnUs ;
: POT_N ;
: POTPL3_tAnneh ;
: IMPRT_KA ;
: IMPRTPL3_tAhes ;

:%^VOICEi ACT-PRSPRC_PEZII ;
: ACT-PRFPRC_nUH ;
: PSS-PRSPRC_tAvU ;
: PSS-PRFPRC_tU ;
:%^VOICEe M-INF ;
+Inf:%>t%{aäDiph%}%^WGStem K ;

LEXICON V-VV_04
:%>%{aäDiph%} INDPRSSG1 ;
:%>%{aäDiph%} INDPRSSG2 ;
:%>%{aäDiph%} INDPRSSG3 ;
:%>%{aäDiph%} INDPRSPL1 ;
:%>%{aäDiph%} INDPRSPL2 ;
 INDPRSPL3_tAh ;
:%>%{aäDiph%} INDPRSCONNEG ;
:%^SyllBound INDPRTSG1     ;
:%^SyllBound INDPRTSG2     ;
:%^SyllBound INDPRTSG3     ;
:%^SyllBound INDPRTPL1     ;
:%^SyllBound INDPRTPL2     ;
 INDPRTPL3_ttih     ; 
 INDPRTCONNEG_nnUh ; !!
 INDPRTCONNEGPL3_ttU ;
:%>%{aäDiph%} CONDPRS_S ;
 CONDPRSPL3_ttAs ;
 CONDPRT_nnUs ;
 CONDPRTPL3_tAnnUs ;

 POT_nnE ;
 POTPL3_tAnneh ;
 IMPRT_WEAK-kkA ;
 IMPRTPL3_ttAhes ;
!:%>%{aäDiph%} ACT-PRSPRC_vU ;
:%>%{aäDiph%} ACT-PRSPRC_AVUAJU ;
 PSS-PRSPRC_ttAvU ;
 ACT-PRFPRC_nnUH ;
 PSS-PRFPRC_ttU ;
+Inf:%^WGStem%>t%{aäDiph%} K ;
!€# kergi%^WGStem%>t%{aäDiph%}
!€0 kerri00tä
:%>%{aäDiph%} M-INF ; 

LEXICON V-VV_05_SUVAIJA !! * @CODE@ suvaija:suvai
:čče INDPRSSG1 ;
:čče INDPRSSG2 ;
:čč%{oö%} INDPRSSG3 ;
:čče INDPRSPL1 ;
:čče INDPRSPL2 ;
 INDPRSPL3_jAh ;
:čče INDPRSCONNEG_WEAK ;
:čč INDPRTSG1     ;
:čč INDPRTSG2     ;
:čč INDPRTSG3     ;
:čč INDPRTPL1     ;
:čč INDPRTPL2     ;
 INDPRTPL3_ttih     ;
 INDPRTCONNEG_nnUh	   ;
 INDPRTCONNEGPL3_ttU	   ;
:čč CONDPRS_IS ;
 CONDPRSPL3_ttAs ;
 CONDPRT_nnUs ;
 CONDPRTPL3_tAnnUs ;
 POT_nnE ;
 POTPL3_tAnneh ;
 IMPRT_kkA ;
 IMPRTPL3_ttAhes ;

:čči ACT-PRSPRC_PEZII ;
 ACT-PRFPRC_nnUH ;
 PSS-PRSPRC_ttAvU ;
 PSS-PRFPRC_ttU ;

:čče M-INF	      ;
+Inf:%>j%{aäDiph%} K ;


LEXICON V-VV_05_EMÄNDÖIJÄ !! * @CODE@ emändöijä:emändöi
:čče INDPRSSG1 ;
:čče INDPRSSG2 ;
:čč%{oö%} INDPRSSG3 ;
:čče INDPRSPL1 ;
:čče INDPRSPL2 ;
 INDPRSPL3_jAh ;
:čče INDPRSCONNEG_WEAK ;
:čč INDPRTSG1     ;
:čč INDPRTSG2     ;
:čč INDPRTSG3     ;
:čč INDPRTPL1     ;
:čč INDPRTPL2     ;
 INDPRTPL3_ttih     ;
 INDPRTCONNEG_nnUh	   ;
 INDPRTCONNEGPL3_ttU	   ;
:čč CONDPRS_IS ;
 CONDPRSPL3_ttAs ;
 CONDPRT_nnUs ;
 CONDPRTPL3_tAnnUs ;
 POT_nnE ;
 POTPL3_tAnneh ;
 IMPRT_kkA ;
 IMPRTPL3_ttAhes ;

:čči ACT-PRSPRC_PEZII ;
 ACT-PRFPRC_nnUH ;
 PSS-PRSPRC_ttAvU ;
 PSS-PRFPRC_ttU ;

:čče M-INF	      ;
+Inf:%>j%{aäDiph%} K ;


LEXICON V-VV_05_MERKITÄ !! * @CODE@ merkitä:merki
:čče INDPRSSG1 ;
:čče INDPRSSG2 ;
:čč%{oö%} INDPRSSG3 ;
:čče INDPRSPL1 ;
:čče INDPRSPL2 ;
 INDPRSPL3_tAh ;
:čče INDPRSCONNEG_WEAK ;
:čč INDPRTSG1     ;
:čč INDPRTSG2     ;
:čč INDPRTSG3     ;
:čč INDPRTPL1     ;
:čč INDPRTPL2     ;
 INDPRTPL3_ttih     ;
 INDPRTCONNEG_nnUh	   ;
 INDPRTCONNEGPL3_ttU	   ;
:čč CONDPRS_IS ;
 CONDPRSPL3_ttAs ;
 CONDPRT_nnUs ;
 CONDPRTPL3_tAnnUs ;
 POT_nnE ;
 POTPL3_tAnneh ;
 IMPRT_kkA ;
 IMPRTPL3_ttAhes ;

:čči ACT-PRSPRC_PEZII ;
 ACT-PRFPRC_nnUH ;
 PSS-PRSPRC_ttAvU ;
 PSS-PRFPRC_ttU ;

:čče M-INF	      ;
+Inf:%>t%{aäDiph%} K ;


LEXICON V-VV_06
:ne INDPRSSG1 ;
:ne INDPRSSG2 ;
:n%{oö%} INDPRSSG3 ;
:ne INDPRSPL1 ;
:ne INDPRSPL2 ;
 INDPRSPL3_jAh ;
:ne INDPRSCONNEG ;
:n INDPRTSG1     ;
:n INDPRTSG2     ;
:n INDPRTSG3     ;
:n INDPRTPL1     ;
:n INDPRTPL2     ;
 INDPRTPL3_tih     ;
 INDPRTCONNEG_nnUh	   ;
 INDPRTCONNEGPL3_ttU	   ;
:n CONDPRS_IS ;
 CONDPRSPL3_ttAs ;
 CONDPRT_nnUs ;
 CONDPRTPL3_tAnnUs ;
 POT_nnE ;
 POTPL3_tAnneh ;
 IMPRT_kkA ;
 IMPRTPL3_ttAhes ;

 ACT-PRSPRC_AVUAJU ;
 ACT-PRFPRC_nnUH ;
 PSS-PRSPRC_ttAvU ;
 PSS-PRFPRC_ttU ;

:ne M-INF	      ;
+Inf:%>t%{aäDiph%} K ;


!! !REFLEXIVE CONJUGATION

LEXICON VR_01
+V:	VR_01-LONGSTEM_ ;
+V:	VR_01-SHORTSTEM_ ;
LEXICON VR_01-LONGSTEM_
 VR_INDPRSSG1 ;
 VR_INDPRSSG2 ;
 VR_INDPRSSG3 ;
 VR_INDPRSPL1 ;
 VR_INDPRSPL2 ;

 VR_INDPRTSG1 ;
 VR_INDPRTSG2 ;
 VR_INDPRTSG3 ;
 VR_INDPRTPL1 ;
 VR_INDPRTPL2 ;

 VR_CONDPRS_IZ ;
 VR_INDPRSCONNEG ;

LEXICON VR_01-SHORTSTEM_
 VR_INDPRSPL3_EtAhes ;

 VR_INDPRTPL3_Ettihes ;
 VR_INDPRTCONNEG_nUhes ;
 VR_INDPRTCONNEGPL3_EttUhes ;

 VR_CONDPRSPL3_EttAzihes ;
 VR_CONDPRT_nUs ;
 VR_CONDPRTPL3_EtAhUzihes ;
 VR_POT_N ;
 VR_POTPL3_EtAnehes ;
 VR_IMPRT_kkA ;
 VR_IMPRTPL3_EttAhes ;

 RCPRFPRC_nUHes ;
+Inf+Rc:%>t%{aäDiph%}kseh K ;

!! HOW WILL THESE WORK
! ACT-PRSPRC_OSTAI ;
! PSS-PRSPRC_EttAvU ;
! ACT-PRFPRC_nUH ;
! PSS-PRFPRC_EttU ;

! M-INF        ;
!+Inf:%>%{aäDiph%} K ;



LEXICON VR_03
+V:%^VOICEe	VR_03-LONGSTEM_ ;
+V:	VR_03-SHORTSTEM_ ;
LEXICON VR_03-LONGSTEM_
 VR_INDPRSSG1 ;
 VR_INDPRSSG2 ;
 VR_INDPRSSG3 ;
 VR_INDPRSPL1 ;
 VR_INDPRSPL2 ;

 VR_INDPRTSG1 ;
 VR_INDPRTSG2 ;
 VR_INDPRTSG3 ;
 VR_INDPRTPL1 ;
 VR_INDPRTPL2 ;

 VR_CONDPRS_IZ ;
 VR_INDPRSCONNEG_WEAK ;

LEXICON VR_03-SHORTSTEM_
 VR_INDPRSPL3_dAhes ;

 VR_INDPRTPL3_tihes ;
 VR_INDPRTCONNEG_nUhes ;
 VR_INDPRTCONNEGPL3_DUhes ;

 VR_CONDPRSPL3_DAzihes ;
 VR_CONDPRT_nUs ;
 VR_CONDPRTPL3_DAhUzihes ;
 VR_POT_N ;
 VR_POTPL3_DAnehes ;
 VR_IMPRT_KA ;
 VR_IMPRTPL3_DAhes ;

 RCPRFPRC_nUHes ;
+Inf+Rc:%>t%{aäDiph%}kseh K ;


!! !!Nonfinites

!! Forthcoming

!! !!Finites

!! !INDICATIVE PRESENT

LEXICON INDPRSSG1
+Ind+Prs+ScSg1:%>n K ;
!+Ind+Prs+ScSg1:%^WGStem%>n K ;
LEXICON INDPRSSG2
+Ind+Prs+ScSg2:%>t K ;
!+Ind+Prs+ScSg2:%^WGStem%>t K ;
LEXICON INDPRSSG3
+Ind+Prs+ScSg3:%>%{uy%} K ;
LEXICON INDPRSPL1
+Ind+Prs+ScPl1:%>mm%{oö%} K ;
!+Ind+Prs+ScPl1:%^WGStem%>mm%{oö%} K ;
LEXICON INDPRSPL2
+Ind+Prs+ScPl2:%>tt%{oö%} K ;
!+Ind+Prs+ScPl2:%^WGStem%>tt%{oö%} K ;
LEXICON INDPRSPL3_jAh
+Ind+Prs+ScPl3:%>j%{aäDiph%}h K ;
+Ind+Prs+ConNeg+ScPl3:%>j%{aäDiph%} K ;
LEXICON INDPRSPL3_hAh
+Ind+Prs+ScPl3:%>h%{aäDiph%}h K ;
+Ind+Prs+ConNeg+ScPl3:%>h%{aäDiph%} K ;
LEXICON INDPRSPL3_vvAh
+Ind+Prs+ScPl3:%>vv%{aäDiph%}h K ;
+Ind+Prs+ConNeg+ScPl3:%>vv%{aäDiph%} K ;
LEXICON INDPRSPL3_dAh
+Ind+Prs+ScPl3:%>d%{aäDiph%}%^WGStemh K ;
+Ind+Prs+ConNeg+ScPl3:%>d%{aäDiph%}%^WGStem K ;
LEXICON INDPRSPL3_tAh
+Ind+Prs+ScPl3:%>t%{aäDiph%}h K ;
+Ind+Prs+ConNeg+ScPl3:%>t%{aäDiph%} K ;
!+Ind+Prs+ScPl3:%^WGStem%>t%{aäDiph%}h K ;
!+Ind+Prs+ConNeg+ScPl3:%^WGStem%>t%{aäDiph%} K ;
LEXICON INDPRSPL3_EtAh
+Ind+Prs+ScPl3:%^E1%>t%{aäDiph%}h K ;
+Ind+Prs+ConNeg+ScPl3:%^E1%>t%{aäDiph%} K ;
!+Ind+Prs+ScPl3:%^WGStem%^E1%>t%{aäDiph%}h K ;
!+Ind+Prs+ConNeg+ScPl3:%^WGStem%^E1%>t%{aäDiph%} K ;
LEXICON INDPRSCONNEG_WEAK
+Ind+Prs+ConNeg: K ;
+Imprt+ScSg2: K ;
+Imprt+ConNeg+ScSg2: K ;
!+Ind+Prs+ConNeg:%^WGStem K ;
!+Imprt+ScSg2:%^WGStem K ;
!+Imprt+ConNeg+ScSg2:%^WGStem K ;
LEXICON INDPRSCONNEG !! * @CODE@ type 04
+Ind+Prs+ConNeg: K ;
+Imprt+ScSg2: K ;
+Imprt+ConNeg+ScSg2: K ;

!! !INDICATIVE PRESENT REFLEXIVE

LEXICON VR_INDPRSSG1
+Ind+Prs+RcSg1:%^WGStem%>mm%{oö%}s K ;
LEXICON VR_INDPRSSG2
+Ind+Prs+RcSg2:%^WGStem%>tt%{oö%}s K ;
LEXICON VR_INDPRSSG3
+Ind+Prs+RcSg3:%^WGStem%>h K ;
+Ind+Prs+RcSg3:%^WGStem%>hes K ;
LEXICON VR_INDPRSPL1
+Ind+Prs+RcPl1:%^WGStem%>mm%{oö%}kseh K ;
LEXICON VR_INDPRSPL2
+Ind+Prs+RcPl2:%^WGStem%>tt%{oö%}kseh K ;
LEXICON VR_INDPRSPL3_jAhes
+Ind+Prs+RcPl3:%>j%{aäDiph%}hes K ;
+Ind+Prs+ConNeg+RcPl3:%>j%{aäDiph%}hes K ;
LEXICON VR_INDPRSPL3_dAhes
+Ind+Prs+RcPl3:%>d%{aäDiph%}%^WGStemhes K ;
+Ind+Prs+ConNeg+RcPl3:%>d%{aäDiph%}%^WGStemhes K ;
!LEXICON VR_INDPRSPL3_tAhes
!+Ind+Prs+RcPl3:%^WGStem%>t%{aäDiph%}hes K ;
!+Ind+Prs+ConNeg+RcPl3:%^WGStem%>t%{aäDiph%}hes K ;
LEXICON VR_INDPRSPL3_EtAhes
+Ind+Prs+RcPl3:%^WGStem%^E1%>t%{aäDiph%}hes K ;
+Ind+Prs+ConNeg+RcPl3:%^WGStem%^E1%>t%{aäDiph%}hes K ;

LEXICON VR_INDPRSCONNEG_WEAK
+Ind+Prs+ConNeg+RcSg:%^WGStemi K ;
+Imprt+RcSg2:%^WGStemi K ;
+Imprt+ConNeg+RcSg2:%^WGStemi K ;
+Ind+Prs+ConNeg+RcPl1:%^WGStemi K ;
+Ind+Prs+ConNeg+RcPl2:%^WGStemi K ;

LEXICON VR_INDPRSCONNEG !! * @CODE@ 
+Ind+Prs+ConNeg+RcSg:i K ;
+Imprt+RcSg2:i K ;
+Imprt+ConNeg+RcSg2:i K ;
+Ind+Prs+ConNeg+RcPl1:i K ;
+Ind+Prs+ConNeg+RcPl2:i K ;

!! !INDICATIVE PRETERITE

LEXICON INDPRTSG1
+Ind+Prt+ScSg1:%>i%>n K ;
!+Ind+Prt+ScSg1:%>i%^WGStem%>n K ;
LEXICON INDPRTSG2
+Ind+Prt+ScSg2:%>i%>t K ;
!+Ind+Prt+ScSg2:%>i%^WGStem%>t K ;
LEXICON INDPRTSG3
+Ind+Prt+ScSg3:%>i K ;
LEXICON INDPRTPL1
+Ind+Prt+ScPl1:%>i%>mm%{oö%} K ;
!+Ind+Prt+ScPl1:%>i%^WGStem%>mm%{oö%} K ;
LEXICON INDPRTPL2
+Ind+Prt+ScPl2:%>i%>tt%{oö%} K ;
!+Ind+Prt+ScPl2:%>i%^WGStem%>tt%{oö%} K ;
LEXICON INDPRTPL3_dih
+Ind+Prt+ScPl3:%>dih K ;
LEXICON INDPRTPL3_tih
+Ind+Prt+ScPl3:%>tih K ;
!+Ind+Prt+ScPl3:%^WGStem%>tih K ;
!LEXICON INDPRTPL3_Etih
!+Ind+Prt+ScPl3:%^WGStem%^E1%>tih K ;
LEXICON INDPRTPL3_ttih
+Ind+Prt+ScPl3:%>ttih K ;
!+Ind+Prt+ScPl3:%^WGStem%>ttih K ;
LEXICON INDPRTPL3_Ettih
+Ind+Prt+ScPl3:%^E1%>ttih K ;
!+Ind+Prt+ScPl3:%^WGStem%^E1%>ttih K ;
!LEXICON INDPRTSGCONNEG
!+Ind+Prt+ConNeg+Sg:%^WGStem K ;
LEXICON INDPRTCONNEG_nUh
+Ind+Prt+ConNeg:%>n%{uy%}h K ;
LEXICON INDPRTCONNEG_nnUh
+Ind+Prt+ConNeg:%>nn%{uy%}h K ;
!+Ind+Prt+ConNeg:%^WGStem%>nn%{uy%}h K ;
LEXICON INDPRTCONNEGPL3_DU
+Ind+Prt+ConNeg+ScPl3:%>d%{uy%} K ;
LEXICON INDPRTCONNEGPL3_tU
+Ind+Prt+ConNeg+ScPl3:%>t%{uy%} K ;
LEXICON INDPRTCONNEGPL3_ttU
+Ind+Prt+ConNeg+ScPl3:%>tt%{uy%} K ;
!+Ind+Prt+ConNeg+ScPl3:%^WGStem%>tt%{uy%} K ;
LEXICON INDPRTCONNEGPL3_EttU
+Ind+Prt+ConNeg+ScPl3:%^E1%>tt%{uy%} K ;
!+Ind+Prt+ConNeg+ScPl3:%^WGStem%^E1%>tt%{uy%} K ;

!! !INDICATIVE PRETERITE REFLEXIVE

LEXICON VR_INDPRTSG1
+Ind+Prt+RcSg1:%>i%^WGStem%>mm%{oö%}s K ;
LEXICON VR_INDPRTSG2
+Ind+Prt+RcSg2:%>i%^WGStem%>tt%{oö%}s K ;
LEXICON VR_INDPRTSG3
+Ind+Prt+RcSg3:%>i%>h K ;
+Ind+Prt+RcSg3:%>i%>hes K ;
LEXICON VR_INDPRTPL1
+Ind+Prt+RcPl1:%>i%^WGStem%>mm%{oö%}kseh K ;
LEXICON VR_INDPRTPL2
+Ind+Prt+RcPl2:%>i%^WGStem%>tt%{oö%}kseh K ;
LEXICON VR_INDPRTPL3_dihes
+Ind+Prt+RcPl3:%>dihes K ;
LEXICON VR_INDPRTPL3_tihes
+Ind+Prt+RcPl3:%^WGStem%>tihes K ;
!LEXICON VR_INDPRTPL3_Etihes
!+Ind+Prt+RcPl3:%^WGStem%^E1%>tihes K ;
!LEXICON VR_INDPRTPL3_ttihes
!+Ind+Prt+RcPl3:%^WGStem%>ttihes K ;
LEXICON VR_INDPRTPL3_Ettihes
+Ind+Prt+RcPl3:%^WGStem%^E1%>ttihes K ;
!LEXICON VR_INDPRTSGCONNEG
!+Ind+Prt+ConNeg+Sg:%^WGStem K ;
LEXICON VR_INDPRTCONNEG_nUhes
+Ind+Prt+ConNeg+RcSg:%>n%{uy%}%^WGStemhes K ;
+Ind+Prt+ConNeg+RcPl1:%>n%{uy%}%^WGStemhes K ;
+Ind+Prt+ConNeg+RcPl2:%>n%{uy%}%^WGStemhes K ;
LEXICON VR_INDPRTCONNEG_nnUhes
+Ind+Prt+ConNeg:%^WGStem%>nn%{uy%}hes K ;
LEXICON VR_INDPRTCONNEGPL3_DUhes
+Ind+Prt+ConNeg+RcPl3:%>d%{uy%}hes K ;
!LEXICON VR_INDPRTCONNEGPL3_ttUhes
!+Ind+Prt+ConNeg+RcPl3:%^WGStem%>tt%{uy%}hes K ;
LEXICON VR_INDPRTCONNEGPL3_EttUhes
+Ind+Prt+ConNeg+RcPl3:%^WGStem%^E1%>tt%{uy%}hes K ;


!! !!Conditional
LEXICON CONDPRS_IS
+Cond+Prs+ScSg1:%>izin K ;
+Cond+Prs+ScSg2:%>izit K ;
+Cond+Prs+ScSg3:%>is K ;
+Cond+Prs+ScSg3:%>iz K ;
+Cond+Prs+ScPl1:%>izimm%{oö%} K ;
+Cond+Prs+ScPl2:%>izitt%{oö%} K ;
+Cond+Prs+ConNeg:%>is K ;

LEXICON CONDPRS_S
+Cond+Prs+ScSg1:%>zin K ;
+Cond+Prs+ScSg2:%>zit K ;
+Cond+Prs+ScSg3:%>s K ;
+Cond+Prs+ScSg3:%>z K ;
+Cond+Prs+ScPl1:%>zimm%{oö%} K ;
+Cond+Prs+ScPl2:%>zitt%{oö%} K ;
+Cond+Prs+ConNeg:%>s K ;

LEXICON CONDPRSPL3
+Cond+Prs+ScPl3:%{aäDiph%}s K ;
+Cond+Prs+ConNeg+ScPl3:%{aäDiph%}s K ;

LEXICON CONDPRSPL3_DAs
+Cond+Prs+ScPl3:%>d%{aäDiph%}s K ;
+Cond+Prs+ConNeg+ScPl3:%>d%{aäDiph%}s K ;

LEXICON CONDPRSPL3_tAs
+Cond+Prs+ScPl3:%>t%{aäDiph%}s K ;
+Cond+Prs+ConNeg+ScPl3:%>t%{aäDiph%}s K ;

LEXICON CONDPRSPL3_ttAs
+Cond+Prs+ScPl3:%>tt%{aäDiph%}s K ;
+Cond+Prs+ConNeg+ScPl3:%>tt%{aäDiph%}s K ;
!+Cond+Prs+ScPl3:%^WGStem%>tt%{aäDiph%}s K ;
!+Cond+Prs+ConNeg+ScPl3:%^WGStem%>tt%{aäDiph%}s K ;

LEXICON CONDPRSPL3_EttAs
+Cond+Prs+ScPl3:%^E1%>tt%{aäDiph%}s K ;
+Cond+Prs+ConNeg+ScPl3:%^E1%>tt%{aäDiph%}s K ;
!+Cond+Prs+ScPl3:%^WGStem%^E1%>tt%{aäDiph%}s K ;
!+Cond+Prs+ConNeg+ScPl3:%^WGStem%^E1%>tt%{aäDiph%}s K ;

LEXICON VR_CONDPRS_IZ
+Cond+Prs+RcSg1:%>i%>zimm%{oö%}s K ;
+Cond+Prs+RcSg2:%>izitt%{oö%}s K ;
+Cond+Prs+RcSg3:%>izihes K ;
+Cond+Prs+RcPl1:%>izimm%{oö%}kseh K ;
+Cond+Prs+RcPl2:%>izitt%{oö%}kseh K ;
+Cond+Prs+ConNeg+RcSg:%>izihes K ;
+Cond+Prs+ConNeg+RcPl1:%>izihes K ;
+Cond+Prs+ConNeg+RcPl2:%>izihes K ;

LEXICON VR_CONDPRSPL3_DAzihes
+Cond+Prs+RcPl3:%>d%{aäDiph%}zihes K ;
+Cond+Prs+ConNeg+RcPl3:%>d%{aäDiph%}zihes K ;

LEXICON VR_CONDPRSPL3_EttAzihes
+Cond+Prs+RcPl3:%^WGStem%^E1%>tt%{aäDiph%}zihes K ;
+Cond+Prs+ConNeg+RcPl3:%^WGStem%^E1%>tt%{aäDiph%}zihes K ;

!! !CONDITIONAL PRETERITE
LEXICON CONDPRT
+Cond+Prt+ScSg1:%>zin K ;
+Cond+Prt+ScSg2:%>zit K ;
+Cond+Prt+ScSg3:%>s K ;
+Cond+Prt+ScSg3:%>z K ;
+Cond+Prt+ScPl1:%>zimm%{oö%} K ;
+Cond+Prt+ScPl2:%>zitt%{oö%} K ;

LEXICON CONDPRT_nUs
+Cond+Prt+ScSg1:%>n%{uy%}%^WGStem%>zin K ;
+Cond+Prt+ScSg2:%>n%{uy%}%^WGStem%>zit K ;
+Cond+Prt+ScSg3:%>n%{uy%}%^WGStem%>s K ;
+Cond+Prt+ScSg3:%>n%{uy%}%^WGStem%>z K ;
+Cond+Prt+ScPl1:%>n%{uy%}%^WGStem%>zimm%{oö%} K ;
+Cond+Prt+ScPl2:%>n%{uy%}%^WGStem%>zitt%{oö%} K ;
+Cond+Prt+ConNeg:%>n%{uy%}%^WGStem%>s K ;

LEXICON CONDPRT_nnUs
+Cond+Prt+ScSg1:%^WGStem%>nn%{uy%}%>zin K ;
+Cond+Prt+ScSg2:%^WGStem%>nn%{uy%}%>zit K ;
+Cond+Prt+ScSg3:%^WGStem%>nn%{uy%}%>s K ;
+Cond+Prt+ScSg3:%^WGStem%>nn%{uy%}%>z K ;
+Cond+Prt+ScPl1:%^WGStem%>nn%{uy%}%>zimm%{oö%} K ;
+Cond+Prt+ScPl2:%^WGStem%>nn%{uy%}%>zitt%{oö%} K ;
+Cond+Prt+ConNeg:%^WGStem%>nn%{uy%}%>s K ;

LEXICON CONDPRTPL3_DAnUs
+Cond+Prt+ScPl3:%>d%{aäDiph%}n%{uy%}s K ;
+Cond+Prt+ConNeg+ScPl3:%>d%{aäDiph%}n%{uy%}s K ;

LEXICON CONDPRTPL3_DAnnUs
+Cond+Prt+ScPl3:%>d%{aäDiph%}nn%{uy%}s K ;
+Cond+Prt+ConNeg+ScPl3:%>d%{aäDiph%}nn%{uy%}s K ;

LEXICON CONDPRTPL3_tAnnUs
+Cond+Prt+ScPl3:%^WGStem%>t%{aäDiph%}nn%{uy%}s K ;
+Cond+Prt+ConNeg+ScPl3:%^WGStem%>t%{aäDiph%}nn%{uy%}s K ;

LEXICON CONDPRTPL3_EtAnnUs
+Cond+Prt+ScPl3:%^WGStem%^E1%>t%{aäDiph%}nn%{uy%}s K ;
+Cond+Prt+ConNeg+ScPl3:%^WGStem%^E1%>t%{aäDiph%}nn%{uy%}s K ;

LEXICON VR_CONDPRT_nUs
+Cond+Prt+RcSg1:%>n%{uy%}%^WGStem%>zimm%{oö%}s K ;
+Cond+Prt+RcSg2:%>n%{uy%}%^WGStem%>zitt%{oö%}s K ;
+Cond+Prt+RcSg3:%>n%{uy%}%^WGStem%>zihes K ;
+Cond+Prt+RcPl1:%>n%{uy%}%^WGStem%>zimm%{oö%}kseh K ;
+Cond+Prt+RcPl2:%>n%{uy%}%^WGStem%>zitt%{oö%}kseh K ;
+Cond+Prt+ConNeg+RcSg:%>n%{uy%}%^WGStem%>zihes K ;
+Cond+Prt+ConNeg+RcPl1:%>n%{uy%}%^WGStem%>zihes K ;
+Cond+Prt+ConNeg+RcPl2:%>n%{uy%}%^WGStem%>zihes K ;

LEXICON VR_CONDPRTPL3_DAhUzihes
+Cond+Prt+RcPl3:%>d%{aäDiph%}h%{uy%}zihes K ;
+Cond+Prt+ConNeg+RcPl3:%>d%{aäDiph%}h%{uy%}zihes K ;

LEXICON VR_CONDPRTPL3_EtAhUzihes
+Cond+Prt+ScPl3:%^WGStem%^E1%>t%{aäDiph%}h%{uy%}zihes K ;
+Cond+Prt+ConNeg+ScPl3:%^WGStem%^E1%>t%{aäDiph%}h%{uy%}zihes K ;



LEXICON POT_N
+Pot+ScSg1:%>n%^WGStemen K ;
+Pot+ScSg2:%>n%^WGStemet K ;
+Pot+ScSg3:%>n%^WGStem%{oö%}%{uy%} K ;
+Pot+ScPl1:%>n%^WGStememm%{oö%} K ;
+Pot+ScPl2:%>n%^WGStemett%{oö%} K ;
+Pot+ConNeg:%>n%^WGSteme K ;

LEXICON POT_nnE
+Pot+ScSg1:%^WGStem%>nnen K ;
+Pot+ScSg2:%^WGStem%>nnet K ;
+Pot+ScSg3:%^WGStem%>nn%{oö%}%{uy%} K ;
+Pot+ScPl1:%^WGStem%>nnemm%{oö%} K ;
+Pot+ScPl2:%^WGStem%>nnett%{oö%} K ;
+Pot+ConNeg:%^WGStem%>nne K ;

LEXICON POTPL3_DAnneh
+Pot+ScPl3:%>d%{aäDiph%}nneh K ;
+Pot+ConNeg+ScPl3:%>d%{aäDiph%}nne K ;

LEXICON POTPL3_DAneh
+Pot+ScPl3:%>d%{aäDiph%}%>neh K ;
+Pot+ConNeg+ScPl3:%>d%{aäDiph%}%>ne K ;

LEXICON POTPL3_tAneh
+Pot+ScPl3:%^WGStem%>t%{aäDiph%}neh K ;
+Pot+ConNeg+ScPl3:%^WGStem%>t%{aäDiph%}ne K ;

LEXICON POTPL3_tAnneh
+Pot+ScPl3:%^WGStem%>t%{aäDiph%}nneh K ;
+Pot+ConNeg+ScPl3:%^WGStem%>t%{aäDiph%}nne K ;

LEXICON POTPL3_EtAnneh
+Pot+ScPl3:%^WGStem%^E1%>t%{aäDiph%}nneh K ;
+Pot+ConNeg+ScPl3:%^WGStem%^E1%>t%{aäDiph%}nne K ;

LEXICON VR_POT_N
+Pot+RcSg1:%>n%^WGStememm%{oö%}s K ;
+Pot+RcSg2:%>n%^WGStemett%{oö%}s K ;
+Pot+RcSg3:%>n%^WGStemehes K ;
+Pot+RcPl1:%>n%^WGStememm%{oö%}kseh K ;
+Pot+RcPl2:%>n%^WGStemett%{oö%}kseh K ;
+Pot+ConNeg+RcSg:%>n%^WGStemehes K ;
+Pot+ConNeg+RcPl1:%>n%^WGStemehes K ;
+Pot+ConNeg+RcPl2:%>n%^WGStemehes K ;

LEXICON VR_POTPL3_DAnehes
+Pot+RcPl3:%>d%{aäDiph%}%>hes K ;
+Pot+RcPl3:%>d%{aäDiph%}%>nehes K ; !!= * __@CODE@__ Shouldn't THIS be the right form
+Pot+ConNeg+RcPl3:%>d%{aäDiph%}%>nehes K ;

LEXICON VR_POTPL3_EtAnehes
+Pot+RcPl3:%^WGStem%^E1%>t%{aäDiph%}nehes K ;
+Pot+ConNeg+RcPl3:%^WGStem%^E1%>t%{aäDiph%}nehes K ;

!! !!IMPERATIVE
LEXICON IMPRT_GA
+Imprt+ScSg3:%>g%{aäDiph%}h K ;
+Imprt+ScPl1:%>g%{ui%}%{aäDiph%}mm%{oö%} K ;
+Imprt+ScPl2:%>g%{ui%}%{aäDiph%} K ;
+Imprt+ScPl2:%>g%{ui%}%{aäDiph%}tt%{oö%} K ;

+Imprt+ConNeg+ScSg3:%>g%{aäDiph%}h K ;
+Imprt+ConNeg+ScPl1:%>g%{ui%}%{aäDiph%}mm%{oö%} K ;
+Imprt+ConNeg+ScPl2:%>g%{ui%}%{aäDiph%} K ;
+Imprt+ConNeg+ScPl2:%>g%{ui%}%{aäDiph%}tt%{oö%} K ;

LEXICON IMPRT_KA
+Imprt+ScSg3:%>k%{aäDiph%}h K ;
+Imprt+ScPl1:%>k%{ui%}%{aäDiph%}mm%{oö%} K ;
+Imprt+ScPl2:%>k%{ui%}%{aäDiph%} K ;
+Imprt+ScPl2:%>k%{ui%}%{aäDiph%}tt%{oö%} K ;

+Imprt+ConNeg+ScSg3:%>k%{aäDiph%}h K ;
+Imprt+ConNeg+ScPl1:%>k%{ui%}%{aäDiph%}mm%{oö%} K ;
+Imprt+ConNeg+ScPl2:%>k%{ui%}%{aäDiph%} K ;
+Imprt+ConNeg+ScPl2:%>k%{ui%}%{aäDiph%}tt%{oö%} K ;

LEXICON IMPRT_kkA
+Imprt+ScSg3:%>kk%{aäDiph%}h K ;
+Imprt+ScPl1:%>kk%{ui%}%{aäDiph%}mm%{oö%} K ;
+Imprt+ScPl2:%>kk%{ui%}%{aäDiph%} K ;
+Imprt+ScPl2:%>kk%{ui%}%{aäDiph%}tt%{oö%} K ;

+Imprt+ConNeg+ScSg3:%>kk%{aäDiph%}h K ;
+Imprt+ConNeg+ScPl1:%>kk%{ui%}%{aäDiph%}mm%{oö%} K ;
+Imprt+ConNeg+ScPl2:%>kk%{ui%}%{aäDiph%} K ;
+Imprt+ConNeg+ScPl2:%>kk%{ui%}%{aäDiph%}tt%{oö%} K ;

LEXICON IMPRT_WEAK-kkA
+Imprt+ScSg3:%^WGStem%>kk%{aäDiph%}h K ;
+Imprt+ScPl1:%^WGStem%>kk%{ui%}%{aäDiph%}mm%{oö%} K ;
+Imprt+ScPl2:%^WGStem%>kk%{ui%}%{aäDiph%} K ;
+Imprt+ScPl2:%^WGStem%>kk%{ui%}%{aäDiph%}tt%{oö%} K ;

+Imprt+ConNeg+ScSg3:%^WGStem%>kk%{aäDiph%}h K ;
+Imprt+ConNeg+ScPl1:%^WGStem%>kk%{ui%}%{aäDiph%}mm%{oö%} K ;
+Imprt+ConNeg+ScPl2:%^WGStem%>kk%{ui%}%{aäDiph%} K ;
+Imprt+ConNeg+ScPl2:%^WGStem%>kk%{ui%}%{aäDiph%}tt%{oö%} K ;

LEXICON IMPRTPL3_DAhes
+Imprt+ScPl3:%>d%{aäDiph%}hes K ;
+Imprt+ConNeg+ScPl3:%>d%{aäDiph%}hes K ;

LEXICON IMPRTPL3_tAhes
+Imprt+ScPl3:%>t%{aäDiph%}hes K ;
+Imprt+ConNeg+ScPl3:%>t%{aäDiph%}hes K ;

LEXICON IMPRTPL3_ttAhes
+Imprt+ScPl3:%^WGStem%>tt%{aäDiph%}hes K ;
+Imprt+ConNeg+ScPl3:%^WGStem%>tt%{aäDiph%}hes K ;

LEXICON IMPRTPL3_EttAhes
+Imprt+ScPl3:%^WGStem%^E1%>tt%{aäDiph%}hes K ;
+Imprt+ConNeg+ScPl3:%^WGStem%^E1%>tt%{aäDiph%}hes K ;

!! !IMPERATIVE REFLEXIVE
LEXICON VR_IMPRT_GA
+Imprt+RcSg3:%>g%{aäDiph%}hes K ;
+Imprt+RcPl1:%>g%{ui%}%{aäDiph%}mm%{oö%}kseh K ;
+Imprt+RcPl2:%>g%{ui%}%{aäDiph%}tt%{oö%}kseh K ;

+Imprt+ConNeg+RcSg3:%>g%{aäDiph%}hes K ;
+Imprt+ConNeg+RcPl1:%>g%{ui%}%{aäDiph%}mm%{oö%}kseh K ;
+Imprt+ConNeg+RcPl2:%>g%{ui%}%{aäDiph%}tt%{oö%}kseh K ;

LEXICON VR_IMPRT_kkA
+Imprt+RcSg3:%>kk%{aäDiph%}hes K ;
+Imprt+RcPl1:%>kk%{ui%}%{aäDiph%}mm%{oö%}kseh K ;
+Imprt+RcPl2:%>kk%{ui%}%{aäDiph%}tt%{oö%}kseh K ;

+Imprt+ConNeg+RcSg3:%>kk%{aäDiph%}hes K ;
+Imprt+ConNeg+RcPl1:%>kk%{ui%}%{aäDiph%}mm%{oö%}kseh K ;
+Imprt+ConNeg+RcPl2:%>kk%{ui%}%{aäDiph%}tt%{oö%}kseh K ;

!LEXICON VR_IMPRT_WEAK-kkA
!+Imprt+RcSg3:%^WGStem%>kk%{aäDiph%}hes K ;
!+Imprt+RcPl1:%^WGStem%>kk%{ui%}%{aäDiph%}mm%{oö%}kseh K ;
!+Imprt+RcPl2:%^WGStem%>kk%{ui%}%{aäDiph%}tt%{oö%}kseh K ;

!+Imprt+ConNeg+RcSg3:%^WGStem%>kk%{aäDiph%}hes K ;
!+Imprt+ConNeg+RcPl1:%^WGStem%>kk%{ui%}%{aäDiph%}mm%{oö%}kseh K ;
!+Imprt+ConNeg+RcPl2:%^WGStem%>kk%{ui%}%{aäDiph%}tt%{oö%}kseh K ;

LEXICON VR_IMPRT_KA
+Imprt+RcSg3:%>k%{aäDiph%}hes K ;
+Imprt+RcPl1:%>k%{ui%}%{aäDiph%}mm%{oö%}kseh K ;
+Imprt+RcPl2:%>k%{ui%}%{aäDiph%}tt%{oö%}kseh K ;

+Imprt+ConNeg+RcSg3:%>k%{aäDiph%}hes K ;
+Imprt+ConNeg+RcPl1:%>k%{ui%}%{aäDiph%}mm%{oö%}kseh K ;
+Imprt+ConNeg+RcPl2:%>k%{ui%}%{aäDiph%}tt%{oö%}kseh K ;

LEXICON VR_IMPRTPL3_DAhes
+Imprt+RcPl3:%>d%{aäDiph%}hes K ;
+Imprt+ConNeg+RcPl3:%>d%{aäDiph%}hes K ;

!LEXICON VR_IMPRTPL3_ttAhes
!+Imprt+RcPl3:%^WGStem%>tt%{aäDiph%}hes K ;
!+Imprt+ConNeg+RcPl3:%^WGStem%>tt%{aäDiph%}hes K ;

LEXICON VR_IMPRTPL3_EttAhes
+Imprt+RcPl3:%^WGStem%^E1%>tt%{aäDiph%}hes K ;
+Imprt+ConNeg+RcPl3:%^WGStem%^E1%>tt%{aäDiph%}hes K ;



!! ... and next chapter

!LEXICON  ACT-PRSPRC_vU
!+Act+PrsPrc:%>v%{uy%} K ;
!+Act+PrsPrc:%>v%{aäPar%} NMN_PÄIVY ;

LEXICON  ACT-PRSPRC_IU
+Act+PrsPrc:%>%{ijPRC%} NMN_OSTAI ;

LEXICON  ACT-PRSPRC_OSTAI
+Act+PrsPrc:%>%{ijPRC%} NMN_OSTAI ;

LEXICON  ACT-PRSPRC_PEZII
+Act+PrsPrc:%>%{ijPRC%} NMN_PEZII ;

LEXICON  ACT-PRSPRC_AVUAJU
+Act+PrsPrc:%>%{ijPRC%}%{aäPar%} NMN_PÄIVY ;

LEXICON  PSS-PRSPRC_DAvU
+Pss+PrsPrc:%>d%{aäDiph%}v%{uy%} K ;
+Pss+PrsPrc:%>d%{aäDiph%}v%{aäPar%} NMN_PÄIVY        ;

LEXICON  PSS-PRSPRC_tAvU
+Pss+PrsPrc:%>t%{aäDiph%}v%{uy%} K ;
+Pss+PrsPrc:%>t%{aäDiph%}v%{aäPar%} NMN_PÄIVY        ;

!LEXICON  PSS-PRSPRC_EtAvU
!+Pss+PrsPrc:%^WGStem%^E1%>t%{aäDiph%}v%{uy%} K ;
!+Pss+PrsPrc:%^WGStem%^E1%>t%{aäDiph%}v%{aäPar%} NMN_PÄIVY        ;

LEXICON  PSS-PRSPRC_ttAvU
+Pss+PrsPrc:%^WGStem%>tt%{aäDiph%}v%{uy%} K	;
+Pss+PrsPrc:%^WGStem%>tt%{aäDiph%}v%{aäPar%} NMN_PÄIVY	;

LEXICON  PSS-PRSPRC_EttAvU
+Pss+PrsPrc:%^WGStem%^E1%>tt%{aäDiph%}v%{uy%} K	;
+Pss+PrsPrc:%^WGStem%^E1%>tt%{aäDiph%}v%{aäPar%} NMN_PÄIVY	;

LEXICON ACT-PRFPRC_nUH
+Act+PrfPrc:%>n%{uy%}h K ;
+Act+PrfPrc+Err/Orth:%>n%{uy%}t K ;
+Act+PrfPrc:%>n%{uy%} NMN_PESSYH ;

LEXICON ACT-PRFPRC_nnUH
+Act+PrfPrc:%^WGStem%>nn%{uy%}h K ;
+Act+PrfPrc+Err/Orth:%^WGStem%>nn%{uy%}h K ;
+Act+PrfPrc:%^WGStem%>nn%{uy%} NMN_PESSYH ;

LEXICON  PSS-PRFPRC_DU
+Pss+PrfPrc:%>d%{uy%} K ;
+Pss+PrfPrc:%>d%{uy%} A_KOIVU ;

LEXICON  PSS-PRFPRC_tU
+Pss+PrfPrc:%>t%{uy%} K ;
+Pss+PrfPrc:%>t%{uy%} A_KOIVU ;

LEXICON  PSS-PRFPRC_ttU
+Pss+PrfPrc:%^WGStem%>tt%{uy%} K ;
+Pss+PrfPrc:%^WGStem%>tt%{uy%} A_KOIVU ;

LEXICON  PSS-PRFPRC_EttU
+Pss+PrfPrc:%^WGStem%^E1%>tt%{uy%} K ;
+Pss+PrfPrc:%^WGStem%^E1%>tt%{uy%} A_KOIVU ;

!!
LEXICON RCPRFPRC_nUHes
+RcPrfPrc:%>n%{uy%}hes K ;
!+RcPrfPrc+Err/Orth:%>n%{uy%}tes K ;

!LEXICON RCPRFPRC_nnUHes
!+RcPrfPrc:%^WGStem%>nn%{uy%}hes K ;
!+RcPrfPrc+Err/Orth:%^WGStem%>nn%{uy%}tes K ;

LEXICON  RCPRFPRC_DUhes
+RcPrfPrc+Pl3:%>d%{uy%}hes K ;

!LEXICON  RCPRFPRC_ttUhes
!+RcPrfPrc+Pl3:%^WGStem%>tt%{uy%} K ;

!LEXICON  RCPRFPRC_EttUhes
!+RcPrfPrc+Pl3:%^WGStem%^E1%>tt%{uy%} K ;

LEXICON M-INF
+Der/mine:%>mi NMN_KIELINE ;
+Der/mA+Inf+Abe:%>m%{aäDiph%}%>tt%{aäDiph%}h K ;
+Der/mA+Inf+Abe+Err/Orth:%>m%{aäDiph%}%>tt%{aäDiph%} K ;
+Der/mA+Inf+Abe:%>m%{aäDiph%}%>t%{aäDiph%} K ;
+Der/mA+Inf+Ade:%>m%{aäDiph%}l K ;
+Der/mA+Inf+Ela:%>m%{aäDiph%}späi K ;
+Der/mA+Inf+Ill:%>m%{aäDiph%}h K ;
+Der/mA+Inf+Ine:%>m%{aäDiph%}s K ;
+Der/mA+Inf+Par:%>m%{ui%}%{aäDiph%} K ;

LEXICON M-INF_BACK
+Der/mine:%>mi NMN_NAINE ;
+Der/mA+Inf+Abe:%>ma%>ttah K ;
+Der/mA+Inf+Abe+Err/Orth:%>ma%>tta K ;
+Der/mA+Inf+Abe:%>ma%>ta K ;
+Der/mA+Inf+Ade:%>mal K ;
+Der/mA+Inf+Ela:%>maspäi K ;
+Der/mA+Inf+Ill:%>mah K ;
+Der/mA+Inf+Ine:%>mas K ;
+Der/mA+Inf+Par:%>mua K ;

LEXICON M-INF_FRONT
+Der/mine:%>mi NMN_KIELINE ;
+Der/mA+Inf+Abe:%>mä%>ttäh K ;
+Der/mA+Inf+Abe+Err/Orth:%>mä%>ttä K ;
+Der/mA+Inf+Abe:%>mä%>tä K ;
+Der/mA+Inf+Ade:%>mäl K ;
+Der/mA+Inf+Ela:%>mäspäi K ;
+Der/mA+Inf+Ill:%>mäh K ;
+Der/mA+Inf+Ine:%>mäs K ;
+Der/mA+Inf+Par:%>miä K ;

LEXICON GER_jen
+Ger+Manner:%>jen K ;
LEXICON GER_ten
+Ger+Manner:%>ten K ;

LEXICON GER-TEMP_jes
+Ger+Temp:%>jes K ;
LEXICON GER-TEMP_tes
+Ger+Temp:%>tes K ;



! vim: set ft=xfst-lexc:

