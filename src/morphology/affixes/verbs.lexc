!! Verb inflection
!! ---------------
!! Verbs in the Livvi (Olonets) Karelian language inflect for mood, number and persons.


LEXICON VTag
+V: K ;

LEXICON V-NEG
!ei:e
+V+Neg+Sg3:i # ;

LEXICON V-olla
!olla:o
+V+Ind+Prs+Sg1:le%^WEAK%>n K ;
+V+Ind+Prs+Sg2:le%^WEAK%>t K ;
+V+Ind+Prs+Sg3:n K ;
+V+Ind+Prs+Pl1:le%^WEAK%>mmO1 K ;
+V+Ind+Prs+Pl2:le%^WEAK%>ttO1 K ;
+V+Ind+Prs+Pl3:l%>dA1h K ;
+V+Ind+Prs+ConNeg:le%^WEAK K ;

+V+Ind+Prt+Sg1:l%^WEAK%>in K ;
+V+Ind+Prt+Sg2:l%^WEAK%>it K ;
+V+Ind+Prt+Sg3:l%>i K ;
+V+Ind+Prt+Pl1:l%^WEAK%>immO1 K ;
+V+Ind+Prt+Pl2:l%^WEAK%>ittO1 K ;
+V+Ind+Prt+Pl3:l%>tih K ;
+V+Ind+Prt+ConNeg:l%>nU1h K ;
+V+Ind+Prt+ConNeg+Pl3:l%>tU1 K ;

LEXICON V-kandua
!Verbs of the Finnish type 1
!V1a
!kandua:kanda
+V: V-VV_01 ;

LEXICON V-punuo
!V1o
!punuo:puno
+V: V-VV_01 ;

LEXICON V-valuo
!V1u
!valuo:valuo
+V: V-VV_01 ;


LEXICON V-sanuo 
+V: V-VV_01 ; ! Trond

!V1i
LEXICON V-potkie
!potkie:potki
+V: K ;

LEXICON V-VV_01
! V1
! This verb type has two final vowels in the first infinitive
+V: INDPRSSG1 ;
+V: INDPRSSG2 ;
+V: INDPRSSG3 ;
+V: INDPRSPL1 ;
+V: INDPRSPL2 ;
+V:%> INDPRSPL3_tAh ;
+V: INDCONNEG ;

+Ind+Prt+Sg1:%^WEAK%>in K ;
+Ind+Prt+Sg2:%^WEAK%>it K ;
+Ind+Prt+Sg3:%>i K ;
+Ind+Prt+Pl1:%^WEAK%>immO1 K ;
+Ind+Prt+Pl2:%^WEAK%>ittO1 K ;
+Ind+Prt+Pl3:%^WEAK%^E1%>ttih K ;
+Ind+Prt+ConNeg:%>nU1h K ;
+Ind+Prt+ConNeg+Pl3:%^WEAK%^E1%>ttU1 K ;


LEXICON V-tulla
!Verbs of the Finnish type 3
!tulla:tul
+V:e INDPRSSG1 ;
+V:e INDPRSSG2 ;
+V:e INDPRSSG3 ;
+V:e INDPRSPL1 ;
+V:e INDPRSPL2 ;
+V:%> INDPRSPL3_dAh ;
+V:e INDCONNEG ;

LEXICON V-suvaija
!Verbs of the Finnish type 5
! suvaija:suvai
+V:čče INDPRSSG1 ;
+V:čče INDPRSSG2 ;
+V:čče INDPRSSG3 ;
+V:čče INDPRSPL1 ;
+V:čče INDPRSPL2 ;
+V:%> INDPRSPL3_jAh ;
+V:čče INDCONNEG ;


!! Nonfinites

!! Finites
LEXICON INDPRSSG1
+Ind+Prs+Sg1:%^WEAK%>n K ;
LEXICON INDPRSSG2
+Ind+Prs+Sg2:%^WEAK%>t K ;
LEXICON INDPRSSG3
+Ind+Prs+Sg3:%>U1 K ;
LEXICON INDPRSPL1
+Ind+Prs+Pl1:%^WEAK%>mmO1 K ;
LEXICON INDPRSPL2
+Ind+Prs+Pl2:%^WEAK%>ttO1 K ;
LEXICON INDPRSPL3_jAh
+Ind+Prs+Pl3:%>jA1h K ;
LEXICON INDPRSPL3_dAh
+Ind+Prs+Pl3:%>dA1h K ;
LEXICON INDPRSPL3_tAh
+Ind+Prs+Pl3:%^WEAK%^E1%>tA1h K ;
LEXICON INDCONNEG
+Ind+Prs+ConNeg:%^WEAK K ;

! vim: set ft=xfst-lexc:

