!!!!!!!!!!!!!!!!
!! ADJECTIVES !!
!!!!!!!!!!!!!!!!

LEXICON A-Tag
+A: # ;

LEXICON A-huoletoi
+A+Sg:oi SGNOMSUF ;
+A+Sg:toma SGPARTSUF-A1 ; 
+A+Pl:tomie PLGENSUF ; 
+A+Pl:tomi PLPARTSUF ; 
+A+Der/MWN: N-huoletoi ; 
+N:toma A-NB ;

LEXICON N-karjalaine
+A+Sg:ine SGNOMSUF ;
+A+Sg:s SGPARTSUF-dU1 ;
+A+Pl:zie PLGENSUF ;
+A+Pl:zii PLPARTSUF ;
+A+Pl:zi PLINSTRSUF ;
+A:ze A-NB-i ;

LEXICON A-kieline
+A+Sg:ne SGNOMSUF ;
+A+Sg:s SGPARTSUF-dU1 ;
+A+Pl:zie PLGENSUF ;
+A+Pl:zii PLPARTSUF ;
+A+Pl:zi PLINSTRSUF ;
+A:ze Noun-NB-i ;

LEXICON A-parembi
+A+Sg:i SGNOMSUF ;
+A+Sg:A1 SGPARTSUF-A1 ;
+A+Pl:ie PLGENSUF ;
+A+Pl:i PLPARTSUF ;
+A+Der/MWN: N-parembi ;
+N:A1 A-NB ;


LEXICON A-NB-i
! These cases are symmetrically marked for number
+Sg: A-NB-01 ;
+Pl:%>i A-NB-01 ;
! The next two share the same stem vowel
+Sg: SGGENSUF ; ! The singular comitative is declined here
+Pl: PLNOMSUF ;

LEXICON A-NB-loi
! These cases are symmetrically marked for number
+Sg: A-NB-01 ;
+Pl:%>loi A-NB-01 ;
! The next two share the same stem vowel
+Sg: SGGENSUF ; ! The singular comitative is declined here
+Pl: PLNOMSUF ;

LEXICON A-NB-01
+Ess:%^WEAK%>nnU1 K ;
+Tra:%^WEAK%>kse K ;
+Ine:%^WEAK%>s K ;
+Ela:%^WEAK%>s K ;
+Ill:%>h K ;
+Ade:%^WEAK%>l K ;
+Abl:%^WEAK%>l K ;
+All:%^WEAK%>le K ;
+Abe:%^WEAK%>ttA1h K ;
+Use/Sub+Abe:%^WEAK%>ttA1 K ;
+Use/Sub+Abe:%^WEAK%>tA1 K ;
+Prl:%>či K ;
+Apr:%^WEAK%>llU1O1 K ;
+Ter:%^WEAK%>ssA1h K ;

!!!!!!!!!!!
!! NOUNS !!
!!!!!!!!!!!

LEXICON N-Tag
+N: # ;

LEXICON N-huoletoi
+N+Sg:oi SGNOMSUF ;
+N+Sg:toma SGPARTSUF-A1 ;
+N+Pl:tomie PLGENSUF ;
+N+Pl:tomi PLPARTSUF ;
+N+Pl:tomi PLINSTRSUF ;
+N:toma Noun-NB-i ;

LEXICON N-kandai
+N+Sg: SGNOMSUF ;
+N+Sg:A1 SGPARTSUF-A1 ;
+N+Pl:ie PLGENSUF ;
+N+Pl:i PLPARTSUF ;
+N+Pl:i PLINSTRSUF ;
+N:A1 Noun-NB-i ;

LEXICON N-karjal
+N+Sg: SGNOMSUF ;
+N+Sg:a SGPARTSUF-dU1 ;
+N+Pl:a%>i PLGENSUF ;
+N+Pl:i PLPARTSUF ;
+N+Pl:oi PLINSTRSUF ;
+N:A1 Noun-NB-i ;


LEXICON N-karjalaine
+N+Sg:ine SGNOMSUF ;
+N+Sg:s SGPARTSUF-dU1 ;
+N+Pl:zie PLGENSUF ;
+N+Pl:zii PLPARTSUF ;
+N+Pl:zi PLINSTRSUF ;
+N:ze Noun-NB-i ;

LEXICON N-kieli
+N+Sg:i SGNOMSUF ;
+N+Sg: SGPARTSUF-dU1 ;
+N+Pl:ie PLGENSUF ;
+N+Pl:ii PLPARTSUF ;
+N+Pl:i PLINSTRSUF ;
+N:e Noun-NB-i ;

LEXICON N-kuu
+N+Sg: SGNOMSUF ;
+N+Sg: SGPARTSUF-dU1 ;
+N+Pl:loi PLGENSUF ;
+N+Pl:loi PLPARTSUF ;
+N+Pl:loi PLINSTRSUF ;
+N: Noun-NB-loi ;

LEXICON N-muarju
+N+Sg:U1 SGNOMSUF ;
+N+Sg:A1 SGPARTSUF-A1 ;
+N+Pl:%>oi PLGENSUF ; ! the plural comitative is declined here
+N+Pl:%>oi PLPARTSUF ;
+N+Pl:%>oi PLINSTRSUF ;
+N:A1 Noun-NB-i ;

LEXICON N-pada
+N+Sg: SGNOMSUF ;
+N+Sg: SGPARTSUF-A1 ;
+N+Pl:i PLGENSUF ;
+N+Pl:i PLPARTSUF ;
+N+Pl:i PLINSTRSUF ;
+N: Noun-NB-i ;

LEXICON N-parembi
+N+Sg:i SGNOMSUF ;
+N+Sg:A1 SGPARTSUF-A1 ;
+N+Pl:ie PLGENSUF ;
+N+Pl:i PLPARTSUF ;
+N+Pl:i PLINSTRSUF ;
+N:A1 Noun-NB-i ;

LEXICON N-koiru
 N-päivy ;

LEXICON N-nimi
+N+Sg:i SGNOMSUF ;
+N+Sg:e SGPARTSUF-A1 ;
+N+Pl:%>ie PLGENSUF ; ! the plural comitative is declined here
+N+Pl:%>i PLPARTSUF ;
+N+Pl:%>i PLINSTRSUF ;
+N:e Noun-NB-i ;
+N+Pl:iloi PLGENSUF ;
+N+Pl:iloi PLPARTSUF-Zero ;
+N+Pl:iloi PLINSTRSUF ;
+N:i Noun-NB-loi ;

LEXICON N-päivy
+N+Sg:U1 SGNOMSUF ;
+N+Sg:A1 SGPARTSUF-A1 ;
+N+Pl:%>ie PLGENSUF ; ! the plural comitative is declined here
+N+Pl:%>i PLPARTSUF ;
+N+Pl:%>i PLINSTRSUF ;
+N:A1 Noun-NB-i ;

LEXICON N-runo
+N+Sg: SGNOMSUF ;
+N+Sg: SGPARTSUF-A1 ;
+N+Pl:loi PLGENSUF ;
+N+Pl:loi PLPARTSUF-Zero ;
+N+Pl:loi PLINSTRSUF ;
+N+Sg: Noun-NB-loi ;


LEXICON Noun-NB-i
! These cases are symmetrically marked for number
+Sg: Noun-NB-01 ;
+Pl:%>i Noun-NB-01 ;
! The next two share the same stem vowel
+Sg: SGGENSUF ; ! The singular comitative is declined here
+Pl: PLNOMSUF ;
 
LEXICON Noun-NB-loi
! These cases are symmetrically marked for number
+Sg: Noun-NB-01 ;
+Pl:%>loi Noun-NB-01 ;
! The next two share the same stem vowel
+Sg: SGGENSUF ; ! The singular comitative is declined here
+Pl: PLNOMSUF ;
 
!!!!!!!!!!!!!!!!!!
!! PROPER NOUNS !!
!!!!!!!!!!!!!!!!!!

LEXICON Prop-Tag
+Prop: N-Tag ;

LEXICON Prop-karjal
+Prop: N-karjal ;

LEXICON Prop-kuu
+Prop: N-kuu ;


LEXICON SGNOMSUF
+Nom: K ;
+Acc: K ;

LEXICON SGPARTSUF-dU1
+Par:%>tU1 K ;


LEXICON SGPARTSUF-A1
+Par:%>A1 K ;

LEXICON SGGENSUF
+Gen:%^WEAK%>n K ;
+Acc:%^WEAK%>n K ;
+Com:%^WEAK%>nke K ;

LEXICON PLNOMSUF
+Nom:%^WEAK%>t K ;
+Acc:%^WEAK%>t K ;

LEXICON PLGENSUF
+Gen:%>n K ;
+Com:%^WEAK%>nke K ;

LEXICON PLPARTSUF-Zero
+Par: K ;

LEXICON PLPARTSUF
+Par:%>A1 K ;

LEXICON PLINSTRSUF
+Instr:%>n K ;

LEXICON Noun-NB-01
+Ess:%^WEAK%>nnU1 K ;
+Tra:%^WEAK%>kse K ;
+Ine:%^WEAK%>s K ;
+Ela:%^WEAK%>s K ;
+Ela:%^WEAK%>späi K ;
+Ill:%>h K ;
+Ade:%^WEAK%>l K ;
+Abl:%^WEAK%>l K ;
+Abl:%^WEAK%>lpäi K ;
+All:%^WEAK%>le K ;
+Abe:%^WEAK%>ttA1h K ;
+Abe:%^WEAK%>ttA1 K ;
+Abe:%^WEAK%>tA1 K ;
+Prl:%>či K ;
+Apr:%^WEAK%>llU1O1 K ;
+Ter:%^WEAK%>ssA1h K ;
