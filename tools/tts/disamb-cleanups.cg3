### A simple CG3 files to select dynamic compounds over lexicalised ones in
### cases where one or more parts are:
### - loan words
### - acronyms

SOFT-DELIMITERS = "<,>" ;
DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>";

# ============= #
# Tags and sets #
# ============= #

SETS

# I define end of clause and beginning of clause in a way so that the file
# may be read both by the CG-2 and the vislcg formalisms.

LIST BOS = (>>>) (<s>);
LIST EOS = (<<<) (</s>);

# CG3 doesn´t function without >>> and <<< !

# Tags declared as single-membered LISTs
# ======================================

# Include a list of all tags declared as themselves:
INCLUDE ../../src/cg3/generated-tag-list.cg3 ;

# Olang tags:
# -----------

LIST OLANG = /^OLang\/.*/r ;

# stuff used in tts

LIST ALGAJEN = ("algua" V Ger Manner) ;

LIST CUASUN = ("č") ;

LIST NUM = (Sg) (Du) (Pl) ;
LIST CASE = (Nom) (Gen) (Par) (Ine) ;

LIST PARTITIVE-ADV = ("läs" Adv) ("enäm" Adv) ("nenga" Adv) ;

LIST NEGATION = ("ei" V Neg) ;

SECTION

ADDCOHORT:add-aigua-to-c-time ("<aigua>" "aiga" N Sg Par)
    AFTER (Sem/Time-clock)
          (NEGATE 1 ALGAJEN)
          (-2 CUASUN) ;
REMCOHORT:rem-c-from-c-time ("č") (1 Sem/Time-clock) ;

# "Pido algau č 12"
# -> "Pido algau kahtentostu aigah"
# Alkohol’an käyttö pidäy loppiekseh č 22
# -> kahtenkymmenen kahten aigah / kahtenkymmenen kahten čuasun aigah
# Linnan pruazniekku ozutetah televiizoras suorannu lähetyksenny č 18.15 algajen.
# -> "Linnan pruazniekku ozutetah televiizoras suorannu lähetyksenny kaheksatostu viizitostu algajen"


SUBSTITUTE:digits-agreement-gen (Num Arab) (Num Arab Card Sg Gen)
                            TARGET (Num Arab)
                            IF (*1 (N Sg Gen) BARRIER (*) - (A Gen) - (N Gen))
                               (NEGATE 0 NUM)
                               (NEGATE 0 CASE)
                               (NEGATE 0 Sem/Date OR Sem/Time-clock) ;

# Valdivonyhtivö on luadinuh 10 vuvven aigua 200 miljonan euron hävivöt.
# -> Valdivonyhtivö on luadinuh kymmenen vuvven aigua kahtensuan miljonan euron hävivöt.

SUBSTITUTE:digits-agreement-ade (Num Arab) (Num Arab Card Sg Ade)
                            TARGET (Num Arab)
                            IF (*1 (N Sg Ade) BARRIER (*) - (A Ade) - (N Ade))
                               (NEGATE 0 NUM)
                               (NEGATE 0 CASE)
                               (NEGATE 0 Sem/Date OR Sem/Time-clock) ;

# Miäry suurenou nenga 40 prosental.
# -> Miäry suurenou nenga nellälkymmenel prosental

SUBSTITUTE:digits-agreement-ter (Num Arab) (Num Arab Card Sg Ter)
                            TARGET (Num Arab)
                            IF (*1 (N Sg Ter) BARRIER (*) - (A Ter) - (N Ade))
                               (NEGATE 0 NUM)
                               (NEGATE 0 CASE)
                               (NEGATE 0 Sem/Date OR Sem/Time-clock) ;

# Alijiämy nouzou piäl 4 prosentassah da velgusuhteh piäl 90 prosentassah.
# -> Alijiämy nouzou piäl nelläh prosentassah da velgusuhteh piäl yheksähkymmeneh prosentassah

SUBSTITUTE:digits-after-par-adverbs (Num Arab) (Num Arab Card Sg Par)
                                    TARGET (Num Arab)
                                    IF (-1 PARTITIVE-ADV)
                                       (NEGATE 0 NUM)
                                       (NEGATE 0 CASE)
                                       (NEGATE 0 Sem/Date OR Sem/Time-clock) ;

SUBSTITUTE:digits-par-after-negs (Num Arab) (Num Arab Card Sg Par)
                                 TARGET (Num Arab)
                                 IF (*-1 NEGATION BARRIER CLB)
                                    (NEGATE 0 NUM)
                                    (NEGATE 0 CASE)
                                    (NEGATE 0 Sem/Date OR Sem/Time-clock) ;


SUBSTITUTE:digits-ine-before-hyphen (Num Arab Sg Nom) (Num Arab Sg Ine)
                                    TARGET (Num Arab)
                                    IF (1 MIDDLE) ;
SUBSTITUTE:digits-ill-after-hyphen (Num Arab Sg Nom) (Num Arab Sg Ill)
                                    TARGET (Num Arab)
                                    IF (-1 MIDDLE) ;
